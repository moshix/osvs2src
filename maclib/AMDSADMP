         MACRO                                                          00050002
&SADMP   AMDSADMP  &IPL=D3330,&OUTPUT=,&CONSOLE=(01F,3215),            X00100002
               &SYSUT=SYSDA,&VOLSER=SADUMP,&ULABEL=NOPURGE,            X00150002
               &TYPE=HI,&ADDR=REAL                                      00200002
         LCLC  &HEXN                                                    00220040
         LCLB  &IPLNGTH                                                 00250002
         LCLC  &IPL2,&CONADR,&CONTYPE,&TYPE2,&OUTPUT2,&JOBNAME,&ADDR2   00300002
         LCLC  &SYSUTP,&VOLSER1,&AP,&UNIT,&DEN,&IPLVAR                  00350002
          AGO   .NOPROA                 SKIP PROLOGUE CODE              00400002
         SPACE                                                          00450002
**********************************************************************  00500002
*                                                                    *  00550002
* STATUS --                                                          *  00600002
*     LEVEL - OS/VS2 RELEASE 3.7                             @ZA15487*  00650037
*A170070,C170140,A170210-170420,C006500                      @ZA15487*  00670037
*A171050                                                     @ZA15487*  00680037
*A000000-999999                                                Y01981*  00700002
*A098300                                                     @G51AGSG*  00720037
* FUNCTION --                                                        *  00750002
*    THE AMDSADMP MACRO GENERATES JCL INPUT TO STAGE 2 OF AMDSADMP   *  00800002
*    INITIALIZATION.  THIS INCLUDES INPUT TO AMDSADM2, AMDSALDR, AND *  00850002
*    IEHDASDR (IF REQUIRED-IPL=D OPTION ONLY).                       *  00900002
*                                                                    *  00950002
* ENTRY POINTS --                                                    *  01000002
*         AMDSADMP                                                   *  01050002
*                                                                    *  01100002
* INPUT --                                                           *  01150002
*    THE FOLLOWING ARE ALLOWED MACRO PARAMETERS:                     *  01200002
*       IPL=, TYPE=, OUTPUT=, CONSOLE=, SYSUT=,VOLSER=,ULABEL=,ADDR= *  01250002
*                                                                    *  01300002
*  OUTPUT --                                                         *  01350002
*    A JOBSTREAM TO INITIALIZE AMDSADMP ON A TAPE OR D.A. DEVICE     *  01400002
*                                                                    *  01450002
* EXTERNAL REFERENCES --                                             *  01500002
*    NONE                                                            *  01550002
*                                                                    *  01600002
*  EXITS, NORMAL --                                                  *  01650002
*    RETURN TO ASSEMBLER                                             *  01700002
*                                                                    *  01750002
* EXITS, ERROR --                                                    *  01800002
*    MNOTES TO USER                                                  *  01850002
*                                                                    *  01900002
* ATTRIBUTES --                                                      *  01950002
*    MACRO                                                           *  02000002
*                                                                    *  02050002
* CHARACTER CODE DEPENDENCY --                                       *  02100002
*    STANDARD CHARACTER SET                                          *  02150002
*                                                                    *  02200002
* NOTES --                                                           *  02250002
*    NONE                                                            *  02300002
*                                                                    *  02350002
**********************************************************************  02400002
*/*AMDSADMP: CHART (SEQ) */                                             02450002
*/*HEADER                                                               02500002
*/*CHART AB                                                             02550002
*/*ROUTINE (AMDSADMP)                                                   02600002
*/*DATE: JULY 19, 1971                                                  02650002
*/*PAGE # */                                                            02700002
*/* E START AMDSADMP */                                                 02750002
*/* N THIS MACRO GENERATES JCL INTERPRETS AND PASSES PARMS TO MACRO     02800002
*/* 2 */                                                                02850002
*/* N KYWRDS: IPL,CONSOLE, TYPE,OUTPUT, SYSUT,VOLSER, ULABEL,ADDR */    02900002
*/* D (YES,%T3,NO,) IPL = NULL? */                                      03000002
*/* D (NO,%A2,YES,) IPL GT OR = 4 CHARS? */                             03050002
*/* D (YES,%A1,NO,%A2) IPL LESS THAN OR = 7 CHARS? */                   03100002
*/*%A1: D (YES,%A3,NO,) IPL(1) = T OR D? */                             03150002
*/*%A2: S (,%T3) MNOTE: IPL=&IPL INVALID, IPL=D3330 ASSUMED */          03200002
*/*%T3: P (,%T4)  SET IPL2  = D3330 */                                  03250002
*/*%A3: P SET IPL2 = IPL */                                             03300002
*/*%T4: D (YES,%T5,NO,) CONSOLE = NULL? */                              03350002
*/* D (YES,%B1,NO,) CONSOLE(1) = NULL? */                               03400002
*/* D (YES,%B2,NO,) CONSOLE(1) = XXX? */                                03450002
*/* S (,%B1) MNOTE: CONADDR =&CONADDR INVALID 01F ASSUMED */            03500002
*/*%B1: P (,%B3) CONADDR = 01F */                                       03550002
*/*%B2: P CONADDR = XXX */                                              03600002
*/*%B3: D (YES,%B4,NO,) CONSOLE(2) = NULL? */                           03650002
*/* D (YES,%B5,NO,) CONSOLE(2) = XXXX? */                               03700002
*/*%B6: S (,%B4) MNOTE: CONTYPE =&CONTYPE INVALID, 3215 ASSUM */        03750002
*/*%B4: P (,%T7) CONTYPE = 3215 */                                      03800002
*/*%B5: D (NO,%B6,YES,) CONSOLE(2) = VALID TYPE? */                     03850002
*/* P (,%T7) CONTYPE = XXXX */                                          03900002
*/*%T5: P (,%T7) CONADDR = 01F, CONTYPE = 3215 */                       03950002
*/*%T7: D (YES,TYPE03,NO,) TYPE = LO? */                                04000002
*/* D (YES,%T13,NO,%T12) TYPE = HI? */                                  04050002
*/*TYPE03: D (YES,TYPE04,NO,) IPL2 EQ TAPE? */                          04100002
*/* P (,%T20) SET TYPE2 = LO */                                         04150002
*/*TYPE04: P MNOTE: IPL = &IPL2 AND TYPE2 = &TYPE ARE INVALID */        04200002
*/* R EXIT */                                                           04250002
*/*%T12: D (YES,%T13,NO,) TYPE = NULL? */                               04300002
*/* S (,%T13) MNOTE: TYPE =&TYPE INVALID, HI ASSUMED */                 04350002
*/*%T13: P (,%T18) SET TYPE2 = HI */                                    04400002
*/*%T14: D (YES,%T22,NO,%T15) OUTPUT = 4 CHARS? */                      04450002
*/*%T15: S (,%T19) MNOTE: 1 OUTPUT =&OUTPUT INVALID, T282 ASSUMED */    04500002
*/*%T18: N TEST OUTPUT PARM FOR TYPE=HI */                              04550002
*/* D (NO,%T14,YES,) OUTPUT = NULL? */                                  04600002
*/*%T19: P (,%T26) SET OUTPUT2 = T282 */                                04650002
*/*%T22: D (NO,%T15,YES,) OUTPUT(1) = T? */                             04700002
*/*%T25: P (,%T26) SET OUTPUT2 = &OUTPUT */                             04750002
*/*%T16: D (YES,%T23,NO,) OUTPUT = 4 CHARS? */                          04800002
*/*%T17: S (,%T21) MNOTE: OUTPUT =&OUTPUT INVALID, P00E ASSUMED */      04850002
*/*%T20: N TEST OUTPUT PARMS FOR TYPE=LO */                             04900002
*/*      D (NO,%AD2,YES,) ADDR= NULL? */                                04910002
*/*%AD7: P (,%T31) SET ADDR2= REAL */                                   04920002
*/*%AD2: D (YES,%AD7,NO,) ADDR= REAL? */                                04930002
*/*      D (YES,%AD5,NO,) ADDR= VIRTUAL? */                             04940002
*/*      S (,%AD7) MNOTE: ADDR= &ADDR INVALID, REAL ASSUMED */          04942002
*/*%AD5: P (,%T31) SET ADDR2 = VIRTUAL */                               04944002
*/*%T31: D (NO,%T16,YES,) OUTPUT = NULL? */                             04950002
*/*%T21: P (,%T26) SET OUTPUT2 = P00E */                                05000002
*/*%T23: D (NO,%T17,YES,) OUTPUT(1) = T OR P? */                        05050002
*/*P SET OUTPUT2 =&OUTPUT */                                            05060002
*/*%T26: D (YES,%X1,NO,) SYSUT = NULL? */                               05100002
*/* D (YES,%P1,NO,) SYSUT LE 6 CHARS? */                                05150002
*/* S MNOTE: SYSUT =&SYSUT INVALID, SYSDA ASSUMED */                    05200002
*/*%X1: P (,%X2) SET SYSUTP = SYSDA */                                  05250002
*/*%P1: P (,%X2) SET SYSUTP = SYSUT */                                  05300002
*/*%X2: D (YES,%X5,NO,) MACRO1 LABEL = NULL? */                         05350002
*/* P (,%X6) JOB NAME = LABEL */                                        05400002
*/*%X5: P JOB NAME = AMDSADMP */                                        05450002
*/*%X6: P ASMFC JCL TO ASSEMBLE MACRO 2 */                              05500002
*/* N ALL JCL CONDITIONAL ON PRECEDING STEPS */                         05550002
*/*%T28: D (YES,%X4,NO,) VOLSER = NULL? */                              05600002
*/* D (YES,%V1,NO,) VOLSER LE 6 CHARS? */                               05650002
*/* S MNOTE: VOLSER =&VOLSER INVALID, SADUMP ASSUMED */                 05700002
*/*%X4: P (,%W3) SET VOLSER1 = SADUMP */                                05750002
*/*%V1: P (,%W3) SET VOLSER1 = VOLSER */                                05800002
*/*%X3: D (TAPE,%T30,D.A.,) IPL2 = T OR D ? */                          05850002
*/* D (YES,%M7,NO,) ULABEL = PURGE? */                                  05900002
*/* D (YES,%M4,NO,) ULABEL = NOPURGE? */                                05950002
*/* D (YES,%M4,NO,) ULABEL = NULL? */                                   06000002
*/* S MNOTE: ULABEL =&ULABEL INVALID, NOPURGE ASSUMED */                06050002
*/*%M4: P (,%T30) IEHDASDR JCL FOR LDMKR OUTPUT TO DA (NO PURGE OF USER 06100002
*/*LABELS) */                                                           06150002
*/*%W3: D (LO,%T27,HI,) TYPE2 = ? */                                    06200002
*/* P (,%X3)  JCL FOR AMDSADM2 AND THE LOADER MAKER FOR HI OPTION       06250002
*/*  */                                                                 06300002
*/*%M7: P IEHDASDR JCL FOR LDMKR OUTPUT TO D. A. (PURGE USER            06350002
*/*LABELS) */                                                           06400002
*/*%T30: R END */                                                       06450002
*/*%T27: P (,%X3)  JCL FOR AMDSADM2 AND THE LOADER MAKER FOR LO OPTION  06500002
*/*  */                                                                 06550002
*/*AMDSADMP: END */                                                     06600002
.NOPROA   ANOP                          MACRO 1 CODE START              06650002
         SPACE                                                          06700002
.********************************************************************** 06750002
.*                                                                    * 06800002
.*  PROCESS IPL= PARAMETER.  TWO DEVICE TYPES D (DISK) AND T (TAPE)   * 06850002
.*  ARE ALLOWED.  IF IPL= IS NOT SPECIFIED D3330 IS ASSUMED.          * 06900002
.*                                                                    * 06950002
.********************************************************************** 07000002
.*       SPACE                                                          07050002
         AIF   ('&IPL' EQ '').IPL03 .  IS IPL SPECIFIED?                07100002
         AIF   (K'&IPL LT 4).IPL02 .   VALIDITY CHECK OF IPL            07150002
         AIF   (K'&IPL GT 7).IPL02 .   FOR HIGH AND LOW LIMITS          07200002
         AIF   (K'&IPL NE 4).IPL04     IF NOT DEV ADDR FORMAT, THEN     07202040
.*                                     SKIP ADDR VALIDITY CHECK         07204040
.*                                                             @ZM40017 07206040
.*                                     THE NEXT 7 LINES CHECK WHETHER   07208040
.*                                     A 3 CHARACTER INPUT,PUT IN HEXN, 07210040
.*                                     REPRESENTS A VALID 3 DIGIT HEX   07212040
.*                                     NUMBER. IF THE CHECK FAILS ON    07214040
.*                                     ANY DIGIT, A BRANCH IS TAKEN TO  07216040
.*                                     THE ERROR ROUTINE FOR THE INPUT. 07218040
&HEXN    SETC  '&IPL'(2,3)                                     @ZM40017 07220040
.* THE NEXT 6 LINES WERE ADDED FOR PTM VS40017                 @ZM40017 07222040
        AIF (NOT(('&HEXN'(1,1) GE 'A' AND '&HEXN'(1,1) LE 'F') OR      X07224040
               ('&HEXN'(1,1) GE '0' AND '&HEXN'(1,1) LE '9'))).IPL02    07226040
        AIF (NOT(('&HEXN'(2,1) GE 'A' AND '&HEXN'(2,1) LE 'F') OR      X07228040
               ('&HEXN'(2,1) GE '0' AND '&HEXN'(2,1) LE '9'))).IPL02    07230040
        AIF (NOT(('&HEXN'(3,1) GE 'A' AND '&HEXN'(3,1) LE 'F') OR      X07232040
               ('&HEXN'(3,1) GE '0' AND '&HEXN'(3,1) LE '9'))).IPL02    07234040
.IPL04   ANOP                                                  @ZM40017 07236040
         AIF   ('&IPL'(1,1) EQ 'T').IPL00 VALIDITY CHECK TAPE IPL       07250002
         AIF   ('&IPL'(1,1) NE 'D').IPL02 VALIDITY CHECK DISK IPL       07300002
.IPL01   ANOP .                        HERE IF IPL PARM IS VALID        07350002
&IPL2    SETC  '&IPL' .                SET IPL2 TO WHAT USER REQUESTED  07400002
&IPLVAR  SETC  '&IPL2'(1,1) .          SET UP LDR MAKER PARMS           07450002
         AGO   .CONSLE1 .              GO PREPARE CONSOLE PROCESSING    07500002
.IPL00   ANOP .                        CHECK FOR 7 TRK TAPE             07550002
         AIF   (K'&IPL NE 7).IPL01     IF NOT X400-X USE AS IS          07600002
         AIF   ('&IPL'(6,7) NE '-2').IPL01 IF NOT 7 TRACK USE AS IS     07650002
&IPLNGTH SETB  1 .                     SET SW FOR 7 TRACK TAPE IPL      07700002
         AGO   .IPL01 .                GO SET IPL PARMS                 07750002
.IPL02   ANOP .                        ERROR. DEFAULT TO BE ASSUMED     07800002
         MNOTE 4,'IPL=&IPL IS INVALID, IPL=D3330 IS ASSUMED'            07850002
.IPL03   ANOP .                        SET UP THE DEFAULT DEVICE        07900002
&IPL2    SETC  'D3330' .               DEFAULT IPL DEVICE TYPE          07950002
&IPLVAR  SETC  '&IPL2'(1,1) .          SET UP LDR MAKER PARMS           08000002
.CONSLE1 ANOP .                        CONSOLE PROCESSING               08050002
.********************************************************************** 08100002
.*                                                                    * 08150002
.*  PROCESS THE CONSOLE= PARAMETER.  THE CONSOLE PARAMETER PROVIDES   * 08200002
.*  TWO VARIABLES, A CONSOLE ADDRESS (CONADDR) AND A CONSOLE TYPE     * 08250002
.*  (CONTYPE).  01F IS THE DEFAULT CONSOLE ADDRESS WHILE 3215 IS THE  * 08300002
.*  DEFAULT CONSOLE TYPE                                              * 08350002
.*                                                                    * 08400002
.********************************************************************** 08450002
.*       SPACE                                                          08500002
&UNIT    SETC  '&IPL2'(2,7) .          SET UP UNIT PARM                 08550002
         AIF   ('&CONSOLE' EQ '').CONSLE9 CONSOLE DEFAULT SPECIFIED     08600002
         AIF   ('&CONSOLE(1)' EQ '').CONSLE2 IS CONSOLE ADDR DEF?       08650002
         AIF   (K'&CONSOLE(1) NE 3).CONSLEA VALIDITY CHECK     @ZM40017 08700040
.*                                                             @ZM40017 08702040
.*                                     THE NEXT 7 LINES CHECK WHETHER   08704040
.*                                     A 3 CHARACTER INPUT,PUT IN HEXN, 08706040
.*                                     REPRESENTS A VALID 3 DIGIT HEX   08708040
.*                                     NUMBER. IF THE CHECK FAILS ON    08710040
.*                                     ANY DIGIT, A BRANCH IS TAKEN TO  08712040
.*                                     THE ERROR ROUTINE FOR THE INPUT. 08714040
&HEXN    SETC  '&CONSOLE(1)'(1,3)                              @ZM40017 08716040
.* THE NEXT 6 LINES WERE ADDED FOR PTM VS40017                 @ZM40017 08718040
        AIF (NOT(('&HEXN'(1,1) GE 'A' AND '&HEXN'(1,1) LE 'F') OR      X08720040
               ('&HEXN'(1,1) GE '0' AND '&HEXN'(1,1) LE '9'))).CONSLEA  08722040
        AIF (NOT(('&HEXN'(2,1) GE 'A' AND '&HEXN'(2,1) LE 'F') OR      X08724040
               ('&HEXN'(2,1) GE '0' AND '&HEXN'(2,1) LE '9'))).CONSLEA  08726040
        AIF (NOT(('&HEXN'(3,1) GE 'A' AND '&HEXN'(3,1) LE 'F') OR      X08728040
               ('&HEXN'(3,1) GE '0' AND '&HEXN'(3,1) LE '9'))).CONSLEA  08730040
         AGO   .CONSLE3                                        @ZM40017 08732040
.CONSLEA ANOP                                                  @ZM40017 08734040
         MNOTE 4,'CONSOLE ADDR=&CONSOLE(1) IS INVALID, CONSOLE ADDR=01FX08750002
                IS ASSUMED'                                             08800002
.CONSLE2 ANOP .                        SET UP CONSOLE DEFAULT           08850002
&CONADR  SETC  '01F' .                 DEFAULT CONSOLE ADDR             08900002
         AGO   .CONSLE4 .              GO HANDLE CONSOLE DEV TYPE       08950002
.CONSLE3 ANOP .                        ADDR IS VALID, SET UP ADDR       09000002
&CONADR SETC  '&CONSOLE(1)' .          SET CONADDR TO WHAT USER REQ     09050002
.CONSLE4 ANOP .                        HANDLE CONSOLE DEV TYPE          09100002
         AIF   ('&CONSOLE(2)' EQ '').CONSLE6 DEFAULT DEV TYPE?          09150002
         AIF   (K'&CONSOLE(2) EQ 4).CONSLE7 VALIDITY CHECK DEV TYPE     09200002
.CONSLE5 ANOP .                        DEV TYPE IN ERROR                09250002
         MNOTE 4,'CONSOLE TYPE=&CONSOLE(2) IS INVALID, CONSOLE TYPE=321X09300002
               5 IS ASSUMED'                                            09350002
.CONSLE6 ANOP .                        SET DEFAULT CONSOLE TYPE         09400002
&CONTYPE SETC  '3215' .                DEFAULT CONSOLE TYPE             09450002
         AGO   .TYPE01 .               GO CHECK TYPE                    09500002
.CONSLE7 ANOP .                        VALIDITY CHECK CONSOLE TYPE      09550002
         AIF   ('&CONSOLE(2)' EQ '1052').CONSLE8 1052 CONSOLE TYPE?     09600002
         AIF   ('&CONSOLE(2)' EQ '2150').CONSLE8 2150 CONSOLE TYPE?     09650002
         AIF   ('&CONSOLE(2)' EQ '3210').CONSLE8 3210 CONSOLE TYPE?     09700002
         AIF   ('&CONSOLE(2)' EQ '3215').CONSLE8 3215 CONSOLE TYPE?     09750002
         AIF   ('&CONSOLE(2)' EQ '3066').CONSLE8 3066 CONSOLE TYPE?     09800002
         AIF ('&CONSOLE(2)' EQ '3158').CONSLE8  3158 TYPE      @ZA00418 09810000
         AIF ('&CONSOLE(2)' EQ '3277').CONSLE8  3277 TYPE      @ZA00418 09820000
         AIF   ('&CONSOLE(2)' EQ '3036').CONSLE8 3036 TYPE     @G51AGSG 09830037
         AGO   .CONSLE5 .               ERROR IN DEV TYPE, TAKE DEFAULT 09850002
.CONSLE8 ANOP .                         SET CONSOLE TYPE                09900002
&CONTYPE SETC  '&CONSOLE(2)' .          SET CONSOLE TYPE TO USER SPEC   09950002
         AGO   .TYPE01 .                HANDLE TYPE PARM                10000002
.CONSLE9 ANOP .                         SET DEFAULT CONSOLE ADDR        10050002
&CONADR  SETC  '01F'                    DEFAULT CONSOLE ADDR IS 01F     10100002
         AGO   .CONSLE6 .               SET UP DEFAULT CONSOLE TYPE     10150002
.********************************************************************** 10200002
.*                                                                    * 10250002
.*  PROCESS THE TYPE= PARAMETER.  THE POSSIBILITIES ARE TYPE=HI       * 10300002
.*  (DEFAULT SPECIFICATION) AND TYPE=LO                               * 10350002
.*                                                                    * 10400002
.********************************************************************** 10450002
.*       SPACE                                                          10500002
.TYPE01  ANOP .                        TYPE PROCESSING ENTRY POINT      10550002
         AIF   ('&TYPE' EQ 'LO').TYPE03 CHECK TYPE = LO                 10600002
         AIF   ('&TYPE' EQ 'HI').TYPE02 CHECK TYPE = HI                 10650002
         AIF   ('&TYPE' EQ '').TYPE02  CHECK DEFAULT                    10700002
         MNOTE 4,'TYPE=&TYPE IS INVALID, TYPE=HI IS ASSUMED'            10750002
.TYPE02  ANOP .                        SET THE TYPE FIELD TO HI         10800002
&TYPE2   SETC  'HI' .                  TYPE2 PARM FOR LDRMKER AND MAC2  10850002
         AGO   .OUTPUT3 .              GO HANDLE OUTPUT PARMS           10900002
.TYPE03  ANOP .                        PROCESS TYPE = LO                10950002
         AIF   ('&IPL2'(1,1) EQ 'T').TYPE04 IF IPL=TAPE;HAVE ERROR      11000002
&TYPE2   SETC  'LO' .                  SET LO SPECIFIED                 11050002
.*       SPACE                                                          11060002
.********************************************************************** 11070002
.*                                                                    * 11080002
.*  THIS SECTION PROCESSES THE ADDR= PARAMETER FOR TYPE=LO.  THE    *   11090002
.*  DEFAULT USED IS ADDR=REAL.                                      *   11092002
.*                                                                    * 11094002
.********************************************************************** 11096002
.*       SPACE                                                          11098002
         AIF   ('&ADDR' EQ '').ADDR01  ADDR DEFAULT SPECIFIED    Y02006 11098402
         AIF   ('&ADDR' EQ 'REAL').ADDR01 CHECK ADDR = REAL      Y02006 11098802
         AIF   ('&ADDR' EQ 'VIRTUAL').ADDR02 CHECK ADDR =VIRTUAL Y02006 11099202
         MNOTE 4,'ADDR=&ADDR IS INVALID. ADDR=REAL IS ASSUMED'   Y02006 11099602
.ADDR01  ANOP .                        SET ADDR                  Y02006 11099702
&ADDR2   SETC  'REAL' .                SET ADDR2=REAL            Y02006 11099802
         AGO   .ADDR04 .               GO CHECK OUTPUT           Y02006 11099902
.ADDR02  ANOP .                        SET ADDR                  Y02006 11116602
&ADDR2   SETC  'VIRTUAL' .             SET ADDR=VIRTUAL          Y02006 11126602
.ADDR04  ANOP .                        OUTPUT CHECK FOR TYPE =LO Y02006 11128602
.*       SPACE                                                          11130602
.********************************************************************** 11133302
.*                                                                    * 11150002
.*  THIS SECTION PROCESSES THE OUTPUT= PARAMETER FOR TYPE=LO. THE     * 11200002
.*  DEFAULT DEVICE USED IS OUTPUT=P00E                                * 11250002
.*                                                                    * 11300002
.********************************************************************** 11350002
.*       SPACE                                                          11400002
         AIF   ('&OUTPUT' EQ '').OUTPUT2 CHECK OUTPUT PARM DEF          11450002
         AIF   (K'&OUTPUT NE 4).OUTPUT1 VALIDITY CHECK LENGTH           11500002
.*                                                             @ZM40017 11503040
.*                                     THE NEXT 7 LINES CHECK WHETHER   11506040
.*                                     A 3 CHARACTER INPUT,PUT IN HEXN, 11509040
.*                                     REPRESENTS A VALID 3 DIGIT HEX   11512040
.*                                     NUMBER. IF THE CHECK FAILS ON    11515040
.*                                     ANY DIGIT, A BRANCH IS TAKEN TO  11518040
.*                                     THE ERROR ROUTINE FOR THE INPUT. 11521040
&HEXN    SETC  '&OUTPUT'(2,3)                                  @ZM40017 11524040
.* THE NEXT 6 LINES WERE ADDED FOR PTM VS40017                 @ZM40017 11527040
        AIF (NOT(('&HEXN'(1,1) GE 'A' AND '&HEXN'(1,1) LE 'F') OR      X11530040
               ('&HEXN'(1,1) GE '0' AND '&HEXN'(1,1) LE '9'))).OUTPUT1  11533040
        AIF (NOT(('&HEXN'(2,1) GE 'A' AND '&HEXN'(2,1) LE 'F') OR      X11536040
               ('&HEXN'(2,1) GE '0' AND '&HEXN'(2,1) LE '9'))).OUTPUT1  11539040
        AIF (NOT(('&HEXN'(3,1) GE 'A' AND '&HEXN'(3,1) LE 'F') OR      X11542040
               ('&HEXN'(3,1) GE '0' AND '&HEXN'(3,1) LE '9'))).OUTPUT1  11545040
         AIF   ('&OUTPUT'(1,1) EQ 'T').OUTPUT6 IF TAPE OKAY             11550002
         AIF   ('&OUTPUT'(1,1) EQ 'P').OUTPUT6 IF PRINTER OKAY          11600002
.OUTPUT1 ANOP .                        ERROR DEFAULT ASSUMED            11650002
         MNOTE 4,'OUTPUT=&OUTPUT IS INVALID, OUTPUT=P00E IS ASSUMED'    11700002
.OUTPUT2 ANOP .                        SET DEFAULT                      11750002
&OUTPUT2 SETC  'P00E' .                DEFAULT DEV TYPE FOR TYPE=LO     11800002
         AGO   .SYSUT01 .              HANDLE SYSUT PARM                11850002
.TYPE04  ANOP .                        SEVERE ERROR-INCOMPATIBLE        11900002
.*                                     ATTRIBUTES SPECIFIED             11950002
         MNOTE 8,'PARAMETERS IPL=&IPL2 AND TYPE=&TYPE ARE INCOMPATIBLE X12000002
               MACRO PROCESSING TERMINATED'                             12050002
         MEXIT                                                          12100002
.********************************************************************** 12150002
.*                                                                    * 12200002
.*  THIS OUTPUT PROCESSING IS FOR TYPE = HI PROCESSING ONLY           * 12250002
.*                                                                    * 12300002
.********************************************************************** 12350002
.*       SPACE                                                          12400002
.OUTPUT3 ANOP .                       HANDLE OUTPUT PARMS               12450002
         AIF   ('&OUTPUT' EQ '').OUTPUT5 DEFAULT SPECIFIED?             12500002
         AIF   (K'&OUTPUT NE 4).OUTPUT4 VALIDITY CHECK LENGTH           12550002
.*                                                             @ZM40017 12553040
.*                                     THE NEXT 7 LINES CHECK WHETHER   12556040
.*                                     A 3 CHARACTER INPUT,PUT IN HEXN, 12559040
.*                                     REPRESENTS A VALID 3 DIGIT HEX   12562040
.*                                     NUMBER. IF THE CHECK FAILS ON    12565040
.*                                     ANY DIGIT, A BRANCH IS TAKEN TO  12568040
.*                                     THE ERROR ROUTINE FOR THE INPUT. 12571040
&HEXN    SETC  '&OUTPUT'(2,3)                                  @ZM40017 12574040
.* THE NEXT 6 LINES WERE ADDED FOR PTM VS40017                 @ZM40017 12577040
        AIF (NOT(('&HEXN'(1,1) GE 'A' AND '&HEXN'(1,1) LE 'F') OR      X12580040
               ('&HEXN'(1,1) GE '0' AND '&HEXN'(1,1) LE '9'))).OUTPUT4  12583040
        AIF (NOT(('&HEXN'(2,1) GE 'A' AND '&HEXN'(2,1) LE 'F') OR      X12586040
               ('&HEXN'(2,1) GE '0' AND '&HEXN'(2,1) LE '9'))).OUTPUT4  12589040
        AIF (NOT(('&HEXN'(3,1) GE 'A' AND '&HEXN'(3,1) LE 'F') OR      X12592040
               ('&HEXN'(3,1) GE '0' AND '&HEXN'(3,1) LE '9'))).OUTPUT4  12595040
         AIF   ('&OUTPUT'(1,1) EQ 'T').OUTPUT6 IF TAPE OUTPUT VALID     12600002
.OUTPUT4 ANOP .                       ANY OTHER SPEC IS IN ERROR        12650002
         MNOTE 4,'OUTPUT=&OUTPUT IS INVALID, OUTPUT=T282 IS ASSUMED'    12700002
.OUTPUT5 ANOP .                        DEFAULT SET UP                   12750002
&OUTPUT2 SETC  'T282' .                TAPE DEFAULT ADDR                12800002
         AGO   .SYSUT01 .              GO HANDLE SYSUT PARM             12850002
.OUTPUT6 ANOP .                        SET UP USER SPECIFIED PARM       12900002
&OUTPUT2 SETC  '&OUTPUT' .             TAPE ADDR                        12950002
.SYSUT01 ANOP .                        PROCESS SYSUT PARMS              13000002
.********************************************************************** 13050002
.*                                                                    * 13100002
.*  THIS CODE PROCESSES SYSUT SPECIFICATION.  SYSUT=SYSDA IS THE      * 13150002
.*  DEFAULT SPECIFICATION.                                            * 13200002
.*                                                                    * 13250002
.********************************************************************** 13300002
.*       SPACE                                                          13350002
         AIF   ('&SYSUT' EQ '').SYSUT02 DEFAULT SPECIFIED?              13400002
         AIF   (K'&SYSUT LE 6).SYSUT03  PROPER LENGTH?                  13450002
         MNOTE 4,'SYSUT=&SYSUT IS INVALID, SYSUT=SYSDA IS ASSUMED'      13500002
.SYSUT02 ANOP .                        SET SYSUT DEFAULT                13550002
&SYSUTP  SETC  'SYSDA'                 DEFAULT UNIT IS SYSDA            13600002
         AGO   .LBL01 .                PROCESS JOBNAME STATEMENT        13650002
.SYSUT03 ANOP .                        SET THE SYSUT PARM               13700002
&SYSUTP  SETC  '&SYSUT' .              SET TO USER SPECIFIED            13750002
.LBL01   ANOP .                        PROCESSING JOBNAME               13800002
.********************************************************************** 13850002
.*                                                                    * 13900002
.*  THE LABEL NAME OF THE MACRO IS USED FOR THE JOBNAME               * 13950002
.*                                                                    * 14000002
.********************************************************************** 14050002
.*       SPACE                                                          14100002
         AIF   ('&SADMP' EQ '').LBL02 IS LABEL BLANKS?                  14150002
&JOBNAME SETC  '&SADMP' .              SET JOBNAME TO SADMP LABEL       14200002
         AGO   .JCLASM1 .              PROCESS JCL                      14250002
.LBL02   ANOP .                        SET DEFAULT JOBNAME              14300002
&JOBNAME SETC  'AMDSADMP'              DEFAULT JOBNAME                  14350002
.********************************************************************** 14400002
.*                                                                    * 14450002
.*  THIS IS THE JCL TO EXECUTE THE ASSEMBLER. THE INPUT FOR MACRO TWO * 14500002
.*  IS CREATED AND PASSED AT THIS TIME.                               * 14550002
.*                                                                    * 14600002
.********************************************************************** 14650002
         SPACE                                                          14700002
.JCLASM1 ANOP .                        ASSEMBLY JCL                     14750002
         PUNCH '//&JOBNAME JOB MSGLEVEL=(1,1),REGION=128K'              14800002
         PUNCH '//ASM EXEC PGM=ASMBLR,PARM=''LOAD,NODECK'''             14850002
         PUNCH '//SYSLIB DD DSN=SYS1.MACLIB,DISP=SHR'                   14900002
         PUNCH '//SYSUT1 DD UNIT=&SYSUTP,SPACE=(1700,(400,50))'         14950002
         PUNCH '//SYSUT2 DD UNIT=&SYSUTP,SPACE=(1700,(400,50))'         15000002
         PUNCH '//SYSUT3 DD UNIT=&SYSUTP,SPACE=(1700,(400,50))'         15050002
         PUNCH '//SYSPRINT DD SYSOUT=A'                                 15100002
         PUNCH '//SYSGO DD DSN=&&LOADSET,UNIT=&SYSUTP,'                 15150002
         PUNCH '// SPACE=(80,(250,50)),DISP=(,PASS)'                    15200002
         PUNCH '//SYSIN DD *'                                           15250002
         PUNCH '         CSECT'                                         15300002
         PUNCH '         AMDSADM2   TYPE2=&TYPE2,CONTYPE=&CONTYPE,CONADX15350002
               DR=&CONADR,                 X'                           15400002
         AIF   ('&TYPE2' EQ 'LO').JCLADR1  IF TYPE=LO PUNCH ADDR=  PARM 15410002
         PUNCH '               OUTPUT2=&OUTPUT2,IPL2=&IPL2'             15450002
         AGO   .JCLWK1          SKIP LO JCL                             15460002
.JCLADR1 ANOP .                 PUNCH ADDR= FOR LO                      15470002
         PUNCH '               OUTPUT2=&OUTPUT2,IPL2=&IPL2,ADDR2=&ADDR2X15480002
               '                                                        15490002
.JCLWK1  ANOP .                CONTINUE COMMON JCL PROCESSING           15492002
         PUNCH '         END'                                           15500002
         PUNCH '/*'                                                     15550002
.********************************************************************** 15600002
.*                                                                    * 15650002
.*  THIS CODE HANDLES THE VOLSER PARM.  THE DEFAULT IS SYSDA          * 15700002
.*                                                                    * 15750002
.********************************************************************** 15800002
.*       SPACE                                                          15850002
         AIF   ('&VOLSER' EQ '').JCLVOL2 DEFAULT REQUESTED?             15900002
         AIF   (K'&VOLSER LE 6).JCLVOL3 VOLSER VALID; HANDLE USR LBL    15950002
         MNOTE 4,'VOLSER=&VOLSER IS INVALID, VOLSER=SADUMP IS ASSUMED'  16000002
.JCLVOL2 ANOP .                        SET DEFAULT FOR VOLSER           16050002
&VOLSER1 SETC  'SADUMP' .              VOLSER DEFAULT                   16100002
         AGO   .LDR1 .                 SET THE VOLSER                   16150002
.JCLVOL3 ANOP .                        THIS CODE SETS THE VOLSER        16200002
&VOLSER1 SETC  '&VOLSER'               SPECIFIED BY THE USER            16250002
.LDR1    ANOP .                        LOADER MAKER CODE SECTION        16300002
.********************************************************************** 16350002
.*                                                                    * 16400002
.*  THIS JCL WILL EXECUTE THE LOADER MAKER PROGRAM WHICH CONVERTS THE * 16450002
.*  OBJECT CODE AND LOAD MODULE CODE TO MACHINE EXECUTABLE CODE.      * 16500002
.*                                                                    * 16550002
.********************************************************************** 16600002
         SPACE                                                          16650002
         PUNCH '//LDMKER EXEC PGM=AMDSALDR,COND=(1,LE),'                16700002
         PUNCH '// PARM=''IPL2=&IPLVAR,TYPE2=&TYPE2'''                  16750002
         PUNCH '//SYSPRINT DD SYSOUT=A'                                 16800002
         PUNCH '//SYSUT1 DD DSN=&&LOADSET,DISP=(OLD,DELETE)'            16850002
         PUNCH '//SYSUT2 DD DSN=SYS1.LINKLIB,DISP=SHR'                  16950002
         AIF   ('&IPL2'(1,1) EQ 'T').TLDR IF TAPE IPL SET UP DCB PARMS  17000002
         AIF   ('&SYSUTP' NE 'SYSDA').JCLLDR2 NOT SYSDA?       @ZA15487 17007037
         PUNCH '//SYSUT3 DD DSN=&&REALDUMP,DISP=(,PASS),'      @ZA15487 17014037
         PUNCH '// SPACE=(TRK,(5,1)),VOL=REF=SYS1.NUCLEUS,'    @ZA15487 17021037
         PUNCH '// UNIT=&SYSUTP'                               @ZA15487 17028037
         AGO   .JCLLDR3             GOTO SYSUT4 PROCESSING     @ZA15487 17035037
.JCLLDR2 ANOP                       SYSUTP IS NOT SYSDA        @ZA15487 17042037
         PUNCH '//SYSUT3 DD DSN=&&REALDUMP,DISP=(,PASS),UNIT=&SYSUTP,'  17050002
         PUNCH '// SPACE=(TRK,(5,1))'                                   17100002
.JCLLDR3 ANOP                       SELECT PROPER SYSUT4       @ZA15487 17105037
         AIF   ('&TYPE2' EQ 'LO').JCLLDR1 FOR TYPE=LO PGE NOT NEEDED    17110002
         PUNCH '//SYSUT4 DD UNIT=&UNIT,DISP=(,KEEP),'                   17150002
         PUNCH '// VOL=SER=&VOLSER1,DSN=SYS1.PAGEDUMP,'                 17200002
         PUNCH '// SPACE=(6144,(45),,CONTIG),LABEL=EXPDT=99366'         17250002
         AGO   .ULBL1 .                GO PROCESS VOLSER DATA           17252002
.JCLLDR1 ANOP .                        THIS IS LOW SPEED LDRMAKER JCL   17260002
         PUNCH '//SYSUT4 DD UNIT=&UNIT,DISP=(,KEEP),'                   17270002
         PUNCH '// VOL=SER=&VOLSER1,DSN=SYS1.PAGEDUMP,'                 17280002
         PUNCH '// SPACE=(TRK,(1),,CONTIG),LABEL=EXPDT=99366'           17290002
         AGO   .ULBL1 .                GO PROCESS VOLSER DATA           17300002
.TLDR    ANOP .                        TAPE LOADER MAKER JCL            17350002
&AP      SETC  ')'                     SET UP PAREN                     17400002
&DEN     SETC  ' ' .                   INIT FOR 9 TRK TAPE              17450002
         AIF   (NOT(&IPLNGTH)).TLDR1   CHECK FOR 7 TRK TAPE             17500002
&AP      SETC  ',TRTCH=C'              7 TRK TAPE SET UP DCB PARMS      17550002
&DEN     SETC  ',DEN=2)' .             SET UP DENSITY                   17600002
.TLDR1   ANOP .                        HERE FOR DCB INPUT TO GENER      17650002
         PUNCH '//SYSUT3 DD UNIT=&UNIT,DISP=OLD,LABEL=(,NL),DSN=SADMP,' 17700002
         PUNCH '// VOL=SER=&VOLSER1,DCB=(BLKSIZE=24576&AP&DEN'          17750002
.ULBL1   ANOP .                        USER LABEL PROCESSING            18000002
.********************************************************************** 18050002
.*                                                                    * 18100002
.*  THIS CODE PRODUCES THE USER LABEL PURGE OR NO PURGE PARMS FOR     * 18150002
.*  IEHDASDR PROGRAM.  THE DEFAULT IS NO PURGE.                       * 18200002
.*                                                                    * 18250002
.********************************************************************** 18300002
.*       SPACE                                                          18350002
         AIF   ('&IPL2'(1,1) EQ 'T').ENDUP1 IF TAPE SKIP DASDR          18400002
         SPACE                                                          18450002
         PUNCH '//DASDR EXEC PGM=IEHDASDR,COND=(1,LE)'                  18500002
         PUNCH '//SYSPRINT DD SYSOUT=A'                                 18550002
         PUNCH '//IPLDEV DD DISP=OLD,UNIT=&UNIT,VOL=SER=&VOLSER1'       18600002
         PUNCH '//INPUT DD DSN=&&REALDUMP,DISP=(OLD,DELETE)'            18650002
         PUNCH '//SYSIN DD *'                                           18700002
         AIF   ('&ULABEL' EQ 'PURGE').ULBL3 IF PURGE OUTPUT CARD        18750002
         AIF   ('&ULABEL' EQ 'NOPURGE').ULBL2 IF NOPURGE VALID INPUT    18800002
         AIF   ('&ULABEL' EQ '').ULBL2 DEFAULT REQUESTED                18850002
         MNOTE 4,'ULABEL=&ULABEL IS INVALID, ULABEL=NOPURGE IS ASSUMED' 18900002
.ULBL2   ANOP .                        DEFAULT PROCESSING               18950002
         PUNCH '          PUTIPL   FROMDD=INPUT,TODD=IPLDEV,PURGE=NO'   19000002
         AGO   .ULBL4 .                HANDLE /*                        19050002
.ULBL3   ANOP .                        HERE FOR PURGE KEYWORD           19100002
         PUNCH '          PUTIPL   FROMDD=INPUT,TODD=IPLDEV,PURGE=YES'  19150002
.ULBL4   ANOP .                        THE /* PROCESSING                19200002
         PUNCH '/*'                                                     19250002
.ENDUP1  ANOP .                        END OF PROCESSING                19300002
         MNOTE *,'THE ASSEMBLY OF THIS MACRO HAS BEEN SUCCESSFULLY'     19350002
         MNOTE *,'COMPLETED.  THE OUTPUT CONSISTS OF JCL TO WHICH'      19400002
         MNOTE *,'A START RDR COMMAND MAY BE ISSUED IN ORDER TO'        19450002
         MNOTE *,'INITIALIZE THE AMDSADMP PROGRAM.'                     19500002
         MEND                                                           19550002
