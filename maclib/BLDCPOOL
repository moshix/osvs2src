         MACRO                                                          00050002
&NAME    BLDCPOOL  &CPID=,&BRANCH=YES,&SP=,&CSIZE=,&POOLSIZ=,&CPADDR=, *00100002
               &RECOVER=,&RELATED=,&AUTODEL=NO,&CELLCNT=,&BNDRY=FWORD, *00110003
               &SERIAL=NO                                      @ZA28730 00130003
*     OS/VS2 RELEASE 3.0 VERSION 5/27/77                       @ZA28730 00140003
         LCLA  &REG,&GMFAIL                                             00150003
         LCLB  &R,&G,&C,&DBL,&AUTO,&SER                        @ZA28730 00160003
         LCLC  &NMST,&CSTRNG,&BSTRNG,&ASTRNG                            00252002
&NMST    SETC  'IHA'.'&SYSNDX'                                          00254002
&GMFAIL  SETA  4                   RETURN CODE FOR GETMAIN ERROR        00260002
         AIF   ('&BRANCH' NE 'YES').ERR1    BRANCH=YES MANDATORY        00300002
&DBL     SETB  ('&BNDRY' EQ 'DWORD')                                    00310002
         AIF   ('&BNDRY' EQ 'FWORD' OR '&BNDRY' EQ 'DWORD').BNDRYOK     00320002
         IHBERMAC &BNDRY,BNDRY,FWORD                                    00330002
.BNDRYOK AIF   ('&RECOVER' EQ '').CONT1                                 00350002
         AIF   ('&RECOVER' NE 'YES').ERR5   RECOVER MUST=YES, IF GIVEN  00400002
.*       RECOVER=YES SPECIFIED                                          00410002
         AIF   ('&CSIZE'.'&POOLSIZ'.'&CELLCNT'.'&SP' NE '').ERR9        00420002
.*       NO EXTRANEOUS PARAMETERS SPECIFIED FOR RECOVER=YES             00430002
&R       SETB  1              INDICATE RECOVERY TYPE                    00550002
         AIF   ('&CPID' EQ '').ERR6         CPID MUST BE GIVEN          00600002
         AIF   ('&CPID'(1,1) NE '(').ERR2   CPID MUST BE REGISTER       00610002
&C       SETB  1              INDICATE CPID PRESENT                     00620002
         AGO   .GEN1                                                    00650002
.*       RECOVER PARAMETER NOT SPECIFIED                                00652002
.CONT1   AIF   ('&CSIZE' EQ '').ERR7        CSIZE MUST BE GIVEN         00660002
         AIF   ('&POOLSIZ' NE '' AND '&CELLCNT' NE '').ERR11            00670002
.*       CELLCNT AND POOLSIZ ARE MUTUALLY EXCLUSIVE PARAMETERS          00680002
         AIF   ('&CPID' EQ '').CONT2        CPID OPTION PRESENT?        00710002
         AIF   ('&CPID'(1,1) NE '(').ERR2   CPID MUST BE REGISTER       00760002
&C       SETB  1              INDICATE CPID PRESENT                     00810002
.CONT2   AIF   ('&CPADDR' NE '').CONT3                                  00850002
&G       SETB  1              INDICATE GETMAIN TO BE ISSUED             00900002
&REG     SETA  4              INDICATE R4 USED INSTEAD OF R0 FOR CPID   00950002
         AGO   .GEN1                                                    01000002
.CONT3   AIF   ('&CPADDR'(1,1) NE '(').ERR2A CPADDR MUST BE REGISTER    01050002
.*       COMMON EXPANSION                                               01060002
.GEN1    ANOP                                                           01100002
&NAME    STM   2,12,28(13)         SAVE CALLERS REGISTERS 2-12          01150002
         AIF   (&C EQ 0).SKIP1          SKIP IF CPID NOT GIVEN          01200002
         AIF   (&G EQ 1).SET            SKIP IF GETMAIN TO BE ISSUED    01250002
         AIF   ('&CPID(1)' EQ '0').SKIP2                                01300002
.SET     LR    &REG,&CPID(1)       PRESERVE CPID VALUE AROUND GETMAIN   01400002
         AGO   .SKIP2                                                   01450002
.SKIP1   SR    &REG,&REG           INDICATE NO CPID (DYNAMIC POOL)      01550002
.SKIP2   AIF   (&R EQ 1).GENR           SKIP IF RECOVER OPTION GIVEN    01600002
         AIF   ('&SP' EQ '').ERR8       SUBPOOL MUST BE GIVEN           01610002
.*       FILTER OUT INVALID SUBPOOL SPECIFICATIONS                      01620002
         AIF   ('&SP'(1,1) EQ '(' OR T'&SP NE 'N').SPOK                 01630002
.*       NO CHECK CAN BE MADE IF SP# EQUATED OR IN A REGISTER           01640002
         AIF   (&SP GT 255).ERR3        SUBPOOL MUST BE LE 255          01650002
.*       VALID SUBPOOL PARAMETER GIVEN                                  01696002
.SPOK    AIF   ('&POOLSIZ' EQ '').PSKIP                                 01698002
&CSTRNG  SETC  'POOLSIZ'                                                01698402
&BSTRNG  SETC  '1024'                                                   01698502
&ASTRNG  SETC  ''                                                       01698602
         AIF   ('&POOLSIZ'(1,1) EQ '(' OR T'&POOLSIZ NE 'N').#SKIP      01698802
.*       LIMIT TEST CANNOT BE MADE IF EQUATED OR REGISTER               01698902
         AIF   (&POOLSIZ LT 1 OR &POOLSIZ GT 255).ERR4A                 01700002
         AGO   .#SKIP         SKIP OVER CELL COUNT CHECKS               01710002
.PSKIP   AIF   ('&CELLCNT' EQ '').ERR10      CELLCNT OR POOLSIZ REQ'D   01720002
&CSTRNG  SETC  'CELLCNT'                                                01730002
&BSTRNG  SETC  'CSIZE+4'                                                01740002
&ASTRNG  SETC  '32+'                                                    01742002
.#SKIP   AIF   ('&CSIZE'(1,1) EQ '(' OR T'&CSIZE NE 'N').CSKIP          01750002
.*       LIMIT TEST CANNOT BE MADE IF EQUATED OR REGISTER               01760002
         AIF   (&CSIZE LT 8 OR &CSIZE GT 4092).ERR4                     01800002
.CSKIP   AIF   ('&CELLCNT' EQ '').POOLS CELL COUNT SPECIFIED?           01850002
         AIF   ('&CSIZE'(1,1) NE '(').CNT#EQ CSIZE IN A REGISTER?       01910002
         LA    15,4(&CSIZE(1).,0)  CELL SIZE FOR MULTIPLICATION YM02296 01930002
         AGO   .MULTP                                                   01940002
.CNT#EQ  LA    15,&CSIZE.+4(0,0)   CELL SIZE FOR MULTIPLICATION YM02296 01942002
.MULTP   LA    14,3(0,0)           SET UP MASK TO ROUND CSIZE   YM02296 01942602
         NR    14,15               REDUCE CSIZE TO REMAINDER OF         01943202
*                                  DIVISION BY 3                        01943602
         IC    14,*+8(14)          SELECT PROPER ROUNDING FACTOR        01943702
         B     *+8                 BRANCH AROUND TABLE                  01943802
         DC    FL1'0,3,2,1'                                             01943902
         AR    15,14               ADD THE ROUNDING FACTOR TO CSIZE     01945602
         AIF   (NOT &DBL).NOADJB NO ADJUSTMENT CODE IF BNDRY=FWORD      01945802
         LA    14,4(0,0)           CELLSIZE IS NOW A MULTIPLE   YM02296 01946002
         NR    14,15               OF 4, IF IT IS ALSO A MULTIPLE       01946202
         BZ    &NMST.0             OF 8, THEN ADD 4 TO FORCE            01946402
         LA    15,4(15,0)          DOUBLE WORD CELL BOUNDARIES. YM02296 01946602
&NMST.0  DS    0H                                                       01946802
.NOADJB  SR    14,14               RESET REGISTER FOR MULTIPLY          01947602
         AIF   ('&CELLCNT'(1,1) EQ '(').CNTREG CELL COUNT IN REG?       01948102
         LA    2,&CELLCNT.(0,0)    CELL COUNT TO MULTIPLY       YM02296 01949802
         MR    14,2                COMPUTE REQUIRED POOL SIZE           01951502
         AGO   .ADDCPAB                                                 01953202
.CNTREG  MR    14,&CELLCNT(1)      COMPUTE REQUIRED POOL SIZE           01954902
.ADDCPAB LA    2,32(15,0)          ADD THE CPAB LENGTH          YM02296 01956602
.POOLS   AIF   ('&CPADDR(1)' EQ '1' OR &G EQ 1).SKIP3                   01958302
         AIF   ('&CPADDR(1)' EQ '0').ERR2A    ERROR, CPID=CPADDR=(0)    01960002
.*       ALL PARAMETERS VALID                                           01970002
         LR    1,&CPADDR(1)        LOAD R1 WITH CPADDR                  02000002
         MNOTE *,'IT IS ASSUMED THAT &ASTRNG.(&CSTRNG.*(&BSTRNG.)) BYTES02010002
               S ARE AVAILABLE STARTING AT CPADDR.'                     02020002
.*       COMMON TO ALL EXPANSIONS EXCEPT RECOVER=YES                    02050002
.SKIP3   AIF   ('&CELLCNT' NE '').SKIP3C                                02052002
         AIF   ('&POOLSIZ'(1,1) EQ '(').SKIP3A POOLSIZ = REGISTER?      02060002
         LA    2,&POOLSIZ.(0,0)    LOAD POOLSIZE IN 1K BLOCKS   YM02296 02100002
         AGO   .SKIP3B                                                  02110002
.SKIP3A  AIF   ('&POOLSIZ(1)' EQ '2').SKIP3B STORE DIRECT, POOLSIZ=(2)  02120002
         LR    2,&POOLSIZ(1)       LOAD POOL SIZE IN 1K BLOCKS          02130002
.SKIP3B  SLL   2,10(0)             TIMES 1024 FOR SIZE IN BYTES YM02296 02150002
.SKIP3C  AIF   (&G EQ 0).GENC                                           02200002
.*       COMMON TO ALL EXPANSIONS EXCEPT RECOVER=YES AND CPADDR=(R)     02210002
         GETMAIN RC,LV=(2),SP=&SP  GETMAIN THE POOL SEGMENT             02260002
         LTR   15,15               TEST GETMAINS RETURN CODE IN R15     02500002
         BZ    &NMST.A             GETMAIN WORKED, SKIP ERROR SEQUENCE  02670002
         LR    0,15                SAVE GETMAIN'S CODE AS REASON CODE   02672002
         LA    15,&GMFAIL.(0,0)    SET UP GETMAIN FAILURE CODE  YM02296 02674002
         B     &NMST.B             RETURN TO USER                       02676002
&NMST.A  LR    0,4                 GETMAIN OK, RESTORE CPID TO REG 0    02680002
.*       COMMON TO ALL EXPANSIONS EXCEPT RECOVER=YES                    02702802
.GENC    XC    0(32,1),0(1)        INITIALIZE CPAB(E)                   02705002
         ST    0,0(0,1)            SAVE CPID IN CPAB(E)                 02710002
         ST    2,24(0,1)           SAVE POOL SIZE IN CPAB(E)            02715002
         AIF   ('&AUTODEL' EQ 'NO').CKSER                      @ZA28730 02717003
&AUTO    SETB  1         AUTO DELETE SPECIFIED                 @ZA28730 02718003
         AIF   ('&AUTODEL' EQ 'YES').CKSER                     @ZA28730 02719003
&AUTO    SETB  0         AUTODEL PARM INVALID--DEFAULT TO NO   @ZA28730 02720003
         IHBERMAC 1015,&AUTODEL,AUTODEL,NO                              02721003
.CKSER   AIF   ('&SERIAL' EQ 'NO').CKBNDRY                     @ZA28730 02722003
         AIF   ('&SERIAL' EQ 'YES').SERON                      @ZA28730 02723003
         IHBERMAC 1015,&SERIAL,SERIAL,NO                       @ZA28730 02724003
         AGO   .CKBNDRY           SERIAL=NO                             02725003
.SERON   ANOP                                                  @ZA28730 02726003
&SER     SETB  1         SERIALIZATION SPECIFIED               @ZA28730 02727003
         AIF   (&AUTO).CKBNDRY    IF SERIAL, MUST HAVE AUTODEL @ZA28730 02728003
&SER     SETB  0         SERIAL PARM INVALID--DEFAULT TO NO    @ZA28730 02729003
         IHBERMAC 1016,''SERIAL=YES'',''AUTODEL=NO''           @ZA28730 02730003
.CKBNDRY AIF   (&SER+&AUTO+&DBL EQ 0).NOAUTO2     ANY OPTIONS? @ZA28730 02731003
         OI    17(1),B'0&DBL&AUTO.00&SER.00' SET OPTION FLAGS  @ZA28730 02732003
.NOAUTO2 AIF   ('&SP'(1,1) EQ '(').SPREG IS SP A REGISTER?              02745002
         MVI   16(1),&SP           SAVE SUBPOOL ID IN CPAB(E)           02750002
         AGO   .GENC1             BACK TO MAINLINE                      02752002
.SPREG   STC   &SP(1),16(0,1)      SAVE SUBPOOL ID IN CPAB(E)           02754002
.GENC1   AIF   ('&CSIZE'(1,1) EQ '(').GENM IS CSIZE A REGISTER?         02756002
         LA    2,&CSIZE.(0,0)      LOAD CELL SIZE               YM02296 02820002
         ST    2,4(0,1)            SAVE CELL SIZE IN CPAB(E)            02850002
         AGO   .GENX              RETURN TO MAINLINE                    02860002
.GENM    ST    &CSIZE(1),4(0,1)    SAVE CELL SIZE IN CPAB(E)            02870002
.*       COMMON TO ALL EXPANSIONS                                       02900002
.GENX    L     3,16(0,0)           CVT ADDRESS FOR BLDCPOOL     YM02296 02950002
         L     15,CVTBLDCP-CVTMAP(0,3)    LOAD BLDCPOOL BRANCH ENTRY    03000002
         BALR  14,15               GO TO BLDCPOOL SERVICE               03050002
         AIF   (&G EQ 0).SKIP4                                          03100002
&NMST.B  DS    0H                  BRANCH HERE ON A GETMAIN FAILURE     03150002
.SKIP4   LM    2,12,28(13)         RESTORE CALLERS REGISTERS            03250002
         MEXIT                                                          03300002
.*       RECOVER=YES REQUEST SPECIAL PROCESSING                         03350002
.GENR    SR    1,1                 INDICATE RECOVERY REQUEST            03400002
         AGO   .GENX                                                    03450002
.ERR1    IHBERMAC 1006,''BRANCH=YES''                                   03500002
         MEXIT                                                          03550002
.ERR2    IHBERMAC 1001,CPID,&CPID                                       03600002
         MEXIT                                                          03650002
.ERR2A   IHBERMAC 1001,CPADDR,&CPADDR                                   03700002
         MEXIT                                                          03750002
.ERR3    IHBERMAC 1001,SP,&SP                                           03800002
         MEXIT                                                          03900002
.ERR4    IHBERMAC 1001,CSIZE,&CSIZE                                     03950002
         MEXIT                                                          04000002
.ERR4A   IHBERMAC 1001,POOLSIZ,&POOLSIZ                                 04060002
         MEXIT                                                          04070002
.ERR5    IHBERMAC 1001,RECOVER,&RECOVER                                 04100002
         MEXIT                                                          04110002
.ERR6    IHBERMAC 1006,CPID                                             04120002
         MEXIT                                                          04122002
.ERR7    IHBERMAC 1006,CSIZE                                            04130002
         MEXIT                                                          04140002
.ERR8    IHBERMAC 1006,SP                                               04142002
         MEXIT                                                          04144002
.ERR9    AIF   ('&POOLSIZ' EQ '').ERR9A                                 04146002
         IHBERMAC 1020,POOLSIZ,''RECOVER=YES''                          04148002
.ERR9A   AIF   ('&SP' EQ '').ERR9B                                      04198002
         IHBERMAC 1020,SP,''RECOVER=YES''                               04200002
.ERR9B   AIF   ('&CSIZE' EQ '').ERR9C                                   04208002
         IHBERMAC 1020,CSIZE,''RECOVER=YES''                            04210002
.ERR9C   AIF   ('&CELLCNT' EQ '').ERR9D                                 04212002
         IHBERMAC 1020,CELLCNT,''RECOVER=YES''                          04214002
.ERR9D   MEXIT                                                          04218002
.ERR10   IHBERMAC 1006,POOLSIZ                                          04298002
         MEXIT                                                          04308002
.ERR11   IHBERMAC 1020,CELLCNT,POOLSIZ                                  04318002
         MEND                                                           04348002
