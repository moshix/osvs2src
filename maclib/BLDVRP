         MACRO                                                          00050000
&NAME    BLDVRP &TYPE=,&FIX=,&KEYLEN=,&STRNO=,&BUFFERS=,&MF=I           00100000
         LCLA  &ORCODE                  CODES TO BE TURNED ON IN LIST   00150000
         LCLA  &ANDCODE                 CODES TO BE TURNED OFF IN LIST  00200000
         LCLA  &I,&J,&K,&L              WORK VARIABLES                  00250000
         LCLB  &MFE                     EXECUTE FORM INDICATOR          00300000
         LCLB  &ERR                     ERROR INDICATOR                 00350000
         LCLB  &FIOB                    FIX IO RELATED BLOCKS IND       00400000
         LCLB  &FBFR                    FIX BUFFERS INDICATOR           00450000
         LCLB  &BFR                     BUFFER SPECIFIED INDICATOR      00460000
         LCLC  &BUFRSZ(50)              BUFFER SIZE TABLE               00500000
         LCLC  &BUFRCT(50)              BUFFER COUNT TABLE              00550000
         LCLC  &EREG                    EXECUTE FORM LIST ADDR REG      00600000
&ORCODE  SETA  128+64                   INDICATE DB/DC AND BLDVRP       00650000
&ANDCODE SETA  255-32                   RESET DLVRP                     00700000
         AIF   (N'&SYSLIST EQ 0).NOPOS  GO IF NO POSITIONAL OPERANDS    00750000
.POSLOOP ANOP                                                           00760000
&I       SETA  &I+1                     POINT TO NEXT POS OPERAND       00770000
         IDAERMAC 1,&SYSLIST(&I)        ISSUE WARNING MNOTE             00800000
         AIF   (&I LT N'&SYSLIST).POSLOOP  GO IF MORE POS OPERANDS      00810000
.NOPOS   ANOP                                                           00850000
         AIF   ('&MF' EQ 'I').MFB       GO IF STANDARD FORM             00900000
         AIF   ('&MF' EQ 'L').MFB       GO IF LIST FORM                 00950000
         AIF   (N'&MF LT 2).MFERR       GO IF NOT ENOUGH PARMS          01000000
         AIF   ('&MF(1)' NE 'E').MFERR  GO IF NOT EXECUTE FORM          01050000
&MFE     SETB  1                        INDICATE EXECUTE FORM           01100000
         AGO   .MFB                     GO AROUND ERROR                 01150000
.MFERR   ANOP                                                           01200000
         IDAERMAC 3,MF,&MF              ISSUE ERROR MNOTE               01250000
&ERR     SETB  1                        INDICATE ERROR                  01300000
.MFB     ANOP                                                           01350000
         AIF   ('&TYPE' NE '').TOKY     GO IF TYPE IS SPECIFIED         01400000
         AIF   (&MFE).TYPEB             GO IF EXECUTE FORM              01450000
         AGO   .TLSR                    GO TAKE DEFAULT                 01500000
.TOKY    ANOP                                                           01550000
         AIF   ('&TYPE' NE 'LSR').TLSRB  GO IF NOT LOCALLY SHARED       01600000
.TLSR    ANOP                                                           01650000
&ORCODE  SETA  &ORCODE+16               INDICATE LOCALLY SHARED         01700000
&ANDCODE SETA  &ANDCODE-8               RESET GLOBALLY SHARED           01750000
         AGO   .TYPEB                   GO AROUND TYPE ANALYSIS         01800000
.TLSRB   ANOP                                                           01850000
         AIF   ('&TYPE' NE 'GSR').TGSRB  GO IF NOT GLOBALLY SHARED      01900000
&ORCODE  SETA  &ORCODE+8                INDICATE GLOBALLY SHARED        01950000
&ANDCODE SETA  &ANDCODE-16              RESET LOCALLY SHARED            02000000
         AGO   .TYPEB                   GO AROUND TYPE ANALYSIS         02050000
.TGSRB   ANOP                                                           02100000
         IDAERMAC 3,TYPE,&TYPE          ISSUE ERROR MNOTE               02150000
&ERR     SETB  1                        INDICATE ERROR                  02200000
.TYPEB   ANOP                                                           02250000
         AIF   ('&FIX' EQ '').FIXB      GO IF FIX NOT SPECIFIED         02300000
&ANDCODE SETA  &ANDCODE-6               RESET FIX IO BLOCKS AND BUFFERS 02350000
&I       SETA  0                        INIT FIX SUBLIST SUBSCRIPT      02400000
.FIXL    ANOP                                                           02450000
&I       SETA  &I+1                     POINT TO NEXT ENTRY IN SUBLIST  02500000
         AIF   (&I GT N'&FIX).FIXB      GO IF NO MORE ENTRIES           02550000
         AIF   ('&FIX(&I)' EQ '').FIXL  GO IF NULL ENTRY                02600000
         AIF   ('&FIX(&I)' NE 'IOB').FIOBB  GO IF NOT IO BLOCKS         02650000
         AIF   (&FIOB).FERR             GO IF IO BLOCKS ALREADY SPECD   02700000
&FIOB    SETB  1                        INDICATE FIX                    02750000
&ORCODE  SETA  &ORCODE+4                ..I/O BLOCKS SPECIFIED          02800000
         AGO   .FIXL                    GO CHECK NEXT ENTRY             02850000
.FIOBB   ANOP                                                           02900000
         AIF   ('&FIX(&I)' NE 'BFR').FBFRB  GO IF NOT BUFFERS           02950000
         AIF   (NOT &FBFR).FERRB        GO IF BUFFERS NOT ALREADY SPECD 03000000
.FERR    ANOP                                                           03050000
         IDAERMAC 8,FIX,&FIX(&I),&FIX(&I)  ISSUE ERROR MNOTE            03100000
&ERR     SETB  1                        INDICATE ERROR                  03150000
         AGO   .FIXL                    GO CHECK NEXT ENTRY             03200000
.FERRB   ANOP                                                           03250000
&FBFR    SETB  1                        INDICATE FIX                    03300000
&ORCODE  SETA  &ORCODE+2                ..BUFFERS SPECIFIED             03350000
         AGO   .FIXL                    GO CHECK NEXT ENTRY             03400000
.FBFRB   ANOP                                                           03450000
         IDAERMAC 3,FIX,&FIX(&I)        ISSUE ERROR MNOTE               03500000
&ERR     SETB  1                        INDICATE ERROR                  03550000
         AGO   .FIXL                    GO CHECK NEXT ENTRY             03600000
.FIXB    ANOP                                                           03650000
         AIF   ('&MF' NE 'I').STRNOB    GO IF NOT STANDARD FORM         03660000
         AIF   ('&STRNO' NE '').STRNOB  GO IF STRNO SPECIFIED           03670000
         IDAERMAC 2,STRNO               ISSUE ERROR MNOTE               03680000
&ERR     SETB  1                        INDICATE ERROR                  03690000
.STRNOB  ANOP                                                           03692000
         AIF   (&MFE).BUFRO             GO IF EXECUTE FORM              03700000
&J       SETA  0                        INIT BUFFER SIZE/CNT SUBSCRIPT  03750000
         AIF   ('&BUFFERS' NE '').BOKY  GO IF BUFFERS SPECIFIED         03800000
         IDAERMAC 2,BUFFERS             ISSUE ERROR MNOTE               03850000
&ERR     SETB  1                        INDICATE ERROR                  03900000
         AGO   .BUFRB1                  GO AROUND BUFFERS ANALYSIS      03950000
.BOKY    ANOP                                                           04000000
&I       SETA  0                        INIT BUFFERS SUBLIST SUBSCRIPT  04050000
.BUFRL10 ANOP                                                           04100000
&J       SETA  &J+1                     POINT TO NEXT BUFFER SIZE/COUNT 04150000
.BUFRL11 ANOP                                                           04200000
&I       SETA  &I+1                     POINT TO NEXT ENTRY IN SUBLIST  04250000
         AIF   (&I GT N'&BUFFERS).BUFRB0  GO IF NO MORE ENTRIES         04300000
         AIF   ('&BUFFERS(&I)' EQ '').BUFRL11  GO IF NULL ENTRY         04350000
&BFR     SETB  1                        INDICATE BUFFER SPECIFIED       04360000
         AIF   ('&BUFFERS(&I)'(1,1) EQ '(').BUFRER  GO IF 1ST CHAR IS ( 04400000
&K       SETA  1                        INIT CHARACTER POINTER          04450000
.BUFRSL  ANOP                                                           04500000
&K       SETA  &K+1                     POINT TO NEXT CHARACTER         04550000
         AIF   (&K GT K'&BUFFERS(&I)).BUFRER  GO IF NO MORE CHARACTERS  04600000
         AIF   ('&BUFFERS(&I)'(&K,1) NE '(').BUFRSL  GO IF NOT TO ( YET 04650000
&BUFRSZ(&J) SETC '&BUFFERS(&I)'(1,&K-1)  NUMBER IN FRONT OF ( IS SIZE   04700000
&L       SETA  &K+1                     SAVE POINTER TO CHAR AFTER (    04750000
         AIF   ('&BUFFERS(&I)'(&L,1) EQ ')').BUFRER  GO IF NO SIZE '()' 04800000
&K       SETA  &L                       POINT TO CHARACTER AFTER (      04850000
.BUFRCL  ANOP                                                           04900000
&K       SETA  &K+1                     POINT TO NEXT CHARACTER         04950000
         AIF   (&K GT K'&BUFFERS(&I)).BUFRER  GO IF NO MORE CHARACTERS  05000000
         AIF   ('&BUFFERS(&I)'(&K,1) NE ')').BUFRCL  GO IF NOT TO ) YET 05050000
&BUFRCT(&J) SETC '&BUFFERS(&I)'(&L,&K-&L)  NUMBER BETWEEN () IS COUNT   05100000
         AIF   ('&BUFRCT(&J)' LT '3').BUFRER  COUNT MUST BE AT LEAST 3  05110000
         AIF   ('&BUFRCT(&J)' GT '32767').BUFRER  ..AND LESS THAN 32K   05120000
&K       SETA  512                      START BUFFER SIZE AT 512        05130000
.BUFRL12 ANOP                                                           05140000
         AIF   ('&BUFRSZ(&J)' EQ '&K').BUFRL10  GO IF VALID BUFFER SIZE 05142000
         AIF   (&K GT 2048).BFRA4K      IF > 2K MUST BE MULTIPLE OF 4K  05144000
&K       SETA  &K*2                     ..OTHERWISE MUST BE POWER OF 2  05146000
         AGO   .BUFRL12                 GO TEST BUFFER SIZE             05150000
.BFRA4K  ANOP                                                           05170000
         AIF   (&K GE 32768).BUFRER     GO IF INVALID BUFFER SIZE       05180000
&K       SETA  &K+4096                  NEXT BUFFER SIZE VALUE          05190000
         AGO   .BUFRL12                 GO TEST BUFFER SIZE             05192000
.BUFRER  ANOP                                                           05200000
         IDAERMAC 3,BUFFERS,&BUFFERS(&I)  ISSUE ERROR MNOTE             05250000
&ERR     SETB  1                        INDICATE ERROR                  05300000
         AGO   .BUFRL11                 GO TO NEXT ENTRY                05350000
.BUFRB0  ANOP                                                           05360000
         AIF   (&BFR).BUFRB1            GO IF BUFFER SPECIFIED          05370000
         IDAERMAC 3,BUFFERS,&BUFFERS    ISSUE ERROR MNOTE               05380000
&ERR     SETB  1                        INDICATE ERROR                  05390000
         AGO   .BUFRB1                  GO AROUND BUFFERS ANALYSIS      05392000
.BUFRO   ANOP                                                           05400000
         AIF   ('&BUFFERS' EQ '').BUFRB1  GO IF BUFFERS NOT SPECIFIED   05450000
         IDAERMAC 4,BUFFERS             ISSUE WARNING MNOTE             05500000
.BUFRB1  ANOP                                                           05600000
         AIF   (&ERR).END               GO IF ANY ERRORS                05650000
         AIF   (&MFE).EXEC              GO IF EXECUTE FORM              05700000
         AIF   ('&MF' EQ 'L').LIST      GO IF LIST FORM                 05750000
         AIF   ('&NAME' EQ '').NAMEB1   GO IF NO NAME                   05760000
&NAME    DS    0H                                                       05800000
.NAMEB1  ANOP                                                           05810000
         CNOP  0,4                      ALIGN ON WORD BOUNDARY          05850000
&I       SETA  ((&J-1)*8)+60            CALC DISP TO SVC                05900000
         BAL   1,*+&I                   PLACE LIST ADDRESS IN 1         05950000
         AGO   .GENACB                                                  06000000
.LIST    ANOP                                                           06050000
&NAME    DS    0F                       ALIGN ON WORD BOUNDARY          06100000
.GENACB  ANOP                                                           06150000
         DC    X'80'                    INDICATE END OF OPEN LIST       06160000
         DC    AL3(*+3)                 ACB ADDRESS                     06170000
         DC    X'A011'                  ID AND SUBTYPE                  06200000
         DC    AL2(0)                   RESERVED                        06250000
         DC    A(*+48)                  BUFFER LIST ADDRESS             06300000
         AIF   ('&KEYLEN' NE '').CHKKEYL  GO IF KEY LENGTH SPECIFIED    06350000
         DC    AL1(255)                 KEY LENGTH                      06400000
         AGO   .AKEYLEN                 GO AROUND KEY LENGTH            06450000
.CHKKEYL AIF   ('&KEYLEN' LT '256').GENKEYL GO IF KEY LENGTH NOT TO BIG 06460000
         IDAERMAC 3,KEYLEN,&KEYLEN      ISSUE ERROR MESSAGE             06470000
         AGO   .AKEYLEN                 GO AROUND KEY LENGTH            06480000
.GENKEYL ANOP                                                           06500000
         AIF   ('&KEYLEN' GT '255').KEYLNER  GO IF KEYLEN GT 255        06510000
         DC    AL1(&KEYLEN)             KEY LENGTH                      06550000
         AGO   .AKEYLEN                 GO AROUND ERROR MESSAGE         06560000
.KEYLNER IDAERMAC 3,KEYLEN,&KEYLEN      ISSUE ERROR MNOTE               06570000
.AKEYLEN ANOP                                                           06600000
         AIF   ('&STRNO' NE '').GENSTRN  GO IF STRING NUMBER SPECIFIED  06650000
         DC    AL1(0)                   STRING NUMBER                   06700000
         AGO   .ASTRNO                  GO AROUND STRING NUMBER         06750000
.GENSTRN ANOP                                                           06800000
         DC    AL1(&STRNO)              STRING NUMBER                   06850000
.ASTRNO  ANOP                                                           06900000
         DC    AL1(&ORCODE)             VRP CODES                       06950000
         DC    AL1(0)                   RESERVED                        07000000
         DC    3A(0)                    RESERVED                        07050000
         DC    XL3'0'                   RESERVED                        07100000
         DC    X'08'                    DSORG                           07150000
         DC    5A(0)                    RESERVED                        07200000
         DC    X'02'                    OFLAGS                          07250000
         DC    XL3'0'                   RESERVED                        07300000
&I       SETA  0                        INIT BUFFER SIZE/COUNT POINTER  07350000
&K       SETA  &J-1                     POINT TO LAST BUFFER SIZE/COUNT 07400000
.BUFRL2  ANOP                                                           07450000
&I       SETA  &I+1                     POINT TO NEXT BUFFER SIZE/COUNT 07500000
         AIF   (&I GE &J).BUFRB2        GO IF NO MORE ENTRIES           07550000
         DC    A(&BUFRSZ(&I))           BUFFER SIZE                     07600000
         AIF   (&I EQ &K).GENEND        GO IF LAST BUFFER SIZE/COUNT    07650000
         DC    AL2(0)                   RESERVED                        07700000
         AGO   .AREND                   GO AROUND GEN OF END OF LIST    07750000
.GENEND  ANOP                                                           07800000
         DC    X'80'                    INDICATE LAST LIST ENTRY        07850000
         DC    AL1(0)                   RESERVED                        07900000
.AREND   ANOP                                                           07950000
         DC    AL2(&BUFRCT(&I))         BUFFER COUNT                    08000000
         AGO   .BUFRL2                  GO GEN NEXT ENTRY               08050000
.BUFRB2  ANOP                                                           08100000
         AIF   ('&MF' NE 'L').ISUSVC    GO IF NOT LIST FORM             08150000
         MEXIT                                                          08200000
.EXEC    ANOP                                                           08250000
         AIF   ('&NAME' EQ '').NAMEB2   GO IF NO NAME                   08260000
&NAME    DS    0H                                                       08300000
.NAMEB2  ANOP                                                           08310000
         AIF   ('&MF(2)'(1,1) EQ '(').EREG  GO IF LIST ADDRESS IN REG   08350000
         LA    1,&MF(2)                 LOAD LIST ADDRESS               08400000
         AGO   .AEREG                   GO AROUND REG CODE              08450000
.EREG    ANOP                                                           08500000
         AIF   ('&MF(2)' EQ '(1)').AEREG  GO IF LIST ADDR ALREADY IN 1  08550000
&I       SETA  K'&MF(2)-2               CALC LENGTH OF REG NAME         08600000
&EREG    SETC  '&MF(2)'(2,&I)           PICK UP REG NAME                08650000
         LR    1,&EREG                  LOAD LIST ADDRESS               08700000
.AEREG   ANOP                                                           08750000
         AIF   ('&KEYLEN' EQ '').AEKEYL  GO IF NO KEY LENGTH            08800000
         MVI   12(1),&KEYLEN            ALTER KEY LENGTH                08850000
.AEKEYL  ANOP                                                           08900000
         AIF   ('&STRNO' EQ '').AESTRN  GO IF NO STRING NUMBER          08950000
         MVI   13(1),&STRNO             ALTER STRING NUMBER             09000000
.AESTRN  ANOP                                                           09050000
         NI    14(1),&ANDCODE           REMOVE OLD VRP CODES            09100000
         OI    14(1),&ORCODE            INSERT NEW VRP CODES            09150000
.ISUSVC  ANOP                                                           09200000
         SVC   19                       ISSUE OPEN SVC                  09250000
.END     ANOP                                                           09300000
         MEND                                                           09350000
