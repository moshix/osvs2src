         MACRO                                                          00050002
&NAME    CALLDISP  &BRANCH=NO                                           00062040
.*************************************************************          00065040
.*       VALIDITY CHECK PARAMETER                                       00068040
.*************************************************************          00071040
         AIF ('&BRANCH' EQ 'YES' OR                                    X00074040
               '&BRANCH' EQ 'NO').OK                                    00077040
         LCLC  &OPERAND                                                 00077740
&OPERAND SETC  'BRANCH='                                                00078440
         IHBERMAC  1014,&OPERAND                                        00080040
         MEXIT                                                          00081040
.*************************************************************          00083040
.*       GENERATE BAL CODE                                              00086040
.*       OZ30340 - MAKE THE RBOPSW ENABLED FOR I/O & EXTERNAL  @ZA30340 00087003
.*       OZ51780 - SAVE PROGRAM MASK WHEN BRANCH=YES                    00087603
.*                 IS SPECIFIED ON CALLDISP MACRO              @ZA51780 00088203
.*************************************************************          00089040
.OK      ANOP                                                           00090040
         AIF ('&BRANCH' EQ 'YES').YES                                   00092040
&NAME    LA    15,8(0,0)           . CALLDISP ESR CODE                  00150002
         SVC   116                 . EXTEND. SVC ROUTER - TYPE 1        00250002
         MEXIT                                                          00300040
.YES     ANOP                                                           00350040
         LCLC  &LAB                                                     00360040
&LAB     SETC  '@PSW&SYSNDX'       UNIQUE  LABEL                        00370040
&NAME    DS    0H                  BRANCH ENTER DISPATCHER              00400040
         STNSM PSAPSWSV-FLC(0),X'FC'     DISABLE                        00450040
         ST    15,PSAPSWSV+4-FLC(0,0)    SAVE REG 15                    00480040
         L     15,PSATOLD-FLC(0,0)    GET CURRENT TCB                   00510040
         STM   0,14,TCBGRS-TCB(15)      SAVE 0 THRU 14 IN TCB           00650040
         L     14,PSAPSWSV+4-FLC(0,0)    SAVE 15                        00670040
         ST    14,TCBGRS15-TCB(0,15)       IN TCB                       00730040
         L     15,TCBRBP-TCB(0,15)    GET ADDRESS OF RB                 00770040
         MVC   RBOPSW-RBSECT(8,15),&LAB     MOVE MODEL PSW              00900040
         OC    RBOPSW-RBSECT(1,15),PSAPSWSV-FLC(0) SYSTEM MASK @ZA30340 00940003
         BALR  14,0                GET PROGRAM MASK IN R14 AND @ZA51780 00960003
         STCM  14,8,18(15)         STORE IT INTO RBOPSW        @ZA51780 00980003
         NI    18(15),X'0F'        CLEAN UP EXTRA BITS         @ZA51780 01000003
         L     15,FLCCVT-FLC(0,0)  GET CVT ADDRESS                      01020040
         L     14,CVT0DS-CVT(0,15) GET DISPATCHER E.P.                  01150040
         BR    14                  EXIT TO DISPATCHER                   01200040
&LAB     DS    0F                  MODEL PSW                            01300040
         DC    X'070C0000'         FIRST HALF - MAKE IT ENABLED@ZA30340 01350003
         DC    A(*+4)              RETURN ADDRESS                       01400003
         MEND                                                           01450003
