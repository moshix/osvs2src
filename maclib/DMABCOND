         MACRO                                                          00100021
&NAME    DMABCOND &CODE,&MODID,&SVC=,&DCB=,&RETURN=,&RETCODE=,         X00200021
               &RES=YES,&REGSAVE=                                       00300021
.*                                                                      00400021
.*  THIS MACRO IS CALLED ONLY BY OPEN/CLOSE/EOV TRANSIENT SVC MODULES   00500021
.*  AND A FEW PROBLEM PROGRAM STATE ACCESS METHOD MODULES.              00600021
.*  IT IS USED IN PLACE OF AN ABEND MACRO CALL IN ORDER TO XCTL TO AN   00700021
.*  OPEN/CLOSE/EOV PROBLEM DETERMINATION MODULE, WHICH WILL ISSUE       00800021
.*  A WRITE-TO-PROGRAMMER AND MAY POSSIBLY RECOVER FROM THE ERROR       00900021
.*  OR ELSE THEN ISSUE THE ABEND.                                       01000021
.*                                                                      01100021
         AIF   ('&CODE' EQ '').E1       IF ERROR CODE MISSING           01200021
         AIF   ('&CODE' EQ 'PCK').DMA1000 INTERFACE ERROR        Y02080 01250002
         AIF   ('&DCB' EQ '').GEN3      IF NO DCB ADDR                  01300021
         AIF   ('&DCB'(1,1) EQ '(').GEN4  IF DCB ADDR IN REGISTER       01400021
.GEN3    ANOP                                                           01500021
&NAME    IHBINNRA &DCB,&CODE            LOAD REGS 1 AND 0               01600021
         AGO   .GEN0                                                    01700021
.GEN4    ANOP                           FORCE LA INSTEAD OF LR 1,DCB    01800021
&NAME    IHBINNRA 0&DCB,&CODE           LOAD REGS 1 AND 0               01900021
.GEN0    AIF   ('&SVC' EQ 'YES').GENSVC                                 02000021
         MVC   DXCALLID,6(RWTG)         SAVE ID OF CALLING MODULE       02100021
         AIF   ('&RETURN' EQ '').CHKWTG                                 02150021
         AIF   ('&RETURN' EQ 'NONE').CHKWTG                             02160021
         AIF   ('&RETURN'(1,1) EQ '(').REGRET                           02170021
         MVC   DXRETMOD,&RETURN         SAVE ID & TTR OF RETURN MODULE  02180021
         AGO   .CHKWTG                                                  02190021
.REGRET  MVC   DXRETMOD,0&RETURN        SAVE ID & TTR OF RETURN MODULE  02192021
.*                                                                      02194021
.CHKWTG  AIF   ('&MODID' EQ '').GEN2                                    02200021
         AIF   ('&MODID(1)' EQ '(').GEN1                                02300021
         MVC   0(5,RWTGC),&MODID        MOVE ID & TTR INTO WTG TABLE    02400021
         AGO   .GEN2                                                    02500021
.GEN1    MVC   0(5,RWTGC),0&MODID       MOVE ID & TTR INTO WTG TABLE    02600021
.*                                                                      02700021
.GEN2    AIF   ('&RES' EQ 'NO').SKPRES                                  03500021
         IECRES LOAD,BRCODE=&RETCODE,BRANCH=QUEUED               Y02080 03650002
.*                                BR TO RESIDENT ROUTINE TO XCTL Y02080 03660002
         MEXIT                                                          03700021
.SKPRES  AIF   ('&REGSAVE' NE 'YES').MEXIT                              03800021
         STM   9,14,DXREG9              SAVE REGISTERS 9-14             03900021
.MEXIT   MEXIT                                                          04000021
.*                                                                      04100021
.GENSVC  LNR   1,1                      INDICATE ABEND CONDITION        04200021
         SVC   55                       ISSUE EOV SVC                   04300021
         MEXIT                                                          04400021
.*                                                               Y02080 04450002
.DMA1000 ANOP                                                    YM2869 04460002
&NAME    DC    XL1'00',C'&SYSECT &MODID ERROR'         ABEND 0C1        04470002
         DS    0H                                                       04480002
         MEXIT                                                   Y02080 04490002
.*                                                                      04500021
.E1      MNOTE 8,'NO CODE SPECIFIED'                                    04600021
         MEND                                                           04700021
