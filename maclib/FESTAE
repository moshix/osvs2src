         MACRO                                                          00250040
&NAME    FESTAE &CNCL,&WRKREG=,&EXITADR=,&RBADDR=,&PARAM=,&TCBADDR=,   X00300040
               &PURGE=,&ASYNCH=,&TERM=,&RECORD=,&XCTL=,&ERRET=          00350040
*   MACDATE 75293                                                       00370040
*   MACCOMP SC1CM/PZD81                                                 00410040
*   MACSTAT 75293/Z40FPXE                                               00420040
         LCLA  &FLG1                                                    00450040
         LCLA  &FLG2                                                    00500040
         LCLA  &FLG3                                                    00550040
         LCLB  &ESTA                                                    00600040
         LCLB  &ASY                                                     00650040
         LCLB  &PNONE                                                   00700040
         LCLB  &HALT                                                    00750040
         LCLB  &XCTLB                                                   00800040
         LCLB  &KEY0                                                    00850040
         LCLB  &SUPER                                                   00900040
         LCLB  &TERMB                                                   00950040
         LCLB  &REC                                                     01000040
         LCLB  &BRNTR                                                   01050040
         LCLB  &ERRBIT                                                  01100040
         LCLC  &L1                                                      01150040
&L1      SETC  '&SYSNDX'                                                01200040
         AIF   ('&CNCL' NE '').CANCEL                                   01250040
&ESTA    SETB  1                                                        01300040
&ASY     SETB  ('&ASYNCH' EQ 'YES' OR '&ASYNCH' EQ '')                  01350040
&PNONE   SETB  ('&PURGE' EQ 'NONE' OR '&PURGE' EQ '')                   01400040
&HALT    SETB  ('&PURGE' EQ 'HALT')                                     01450040
&XCTLB   SETB  ('&XCTL' EQ 'YES')                                       01500040
&KEY0    SETB  1                                                        01550040
&SUPER   SETB  1                                                        01600040
&TERMB   SETB  ('&TERM' EQ 'YES')                                       01650040
&REC     SETB  ('&RECORD' EQ 'YES')                                     01700040
&BRNTR   SETB  1                                                        01750040
         AIF   ('&XCTL' NE 'YES' AND '&XCTL' NE 'NO' AND '&XCTL' NE '')X01800040
               .XCTLERR                                                 01850040
.PURCK   AIF   ('&PURGE' NE 'QUIESCE' AND '&PURGE' NE 'NONE' AND       X01900040
               '&PURGE' NE 'HALT' AND '&PURGE' NE '').PURERR            01950040
.ASYCK   AIF   ('&ASYNCH' NE 'YES' AND '&ASYNCH' NE 'NO' AND '&ASYNCH' X02000040
               NE '').ASYERR                                            02050040
.TERCK   AIF   ('&TERM' NE 'YES' AND '&TERM' NE 'NO' AND '&TERM' NE '')X02100040
               .TERERR                                                  02150040
.RECCK   AIF   ('&RECORD' NE 'YES' AND '&RECORD' NE 'NO' AND '&RECORD' X02200040
               NE '').RECERR                                            02250040
.CKERR   AIF   ('&ERRBIT' EQ '0').NOERR                                 02300040
         MEXIT                                                          02350040
.NOERR   ANOP                                                           02400040
&FLG1    SETA  16*&ESTA+4*&ASY+2*&PNONE+&HALT                           02450040
&FLG2    SETA  64*&XCTLB+2*&KEY0+&SUPER                                 02500040
&FLG3    SETA  64*&TERMB+32*&REC+4*&BRNTR                               02550040
         AIF   ('&EXITADR' EQ '').CANERR                                02600040
         AIF   ('&EXITADR'(1,1) NE '(').EXNREG                          02650040
         AIF   ('&RBADDR' EQ '').NRBA                                   02700040
         AIF   ('&RBADDR'(1,1) NE '(').RBNREG                           02750040
&NAME    L     15,RBSEXIT-RBSECT+RBPRFXLN(0,&RBADDR(1)) OBTAIN CURRENT  02800040
*                                            RB SCBEXIT VALUE           02830040
         LTR   15,15                         IF ZERO - SCB AVAILABLE    02900040
         BZ    IHB1&L1                       CONTINUE PROCESSING        02950040
         LA    15,8(0,0)                     INDICATE PREVIOUS EXIT     03000040
*                                            IN THIS RB                 03050040
         AIF   ('&ERRET' EQ '').NERRET                                  03060040
         B     &ERRET                                                   03070040
         AGO   .NERRET1                                                 03080040
.NERRET  ANOP                                                           03090040
         B     IHB2&L1                       CONTINUE USERS EXECUTION   03100040
.NERRET1 ANOP                                                           03120040
IHB1&L1  DS    0H                                                       03150040
         ST    &EXITADR(1),RBSEXIT-RBSECT+RBPRFXLN(0,&RBADDR(1)) STORE  03200040
*                                            EXIT ADDR IN SCB           03230040
         AIF   ('&PARAM' EQ '').GETPARM                                 03300040
         AIF   ('&PARAM'(1,1) NE '(').PARERR                            03350040
         ST    &PARAM(1),RBSPARM-RBSECT+RBPRFXLN(0,&RBADDR(1))  STORE   03400040
*                                            PARM ADDR IN SCB           03410040
         AGO   .CONT                                                    03450040
.GETPARM ANOP                                                           03500040
         LA    &EXITADR(1),RBFEPARM-RBSECT+RBPRFXLN(0,&RBADDR(1)) GET   03550040
*                                            DEFAULT PARAMETER AREA     03600040
         ST    &EXITADR(1),RBSPARM-RBSECT+RBPRFXLN(0,&RBADDR(1)) STORE  03650040
*                                            IT IN SCB                  03660040
.CONT    ANOP                                                           03700040
         MVI   RBSFLGS1-RBSECT+RBPRFXLN(&RBADDR(1)),&FLG1 INIT FLAGS    03750040
         MVI   RBSFLGS2-RBSECT+RBPRFXLN(&RBADDR(1)),&FLG2 INIT FLAGS    03780040
         MVI   RBSFLG3-RBSECT+RBPRFXLN(&RBADDR(1)),&FLG3  INIT FLAGS    03810040
         AIF   ('&TCBADDR' EQ '').NTCB                                  03900040
         AIF   ('&TCBADDR'(1,1) NE '(').TCBERR                          03950040
         L     &EXITADR(1),TCBSTAB-TCB(0,&TCBADDR(1)) GET ADDR PREVIOUS 04000040
*                                            SCB                        04050040
         LA    &EXITADR(1),0(0,&EXITADR(1))   CLEAR HIGH BYTE           04070040
         ST    &EXITADR(1),RBSCHAIN-RBSECT+RBPRFXLN(0,&RBADDR(1)) CHAIN 04100040
*                                            TO CURRENT SCB             04140040
         LA    &EXITADR(1),RBSCBB-RBSECT+RBPRFXLN(0,&RBADDR(1)) GET     04180040
*                                            ADDR CURRENT SCB           04220040
         STCM  &EXITADR(1),7,TCBSTABB-TCB(&TCBADDR(1)) MAKE IT CURRENT  04300040
         AGO   .CONT1                                                   04350040
.NTCB    ANOP                                                           04400040
         L     15,PSATOLD-PSA(0,0)           GET CURRENT TCB ADDRESS    04450040
         L     &EXITADR(1),TCBSTAB-TCB(0,15)  GET ADDRESS PREVIOUS SCB  04500040
         LA    &EXITADR(1),0(0,&EXITADR(1))   CLEAR HIGH BYTE           04520040
         ST    &EXITADR(1),RBSCHAIN-RBSECT+RBPRFXLN(0,&RBADDR(1)) CHAIN 04550040
*                                            TO CURRENT SCB             04590040
         LA    &EXITADR(1),RBSCBB-RBSECT+RBPRFXLN(0,&RBADDR(1)) GET     04630040
*                                            ADDR CURRENT SCB           04670040
         STCM  &EXITADR(1),7,TCBSTABB-TCB(15) MAKE IT CURRENT           04750040
         SLR   15,15                                                    04800040
.CONT1   ANOP                                                           04850040
         OI    RBFLAGS1-RBPRFX(&RBADDR(1)),X'02'  MARK RB AS OWNING SCB 04900040
IHB2&L1  DS    0H                                                       04950040
         MEXIT                                                          05000040
.XCTLERR MNOTE 4,'***  XCTL   INVALID VALUE SPECIFIED - &XCTL'          05050040
&ERRBIT  SETB  1                                                        05100040
         AGO   .PURCK                                                   05150040
.PURERR  MNOTE 4,'***  PURGE  INVALID VALUE SPECIFIED - &PURGE'         05200040
&ERRBIT  SETB  1                                                        05250040
         AGO   .ASYCK                                                   05300040
.ASYERR  MNOTE 4,'***  ASYNCH INVALID VALUE SPECIFIED - &ASYNCH'        05350040
&ERRBIT  SETB  1                                                        05400040
         AGO   .TERCK                                                   05450040
.TERERR  MNOTE 4,'***  TERM   INVALID VALUE SPECIFIED - &TERM'          05500040
&ERRBIT  SETB  1                                                        05550040
         AGO   .RECCK                                                   05600040
.RECERR  MNOTE 4,'***  RECORD INVALID VALUE SPECIFIED - &RECORD'        05650040
&ERRBIT  SETB  1                                                        05700040
         AGO   .CKERR                                                   05750040
.EXNREG  MNOTE 12,'*** EXITADR SPECIFIED - NOT IN REGISTER'             05800040
         MEXIT                                                          05850040
.NRBA    MNOTE 12,'***RBADDR MUST BE SPECIFIED WITH EXITADR'            05900040
         MEXIT                                                          05950040
.RBNREG  MNOTE 12,'*** RBADDR NOT IN A REGISTER'                        06000040
         MEXIT                                                          06050040
.PARERR  MNOTE 8,'*** PARAM SPECIFIED - NOT IN REGISTER'                06100040
         MEXIT                                                          06150040
.TCBERR  MNOTE 8,'***TCBADDR SPECIFIED - NOT IN REGISTER'               06200040
         MEXIT                                                          06250040
.CANCEL  ANOP                                                           06300040
         AIF   ('&CNCL' NE '0').CANERR                                  06350040
         AIF   ('&WRKREG' EQ '').WRKERR                                 06400040
         AIF   ('&WRKREG'(1,1) NE '(').WRKNREG                          06450040
         AIF   ('&EXITADR' NE '' OR '&RBADDR' NE '' OR '&PARAM' NE ''  X06500040
               OR '&PURGE' NE '' OR '&ASYNCH' NE '' OR '&TERM' NE ''   X06550040
               OR '&RECORD' NE '' OR '&XCTL' NE '').EXCESS              06600040
.CONT3   AIF   ('&TCBADDR' NE '').CKTCB                                 06650040
&NAME    L     15,PSATOLD-PSA(0,0)           GET CURRENT TCB ADDRESS    06700040
         ICM   &WRKREG(1),7,TCBSTABB-TCB(15)  GET CURRENT SCB ADDRESS   06750040
         BZ    IHB3&L1                       ZERO IF NO SCB FOR TCB     06800040
         CLC   TCBRBP-TCB+1(3,15),SCBOWNRA-SCB(&WRKREG(1)) IS SCB FOR   07000040
*                                            CURRENT RB                 07050040
         BNE   IHB3&L1                                                  07100040
         TM    SCBFLGS3-SCB(&WRKREG(1)),X'04' WAS SCB CREATED BY        07350040
*                                            FESTAE OR BRANCH ENTRY SVC 07400040
         BO    IHB5&L1                       CONTINUE CHECKING          07450040
IHB3&L1  DS    0H                                                       07470040
         LA    15,12(0,0)                    INDICATE CURRENT SCB NOT   07500040
*                                            CREATED BY FESTAE          07550040
         AIF   ('&ERRET' EQ '').NERRE                                   07560040
         B     &ERRET                                                   07570040
         AGO   .NERRE1                                                  07580040
.NERRE   ANOP                                                           07590040
         B     IHB8&L1                       EXIT                       07600040
.NERRE1  ANOP                                                           07620040
IHB5&L1  DS    0H                                                       07650040
         SLR   15,15                         GET ZEROS                  07700040
         ST    15,SCBEXIT-SCB(0,&WRKREG(1))   MAKE SCB AVAILABLE        07750040
         L     15,PSATOLD-PSA(0,0)           GET CURRENT TCB ADDRESS    07800040
         L     &WRKREG(1),SCBCHAIN-SCB(0,&WRKREG(1)) GET PREVIOUS SCB   07850040
         STCM  &WRKREG(1),7,TCBSTABB-TCB(15)  MAKE IT CURRENT           07900040
         LTR   &WRKREG(1),&WRKREG(1)         IS THERE AN SCB            07950040
         BZ    IHB6&L1                       GO INDICATE NO MORE SCBS   08000040
         CLC   TCBRBP-TCB+1(3,15),SCBOWNRA-SCB(&WRKREG(1)) PREVIOUS FOR 08050040
*                                            CURRENT RB                 08100040
         BE    IHB7&L1                       SET RETURN CODE AND EXIT   08150040
IHB6&L1  DS    0H                                                       08200040
         L     15,TCBRBP-TCB(0,15)            GET CURRENT RB ADDRESS    08250040
         LA     &WRKREG(1),RBPRFXLN           GET LENGTH OF PREFIX      08260040
         SLR    15,&WRKREG(1)                 CALC PREFIX ADDRESS       08270040
         NI    RBFLAGS1-RBPRFX(15),X'FD'      INDICATE  NO MORE SCB FOR 08300040
*                                             THIS RB                   08330040
IHB7&L1  DS    0H                                                       08400040
         SLR   15,15                                                    08450040
IHB8&L1  DS    0H                                                       08500040
         MEXIT                                                          08550040
.CKTCB   ANOP                                                           08600040
         AIF   ('&TCBADDR'(1,1) NE '(').TCBERR                          08650040
&NAME    ICM   &WRKREG(1),7,TCBSTABB-TCB(&TCBADDR(1)) GET CURRENT SCB   08700040
         BZ    IHB3&L1                       ZERO IF NO SCB FOR TCB     08750040
         CLC   TCBRBP-TCB+1(3,&TCBADDR(1)),SCBOWNRA-SCB(&WRKREG(1)) IS  08950040
*                                            SCB FOR CURRENT RB         09000040
         BNE   IHB3&L1                       CONTINUE CHECKING          09050040
*                                            NOT OWNED BY CURRENT RB    09150040
         TM    SCBFLGS3-SCB(&WRKREG(1)),X'04'    WAS SCB CREATED BY     09300040
*                                            FESTAE OR BRANCH ENTRY SVC 09350040
         BO    IHB5&L1                       CONTINUE CHECKING          09400040
IHB3&L1  DS    0H                                                       09420040
         LA    15,12(0,0)                    INDICATE CURRENT SCB NOT   09450040
*                                            CREATED BY FESTAE          09500040
         AIF   ('&ERRET' EQ '').NERET                                   09510040
         B     &ERRET                                                   09520040
         AGO   .NERET1                                                  09530040
.NERET   ANOP                                                           09540040
         B     IHB8&L1                       EXIT                       09550040
.NERET1  ANOP                                                           09570040
IHB5&L1  DS    0H                                                       09600040
         L     15,SCBCHAIN-SCB(0,&WRKREG(1))  GET PREVIOUS SCB ADDRESS  09650040
         STCM  15,7,TCBSTABB-TCB(&TCBADDR(1))    MAKE IT CURRENT        09700040
         LTR   15,15                         IS THERE AN SCB            09750040
         BZ    IHB6&L1                                                  09800040
         CLC   TCBRBP-TCB+1(3,&TCBADDR(1)),SCBOWNRA-SCB(15) IS PREVIOUS 09850040
*                                            FOR CURRENT RB             09900040
         BE    IHB7&L1                                                  09950040
IHB6&L1  DS    0H                                                       10000040
         LA    15,RBPRFXLN                    GET LENGTH OF PREFIX      10050040
         LNR   15,15                          NEGATE FOR SUBTRACTION    10057040
         AL    15,TCBRBP-TCB(0,&TCBADDR(1))   CALC PREFIX ADDRESS       10064040
         NI    RBFLAGS1-RBPRFX(15),X'FD'      INDICATE  NO MORE SCB FOR 10080040
*                                            THIS RB                    10150040
IHB7&L1  DS    0H                                                       10170040
         SLR   15,15                GET ZEROS                           10190040
         ST    15,SCBEXIT-SCB(0,&WRKREG(1))   MAKE SCB AVAILABLE        10210040
IHB8&L1  DS    0H                                                       10350040
          MEXIT                                                         10400040
.CANERR  MNOTE 12,'*** EXITADR/0 OPERAND MISSING'                       10450040
         MEXIT                                                          10500040
.WRKERR  MNOTE 12,'*** WRKREG MUST BE SPECIFIED WITH CANCEL REQUEST'    10550040
         MEXIT                                                          10600040
.WRKNREG MNOTE 8,'*** WRKREG SPECIFIED - NOT IN REGISTER'               10650040
         MEXIT                                                          10700040
.EXCESS  MNOTE *,'***EXCESSIVE PARAMETERS W/CANCEL REQUEST IGNORED'     10750040
         AGO   .CONT3                                                   10800040
         MEND                                                           10850040
