         MACRO                                                          00050002
&NAME    FREECELL   &CPID=,&CELL=,&BRANCH=YES,&SAVE=YES,&RELATED=       00100002
*        OS/VS2 RELEASE 2   ---  9/15/73                                00120002
         AIF   ('&BRANCH' NE 'YES').ERR1  BRANCH=YES MANDATORY          00150002
         AIF   ('&CPID' EQ '' ).ERR3        CPID REQUIRED               00160002
         AIF   ('&CELL' EQ '').ERR3A        CELL REQUIRED               00170002
         AIF   ('&CPID'(1,1) NE '(').ERR2   CPID MUST BE A REGISTER     00200002
         AIF   ('&CELL'(1,1) NE '(').ERR2B  CELL MUST BE A REGISTER     00250002
         AIF   ('&CELL(1)' EQ '0').ERR2B   REG. ZERO RESERVED FOR CPID  00252002
         AIF   ('&SAVE' EQ 'YES' OR '&SAVE' EQ 'NO').SAVOK              00256002
         IHBERMAC 1015,&SAVE,''SAVE='',YES    INCORRECT 'SAVE=' VALUE   00258402
.*       SYNTAX OK, CONTINUE WITH EXPANSION OF MACRO                    00258802
.SAVOK   AIF   ('&NAME' EQ '').SKPN                                     00260002
&NAME    DS    0H                  FREECELL MACRO                       00300002
.SKPN    AIF   ('&SAVE' EQ 'NO').NRGS                                   00350002
         STM   2,12,28(13)         SAVE CALLERS REGISTERS 2-12          00400002
.NRGS    AIF   ('&CPID(1)' EQ '0').NZLD                                 00450002
         LR    0,&CPID(1)          LOAD R0 WITH CPID                    00500002
.NZLD    AIF   ('&CELL(1)' EQ '1').NOLD                                 00550002
         LR    1,&CELL(1)          LOAD R1 WITH CELL ADDRESS            00600002
.NOLD    L     3,16(0,0)           CVT ADDRESS FOR FREECELL     YM02296 00700002
         L     15,CVTFRECL-CVTMAP(0,3)    LOAD FREECELL RT ENTRY POINT  00710002
         BALR  14,15               GO TO FREECELL ROUTINE               00800002
         AIF   ('&SAVE' EQ 'NO').NRGR                                   00850002
         LM    2,12,28(13)         RESTORE CALLERS REGISTERS            00900002
.NRGR    MEXIT                                                          00950002
.ERR1    IHBERMAC  1006,''BRANCH=YES''                                  01000002
         MEXIT                                                          01050002
.ERR2    IHBERMAC  1001,CPID,&CPID                                      01100002
         MEXIT                                                          01150002
.ERR2B   IHBERMAC  1001,CELL,&CELL                                      01200002
         MEXIT                                                          01202002
.ERR3    IHBERMAC  1006,CPID                                            01210002
         MEXIT                                                          01220002
.ERR3A   IHBERMAC  1006,CELL                                            01230002
         MEND                                                           01250002
