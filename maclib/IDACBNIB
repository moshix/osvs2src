         MACRO                                                          00050001
         IDACBNIB &EXLST=,&MF=,&CALLER=,                               C00100001
               &NAME=,&USERFLD=,&MODE=,      *** NIB KEYWORDS ***X03004C00200001
               &LISTEND=,&CONDN=,&PROC=,&NIBLEN=,                X03004C00250001
               &CID=,&DEVCHAR=,&CON=,&RESPLIM=,&SDT=,           X3004BSC00300001
               &LOGMODE=,&BNDAREA=,                            @Z40BHUC*00350040
               &ENCR=                                          @G40AKSL 00370040
.*                                                                      00400001
.* FUNCTION    THIS MACRO ACTS AS A CONTROLLER FOR GENCB, MODCB, AND    00450001
.*             TESTCB FOR PROCESSING A NIB.                             00500001
.*             IT IS CALLED BY IDACB2 TO BUILD VARIOUS ARRAYS TO BE     00550001
.*             USED BY THAT MACRO TO BUILD A PARAMETER LIST OF ARGUMENT 00600001
.*             CONTROL ENTRIES THAT WILL BE PASSED TO THE CONTROL       00650001
.*             BLOCK MANIPULATION MODULE AT EXECUTE TIME.               00700001
.*             FOR NON-BIT KEYWORDS, THIS MACRO CALLS IDACB3 WHICH      00750001
.*             ACTUALLY SETS VALUES INTO ALL THE ARRAYS THAT ARE        00800001
.*             REQUIRED.                                                00850001
.*                                                                      00900001
.*             FOR BIT KEYWORDS, IDACB3 IS CALLED, BUT THIS MACRO       00950001
.*             SETS THE VALUE IN THE &IDAVALC ARRAY.                    01000001
.*             IT ALSO VALIDITY CHECKS BIT OPTIONS AND IF MUTUALLY      01050001
.*             EXCLUSIVE OPTIONS ARE SPECIFIED, AN ERROR MESSAGE IS     01060001
.*             ISSUED. THE ONE EXCEPTION IS PROC.                       01070001
.*             IDACBPRC IS ISSUED TO PROCESS PROC                       01080001
.*                                                                      01090001
.* GLOBALS                                                              01092001
         GBLB  &IDACB01                ERROR INDICATOR                  01100001
         GBLA  &IDAENO                 NUMBER OF ELEMENTS               01150001
         GBLA  &IDAELEN(50)            ELEMENT FIELD LENGTH ARRAY       01200001
         GBLC  &IDAVALC(50)            ELEMENT CHARACTER VALUE ARRAY    01350001
.* LOCALS                                                               01360001
         LCLA  &I                      WORK VARIABLE                    01400001
         LCLB  &SYSTEM,&APPL           SDT CODES                X3004BS 01900001
         LCLB  &YES,&NO                LISTEND CODES                    01910001
         LCLB  &CONYES,&CONNO          CON CODES                        01920001
         LCLC  &LGMODE                 ALTER LOGMODE VALUE     @X50BHUC 01960001
         LCLC  &Z16                    UNUSED PORTION OF BIT STRING     02000001
         LCLB  &SEL,&NONE,&REQD        ENCRIPTION CODES        @G40AKSL 02005040
.* INITIALIZATION                                                       02010001
&Z16     SETC  '0000000000000000'      INITIALIZE CONSTANT              02020001
.* SCAN NIB KEYWORDS ******************************************* X03004 02050001
.*                                      NAME                            02060001
         AIF   ('&NAME' EQ '').N000300 CHECK FOR NULL NAME      X04SVHS 02070001
         AIF   ('&NAME(1)' EQ '*').N000200                      X04SVHS 02150001
.*             IF INDIRECT ADDRESS WAS SPECIFIED, BYPASS                02200001
.*             VALIDITY CHECK                                           02250001
         AIF   (K'&NAME GT 8 OR '&NAME'(1,1) LT 'A' OR                 *02750001
               '&NAME'(1,1) GT 'Z').N000100                     X04SVHS 02800001
.*             NAME MUST BE 8 OR FEWER CHARS AND THE FIRST CHAR         02850001
.*             MUST BE ALPHABETIC                                       02900001
         AGO   .N000200                                         X04SVHS 02950001
.N000100 IDAERMAC 3,NAME,&NAME                                   X03004 03000001
&IDACB01 SETB  1                   SET ERROR SWITCH              X03004 03050001
         AGO   .N000300            BYPASS PROCESSING IF ERROR   X04SVHS 03050401
.N000200 ANOP                                                           03052001
         IDACB3 ARG=&NAME,ARGNAME=NAME,KWTC=X'5B',MF=&MF,INDRECT=YES    03054001
.*                                                              X04SVHS 03056001
&IDAELEN(&IDAENO) SETA 8           SET ELEMENT LENGTH            X03004 03058001
.N000300 ANOP                                                           03058401
.********************************************************************** 03058801
.*                                      USERFLD                         03060001
         IDACB3 ARG=&USERFLD,ARGNAME=USERFLD,KWTC=X'51',MF=&MF  X04SVHS 03100001
.********************************************************************** 03110001
.*                                      EXLST                           03150001
         IDACB3 ARG=&EXLST,ARGNAME=EXLST,KWTC=X'82',MF=&MF      X04SVHS 03400001
.********************************************************************** 03410001
.*                                      RESPLIM                         03450001
         IDACB3 ARG=&RESPLIM,ARGNAME=RESPLIM,KWTC=X'83',MF=&MF  X04SVHS 03700001
.********************************************************************** 03710001
.*                                      MODE                            03750001
         AIF   ('&MODE' EQ '').N000600  CHECK FOR NULL MODE      X03004 04000001
         AIF   ('&MODE(1)' EQ '*').N000500                              04050001
.*             IF INDIRECT ADDRESS WAS SPECIFIED, BYPASS                04100001
.*             VALIDITY CHECK                                           04150001
         AIF   (K'&MODE GT 8 OR '&MODE'(1,1) LT 'A' OR                 *04650001
               '&MODE'(1,1) GT 'Z').N000400                      X03004 04700001
.*             MODE MUST BE 8 OR FEWER CHARS AND FIRST CHAR MUST        04710001
.*             BE ALPHABETIC                                            04720001
         AGO   .N000500                                         X04SVHS 04750001
.N000400 IDAERMAC 3,MODE,&MODE                                   X03004 04760001
&IDACB01 SETB  1                 SET ERROR SWITCH                X03004 04770001
.N000500 ANOP                                                           04800001
         IDACB3 ARG=&MODE,ARGNAME=MODE,KWTC=X'54',MF=&MF,INDRECT=YES    04850001
.*                                                              X04SVHS 04852001
&IDAELEN(&IDAENO) SETA 8         SET ELEMENT LENGTH             X04SVHS 04860001
.********************************************************************** 04870001
.*                                      LISTEND                         04910001
.N000600 AIF   ('&LISTEND' EQ '').N000800 CHECK FOR NULL LISTEND X03004 05000001
         IDACB3 ARG=&LISTEND,KWTC=X'52',BIT=YES                 X04SVHS 05050001
&YES     SETB  ('&LISTEND' EQ 'YES')                            X04SVHS 05200001
&NO      SETB  ('&LISTEND' EQ 'NO')                             X04SVHS 05250001
         AIF   (&YES OR &NO).N000700    CHECK FOR VALID OPTION  X04SVHS 05300001
         IDAERMAC 3,LISTEND,&LISTEND                             X03004 05350001
&IDACB01 SETB  1                        SET ERROR SWITCH        X04SVHS 05352001
.N000700 ANOP                                                   X04SVHS 05360001
&IDAVALC(&IDAENO) SETC '&YES&NO.00000000000000&Z16'              X03004 05400001
.********************************************************************** 05410001
.N000800 ANOP                                                           05450001
.*                                      CONDN                           05510001
         IDACB3 ARG=&CONDN,ARGNAME=CONDN,KWTC=X'55',MF=&MF      X04SVHS 05550001
.********************************************************************** 05560001
.*                                      PROC                            05600001
         AIF   ('&PROC' EQ '').N000900  CHECK FOR NULL PROC     X04SVHS 05650001
         IDACBPRC PROC=&PROC,MF=&MF     PROCESS PROC KEYWORD    X04SVHS 05700001
.********************************************************************** 05800001
.*                                      LOGMODE                @Z40BHUC 05850001
.N000900 AIF ('&LOGMODE' EQ '').N000942                        @G40AKSL 05900040
         AIF ('&LOGMODE(1)' EQ '*').N000930                    @Z40BHUC 05920001
.*             IF INDIRECT ADDRESS WAS SPECIFIED, BYPASS       @Z40BHUC 05940001
.*             VALIDITY CHECK                                  @Z40BHUC 05950000
         AIF ('&LOGMODE'(1,1) EQ '0').N000935  VALID IF ZERO   @ZA36268 05960000
         AIF ('&LOGMODE'(2,2) EQ 'S,').N000910   GET ERROR IF  @ZA38191 06000001
.*             VALUE OTHER THAN FIXED OR INDIRECT              @ZA38191 06020001
         AIF  ('&LOGMODE' EQ 'C'''(1,2).' '.''''(1,1)).N000920 @Z40BHUC 06060001
.*          SET ALL BLANKS IF C' ' IS SPECIFIED                @Z40BHUC 06070001
         AIF (K'&LOGMODE LE 8 AND '&LOGMODE'(1,1) GE 'A' AND           *06100001
               '&LOGMODE'(1,1) LE 'Z').N000930                 @Z40BHUC 06150001
.*  LOGMODE MUST BE <= 8 CHARS AND 1ST CHAR MUST BE ALPHABETIC @ZA36268 06200001
.N000910 ANOP                    GET ERROR MESSAGE             @ZA32653 06320140
         IDAERMAC 3,LOGMODE,&LOGMODE                           @Z40BHUC 06350001
&IDACB01 SETB  1                 SET ERROR SWITCH              @Z40BHUC 06400001
         AGO   .N000930                                                 06410001
.N000920 ANOP                    SET VALUE OF 8 BLANKS IF C' ' @Z40BHUC 06430001
&LGMODE  SETC  '        '         SET EIGHT BLANKS             @X50BHUC 06440001
         IDACB3 ARG=&LGMODE,ARGNAME=LOGMODE,KWTC=X'8E',MF=&MF,         *06460001
               INDRECT=YES                                     @X50BHUC 06480001
         AGO   .N000940                                                 06500001
.N000930 ANOP                                                  @X50BHUC 06520001
         IDACB3 ARG=&LOGMODE,ARGNAME=LOGMODE,KWTC=X'8E',MF=&MF,        *06540001
               INDRECT=YES                                     @X50BHUC 06560001
         AGO   .N000940                                                 06580001
.N000935 ANOP                                                  @ZA07556 06600001
         IDACB3 ARG=&LOGMODE,ARGNAME=LOGMODE,KWTC=X'8E',MF=&MF @ZA07556 06650001
.*                                                             @Z40BHUC 06700001
.N000940 ANOP                                                           06702001
&IDAELEN(&IDAENO) SETA 8         SET ELEMENT LENGTH            @Z40BHUC 06710001
.********************************************************************** 06750001
.*                                      ENCR                   @G40AKSL 06753040
.N000942 ANOP                                                  @G40AKSL 06755040
         AIF ('&ENCR' EQ '').N000950                           @G40AKSL 06757040
&SEL     SETB ('&ENCR' EQ 'SEL')                               @G40AKSL 06759040
&REQD    SETB ('&ENCR' EQ 'REQD')                              @G40AKSL 06761040
&NONE    SETB ('&ENCR' EQ 'NONE')                              @G40AKSL 06763040
         AIF (&SEL OR &REQD OR &NONE).N000948                  @G40AKSL 06765040
         IDAERMAC 3,ENCR,&ENCR                                 @G40AKSL 06767040
&IDACB01 SETB 1                                                @G40AKSL 06769040
.N000948 ANOP                                                  @G40AKSL 06771040
         IDACB3 ARG=&ENCR,ARGNAME=ENCR,KWTC=X'96',MF=&MF,BIT=YES        06773040
&IDAVALC(&IDAENO) SETC '&SEL&REQD&NONE.0000000000000&Z16'      @G40AKSL 06775040
.********************************************************************** 06786040
.*                                      BNDAREA                @Z40BHUC 06800001
.N000950 AIF ('&BNDAREA' EQ '').N000980                        @Z40BHUC 06850001
         IDACB3 ARG=&BNDAREA,ARGNAME=BNDAREA,KWTC=X'8F',MF=&MF @Z40BHUC 06900001
.********************************************************************** 06950001
.*                                      SDT                             07000001
.N000980 AIF   ('&SDT' EQ '').N001100   CHECK FOR NULL SDT      X3004BS 11650001
         IDACB3 ARG=&SDT,KWTC=X'84',BIT=YES                     X04SVHS 11700001
&SYSTEM  SETB  ('&SDT' EQ 'SYSTEM')                             X04SVHS 12050001
&APPL    SETB  ('&SDT' EQ 'APPL')                               X04SVHS 12100001
         AIF   (&SYSTEM OR &APPL).N001000 CHECK FOR VALID OPTION        12110001
.*                                                              X04SVHS 12120001
         IDAERMAC 5,SDT,&SDT(&I)                                X3004BS 12150001
&IDACB01 SETB  1                        SET ERROR SWITCH        X3004BS 12200001
.N001000 ANOP                                                   X3004BS 12750001
&IDAVALC(&IDAENO) SETC '&SYSTEM&APPL.00000000000000&Z16'        X3004BS 12800001
.********************************************************************** 12810001
.N001100 AIF   ('&CALLER' EQ 'GEN' OR '&CALLER' EQ 'MOD').N001500       12850001
.*                                                              X04SVHS 12852001
.*       THE FOLLOWING KEYWORDS ARE VALID ONLY FOR TESTCB OR SHOWCB     12854001
.*                                                                      12856001
.********************************************************************** 12858001
.*                                      CID                             12860001
         IDACB3 ARG=&CID,ARGNAME=CID,KWTC=X'5A',MF=&MF          X04SVHS 12900001
.********************************************************************** 12910001
.*                                      DEVCHAR                         12950001
         AIF   ('&DEVCHAR' EQ '').N001300 CHECK FOR NULL DEVCHAR X03004 13200001
         AIF   ('&DEVCHAR(1)' EQ '*').N001200                    X03004 13300001
.*                                      DEVCHAR MUST SPECIFIED AS       13310001
.*                                      INDIRECT ADDRESS                13320001
         IDAERMAC 3,DEVCHAR,&DEVCHAR                             X03004 13350001
&IDACB01 SETB  1                        SET ERR SWITCH          X04SVHS 13360001
         AGO   .N001300                 BYPASS PROCESSING IF ERROR      13400001
.*                                                              X04SVHS 13410001
.N001200 ANOP                                                           13450001
         IDACB3 ARG=&DEVCHAR,ARGNAME=DEVCHAR,KWTC=X'56',MF=&MF,        *13500001
               INDRECT=YES                                      X04SVHS 13550001
&IDAELEN(&IDAENO) SETA 8                SET ELEMENT LENGTH       X03004 13750001
.********************************************************************** 13800001
.*                                      CON                             13850001
.N001300 AIF   ('&CON' EQ '').N001500   CHECK FOR NULL CON      X04SVHS 13900001
         IDACB3 ARG=&CON,KWTC=X'5C',BIT=YES                     X04SVHS 13950001
&CONYES  SETB  ('&CON' EQ 'YES')                                X04SVHS 14100001
&CONNO   SETB  ('&CON' EQ 'NO')                                 X04SVHS 14150001
         AIF   (&CONYES OR &CONNO).N001400 CHECK FOR VALID OPTION       14200001
.*                                                              X04SVHS 14210001
         IDAERMAC 3,CON,&CON                                     X03004 14250001
&IDACB01 SETB  1                        SET ERR SWITCH                  14260001
.N001400 ANOP                                                   X04SVHS 14280001
&IDAVALC(&IDAENO) SETC '&CONYES&CONNO.00000000000000&Z16'        X03004 14300001
.********************************************************************** 14350001
.N001500 ANOP                                                   X04SVHS 14450001
         MEND                                                           14600001
