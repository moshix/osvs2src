         MACRO                                                          00050040
         IDAVSACB &AM=,&MF=,&CALLER=,                          @ZA34940C00100040
               &ACBLEN=,&ATRB=,&AVSPAC=,&BUFND=,   *** ACB KEYWORDS ***C00150040
               &BUFNI=,&BUFNO=,&BUFSP=,&CINV=,&DDNAME=,&ENDRBA=,       C00200040
               &ERROR=,&EXLST=,&FS=,&KEYLEN=,&LRECL=,                  C00250040
               &MACRF=,&NCIS=,&NDELR=,&NEXCP=,&NEXT=,&NINSR=,&NIXL=,   C00300040
               &NLOGR=,&NRETR=,&NSSS=,&NUPDR=,&OFLAGS=,&PASSWD=,&RKP=, C00350040
               &BSTRNO=,&CRA=,&OPENOBJ=,&AIXRKP=,               X04SVHSC00400040
               &MAREA=,&MLEN=,&JFCB=,                          @XM01140*00450040
               &USERPTR=,                                      @XM01129*00500040
               &STRNO=,&STMST=,&CATALOG=,&APPLID=                       00550040
.*                                                                      00600040
.*       CHANGE LEVEL = 1                                      @ZA37504 00650040
.*       DATE OF LAST CHANGE =  29 JAN 79 ( 79*029 )           @ZA37504 00680040
.*                                                                      00750040
.* FUNCTION    THIS MACRO ACTS AS A CONTROLLER FOR GENCB, MODCB, AND    00800040
.*             TESTCB FOR PROCESSING AN ACB.                            00850040
.*             IT IS CALLED BY IDACB2 TO BUILD VARIOUS ARRAYS TO BE     00900040
.*             USED BY IDACB2 TO BUILD A PARAMETER LIST OF ARGUMENT     00950040
.*             CONTROL ENTRIES THAT WILL BE PASSED TO THE CONTROL       01000040
.*             BLOCK MANIPULATION MODULE AT EXECUTE TIME.               01050040
.*             FOR NON-BIT KEYWORDS, THIS MACRO CALLS IDAVSCB3 @ZA34940 01100040
.*             WHICH ACTUALLY SETS VALUES INTO ALL THE ARRAYS  @ZA34940 01140040
.*             THAT ARE REQUIRED.                              @ZA34940 01180040
.*                                                                      01220040
.*             FOR BIT KEYWORDS, IDAVSCB3 IS CALLED, BUT THIS  @ZA34940 01260040
.*             MACRO SETS THE VALUE IN &IDAVALC ARRAY.         @ZA34940 01300040
.*             IT ALSO VALIDITY CHECKS BIT OPTIONS AND IF MUTUALLY      01400040
.*             EXCLUSIVE OPTIONS ARE SPECIFIED, AN ERROR MESSAGE IS     01450040
.*             ISSUED.                                                  01500040
.*                                                                      01550040
.*             IT IS ALSO CALLED BY THE ACB DECLARATIVE MACRO TO        01600040
.*             PROCESS THE BIT KEYWORDS MACRF, CATALOG, AND CRA.        01650040
.*             WHEN CALLED BY ACB, DIFFERENT GLOBALS ARE SET THAN       01700040
.*             THOSE USED BY THE CBMM. THESE GLOBALS ARE SET IN SUCH    01750040
.*             A MANNER TO ALLOW THE DECLARATIVE MACRO TO GENERATE      01800040
.*             A 'DC' USING THE GLOBALS DIRECTLY.                       01850040
.*                                                             @ZA34940 01860040
.* CHANGE ACTIVITY                                             @ZA34940 01870040
.*      CBMM SPLIT SUPPORT                                     @ZA34940 01880040
.*                                                             @ZA34940 01890040
.* GLOBALS                                                              01900040
         GBLB  &IDACB01                ERROR INDICATOR                  01950040
         GBLA  &IDAENO                 NUMBER OF ELEMENTS               02000040
         GBLA  &IDAELEN(50)            ELEMENT FIELD LENGTH ARRAY       02050040
         GBLC  &IDAVALC(50)            ELEMENT CHARACTER VALUE ARRAY    02100040
         GBLC  &IDAMAC1,&IDAMAC2,&IDAMAC3                       X04SVHS 02150040
.*             MACRF VALUES FOR ACB DECLARATIVE MACRO           X04SVHS 02200040
         GBLC  &IDAIFL1                 CATALOG/CRA VALUES FOR  X04SVHS 02250040
.*                                      ACB DECLARATIVE MACRO   X04SVHS 02300040
.* LOCALS                                                               02350040
         LCLA  &I,&A,&B,&C                       WORK VARIABLES         02400040
         LCLB  &ESDS,&KSDS,&WCK,&SSWD,&REPL      ATRB CODES             02450040
         LCLB  &RRDS,&SPAN,&UNQ                  ATRB CODES     X04SVHS 02500040
         LCLB  &PATH,&BASE,&OAIX                 OPENOBJ CODES  X04SVHS 02550040
         LCLB  &SCRA,&UCRA                       CRA CODES      X04SVHS 02600040
         LCLB  &MKEY,&MADR,&MCNV,&MSEQ,&MSKP     MACRF CODES            02650040
         LCLB  &MDIR,&IN,&OUT,&NUB,&UBF          MACRF CODES            02700040
         LCLB  &LOGONF,&NLOGON                   MACRF CODES     X03004 02750040
         LCLB  &NFX,&CFX,&RST,&NRS,&DDN,&DSN     MACRF CODES   @XM01125 02800040
         LCLB  &NRM,&AIX,&NSR,&LSR,&GSR          MACRF CODES    X04SVHS 02850040
         LCLB  &NCI,&ICI,&NDF,&DFR,&NIS,&SIS     MACRF CODES    X04SVHS 02900040
         LCLB  &OPEN                             OFLAGS CODE    X04SVHS 02950040
         LCLB  &VSAM,&VTAM                       AM CODES        X03004 03000040
         LCLB  &YES,&NO                          CATALOG CODES  X04SVHS 03050040
         LCLC  &Z16                     UNUSED PORTION OF BIT STRING    03100040
.* INITIALIZATION                                                       03150040
&Z16     SETC  '0000000000000000'       SET TO 16 ZEROES                03200040
.* SCAN ACB KEYWORDS ************************************************** 03250040
.*                                      BUFND                           03300040
         IDAVSCB3 ARG=&BUFND,ARGNAME=BUFND,KWTC=X'04',MF=&MF   @ZA34940 03350040
.********************************************************************** 03390040
.*                                      BUFNI                   X04SVHS 03430040
         IDAVSCB3 ARG=&BUFNI,ARGNAME=BUFNI,KWTC=X'05',MF=&MF   @ZA34940 03470040
.********************************************************************** 03510040
.*                                      BUFSP                   X04SVHS 03550040
         IDAVSCB3 ARG=&BUFSP,ARGNAME=BUFSP,KWTC=X'07',MF=&MF   @ZA34940 03590040
.********************************************************************** 03700040
.*                                      DDNAME                          03750040
         AIF   ('&DDNAME' EQ '').A000300  *** DDNAME ***                03800040
         AIF   ('&DDNAME(1)' EQ '*').A000200                            03850040
.*             IF INDIRECT ADDRESS SPECIFIED, BYPASS VALIDITY CHECKS    03900040
         AIF   (K'&DDNAME GT 8).A000100                                 03950040
         AIF   (('&DDNAME'(1,1) LT 'A' OR '&DDNAME'(1,1) GT 'Z') AND   C03970040
               ('&DDNAME'(1,1) NE '#' AND '&DDNAME'(1,1) NE '@' AND    C03990040
               '&DDNAME'(1,1) NE '$')).A000100                 @ZA37504 04010040
.*             DDNAME MUST BE 8 OR FEWER CHARS, AND FIRST CHAR MUST     04030040
.*                                   BE ALPHABETIC OR NATIONAL@ZA37504  04050040
         AGO   .A000200                                                 04150040
.A000100 IDAVSERR 3,DDNAME,&DDNAME      ISSUE ERROR MESSAGE    @ZA34940 04200040
&IDACB01 SETB  1                        SET ERROR SWITCH                04240040
         AGO   .A000300                 SKIP PROCESSING                 04280040
.A000200 ANOP                                                           04320040
         IDAVSCB3 ARG=&DDNAME,ARGNAME=DDNAME,KWTC=X'09',       @ZA34940*04360040
               MF=&MF,INDRECT=YES                              @ZA34940 04400040
&IDAELEN(&IDAENO) SETA 8                ELEMENT LENGTH IS 8 BYTES       04500040
.********************************************************************** 04550040
.A000300 ANOP                                                           04600040
.*                                      EXLST                           04650040
         IDAVSCB3 ARG=&EXLST,ARGNAME=EXLST,KWTC=X'0C',MF=&MF   @ZA34940 04700040
.********************************************************************** 04740040
.*                                      JFCB                   @XM01140 04780040
         IDAVSCB3 ARG=&JFCB,ARGNAME=JFCB,KWTC=X'8D',MF=&MF     @ZA34940 04820040
.********************************************************************** 04860040
.*                                      CATALOG                         04900040
         AIF   ('&CATALOG' EQ '').A000500 *** CATALOG ***               04940040
         IDAVSCB3 ARG=&CATALOG,ARGNAME=CATALOG,KWTC=X'22',     @ZA34940*04980040
               MF=&MF,INDRECT=YES,BIT=YES                      @ZA34940 05020040
         AIF   ('&CATALOG(1)' EQ '*').A000500                   X04SVHS 05060040
.*             IF INDIRECT ADDRESS SPECIFIED, VALUE ARRAY(&IDAVALC)     05100040
.*                                      WAS SET IN IDAVSCB3    @ZA34940 05140040
&YES     SETB  ('&CATALOG' EQ 'YES')                            X04SVHS 05180040
&NO      SETB  ('&CATALOG' EQ 'NO')                             X04SVHS 05220040
         AIF   (&YES OR &NO).A000400                            X04SVHS 05260040
         IDAVSERR 3,CATALOG,&CATALOG  ISSUE ERROR MESSAGE      @ZA34940 05300040
&IDACB01 SETB  1                        SET ERROR SWITCH        X04SVHS 05500040
.A000400 ANOP                                                   X04SVHS 05550040
         AIF   ('&CALLER' EQ 'ACB').A000500                     X04SVHS 05600040
.*             IF CALLER IS ACB, VALUE WILL BE SET IN CRA RTN   X04SVHS 05650040
&IDAVALC(&IDAENO) SETC '&YES&NO.00000000000000&Z16'             X04SVHS 05700040
.********************************************************************** 05750040
.*                                      MACRF                           05800040
.A000500 AIF   ('&MACRF' EQ '').A001750   *** MACRF ***         X04SVHS 05850040
         IDAVSCB3 ARG=&MACRF,ARGNAME=MACRF,KWTC=X'12',MF=&MF,  @ZA34940*05900040
               BIT=YES,INDRECT=YES                             @ZA34940 05940040
         AIF   ('&MACRF(1)' EQ '*').A001800                             05980040
.*             IF INDIRECT ADDRESS SPECIFIED, VALUE ARRAY(&IDAVALC)     06020040
.*                                      WAS SET IN IDAVSCB3    @ZA34940 06060040
&I       SETA  0                        CLEAR                   X04SVHS 06150040
&A       SETA  0                        WORK                    X04SVHS 06200040
&B       SETA  0                        VARIABLES               X04SVHS 06250040
&C       SETA  0                        ZERO ERROR COUNTER      X04SVHS 06300040
.A000600 ANOP                                                           06350040
&I       SETA  &I+1                     BUMP INDEX INTO LIST    X04SVHS 06400040
         AIF   (&I GT N'&MACRF).A000800 HAVE ALL OPTIONS BEEN CHECKED   06450040
&MKEY    SETB  ('&MACRF(&I)' EQ 'KEY' OR &MKEY)                 X04SVHS 06500040
&MADR    SETB  ('&MACRF(&I)' EQ 'ADR' OR &MADR)                 X04SVHS 06550040
&MCNV    SETB  ('&MACRF(&I)' EQ 'CNV' OR &MCNV)                 X04SVHS 06600040
&MSEQ    SETB  ('&MACRF(&I)' EQ 'SEQ' OR &MSEQ)                 X04SVHS 06650040
&MSKP    SETB  ('&MACRF(&I)' EQ 'SKP' OR &MSKP)                 X04SVHS 06700040
&MDIR    SETB  ('&MACRF(&I)' EQ 'DIR' OR &MDIR)                 X04SVHS 06750040
&OUT     SETB  ('&MACRF(&I)' EQ 'OUT' OR &OUT)                  X04SVHS 06800040
&IN      SETB  ('&MACRF(&I)' EQ 'IN' OR &IN)                    X04SVHS 06850040
&NUB     SETB  ('&MACRF(&I)' EQ 'NUB' OR &NUB)                  X04SVHS 06900040
&UBF     SETB  ('&MACRF(&I)' EQ 'UBF' OR &UBF)                  X04SVHS 06950040
&NFX     SETB  ('&MACRF(&I)' EQ 'NFX' OR &NFX)                  X04SVHS 07000040
&CFX     SETB  ('&MACRF(&I)' EQ 'CFX' OR &CFX)                  X04SVHS 07050040
&DDN     SETB  ('&MACRF(&I)' EQ 'DDN' OR &DDN)                  X04SVHS 07100040
&DSN     SETB  ('&MACRF(&I)' EQ 'DSN' OR &DSN)                  X04SVHS 07150040
&NRM     SETB  ('&MACRF(&I)' EQ 'NRM' OR &NRM)                  X04SVHS 07200040
&AIX     SETB  ('&MACRF(&I)' EQ 'AIX' OR &AIX)                  X04SVHS 07250040
&NSR     SETB  ('&MACRF(&I)' EQ 'NSR' OR &NSR)                  X04SVHS 07300040
&LSR     SETB  ('&MACRF(&I)' EQ 'LSR' OR &LSR)                  X04SVHS 07350040
&GSR     SETB  ('&MACRF(&I)' EQ 'GSR' OR &GSR)                  X04SVHS 07400040
&NCI     SETB  ('&MACRF(&I)' EQ 'NCI' OR &NCI)                  X04SVHS 07450040
&ICI     SETB  ('&MACRF(&I)' EQ 'ICI' OR &ICI)                  X04SVHS 07500040
&NDF     SETB  ('&MACRF(&I)' EQ 'NDF' OR &NDF)                  X04SVHS 07550040
&DFR     SETB  ('&MACRF(&I)' EQ 'DFR' OR &DFR)                  X04SVHS 07600040
&NIS     SETB  ('&MACRF(&I)' EQ 'NIS' OR &NIS)                  X04SVHS 07650040
&SIS     SETB  ('&MACRF(&I)' EQ 'SIS' OR &SIS)                  X04SVHS 07700040
&RST     SETB  ('&MACRF(&I)' EQ 'RST' OR &RST)                 @XM01125 07750040
&NRS     SETB  ('&MACRF(&I)' EQ 'NRS' OR &NRS)                 @XM01125 07800040
&A       SETA  &MKEY+&MADR+&MCNV+&MSEQ+&MSKP+&MDIR+&OUT+&IN+&NUB+&UBF+&*07850040
               NFX+&CFX+&DDN+&DSN+&NRM+&AIX+&NSR+&LSR+&GSR+&NCI+&ICI+&N*07900040
               DF+&DFR+&NIS+&SIS+&RST+&NRS                     @XM01125 07950040
.*             &A CONTAINS THE NUMBER OF VSAM OPTIONS SPECIFIED         08000040
.*             &B CONTAINS THE NUMBER OF VTAM OPTIONS SPECIFIED         08050040
.*             &C CONTAINS THE NUMBER OF ERRORS                         08100040
.*             IF THEY EQUAL &I AT THIS POINT, THEN THE LAST            08150040
.*             OPTION SPECIFIED WAS A VALID VSAM OPTION                 08200040
         AIF   (&A+&B+&C EQ &I).A000600 IF NEW VSAM BIT SET CHK X04SVHS 08250040
.*                                      NEXT SUBPARAMETER               08300040
.*                                      ELSE CHECK VTAM OPTIONS X04SVHS 08350040
&LOGONF  SETB  ('&MACRF(&I)' EQ 'LOGON' OR &LOGONF)              X03004 08400040
&NLOGON  SETB  ('&MACRF(&I)' EQ 'NLOGON' OR &NLOGON)             X03004 08450040
&B       SETA  &LOGONF+&NLOGON                                  X04SVHS 08500040
         AIF   (&A+&B+&C EQ &I).A000700 IF NEW VTAM BIT SET CHK X04SVHS 08550040
.*                                      FOR AM=VTAM             X04SVHS 08600040
.*             IF &A + &B + &C DO NOT EQUAL &I AT THIS POINT, THEN THE  08650040
.*             LAST OPTION CHECKED WAS INVALID OR DUPLICATED A          08700040
.*             PREVIOUSLY CHECKED OPTION                                08750040
         IDAVSERR 5,MACRF,&MACRF(&I)    ISSUE ERROR MESSAGE    @ZA34940 08800040
&IDACB01 SETB  1                        SET ERROR SWITCH        X04SVHS 08850040
&C       SETA  &C+1                     BUMP ERROR COUNTER      X04SVHS 08900040
         AGO   .A000600                                                 08950040
.A000700 AIF   ('&AM' EQ 'VTAM').A000600 IF VTAM NOT SPECIFIED,         09000040
         IDAVSERR 20,&MACRF(&I),MACRF    ISSUE ERROR MESSAGE   @ZA34940 09050040
&IDACB01 SETB  1                         SET ERROR SWITCH        X03004 09100040
         AGO   .A000600                  CHECK NEXT OPTION      X04SVHS 09150040
.*                                                                      09200040
.*             THE FOLLOWING CODE CHECKS FOR MUTUALLY EXCLUSIVE         09250040
.*             OPTIONS SPECIFIED,AND IF ANY ARE FOUND, AN ERROR         09300040
.*             MESSAGE IS ISSUED AND THE ERROR SWITCH IS SET            09350040
.A000800 AIF   (NOT(&UBF AND &NUB)).A000900                     X04SVHS 09400040
         IDAVSERR 8,MACRF,UBF,NUB                              @ZA34940 09450040
&IDACB01 SETB  1                                                        09490040
.A000900 AIF   (NOT(&LOGONF AND &NLOGON)).A001000               X04SVHS 09530040
         IDAVSERR 8,MACRF,LOGON,NLOGON                         @ZA34940 09570040
&IDACB01 SETB  1                                                 X03004 09610040
.A000950 AIF   (NOT(&RST AND &NRS)).A001000                    @XM01125 09650040
         IDAVSERR 8,MACRF,RST,NRS                              @ZA34940 09690040
&IDACB01 SETB  1                        SET ERR SWITCH         @XM01125 09730040
.A001000 AIF   (NOT(&NFX AND &CFX)).A001100                     X04SVHS 09770040
         IDAVSERR 8,MACRF,NFX,CFX                              @ZA34940 09810040
&IDACB01 SETB  1                        SET ERR SWITCH          X04SVHS 09850040
.A001100 AIF   (NOT(&DDN AND &DSN)).A001200                     X04SVHS 09890040
         IDAVSERR 8,MACRF,DDN,DSN                              @ZA34940 09930040
&IDACB01 SETB  1                        SET ERR SWITCH          X04SVHS 09970040
.A001200 AIF   (NOT(&NRM AND &AIX)).A001300                     X04SVHS 10010040
         IDAVSERR 8,MACRF,NRM,AIX                              @ZA34940 10050040
&IDACB01 SETB  1                        SET ERR SWITCH          X04SVHS 10090040
.A001300 AIF   ((NOT(&NSR AND &GSR)) AND (NOT(&GSR AND &LSR)) AND      *10130040
               (NOT(&LSR AND &NSR))).A001400                    X04SVHS 10170040
         IDAVSERR 25,MACRF,&NSR,&LSR,&GSR                      @ZA34940 10210040
&IDACB01 SETB  1                        SET ERR SWITCH          X04SVHS 10250040
.A001400 AIF   (NOT(&NCI AND &ICI)).A001500                     X04SVHS 10290040
         IDAVSERR 8,MACRF,NCI,ICI                              @ZA34940 10330040
&IDACB01 SETB  1                        SET ERROR SWITCH        X04SVHS 10370040
.A001500 AIF   (NOT(&NDF AND &DFR)).A001600                     X04SVHS 10410040
         IDAVSERR 8,MACRF,NDF,DFR                              @ZA34940 10450040
&IDACB01 SETB  1                        SET ERR SWITCH          X04SVHS 10490040
.A001600 AIF   (NOT(&NIS AND &SIS)).A001700                     X04SVHS 10530040
         IDAVSERR 8,MACRF,NIS,SIS                              @ZA34940 10570040
&IDACB01 SETB  1                        SET ERR SWITCH          X04SVHS 10900040
.A001700 ANOP                                                   X04SVHS 10950040
         AIF   ('&CALLER' EQ 'ACB').A001750                     X04SVHS 11000040
.*             IF CALLED BY DECLARATIVE, BYPASS CBMM PROCESSING         11050040
.*                                                                      11100040
&IDAVALC(&IDAENO) SETC '&MKEY&MADR&MCNV&MSEQ&MSKP&MDIR&IN&OUT&NUB&UBF&L*11150040
               OGONF&NLOGON.0&DDN&DSN&NRM&AIX&NSR&LSR&GSR&NCI&ICI&NDF&D*11200040
               FR&NIS&SIS&NFX&CFX&RST&NRS.00'                  @XM01139 11250040
.*             SET GLOBAL ARRAY TO BE USED BY IDACB2 TO BUILD           11300040
.*             PARAMETER LIST                                           11350040
         AGO   .A001800                 BYPASS DECLARATIVE PROC X04SVHS 11400040
.A001750 ANOP                                                   X04SVHS 11450040
         AIF   ('&CALLER' NE 'ACB').A001800                     X04SVHS 11500040
.*             THE FOLLOWING CODE SETS DEFAULT VALUES FOR ACB           11550040
.*             DECLARATIVE.  THE DEFAULTS FOR CBM WILL BE SET AT        11600040
.*             EXECUTE TIME.                                            11650040
.*                                                                      11700040
&MKEY    SETB  (NOT(&MADR OR &MCNV) OR &MKEY)                   X04SVHS 11750040
.*             MKEY IS DEFAULT IF MADR OR MCNV ARE NOT SPECIFIED        11800040
&MSEQ    SETB  (NOT(&MSKP OR &MDIR) OR &MSEQ)                   X04SVHS 11850040
.*             MSEQ IS DEFAULT IF MSKP OR MDIR ARE NOT SPECIFIED        11900040
&IN      SETB  (NOT(&OUT) OR &IN)                               X04SVHS 11950040
.*             IN IS DEFAULT IF OUT IS NOT SPECIFIED            X04SVHS 12000040
.*                                                                      12050040
.*             SET MACRF VALUES IN GLOBALS FOR ACB DECLARATIVE  X04SVHS 12100040
&IDAMAC1 SETC  '&MKEY&MADR&MCNV&MSEQ&MDIR&IN&OUT&UBF'           X04SVHS 12150040
&IDAMAC2 SETC  '000&MSKP&NLOGON&RST&DSN&AIX'                    X04SVHS 12200040
&IDAMAC3 SETC  '0&LSR&GSR&ICI&DFR&SIS&CFX.0'                    X04SVHS 12250040
         AGO   .A002200                 PROCESS CRA             X04SVHS 12300040
.********************************************************************** 12350040
.A001800 ANOP                                                   X04SVHS 12400040
.*                                      PASSWD                          12450040
         IDAVSCB3 ARG=&PASSWD,ARGNAME=PASSWD,KWTC=X'1E',MF=&MF @ZA34940 12500040
.********************************************************************** 12540040
.*                                      STRNO                           12580040
         IDAVSCB3 ARG=&STRNO,ARGNAME=STRNO,KWTC=X'20',MF=&MF   @ZA34940 12620040
.********************************************************************** 12660040
.*                                      USERPTR                @XM01129 12700040
         AIF ('&USERPTR' EQ '' OR '&USERPTR' EQ '0').A001850   @XM01129 12740040
         IDAVSCB3 ARG=&USERPTR,ARGNAME=USERPTR,KWTC=X'8C',     @ZA34940X12780040
               MF=&MF                                          @ZA34940 12820040
         AIF   ('&AM' NE 'VSAM' AND '&AM' NE '').A001825       @XM01129 12860040
.*                                   IF AM NOT VSAM OR NULL, ERROR      12900040
         AGO   .A001850                                        @XM01129 12940040
.A001825 IDAVSERR 24,USERPTR                                   @ZA34940 12980040
&IDACB01 SETB  1                                               @XM01129 13150040
.********************************************************************** 13200040
.A001850  ANOP                                                          13250040
.*                                      APPLID                          13300040
         IDAVSCB3 ARG=&APPLID,ARGNAME=APPLID,KWTC=X'41',       @ZA34940*13350040
               MF=&MF,CHKAM=VTAM,AM=&AM                        @ZA34940 13380040
.********************************************************************** 13450040
.*                                       AM                             13500040
         AIF   ('&AM' EQ '' OR '&CALLER' NE 'GEN').A002000      X04SVHS 13550040
.*             ARGUMENT CONTROL ENTRY IS ONLY BUILT FOR GENCB FOR AM    13600040
         IDAVSCB3 ARG=&AM,ARGNAME=AM,KWTC=X'40',BIT=YES        @ZA34940 13650040
&VSAM    SETB  ('&AM' EQ 'VSAM')                                X04SVHS 13700040
&VTAM    SETB  ('&AM' EQ 'VTAM')                                X04SVHS 13750040
         AIF   (&VSAM OR &VTAM).A001900                         X04SVHS 13800040
.*             CHECK FOR VALID OPTION                           X04SVHS 13850040
         IDAVSERR 5,AM,&AM                                     @ZA34940 13900040
&IDACB01 SETB  1                         SET ERROR SWITCH       X04SVHS 13950040
.A001900    ANOP                                                X04SVHS 14000040
&IDAVALC(&IDAENO) SETC '&VSAM&VTAM.00000000000000&Z16'          X04SVHS 14050040
.********************************************************************** 14100040
.A002000 ANOP                                                   X04SVHS 14150040
.*                                      MAREA                           14200040
         IDAVSCB3 ARG=&MAREA,ARGNAME=MAREA,KWTC=X'0E',MF=&MF   @ZA34940 14250040
         AIF   ('&AM' NE 'VSAM' AND '&AM' NE '' AND '&MAREA' NE        *14300040
               '').A002050                                      X04SVHS 14350040
.*                                      IF AM NOT VSAM OR NULL  X04SVHS 14400040
.*                                      ERROR                   X04SVHS 14450040
         AGO   .A002100                                         X04SVHS 14500040
.A002050 ANOP                                                   X04SVHS 14550040
         IDAVSERR 24,MAREA                                     @ZA34940 14600040
&IDACB01 SETB  1                        SET ERR SWITCH          X04SVHS 14650040
.********************************************************************** 14700040
.A002100 ANOP                                                   X04SVHS 14750040
.*                                        MLEN                  X04SVHS 14800040
         IDAVSCB3 ARG=&MLEN,ARGNAME=MLEN,KWTC=X'0F',MF=&MF     @ZA34940 14850040
         AIF   ('&AM' NE 'VSAM' AND '&AM' NE '' AND '&MLEN' NE         *14900040
               '').A002150                                      X04SVHS 14950040
.*                                      IF AM NOT EQUAL TO VSAM X04SVHS 15000040
.*                                      OR NULL, ERROR          X04SVHS 15050040
         AGO   .A002200                                         X04SVHS 15100040
.A002150 ANOP                                                   X04SVHS 15150040
         IDAVSERR 24,MLEN                                      @ZA34940 15200040
&IDACB01 SETB  1                        SET ERROR SWITCH        X04SVHS 15250040
.********************************************************************** 15300040
.A002200 ANOP                                                   X04SVHS 15350040
.*                                      CRA                             15400040
         AIF   ('&CRA' EQ '').A002350 *** CRA ***               X04SVHS 15450040
.*             IF CRA IS NULL, CHECK FOR CALLER = ACB           X04SVHS 15500040
&SCRA    SETB  ('&CRA' EQ 'SCRA')                              @XM07755 15550040
&UCRA    SETB  ('&CRA' EQ 'UCRA')                              @XM07755 15600040
         AIF   (&SCRA OR &UCRA).A002300 CHK FOR VALID OPTION    X04SVHS 15650040
         IDAVSERR 5,CRA,&CRA            ISSUE ERROR MESSAGE    @ZA34940 15700040
&IDACB01 SETB  1                        SET ERR SWITCH          X04SVHS 15750040
         AGO   .A002400                 BYPASSING PROCESSING KEYWORD    15800040
.*                                                              X04SVHS 15850040
.A002300 ANOP                                                   X04SVHS 15900040
         AIF   ('&CALLER' EQ 'ACB').A002375                     X04SVHS 15950040
.*             IF CALLER IS ACB BYPASS CBM PROCESSING           X04SVHS 16000040
         IDAVSCB3 ARG=&CRA,KWTC=X'59',BIT=YES                  @ZA34940 16050040
&IDAVALC(&IDAENO) SETC '&SCRA&UCRA.00000000000000&Z16'          X04SVHS 16100040
         AGO   .A002400                 BYPASS DECLARATIVE PROC X04SVHS 16150040
.A002350 ANOP                                                   X04SVHS 16200040
.*             AT THIS POINT, EITHER CRA IS NOT SPECIFIED, OR   X04SVHS 16250040
.*             CALLER = ACB                                     X04SVHS 16300040
         AIF   ('&CALLER' NE 'ACB').A002400                     X04SVHS 16350040
.*             IF CALLER NOT ACB, BYPASS DECLARATIVE PROCESSING X04SVHS 16400040
.A002375 ANOP                                                   X04SVHS 16450040
&IDAIFL1 SETC  '000&YES&SCRA&UCRA.00'   SET ACB INFLGS BYTE 1   X04SVHS 16500040
.*             ALL BIT KEYWORDS HAVE BEEN PROCESSED FOR DECLARATIVE     16550040
         MEXIT                                                  X04SVHS 16600040
.********************************************************************** 16650040
.A002400 ANOP                                                   X04SVHS 16700040
.*                                      BSTRNO                          16750040
         IDAVSCB3 ARG=&BSTRNO,ARGNAME=BSTRNO,KWTC=X'24',MF=&MF @ZA34940 16800040
.********************************************************************** 16850040
.*                                                                      16900040
         AIF   ('&CALLER' NE 'TEST').A003800                            16950040
.*                                                                      17000040
.*             THE FOLLOWING KEYWORDS ARE VALID FOR TESTCB ONLY         17050040
.*                                                                      17100040
.********************************************************************** 17150040
.*                                      ATRB                            17200040
         AIF   ('&ATRB' EQ '').A002800    *** ATRB ***                  17250040
         IDAVSCB3 ARG=&ATRB,ARGNAME=ATRB,KWTC=X'01',MF=&MF,    @ZA34940*17300040
               INDRECT=YES,BIT=YES                             @ZA34940 17340040
         AIF   ('&ATRB(1)' EQ '*').A002800                      X04SVHS 17380040
.*             IF INDIRECT ADDRESS WAS SPECIFIED, VALUE ARRAY(&IDAVALC) 17420040
.*             WAS SET IN IDAVSCB3                             @ZA34940 17460040
&I       SETA  0                        ZERO INDEX INTO LIST            17550040
&C       SETA  0                        ZERO ERROR COUNTER      X04SVHS 17600040
.A002500 ANOP                                                           17650040
&I       SETA  &I+1                     BUMP INDEX INTO LIST            17700040
         AIF   (&I GT N'&ATRB).A002600  HAVE ALL OPTIONS BEEN CHECKED   17750040
&ESDS    SETB  ('&ATRB(&I)' EQ 'ESDS' OR &ESDS)                 X04SVHS 17800040
&KSDS    SETB  ('&ATRB(&I)' EQ 'KSDS' OR &KSDS)                 X04SVHS 17850040
&WCK     SETB  ('&ATRB(&I)' EQ 'WCK' OR &WCK)                   X04SVHS 17900040
&SSWD    SETB  ('&ATRB(&I)' EQ 'SSWD' OR &SSWD)                 X04SVHS 17950040
&REPL    SETB  ('&ATRB(&I)' EQ 'REPL' OR &REPL)                 X04SVHS 18000040
&RRDS    SETB  ('&ATRB(&I)' EQ 'RRDS' OR &RRDS)                 X04SVHS 18050040
&SPAN    SETB  ('&ATRB(&I)' EQ 'SPAN' OR &SPAN)                 X04SVHS 18100040
&UNQ     SETB  ('&ATRB(&I)' EQ 'UNQ' OR &UNQ)                   X04SVHS 18150040
         AIF   (&ESDS+&KSDS+&WCK+&SSWD+&REPL+&RRDS+&SPAN+&UNQ+&C EQ &I)*18200040
               .A002500                 CHK FOR VALID OPTION    X04SVHS 18250040
.*             IF A NEW BIT WAS NOT SET, THEN LAST OPTION CHECKED       18300040
.*             IS INVALID OR DUPLICATES A PREVIOUSLY SPEC OPTION        18350040
         IDAVSERR 5,ATRB,&ATRB(&I)      ISSUE ERROR MESSAGE    @ZA34940 18400040
&IDACB01 SETB  1                        SET ERROR SWITCH        X04SVHS 18450040
&C       SETA  &C+1                     BUMP ERROR COUNTER      X04SVHS 18500040
         AGO   .A002500                                         X04SVHS 18550040
.A002600 AIF   (NOT(&ESDS AND &KSDS)).A002700                           18600040
.*             CHECK FOR MUTUALLY EXCLUSIVE OPTIONS                     18650040
         IDAVSERR 8,ATRB,ESDS,KSDS      ISSUE ERROR MESSAGE    @ZA34940 18700040
&IDACB01 SETB  1                        SET ERROR SWITCH                18750040
.A002700 ANOP                                                           18800040
&IDAVALC(&IDAENO) SETC '&KSDS&ESDS&WCK&SSWD&REPL&RRDS&SPAN&UNQ.00000000*18850040
               &Z16'                                                    18900040
.********************************************************************** 18950040
.A002800 ANOP                                                           19000040
.*                                      AVSPAC                          19050040
         IDAVSCB3 ARG=&AVSPAC,ARGNAME=AVSPAC,KWTC=X'02',MF=&MF @ZA34940 19100040
.********************************************************************** 19140040
.*                                      BUFNO                           19180040
         IDAVSCB3 ARG=&BUFNO,ARGNAME=BUFNO,KWTC=X'06',MF=&MF   @ZA34940 19220040
.********************************************************************** 19260040
.*                                      CINV                            19300040
         IDAVSCB3 ARG=&CINV,ARGNAME=CINV,KWTC=X'08',MF=&MF     @ZA34940 19340040
.********************************************************************** 19380040
.*                                      ENDRBA                          19420040
         IDAVSCB3 ARG=&ENDRBA,ARGNAME=ENDRBA,KWTC=X'0A',MF=&MF @ZA34940 19460040
.********************************************************************** 19500040
.*                                      ERROR                           19540040
         IDAVSCB3 ARG=&ERROR,ARGNAME=ERROR,KWTC=X'0B',MF=&MF   @ZA34940 19580040
.********************************************************************** 19620040
.*                                      FS                              19660040
         IDAVSCB3 ARG=&FS,ARGNAME=FS,KWTC=X'0D',MF=&MF         @ZA34940 19700040
.********************************************************************** 19740040
.*                                      KEYLEN                          19780040
         IDAVSCB3 ARG=&KEYLEN,ARGNAME=KEYLEN,KWTC=X'10',MF=&MF @ZA34940 19820040
.********************************************************************** 19860040
.*                                      LRECL                           19900040
         IDAVSCB3 ARG=&LRECL,ARGNAME=LRECL,KWTC=X'11',MF=&MF   @ZA34940 19940040
.********************************************************************** 19980040
.*                                      NCIS                            20020040
         IDAVSCB3 ARG=&NCIS,ARGNAME=NCIS,KWTC=X'13',MF=&MF     @ZA34940 20060040
.********************************************************************** 20100040
.*                                      NDELR                           20140040
         IDAVSCB3 ARG=&NDELR,ARGNAME=NDELR,KWTC=X'14',MF=&MF   @ZA34940 20180040
.********************************************************************** 20220040
.*                                      NEXCP                           20260040
         IDAVSCB3 ARG=&NEXCP,ARGNAME=NEXCP,KWTC=X'15',MF=&MF   @ZA34940 20300040
.********************************************************************** 20340040
.*                                      NEXT                            20380040
         IDAVSCB3 ARG=&NEXT,ARGNAME=NEXT,KWTC=X'16',MF=&MF     @ZA34940 20420040
.********************************************************************** 20460040
.*                                      NINSR                           20500040
         IDAVSCB3 ARG=&NINSR,ARGNAME=NINSR,KWTC=X'17',MF=&MF   @ZA34940 20540040
.********************************************************************** 20580040
.*                                      NIXL                            20620040
         IDAVSCB3 ARG=&NIXL,ARGNAME=NIXL,KWTC=X'18',MF=&MF     @ZA34940 20660040
.********************************************************************** 20700040
.*                                      &NLOGR                          20740040
         IDAVSCB3 ARG=&NLOGR,ARGNAME=NLOGR,KWTC=X'19',MF=&MF   @ZA34940 20780040
.********************************************************************** 20820040
.*                                      NRETR                           20860040
         IDAVSCB3 ARG=&NRETR,ARGNAME=NRETR,KWTC=X'1A',MF=&MF   @ZA34940 20900040
.********************************************************************** 20940040
.*                                      NSSS                            20980040
         IDAVSCB3 ARG=&NSSS,ARGNAME=NSSS,KWTC=X'1B',MF=&MF     @ZA34940 21020040
.********************************************************************** 21060040
.*                                      NUPDR                           21100040
         IDAVSCB3 ARG=&NUPDR,ARGNAME=NUPDR,KWTC=X'1C',MF=&MF   @ZA34940 21140040
.********************************************************************** 21180040
.*                                                                      21220040
         AIF   ('&OFLAGS' EQ '').A003000 *** OFLAGS ***                 21260040
         IDAVSCB3 ARG=&OFLAGS,ARGNAME=OFLAGS,KWTC=X'1D',       @ZA34940*21300040
               MF=&MF,BIT=YES,INDRECT=YES                      @ZA34940 21340040
         AIF   ('&OFLAGS(1)' EQ '*').A003000                            21380040
.*             IF INDIRECT ADDRESS WAS SPECIFIED, VALUE ARRAY(&IDAVALC) 21420040
.*             WAS SET IN IDAVSCB3                             @ZA34940 21460040
&OPEN    SETB  ('&OFLAGS' EQ 'OPEN')                            X04SVHS 21500040
         AIF   (&OPEN).A002900                                  X04SVHS 21540040
.*             CHECK FOR VALID OPTION SPECIFIED                         21580040
         IDAVSERR 3,OFLAGS,&OFLAGS                             @ZA34940 21620040
&IDACB01 SETB  1                                                        22300040
.A002900 ANOP                                                           22350040
&IDAVALC(&IDAENO) SETC '&OPEN.000000000000000&Z16'              X04SVHS 22400040
.********************************************************************** 22450040
.A003000 ANOP                                                   X04SVHS 22500040
.*                                      RKP                             22550040
         IDAVSCB3 ARG=&RKP,ARGNAME=RKP,KWTC=X'1F',MF=&MF       @ZA34940 22600040
.********************************************************************** 22640040
.*                                      AIXRKP                          22680040
         IDAVSCB3 ARG=&AIXRKP,ARGNAME=AIXRKP,KWTC=X'3C',MF=&MF @ZA34940 22720040
.********************************************************************** 22800040
.*                                      STMST                           22850040
         AIF   ('&STMST' EQ '').A003300  *** STMST ***                  22900040
         AIF   ('&STMST(1)' EQ '*').A003100                             22950040
.*                                      STMST MUST BE INDIRECT ADDRESS  23000040
         IDAVSERR 3,STMST,&STMST        ELSE, ISSUE ERROR MSG  @ZA34940 23050040
         AGO   .A003300                                                 23090040
.A003100 ANOP                                                           23130040
         IDAVSCB3 ARG=&STMST,ARGNAME=STMST,KWTC=X'23',MF=&MF, @ZA34940 *23170040
               INDRECT=YES                                    @ZA34940  23210040
.*                                                              X04SVHS 23300040
&IDAELEN(&IDAENO) SETA 8                                        X04SVHS 23350040
.********************************************************************** 23400040
.*                                      OPENOBJ                         23450040
.A003300 ANOP                                                   X04SVHS 23500040
         AIF   ('&OPENOBJ' EQ '').A003800 *** OPENOBJ ***       X04SVHS 23550040
&I       SETA  0                        CLEAR SUBPARM CTR       X04SVHS 23600040
&C       SETA  0                        ZERO ERROR COUNTER      X04SVHS 23650040
.A003400 ANOP                                                   X04SVHS 23700040
&I       SETA  &I+1                     BUMP SUBPARM CTR        X04SVHS 23750040
         AIF   (&I GT N'&OPENOBJ).A003500 CHK FOR END OF LIST   X04SVHS 23800040
&PATH    SETB  ('&OPENOBJ(&I)' EQ 'PATH' OR &PATH)              X04SVHS 23850040
&BASE    SETB  ('&OPENOBJ(&I)' EQ 'BASE' OR &BASE)              X04SVHS 23900040
&AIX     SETB  ('&OPENOBJ(&I)' EQ 'AIX' OR &AIX)                X04SVHS 23950040
         AIF   (&PATH+&BASE+&AIX+&C EQ &I).A003400              X04SVHS 24000040
.*                                      CHK FOR NEW BIT SET     X04SVHS 24050040
         IDAVSERR 5,OPENOBJ,&OPENOBJ(&I)                       @ZA34940 24100040
&IDACB01 SETB  1                        SET ERROR SWITCH        X04SVHS 24150040
&C       SETA  &C+1                     BUMP ERROR COUNTER      X04SVHS 24200040
         AGO   .A003400                                         X04SVHS 24250040
.*             CHECK FOR MUTUALLY EXCLUSIVE OPTIONS             X04SVHS 24300040
.A003500 AIF   (NOT(&PATH AND &BASE)).A003600                   X04SVHS 24350040
         IDAVSERR 8,OPENOBJ,PATH,BASE                          @ZA34940 24400040
&IDACB01 SETB  1                        SET ERROR SWITCH        X04SVHS 24440040
.A003600 AIF   (NOT(&PATH AND &AIX)).A003700                    X04SVHS 24480040
         IDAVSERR 8,OPENOBJ,PATH,AIX                           @ZA34940 24520040
&IDACB01 SETB  1                        SET ERROR SWITCH        X04SVHS 24560040
.A003700 ANOP                                                   X04SVHS 24600040
         IDAVSCB3 ARG=&OPENOBJ,KWTC=X'21',BIT=YES              @ZA34940 24640040
&IDAVALC(&IDAENO) SETC '&AIX&PATH&BASE.0000000000000&Z16'      @XM07772 24750040
.********************************************************************** 24800040
.A003800 ANOP                                                   X04SVHS 24850040
         MEND                                                           24900040
