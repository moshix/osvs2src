         MACRO                                                          00700014
   IECICS  &CHAN,&SLCHS                                                 01100002
         GBLB  &IECCSFT                                                 01150002
         LCLA  &COUNT,&LCHNS(60),&LCOUNT,&COUNT2                   I267 02200019
         LCLA  &NUM,&NUM2,&NUM3,&X                                      02800002
         LCLC  &LABEL,&LCHADR(30)                                       03500014
.***************************************************************        03510002
.*                                                                      03512002
.*       THIS MACRO GENERATES THE CHANNEL SEARCH MODULE CODE            03514002
.*       AND ENTRIES TO BE USED BY CHANNEL RESTART                      03516002
.*                                                                      03518002
.*       ITS INPUT CONSISTS OF TWO ARGUMENTS:                           03518402
.*             &CHAN - IS THE CHANNEL NUMBER FOR WHICH                  03518802
.*             THIS ENTRY SET IS BEING BUILT                            03519202
.*             &SLCHS - IS A VECTOR WITH THE FOLLOWING INFORMATION      03519602
.*                 1 - THE LOWEST LCH ADDRESS ASSOCIATED WITH           03519702
.*                     THIS CHANNEL MINUS ONE. THIS IS REPRESENTED      03519802
.*                     BY &SLCHS(1).                                    03519902
.*                 2 - TWO ENTRIES FOR EACH LCH BETWEEN THE             03529902
.*                     LOWEST AND HIGHEST LCH ADDRESSES FOR             03539902
.*                     THIS CHANNEL. THESE ENTRIES ARE:                 03543202
.*                        0 - THIS CHAN NOT ON THIS LCH                 03545202
.*                        1 - THIS CHAN ON THIS LCH                     03545602
.*                        .                                             03546002
.*                        0 - THIS LCH HAS NO STAND ALONE SEEK          03546402
.*                            DEVICES                                   03546502
.*                        1 - THIS LCH HAS STAND ALONE SEEK DEVICES     03546602
.*                     THE FIRST INDICATOR IS USED TO TELL HOW MANY     03559902
.*                     AND WHICH LCHS SHOULD BE INCLUDED AND THE SECOND 03569902
.*                     TO INDICATE WHETHER A STAND ALONE SEEK LCH       03571902
.*                     NEEDS TO BE GENERATED FOR THIS LCH.              03572302
.*                 3 - AN END OF ENTRY INDICATOR                        03572702
.*                     0 - IF THIS IS NOT THE LAST CHANNEL              03573102
.*                     1 - IF THIS IS THE LAST CHANNEL (AND THUS        03573202
.*                         THE LAST ENTRY TO IECICS)                    03573302
.***************************************************************        03573402
         AIF   (&IECCSFT EQ 1).NOHEAD                                   03586702
&IECCSFT SETB 1                                                         03600002
         TITLE 'CHANNEL SEARCH MODULES'                                 03650002
*****************************************************************       03700002
*                                                               *       03750002
*              CHANNEL SEARCH MODULES                           *       03800002
*        ONE PER PHYSICAL CHANNEL DEPICTING THE LOGICAL CHANNELS*       03850002
*        ASSOCIATED WITH EACH CHANNEL AND THE OPERATIONS TO BE  *       03900002
*        PERFORMED FOR THEM. THE OPERATIONS ARE                 *       03950002
*              0  - STAND ALONE SEEKS FOR THIS LCH              *       04000002
*              4  - SENSE LCH FOR CHANNEL                       *       04050002
*              8  - DATA TRANSFER PATH THROUGH CHANNEL SEARCH.  *       04100002
*                   IF MORE THAN ONE OF THESE EXIST, THEY ARE   *       04150002
*                   ALTERNATED.                                 *       04200002
*              12 - END OF ENTRIES                              *       04250002
*                                                               *       04300002
*****************************************************************       04350002
         SPACE 2                                                        04400002
.NOHEAD  ANOP                                                           04450002
&COUNT2  SETA  2                                                        04500002
         AIF   (T'&SLCHS EQ 'O').MPXSRH                                 04510002
.LOOP    AIF   (&SLCHS(&COUNT2) EQ 0).CKEND                             04550002
         AIF   (&COUNT2 EQ N'&SLCHS).ZZ                                 04560002
&LCOUNT  SETA  &LCOUNT+1                                                04600002
.CKEND   AIF   (&COUNT2+1 GE N'&SLCHS).ZZ                               04650002
&COUNT2  SETA  &COUNT2+2                                                04700002
         AGO   .LOOP                                                    04750002
.ZZ      ANOP                                                           04800002
IECCX&CHAN BALR 12,14                   SET POINTER TO C.S.TABLE        04850002
&LCOUNT  SETA  0                                                        05400002
&COUNT2  SETA  1                        INITIALIZE COUNTER              11900014
         AGO   .LCHFND2                 SKIP FIRST UPDATE               12600014
.LCHFND  ANOP                                                           13300014
&COUNT2  SETA  &COUNT2+2                UPDATE THRU SLCH LIST           14000014
.LCHFND2 ANOP                                                           14700014
&COUNT   SETA  &COUNT+1                UPDATE COUNTER                   15400014
         AIF   (&COUNT2+1  EQ  N'&SLCHS).MVCCODE  TEST END OF LIST      16100014
         AIF   (&SLCHS(&COUNT2+1)  EQ  0).LCH3   CHECK IF LCH ASSIGNED  16800014
&LCOUNT  SETA  &LCOUNT+1               UPDATE NUM OF LCHS THIS CHAN     17500014
&LCHNS(&LCOUNT) SETA &COUNT+&SLCHS(1)  SET PROPER LCH NUMBER            18200014
.LCH3   AIF   (&SLCHS(&COUNT2+2)  EQ  0).LCHFND   CHECK FO R DA SK DEV  18900014
         DC    H'0',AL2(LCH&LCHNS(&LCOUNT).-IECVLCH) SA SEEK ENTRY      18950002
         AGO   .LCHFND                 RETURN TO FIND LOOP              23100014
.MVCCODE ANOP                                                           23150002
&X       SETA  &CHAN+1                                                  23200002
         DC    H'4',AL2(SLCH&X.-IECVLCH) SENSE LCH ENTRY                23250002
&X       SETA  1                                                        23450002
.LCH2    ANOP                                                           23500002
         DC    H'8',AL2(LCH&LCHNS(&X).-IECVLCH) DATA XFER ENTRY         23550002
&X       SETA  &X+1                                                     23650002
         AIF   (&X GT &LCOUNT).EOE                                      23700002
         AGO   .LCH2                                                    23850002
.MPXSRH  ANOP                                                           23900002
IECCX&CHAN BALR 12,14                   SET POINTER TO C.S.TABEL        23950002
&X SETA &CHAN+1                                                         24000002
         DC    H'4',AL2(SLCH&X.-IECVLCH) SENSE LCH ENTRY                24050002
         DC    H'8',AL2(LCH&X.-IECVLCH) DATA XFER ENTRY                 24100002
.EOE     DC    H'12',AL2(0)             END OF ENTRIES                  24150002
         SPACE 1                                                        24200002
         MEND                                                           93800014
