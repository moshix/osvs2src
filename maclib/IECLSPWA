         MACRO                                                          00050002
&LABEL   IECLSPWA &EP,&F4,&D1=(0),&ADT=NO                               00100002
.*                                                                      00150002
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 2                      00200002
.*                                                                      00250002
.*                                                                      00300002
.********************************************************************** 00350002
.*                                                                      00400002
.* MODULE NAME = IECLSPWA (VS2-2)                                       00450002
.*                                                                      00500002
.* DESCRIPTIVE NAME = DADSM LSPACE WORK AREA ADDRESSING MACRO           00550002
.*                                                                      00600002
.* COPYRIGHT = NONE                                                     00650002
.*                                                                      00700002
.* STATUS = CHANGE LEVEL 000                                            00750002
.*                                                                      00800002
.* FUNCTION = DADSM LSPACE MACRO WHICH PROVIDES ADDRESSABILITY          00850002
.*            TO THE 540 DECIMAL BYTE WORK AREA (NOT INCLUDING THE      00900002
.*            8 WORD PREFIX OR THE 6 WORD RECOVERY ROUTINE PARAMETER    00950002
.*            LIST) USED BY LSPACE.                                     01000002
.*                                                                      01050002
.* NOTES = THIS MACRO IS AN INTERNAL MACRO USED IN THE FOLLOWING        01100002
.*         DADSM FUNCTIONS:                                             01150002
.*                                                                      01200002
.*             LSPACE                                                   01250002
.*                                                                      01300002
.*         TO REFERENCE THIS WORK AREA WITHIN THE FUNCTIONS LISTED      01350002
.*         ABOVE, CODE A USING STATEMENT ON THE LABEL 'LSPACWKA'.       01400002
.*                                                                      01450002
.*      DEPENDENCIES = THIS MACRO IS DESIGNED TO BE USED OPTIONALLY     01500002
.*                     WITH THE IECSDSL1 DSCB MAPPING MACRO, THE        01550002
.*                     PREFX MACRO (DEFINED IN THE IECDSECS MACRO)      01600002
.*                     WHICH DEFINES THE 8 WORD WORK AREA PREFIX,       01650002
.*                     THE 20 WORD EXTENDED WORK AREA PREFIX, AND       01700002
.*                     THE 4 WORD DADSM WTG TABLE, AND THE DSMADT       01750002
.*                     MACRO (ALSO DEFINED IN THE IECDSECS MACRO)       01800002
.*                     WHICH DEFINES THE 6 WORD DADSM AUDIT TRAIL.      01850002
.*                                                                      01900002
.*      RESTRICTIONS = NONE                                             01950002
.*                                                                      02000002
.*      REGISTER CONVENTIONS = NOT APPLICABLE                           02050002
.*                                                                      02100002
.* PATCH LABEL = NOT APPLICABLE                                         02150002
.*                                                                      02200002
.* MODULE TYPE = MACRO                                                  02250002
.*                                                                      02300002
.*      PROCESSOR = ASSEMBLER LANGUAGE                                  02350002
.*                                                                      02400002
.*      MODULE SIZE = 540 DECIMAL BYTES (NOT INCLUDING THE PREFIX)      02450002
.*                                                                      02500002
.*      ATTRIBUTES = NOT APPLICABLE                                     02550002
.*                                                                      02600002
.* ENTRY POINT = LSPPREFX                                               02650002
.*                                                                      02700002
.*      PURPOSE = THIS MACRO IS CALLED AT ASSEMBLY TIME BY THE          02750002
.*                FUNCTIONS LISTED UNDER 'NOTES' TO EXPAND THE          02800002
.*                LSPACE WORK AREA.                                     02850002
.*                                                                      02900002
.*      LINKAGE =                                                       02950002
.*                                                                      03000002
.*                LABEL IECLSPWA EP,F4,D1=(N),ADT=YES                   03050002
.*                                                                      03100002
.*      INPUT =                                                         03150002
.*                                                                      03200002
.*                OPTIONAL LABEL SPECIFIED AS AN ACCEPTABLE ASSEMBLER   03250002
.*                LABEL WHICH CAN BE USED BY THE CALLING CSECT TO       03300002
.*                ESTABLISH ADDRESSABILITY TO THE ENTIRE WORK AREA.     03350002
.*                                                                      03400002
.*                EP IS THE FIRST POSITIONAL PARAMETER, SPECIFYING      03450002
.*                THAT THE LABELS FOR EACH OF THE FIELDS IN THE 8       03500002
.*                WORD PREFIX, THE 20 WORD EXTENDED PREFIX, AND THE 4   03550002
.*                WORD DADSM WTG TABLE ARE TO BE EXPANDED.  IF THESE    03600002
.*                FIELDS ARE NOT TO BE DEFINED, A COMMA MUST BE CODED   03650002
.*                TO INDICATE THE ABSENCE OF THIS PARAMETER.            03700002
.*                                                                      03750002
.*                F4 IS THE SECOND POSITIONAL PARAMETER, SPECIFYING     03800002
.*                THAT THE FIELDS OF THE FORMAT 4 DSCB ARE TO BE        03850002
.*                EXPANDED USING THE IECSDSL1 DSCB MAPPING MACRO.       03900002
.*                IF THESE FIELDS ARE NOT TO BE DEFINED AND A           03950002
.*                KEYWORD PARAMETER FOLLOWS, A COMMA MUST BE            04000002
.*                CODED TO INDICATE THE ABSENCE OF THIS PARAMETER.      04050002
.*                                                                      04100002
.*                D1=(N) IS A KEYWORD PARAMETER THAT SPECIFIES THE      04150002
.*                FORMAT(S) OF THE DSCB(S) TO BE EXPANDED IN THE        04200002
.*                140 BYTE DSCB AREA, USING THE IECSDSL1 MACRO.         04250002
.*                N CAN BE A NUMBER FROM 0 - 6.  IF N IS 0 OR 4 OR      04300002
.*                IF THIS KEYWORD IS OMITTED, NO LABELS ARE EXPANDED.   04350002
.*                IF N IS 1, 2, 3, 5, OR 6, THE CORRESPONDING FORMAT    04400002
.*                OF THE DSCB WILL BE EXPANDED.  MORE THAN ONE DSCB     04450002
.*                FORMAT CAN BE MAPPED IN THE 140 BYTE DSCB AREA        04500002
.*                BY SEPARATING THE FORMAT NUMBERS BY COMMAS.           04550002
.*                                                                      04600002
.*                ADT=YES IS A KEYWORD PARAMETER WHICH SPECIFIES THAT   04650002
.*                THE LABELS FOR EACH OF THE FIELDS WITHIN THE 6 WORD   04700002
.*                DADSM AUDIT TRAIL ARE TO BE EXPANDED.                 04750002
.*                                                                      04800002
.*      OUTPUT = DADSM LSPACE WORK AREA EXPANSION                       04850002
.*                                                                      04900002
.* EXIT-NORMAL = MEND STATEMENT                                         04950002
.*                                                                      05000002
.* EXIT-ERROR = ERROR MESSAGES:                                         05050002
.*                                                                      05100002
.*      MNOTE 12,***  IHB002 INVALID EP OPERAND SPECIFIED-&EP           05150002
.*      MNOTE 12,***  IHB002 INVALID F4 OPERAND SPECIFIED-&F4           05200002
.*      MNOTE 12,***  IHB002 INVALID ADT OPERAND SPECIFIED-&ADT         05250002
.*                                                                      05300002
.*      THE MACRO EXPANSION CONTINUES AS IF THE INVALID PARAMETER       05350002
.*      WERE NOT SPECIFIED.                                             05400002
.*                                                                      05450002
.* EXTERNAL REFERENCES = NONE                                           05500002
.*                                                                      05550002
.*      ROUTINES = NONE                                                 05600002
.*                                                                      05650002
.*      DATA AREAS = LSPACE WORK AREA                                   05700002
.*                                                                      05750002
.*      CONTROL BLOCKS = DSCB, ECB, IOB, DCB, DEB                       05800002
.*                                                                      05850002
.* TABLES = NONE                                                        05900002
.*                                                                      05950002
.* MACROS = IECDSECS DSMADT                                             06000002
.*          IECDSECS PREFX                                              06050002
.*          IECSDSL1                                                    06100002
.*          IHBERMAC                                                    06150002
.*                                                                      06200002
.* CHANGE ACTIVITY = SEE DELETIONS/CHANGES PRECEDING THE MACRO          06250002
.*                                                                      06300002
.********************************************************************** 06350002
.*                                                                      06400002
         LCLA  &C                                                       06450002
&LABEL   DSECT                                                          06500002
*********************************************************************** 06550002
*                                                                       06600002
*        LSPACE WORK AREA                                               06650002
*                                                                       06700002
*********************************************************************** 06750002
         SPACE 1                                                        06800002
*********************************************************************** 06850002
*        PREFIX, EXTENDED PREFIX, AND WTG TABLE AREA                    06900002
*********************************************************************** 06950002
         SPACE 1                                                        07000002
LSPPREFX DS    8F                       WORK AREA PREFIX                07050002
LSPACWKA DS    20F                      EXTENDED PREFIX                 07100002
FIRST    EQU   LSPACWKA                                                 07150002
LWTGTBL  DS    4F                       DADSM WTG TABLE                 07200002
         AIF   ('&EP' EQ 'EP').LSP0010                                  07250002
         AIF   ('&EP' EQ '').LSP0020                                    07300002
         IHBERMAC 1001,EP,&EP                                           07350002
         AGO   .LSP0020                                                 07400002
.LSP0010 ANOP                                                           07450002
         ORG   LSPPREFX                                                 07500002
*        IECDSECS PREFX,EXPAND=YES                                      07550002
         IECDSECS PREFX,EXPAND=YES                                      07600002
.LSP0020 ANOP                                                           07650002
         SPACE 1                                                        07700002
*********************************************************************** 07750002
*        FORMAT 4 DSCB AREA                                             07800002
*********************************************************************** 07850002
         SPACE 1                                                        07900002
F4IN     DS    XL96                     FORMAT 4 DSCB                   07950002
ENQAREA  EQU   F4IN+4                   ENQ PARAMETER LIST              08000002
MJELNAME EQU   ENQAREA+16               MAJOR ELEMENT NAME              08050002
         AIF   ('&F4' EQ 'F4').LSP0030                                  08100002
         AIF   ('&F4' EQ '').LSP0040                                    08150002
         IHBERMAC 1001,F4,&F4                                           08200002
         AGO   .LSP0040                                                 08250002
.LSP0030 ANOP                                                           08300002
         ORG   F4IN                                                     08350002
*        IECSDSL1 (4)                                                   08400002
         IECSDSL1 (4)                                                   08450002
.LSP0040 ANOP                                                           08500002
         SPACE 1                                                        08550002
*********************************************************************** 08600002
*        140-BYTE DSCB AREA                                             08650002
*********************************************************************** 08700002
         SPACE 1                                                        08750002
DSCBF5   DS    XL140                    140-BYTE DSCB AREA              08800002
&C       SETA  1                                                        08850002
.LSP0050 AIF   (&C GT N'&D1).LSP0060                                    08900002
         AIF   (&D1(&C) EQ 0 OR &D1(&C) EQ 4).LSP0055                   08950002
         ORG   DSCBF5                                                   09000002
         MNOTE '         IECSDSL1 (&D1(&C))'                            09050002
         IECSDSL1 (&D1(&C))                                             09100002
.LSP0055 ANOP                                                           09150002
&C       SETA  &C+1                                                     09200002
         AGO   .LSP0050                                                 09250002
.LSP0060 ANOP                                                           09300002
         DS    F                        UNUSED FULLWORD                 09350002
         SPACE 1                                                        09400002
*********************************************************************** 09450002
*        LSPACE INTERNAL DEB/DCB AREA                                   09500002
*********************************************************************** 09550002
         SPACE 1                                                        09600002
DEB      DS    12F                      DEB                             09650002
DEBPTR   DS    F                        DCB POINTER TO DEB              09700002
DCB      EQU   DEBPTR-44                DCB                             09750002
         SPACE 1                                                        09800002
*********************************************************************** 09850002
*        LSPACE INTERNAL ECB/IOB AREA                                   09900002
*********************************************************************** 09950002
         SPACE 1                                                        10000002
ECB      DS    F                        ECB                             10050002
IOB      DS    8F                       IOB                             10100002
SEEK     DS    D                        DIRECT ACCESS ADDRESS           10150002
         SPACE 1                                                        10200002
*********************************************************************** 10250002
*        LSPACE CHANNEL CONTROL WORDS AREA                              10300002
*********************************************************************** 10350002
         SPACE 1                                                        10400002
CCW0     DS    D                                                        10450002
CCW1     DS    D                                                        10500002
CCW2     DS    D                                                        10550002
CCW3     DS    D                                                        10600002
CCW4     DS    D                                                        10650002
         SPACE 1                                                        10700002
*********************************************************************** 10750002
*        LSPACE WORK AREA FIELDS                                        10800002
*********************************************************************** 10850002
         SPACE 1                                                        10900002
CVTAREA  DS    D                        CONVERSION WORK AREA            10950002
SAVETOT  DS    3F                       TOTAL SPACE IN FORMAT 5         11050002
TCBADDR  DS    F                        POINTER TO THE TCB              11100002
MIELNAME DS    CL6                      MINOR ELEMENT ENQ NAME          11150002
TEMPCTG  DS    XL3                      LARGEST EXTENT                  11200002
THETA    DS    XL1                      SECTOR VALUE FOR RPS DEVICE     11250002
SMFFLAG  DS    XL1                      SMF INDICATOR                   11300002
SWITCH   DS    XL1                      LSPACE SWITCHES                 11350002
*                                                                       11400002
*        DEFINITION OF LSPACE SWITCH BIT SETTINGS                       11450002
*                                                                       11500002
*        BIT 0 = READ ISSUED FOR SECOND FORMAT 5 DSCB INDICATOR         11550002
*        BIT 1 = READ ISSUED FOR VOLUME LABEL INDICATOR                 11600002
*        BITS 4 - 7 = ERROR CODE                                        11650002
*                                                                       11700002
RD2NDF5  EQU   X'80'                    SECOND FORMAT 5 DSCB READ       11750002
RDVOLABL EQU   X'40'                    READ VOLUME LABEL INDICATOR     11800002
*                                                                       11850002
         DS    XL3                      THREE UNUSED BYTES              11900002
         SPACE 1                                                        11950002
*********************************************************************** 12000002
*        ABSOLUTE TRACK ADDRESSES                                       12050002
*********************************************************************** 12100002
         SPACE 1                                                        12150002
VTOCADR  DS    XL5                      FORMAT 4 CCHHR                  12200002
         SPACE 1                                                        12250002
*********************************************************************** 12300002
*        LSPACE RECOVERY ROUTINE AUDIT TRAIL AREA                       12350002
*********************************************************************** 12400002
         SPACE 1                                                        12450002
LSPADTRL DS    6F                       LSPACE AUDIT TRAIL WORDS        12500002
         AIF   ('&ADT' EQ 'YES').LSP0090                                12550002
         AIF   ('&ADT' EQ 'NO').LSP0100                                 12600002
         IHBERMAC 1001,ADT,&ADT                                         12650002
         AGO   .LSP0100                                                 12700002
.LSP0090 ANOP                                                           12750002
         ORG   LSPADTRL                                                 12800002
*        IECDSECS DSMADT,EXPAND=YES                                     12850002
         IECDSECS DSMADT,EXPAND=YES                                     12900002
.LSP0100 ANOP                                                           12950002
         SPACE 1                                                        13000002
*********************************************************************** 13050002
LSPWAEND EQU   *                        LSPACE WORK AREA END            13100002
LWALNGTH EQU   LSPWAEND-LWTGTBL         WORK AREA LENGTH FOR IECRES GET 13150002
*********************************************************************** 13200002
         MEND                                                           13250002
