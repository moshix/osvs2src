         MACRO                                                          00050002
&LABEL   IECOBTWA &EP,&F4,&D1=(0),&ADT=NO                               00100002
.*                                                                      00150002
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 2                      00200002
.*                                                                      00250002
.*       MACRO DELETIONS/ADDITIONS FOR VS2 RELEASE 3.7                  00260037
.*0000009000-010000,024500                             DELETED @ZA31937 00270037
.*0000009000-009600,018600-018700,024500-024600        ADDED   @ZA31937 00280037
.*0000119540-119900                                    ADDED   @ZA31937 00286037
.*                                                                      00300002
.********************************************************************** 00350002
.*                                                                      00400002
.* MODULE NAME = IECOBTWA (VS2-2)                                       00450002
.*                                                                      00500002
.* DESCRIPTIVE NAME = DADSM OBTAIN WORK AREA ADDRESSING MACRO           00550002
.*                                                                      00600002
.* COPYRIGHT = NONE                                                     00650002
.*                                                                      00700002
.* STATUS = CHANGE LEVEL 000                                            00750002
.*                                                                      00800002
.* FUNCTION = DADSM OBTAIN MACRO WHICH PROVIDES ADDRESSABILITY          00850002
.*            TO THE 612 DECIMAL BYTE WORK AREA (NOT INCLUDING @ZA31937 00900037
.*            THE 8 WORD PREFIX AND THE 6 WORD RECOVERY ROUTINE@ZA31937 00930037
.*            PARAMETER LIST) USED BY OBTAIN.                  @ZA31937 00960037
.*                                                                      01050002
.* NOTES = THIS MACRO IS AN INTERNAL MACRO USED IN THE FOLLOWING        01100002
.*         DADSM FUNCTIONS:                                             01150002
.*                                                                      01200002
.*             OBTAIN                                                   01250002
.*                                                                      01300002
.*         TO REFERENCE THIS WORK AREA WITHIN THE FUNCTIONS LISTED      01350002
.*         ABOVE, CODE A USING STATEMENT ON THE LABEL 'OBTNWKA'.        01400002
.*                                                                      01450002
.*      DEPENDENCIES = THIS MACRO IS DESIGNED TO BE USED OPTIONALLY     01500002
.*                     WITH THE IECSDSL1 DSCB MAPPING MACRO, THE        01550002
.*                     PREFX MACRO (DEFINED IN THE IECDSECS MACRO)      01600002
.*                     WHICH DEFINES THE 8 WORD WORK AREA PREFIX,       01650002
.*                     THE 20 WORD EXTENDED WORK AREA PREFIX, AND       01700002
.*                     THE 4 WORD DADSM WTG TABLE, AND THE DSMADT       01750002
.*                     MACRO (ALSO DEFINED IN THE IECDSECS MACRO)       01800002
.*                     WHICH DEFINES THE 6 WORD DADSM AUDIT TRAIL.      01850002
.*                     THE AUDIT TRAIL MUST APPEAR AS THE      @ZA31937 01860037
.*                     FINAL 6 WORDS OF THE OBTAIN WORK AREA.  @ZA31937 01870037
.*                                                                      01900002
.*      RESTRICTIONS = NONE                                             01950002
.*                                                                      02000002
.*      REGISTER CONVENTIONS = NOT APPLICABLE                           02050002
.*                                                                      02100002
.* PATCH LABEL = NOT APPLICABLE                                         02150002
.*                                                                      02200002
.* MODULE TYPE = MACRO                                                  02250002
.*                                                                      02300002
.*      PROCESSOR = ASSEMBLER LANGUAGE                                  02350002
.*                                                                      02400002
.*      MODULE SIZE = 636 DECIMAL BYTES (NOT INCLUDING THE     @ZA31937 02450037
.*                    PREFIX)                                  @ZA31937 02460037
.*                                                                      02500002
.*      ATTRIBUTES = NOT APPLICABLE                                     02550002
.*                                                                      02600002
.* ENTRY POINT = OBTPREFX                                               02650002
.*                                                                      02700002
.*      PURPOSE = THIS MACRO IS CALLED AT ASSEMBLY TIME BY THE          02750002
.*                FUNCTIONS LISTED UNDER 'NOTES' TO EXPAND THE          02800002
.*                OBTAIN WORK AREA.                                     02850002
.*                                                                      02900002
.*      LINKAGE =                                                       02950002
.*                                                                      03000002
.*                LABEL IECOBTWA EP,F4,D1=(N),ADT=YES                   03050002
.*                                                                      03100002
.*      INPUT =                                                         03150002
.*                                                                      03200002
.*                OPTIONAL LABEL SPECIFIED AS AN ACCEPTABLE ASSEMBLER   03250002
.*                LABEL WHICH CAN BE USED BY THE CALLING CSECT TO       03300002
.*                ESTABLISH ADDRESSABILITY TO THE ENTIRE WORK AREA.     03350002
.*                                                                      03400002
.*                EP IS THE FIRST POSITIONAL PARAMETER, SPECIFYING      03450002
.*                THAT THE LABELS FOR EACH OF THE FIELDS IN THE 8       03500002
.*                WORD PREFIX, THE 20 WORD EXTENDED PREFIX, AND THE 4   03550002
.*                WORD DADSM WTG TABLE ARE TO BE EXPANDED.  IF THESE    03600002
.*                FIELDS ARE NOT TO BE DEFINED, A COMMA MUST BE CODED   03650002
.*                TO INDICATE THE ABSENCE OF THIS PARAMETER.            03700002
.*                                                                      03750002
.*                F4 IS THE SECOND POSITIONAL PARAMETER, SPECIFYING     03800002
.*                THAT THE FIELDS OF THE FORMAT 4 DSCB ARE TO BE        03850002
.*                EXPANDED USING THE IECSDSL1 DSCB MAPPING MACRO.       03900002
.*                IF THESE FIELDS ARE NOT TO BE DEFINED AND A           03950002
.*                KEYWORD PARAMETER FOLLOWS, A COMMA MUST BE            04000002
.*                CODED TO INDICATE THE ABSENCE OF THIS PARAMETER.      04050002
.*                                                                      04100002
.*                D1=(N) IS A KEYWORD PARAMETER THAT SPECIFIES THE      04150002
.*                FORMAT(S) OF THE DSCB(S) TO BE EXPANDED IN THE        04200002
.*                140 BYTE DSCB AREA, USING THE IECSDSL1 MACRO.         04250002
.*                N CAN BE A NUMBER FROM 0 - 6.  IF N IS 0 OR 4 OR      04300002
.*                IF THIS KEYWORD IS OMITTED, NO LABELS ARE EXPANDED.   04350002
.*                IF N IS 1, 2, 3, 5, OR 6, THE CORRESPONDING FORMAT    04400002
.*                OF THE DSCB WILL BE EXPANDED.  MORE THAN ONE DSCB     04450002
.*                FORMAT CAN BE MAPPED IN THE 140 BYTE DSCB AREA        04500002
.*                BY SEPARATING THE FORMAT NUMBERS BY COMMAS.           04550002
.*                                                                      04600002
.*                ADT=YES IS A KEYWORD PARAMETER WHICH SPECIFIES THAT   04650002
.*                THE LABELS FOR EACH OF THE FIELDS WITHIN THE 6 WORD   04700002
.*                DADSM AUDIT TRAIL ARE TO BE EXPANDED.                 04750002
.*                                                                      04800002
.*      OUTPUT = DADSM OBTAIN WORK AREA EXPANSION                       04850002
.*                                                                      04900002
.* EXIT-NORMAL = MEND STATEMENT                                         04950002
.*                                                                      05000002
.* EXIT-ERROR = ERROR MESSAGES:                                         05050002
.*                                                                      05100002
.*      MNOTE 12,***  IHB002 INVALID EP OPERAND SPECIFIED-&EP           05150002
.*      MNOTE 12,***  IHB002 INVALID F4 OPERAND SPECIFIED-&F4           05200002
.*      MNOTE 12,***  IHB002 INVALID ADT OPERAND SPECIFIED-&ADT         05250002
.*                                                                      05300002
.*      THE MACRO EXPANSION CONTINUES AS IF THE INVALID PARAMETER       05350002
.*      WERE NOT SPECIFIED.                                             05400002
.*                                                                      05450002
.* EXTERNAL REFERENCES = NONE                                           05500002
.*                                                                      05550002
.*      ROUTINES = NONE                                                 05600002
.*                                                                      05650002
.*      DATA AREAS = OBTAIN WORK AREA                                   05700002
.*                                                                      05750002
.*      CONTROL BLOCKS = DSCB, ECB, IOB, DCB, DEB                       05800002
.*                                                                      05850002
.* TABLES = NONE                                                        05900002
.*                                                                      05950002
.* MACROS = IECDSECS DSMADT                                             06000002
.*          IECDSECS PREFX                                              06050002
.*          IECSDSL1                                                    06100002
.*          IHBERMAC                                                    06150002
.*                                                                      06200002
.* CHANGE ACTIVITY = SEE DELETIONS/CHANGES PRECEDING THE MACRO          06250002
.*                                                                      06300002
.********************************************************************** 06350002
.*                                                                      06400002
         LCLA  &C                                                       06450002
&LABEL   DSECT                                                          06500002
*********************************************************************** 06550002
*                                                                       06600002
*        OBTAIN WORK AREA                                               06650002
*                                                                       06700002
*********************************************************************** 06750002
         SPACE 1                                                        06800002
*********************************************************************** 06850002
*        PREFIX, EXTENDED PREFIX, AND WTG TABLE AREA                    06900002
*********************************************************************** 06950002
         SPACE 1                                                        07000002
OBTPREFX DS    8F                       WORK AREA PREFIX                07050002
OBTNWKA  DS    20F                      EXTENDED PREFIX                 07100002
OWTGTBL  DS    4F                       DADSM WTG TABLE                 07150002
         AIF   ('&EP' EQ 'EP').OBT0010                                  07200002
         AIF   ('&EP' EQ '').OBT0020                                    07250002
         IHBERMAC 1001,EP,&EP                                           07300002
         AGO   .OBT0020                                                 07350002
.OBT0010 ANOP                                                           07400002
         ORG   OBTPREFX                                                 07450002
*        IECDSECS PREFX,EXPAND=YES                                      07500002
         IECDSECS PREFX,EXPAND=YES                                      07550002
.OBT0020 ANOP                                                           07600002
         SPACE 1                                                        07650002
*********************************************************************** 07700002
*        FORMAT 4 DSCB AREA                                             07750002
*********************************************************************** 07800002
         SPACE 1                                                        07850002
F4DSCB   DS    XL96                     FORMAT 4 DSCB                   07900002
ENQAREA  EQU   F4DSCB+4                 ENQ PARAMETER LIST              07950002
MJELNAME EQU   ENQAREA+12               MAJOR ELEMENT NAME              08000002
         AIF   ('&F4' EQ 'F4').OBT0030                                  08050002
         AIF   ('&F4' EQ '').OBT0040                                    08100002
         IHBERMAC 1001,F4,&F4                                           08150002
         AGO   .OBT0040                                                 08200002
.OBT0030 ANOP                                                           08250002
         ORG   F4DSCB                                                   08300002
*        IECSDSL1 (4)                                                   08350002
         IECSDSL1 (4)                                                   08400002
.OBT0040 ANOP                                                           08450002
         SPACE 1                                                        08500002
*********************************************************************** 08550002
*        140-BYTE DSCB AREA                                             08600002
*********************************************************************** 08650002
         SPACE 1                                                        08700002
OBTDSCB  DS    XL140                    140-BYTE DSCB AREA              08750002
&C       SETA  1                                                        08800002
.OBT0050 AIF   (&C GT N'&D1).OBT0060                                    08850002
         AIF   (&D1(&C) EQ 0 OR &D1(&C) EQ 4).OBT0055                   08900002
         ORG   OBTDSCB                                                  08950002
         MNOTE '         IECSDSL1 (&D1(&C))'                            09000002
         IECSDSL1 (&D1(&C))                                             09050002
.OBT0055 ANOP                                                           09100002
&C       SETA  &C+1                                                     09150002
         AGO   .OBT0050                                                 09200002
.OBT0060 ANOP                                                           09250002
         DS    F                        UNUSED FULLWORD                 09300002
         SPACE 1                                                        09350002
*********************************************************************** 09400002
*        OBTAIN INTERNAL DEB/DCB AREA                                   09450002
*********************************************************************** 09500002
         SPACE 1                                                        09550002
DEB      DS    F                        DEB                             09600002
DCB      DS    11F                      DCB                             09650002
DEBPTR   DS    F                        DCB POINTER TO DEB              09700002
         SPACE 1                                                        09750002
*********************************************************************** 09800002
*        OBTAIN INTERNAL ECB/IOB AREA                                   09850002
*********************************************************************** 09900002
         SPACE 1                                                        09950002
ECB      DS    F                        ECB                             10000002
IOB      DS    8F                       IOB                             10050002
SEEK     DS    D                        DIRECT ACCESS ADDRESS           10100002
         SPACE 1                                                        10150002
*********************************************************************** 10200002
*        OBTAIN CHANNEL CONTROL WORDS AREA                              10250002
*********************************************************************** 10300002
         SPACE 1                                                        10350002
CCW0     DS    D                                                        10400002
CCW1     DS    D                                                        10450002
CCW2     DS    D                                                        10500002
CCW3     DS    D                                                        10550002
CCW4     DS    D                                                        10600002
CCW5     DS    D                                                        10650002
CCW6     DS    D                                                        10700002
CCW7     DS    D                                                        10750002
CCW8     DS    D                                                        10800002
CCW9     DS    D                                                        10850002
CCW10    DS    D                                                        10900002
CCW11    DS    D                                                        10950002
CCW12    DS    D                                                        11000002
         SPACE 1                                                        11050002
*********************************************************************** 11100002
*        OBTAIN WORK AREA FIELDS                                        11150002
*********************************************************************** 11200002
         SPACE 1                                                        11250002
ERCODE   DS    H                        ERROR RETURN CODE               11300002
         CNOP  2,4                      ALIGN DSABQDB TO FULLWORD       11350002
TIOTMNR  DS    CL6                      ASID/DSAB QDB ADDRESS           11400002
DSABQDB  EQU   TIOTMNR+2                DSAB QDB ADDRESS                11450002
TCBADDR  DS    F                        POINTER TO THE TCB              11500002
         DS    H                        UNUSED HALFWORD                 11550002
THETA    DS    XL1                      RPS SECTOR VALUE                11600002
         SPACE 1                                                        11650002
*********************************************************************** 11700002
*        ABSOLUTE TRACK ADDRESSES                                       11750002
*********************************************************************** 11800002
         SPACE 1                                                        11850002
NEWCCHHR DS    XL5                      CCHHR OF THE FORMAT 1 DSCB      11900002
         SPACE 1                                                        11950002
***************************************************************@ZA31937 11954037
*        PROTECTED COPIES OF PARAMETERS PASSED BY CALLER       @ZA31937 11958037
***************************************************************@ZA31937 11962037
         SPACE 1                                               @ZA31937 11966037
OPTIONS  DS    XL4                      OBTAIN OPTION CODE     @ZA31937 11970037
PDSNAME  DS    CL44                     DATA SET NAME FOR      @ZA31937 11974037
*                                       SEARCH OPTION          @ZA31937 11978037
PCCHHR   EQU   PDSNAME ,CL5             CCHHR FOR SEEK OPTION  @ZA31937 11982037
PVOLSER  DS    CL6                      VOLUME SERIAL NUMBER   @ZA31937 11986037
         SPACE 1                                               @ZA31937 11990037
*********************************************************************** 12000002
*        OBTAIN RECOVERY ROUTINE AUDIT TRAIL AREA                       12050002
*********************************************************************** 12100002
         SPACE 1                                                        12150002
OBTADTRL DS    6F                       OBTAIN AUDIT TRAIL WORDS        12200002
         AIF   ('&ADT' EQ 'YES').OBT0090                                12250002
         AIF   ('&ADT' EQ 'NO').OBT0100                                 12300002
         IHBERMAC 1001,ADT,&ADT                                         12350002
         AGO   .OBT0100                                                 12400002
.OBT0090 ANOP                                                           12450002
         ORG   OBTADTRL                                                 12500002
*        IECDSECS DSMADT,EXPAND=YES                                     12550002
         IECDSECS DSMADT,EXPAND=YES                                     12600002
.OBT0100 ANOP                                                           12650002
         SPACE 1                                                        12700002
*********************************************************************** 12750002
OBTWAEND EQU   *                        OBTAIN WORK AREA END            12800002
OWALNGTH EQU   OBTWAEND-OWTGTBL         WORK AREA LENGTH FOR IECRES GET 12850002
*********************************************************************** 12900002
         MEND                                                           12950002
