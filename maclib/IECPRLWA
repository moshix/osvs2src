         MACRO                                                          00050002
&LABEL   IECPRLWA &EP,&F4,&D1=(0),&D2=(0),&ADT=NO                       00100002
.*                                                                      00150002
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 2                      00200002
.*                                                                      00250002
.*                                                                      00300002
.********************************************************************** 00350002
.*                                                                      00400002
.* MODULE NAME = IECPRLWA (VS2-2)                                       00450002
.*                                                                      00500002
.* DESCRIPTIVE NAME = DADSM PARTIAL RELEASE WORK AREA ADDRESSING MACRO  00550002
.*                                                                      00600002
.* COPYRIGHT = NONE                                                     00650002
.*                                                                      00700002
.* STATUS = CHANGE LEVEL 000                                            00750002
.*                                                                      00800002
.* FUNCTION = DADSM PARTIAL RELEASE MACRO WHICH PROVIDES ADDRESSABILITY 00850002
.*            TO THE 620 DECIMAL BYTE WORK AREA (NOT INCLUDING THE      00900002
.*            8 WORD PREFIX OR THE 6 WORD RECOVERY ROUTINE PARAMETER    00950002
.*            LIST) USED BY PARTIAL RELEASE.                            01000002
.*                                                                      01050002
.* NOTES = THIS MACRO IS AN INTERNAL MACRO USED IN THE FOLLOWING        01100002
.*         DADSM FUNCTIONS:                                             01150002
.*                                                                      01200002
.*             PARTIAL RELEASE                                          01250002
.*                                                                      01300002
.*         TO REFERENCE THIS WORK AREA WITHIN THE FUNCTIONS LISTED      01350002
.*         ABOVE, CODE A USING STATEMENT ON THE LABEL 'PRLSEWKA'.       01400002
.*                                                                      01450002
.*      DEPENDENCIES = THIS MACRO IS DESIGNED TO BE USED OPTIONALLY     01500002
.*                     WITH THE IECSDSL1 DSCB MAPPING MACRO, THE        01550002
.*                     PREFX MACRO (DEFINED IN THE IECDSECS MACRO)      01600002
.*                     WHICH DEFINES THE 8 WORD WORK AREA PREFIX,       01650002
.*                     THE 20 WORD EXTENDED WORK AREA PREFIX, AND       01700002
.*                     THE 4 WORD DADSM WTG TABLE, AND THE DSMADT       01750002
.*                     MACRO (ALSO DEFINED IN THE IECDSECS MACRO)       01800002
.*                     WHICH DEFINES THE 6 WORD DADSM AUDIT TRAIL.      01850002
.*                                                                      01900002
.*      RESTRICTIONS = NONE                                             01950002
.*                                                                      02000002
.*      REGISTER CONVENTIONS = NOT APPLICABLE                           02050002
.*                                                                      02100002
.* PATCH LABEL = NOT APPLICABLE                                         02150002
.*                                                                      02200002
.* MODULE TYPE = MACRO                                                  02250002
.*                                                                      02300002
.*      PROCESSOR = ASSEMBLER LANGUAGE                                  02350002
.*                                                                      02400002
.*      MODULE SIZE = 620 DECIMAL BYTES (NOT INCLUDING THE PREFIX)      02450002
.*                                                                      02500002
.*      ATTRIBUTES = NOT APPLICABLE                                     02550002
.*                                                                      02600002
.* ENTRY POINT = PRLPREFX                                               02650002
.*                                                                      02700002
.*      PURPOSE = THIS MACRO IS CALLED AT ASSEMBLY TIME BY THE          02750002
.*                FUNCTIONS LISTED UNDER 'NOTES' TO EXPAND THE          02800002
.*                PARTIAL RELEASE WORK AREA.                            02850002
.*                                                                      02900002
.*      LINKAGE =                                                       02950002
.*                                                                      03000002
.*                LABEL IECPRLWA EP,F4,D1=(N),D2=(N),ADT=YES            03050002
.*                                                                      03100002
.*      INPUT =                                                         03150002
.*                                                                      03200002
.*                OPTIONAL LABEL SPECIFIED AS AN ACCEPTABLE ASSEMBLER   03250002
.*                LABEL WHICH CAN BE USED BY THE CALLING CSECT TO       03300002
.*                ESTABLISH ADDRESSABILITY TO THE ENTIRE WORK AREA.     03350002
.*                                                                      03400002
.*                EP IS THE FIRST POSITIONAL PARAMETER, SPECIFYING      03450002
.*                THAT THE LABELS FOR EACH OF THE FIELDS IN THE 8       03500002
.*                WORD PREFIX, 20 WORD EXTENDED PREFIX, AND THE 4       03550002
.*                WORD DADSM WTG TABLE ARE TO BE EXPANDED. IF THESE     03600002
.*                FIELDS ARE NOT TO BE DEFINED, A COMMA MUST BE         03650002
.*                CODED TO INDICATE THE ABSENCE OF THIS PARAMETER.      03700002
.*                                                                      03750002
.*                F4 IS THE SECOND POSITIONAL PARAMETER, SPECIFYING     03800002
.*                THAT THE FIELDS OF THE FORMAT 4 DSCB ARE TO BE        03850002
.*                EXPANDED USING THE IECSDSL1 DSCB MAPPING MACRO.       03900002
.*                IF THESE FIELDS ARE NOT TO BE DEFINED AND A           03950002
.*                KEYWORD PARAMETER FOLLOWS, A COMMA MUST BE            04000002
.*                CODED TO INDICATE THE ABSENCE OF THIS PARAMETER.      04050002
.*                                                                      04100002
.*                D1=(N) IS A KEYWORD PARAMETER THAT SPECIFIES THE      04150002
.*                FORMAT(S) OF THE DSCB(S) TO BE EXPANDED IN THE        04200002
.*                FIRST 140 BYTE DSCB AREA, USING THE IECSDSL1 MACRO.   04250002
.*                N CAN BE A NUMBER FROM 0 - 6.  IF N IS 0 OR 4 OR      04300002
.*                IF THIS KEYWORD IS OMITTED, NO LABELS ARE EXPANDED.   04350002
.*                IF N IS 1, 2, 3, 5, OR 6, THE CORRESPONDING FORMAT    04400002
.*                OF THE DSCB WILL BE EXPANDED.  MORE THAN ONE DSCB     04450002
.*                FORMAT CAN BE MAPPED IN THE FIRST 140 BYTE DSCB       04500002
.*                AREA BY SEPARATING THE FORMAT NUMBERS BY COMMAS.      04550002
.*                                                                      04600002
.*                D2=(N) IS A KEYWORD PARAMETER THAT SPECIFIES          04650002
.*                THE FORMAT(S) OF THE DSCB(S) TO BE EXPANDED IN THE    04700002
.*                SECOND 140 BYTE DSCB AREA, USING THE IECSDSL1 MACRO.  04750002
.*                N CAN BE A NUMBER FROM 0 - 6.  IF N IS 0 OR 4 OR      04800002
.*                IF THIS KEYWORD IS OMITTED, NO LABELS ARE EXPANDED.   04850002
.*                IF N IS 1, 2, 3, 5, OR 6, THE CORRESPONDING FORMAT    04900002
.*                OF THE DSCB WILL BE EXPANDED.  MORE THAN ONE DSCB     04950002
.*                FORMAT CAN BE MAPPED IN THE SECOND 140 BYTE DSCB      05000002
.*                AREA BY SEPARATING THE FORMAT NUMBERS BY COMMAS.      05050002
.*                                                                      05100002
.*                ADT=YES IS A KEYWORD PARAMETER WHICH SPECIFIES THAT   05150002
.*                THE LABELS FOR EACH OF THE FIELDS WITHIN THE 6 WORD   05200002
.*                DADSM AUDIT TRAIL ARE TO BE EXPANDED.                 05250002
.*                                                                      05300002
.*      OUTPUT = DADSM PARTIAL RELEASE WORK AREA EXPANSION              05350002
.*                                                                      05400002
.* EXIT-NORMAL = MEND STATEMENT                                         05450002
.*                                                                      05500002
.* EXIT-ERROR = ERROR MESSAGES:                                         05550002
.*                                                                      05600002
.*      MNOTE 12,***  IHB002 INVALID EP OPERAND SPECIFIED-&EP           05650002
.*      MNOTE 12,***  IHB002 INVALID F4 OPERAND SPECIFIED-&F4           05700002
.*      MNOTE 12,***  IHB002 INVALID ADT OPERAND SPECIFIED-&ADT         05750002
.*                                                                      05800002
.*      THE MACRO EXPANSION CONTINUES AS IF THE INVALID PARAMETER       05850002
.*      WERE NOT SPECIFIED.                                             05900002
.*                                                                      05950002
.* EXTERNAL REFERENCES = NONE                                           06000002
.*                                                                      06050002
.*      ROUTINES = NONE                                                 06100002
.*                                                                      06150002
.*      DATA AREAS = PARTIAL RELEASE WORK AREA                          06200002
.*                                                                      06250002
.*      CONTROL BLOCKS = DSCB                                           06300002
.*                                                                      06350002
.* TABLES = NONE                                                        06400002
.*                                                                      06450002
.* MACROS = IECDSECS DSMADT                                             06500002
.*          IECDSECS PREFX                                              06550002
.*          IECSDSL1                                                    06600002
.*          IHBERMAC                                                    06650002
.*                                                                      06700002
.* CHANGE ACTIVITY = SEE DELETIONS/CHANGES PRECEDING THE MACRO          06750002
.*                                                                      06800002
.********************************************************************** 06850002
.*                                                                      06900002
         LCLA  &C                                                       06950002
&LABEL   DSECT                                                          07000002
*********************************************************************** 07050002
*                                                                       07100002
*        PARTIAL RELEASE WORK AREA                                      07150002
*                                                                       07200002
*********************************************************************** 07250002
         SPACE 1                                                        07300002
*********************************************************************** 07350002
*        PREFIX, EXTENDED PREFIX, AND WTG TABLE AREA                    07400002
*********************************************************************** 07450002
         SPACE 1                                                        07500002
PRLPREFX DS    8F                       WORK AREA PREFIX                07550002
PRLSEWKA DS    20F                      EXTENDED PREFIX                 07600002
FIRST    EQU   PRLSEWKA                                                 07650002
PWTGTBL  DS    4F                       DADSM WTG TABLE                 07700002
         AIF   ('&EP' EQ 'EP').PRL0010                                  07750002
         AIF   ('&EP' EQ '').PRL0020                                    07800002
         IHBERMAC 1001,EP,&EP                                           07850002
         AGO   .PRL0020                                                 07900002
.PRL0010 ANOP                                                           07950002
         ORG   PRLPREFX                                                 08000002
         MNOTE '         IECDSECS PREFX,EXPAND=YES'                     08050002
         IECDSECS PREFX,EXPAND=YES                                      08100002
.PRL0020 ANOP                                                           08150002
         SPACE 1                                                        08200002
*********************************************************************** 08250002
*        FORMAT 4 DSCB AREA                                             08300002
*********************************************************************** 08350002
         SPACE 1                                                        08400002
VTOCDSCB DS    XL96                     FORMAT 4 DSCB                   08450002
         AIF   ('&F4' EQ 'F4').PRL0030                                  08500002
         AIF   ('&F4' EQ '').PRL0040                                    08550002
         IHBERMAC 1001,F4,&F4                                           08600002
         AGO   .PRL0040                                                 08650002
.PRL0030 ANOP                                                           08700002
         ORG   VTOCDSCB                                                 08750002
         MNOTE '         IECSDSL1 (4)'                                  08800002
         IECSDSL1 (4)                                                   08850002
.PRL0040 ANOP                                                           08900002
         SPACE 1                                                        08950002
*********************************************************************** 09000002
*        FIRST 140-BYTE DSCB AREA                                       09050002
*********************************************************************** 09100002
         SPACE 1                                                        09150002
INDSCB   DS    XL140                    140-BYTE DSCB AREA              09200002
&C       SETA  1                                                        09250002
.PRL0050 AIF   (&C GT N'&D1).PRL0060                                    09300002
         AIF   (&D1(&C) EQ 0 OR &D1(&C) EQ 4).PRL0055                   09350002
         ORG   INDSCB                                                   09400002
         MNOTE '         IECSDSL1 (&D1(&C))'                            09450002
         IECSDSL1 (&D1(&C))                                             09500002
.PRL0055 ANOP                                                           09550002
&C       SETA  &C+1                                                     09600002
         AGO   .PRL0050                                                 09650002
.PRL0060 ANOP                                                           09700002
         SPACE 1                                                        09750002
*********************************************************************** 09800002
*        SECOND 140-BYTE DSCB AREA                                      09850002
*********************************************************************** 09900002
         SPACE 1                                                        09950002
OUTDSCB  DS    XL140                    140-BYTE DSCB AREA              10000002
&C       SETA  1                                                        10050002
.PRL0070 AIF   (&C GT N'&D2).PRL0080                                    10100002
         AIF   (&D2(&C) EQ 0 OR &D2(&C) EQ 4).PRL0075                   10150002
         ORG   OUTDSCB                                                  10200002
         MNOTE '         IECSDSL1 (&D2(&C))'                            10250002
         IECSDSL1 (&D2(&C))                                             10300002
.PRL0075 ANOP                                                           10350002
&C       SETA  &C+1                                                     10400002
         AGO   .PRL0070                                                 10450002
.PRL0080 ANOP                                                           10500002
         SPACE 1                                                        10550002
*********************************************************************** 10600002
*        DADSM EXTENT TABLE                                             10650002
*********************************************************************** 10700002
         SPACE 1                                                        10750002
DADSMTBL DS    0XL68                    DADSM EXTENT TABLE              10800002
PTYPEFLG DS    XL1                      PARTIAL RELEASE SWITCHES        10850002
*                                                                       10900002
ERXTENT  EQU   X'80'                    OVERLAPPING EXTENTS FOUND IN F5 11250002
*                                       DSCB                            11260002
FROMCR   EQU   X'40'                    ENTERED FROM CHECKPOINT/RESTART 11300002
DCHNSRCH EQU   X'20'                    DEB CHAIN SEARCH ACTIVE         11310002
RNDXTENT EQU   X'01'                    ROUND THE EXTENT TO BE RELEASED 11350002
*                                       TO A CYLINDER BOUNDARY          11400002
*                                                                       11410002
EXTNUM   DS    XL1                      NUMBER OF ENTRIES IN DADSMTBL   11450002
HOLENUM  DS    H                        NUMBER OF CREATED F0 DSCB'S     11500002
ENTRIES  DS    16F                      SIXTEEN EXTENT ENTRIES          11550002
         SPACE 1                                                        11600002
*********************************************************************** 11650002
*        PARTIAL RELEASE WORK AREA FIELDS                               11700002
*********************************************************************** 11750002
         SPACE 1                                                        11800002
HOLDEXTS DS    F                        EXTENT SAVE AREA                11850002
HOLD     DS    2F                       EXTENT SAVE AREA                11900002
MBBCCHHR DS    D                        CONVERSION AREA                 11950002
SVRBPTR  DS    F                        POINTER TO THE SVRB             12000002
DCBSAVE  DS    F                        USER'S DCB ADDRESS              12050002
CONVCON  DS    H                        CONVERSION WORK AREA            12100002
USEXTNUM DS    XL1                      USER'S EXTENT NUMBER            12150002
*                                       (FROM DCBFDAD)                  12160002
         SPACE 1                                                        12200002
*********************************************************************** 12250002
*        ABSOLUTE TRACK ADDRESSES                                       12300002
*********************************************************************** 12350002
         SPACE 1                                                        12400002
VTOCADR  DS    XL5                      FORMAT 4 CCHHR                  12450002
DADSMADR DS    XL5                      FORMAT 5 CCHHR                  12500002
INCCHHR  DS    XL5                      CHANNEL PROGRAM WORK AREA       12550002
OUTCCHHR DS    XL5                      CHANNEL PROGRAM WORK AREA       12600002
NEXTDADS DS    XL5                      CHANNEL PROGRAM WORK AREA       12650002
         SPACE 1                                                        12700002
*********************************************************************** 12750002
*        PARTIAL RELEASE RECOVERY ROUTINE AUDIT TRAIL AREA              12800002
*********************************************************************** 12850002
         SPACE 1                                                        12900002
PRLADTRL DS    6F                       RELEASE AUDIT TRAIL WORDS       12950002
         AIF   ('&ADT' EQ 'YES').PRL0090                                13000002
         AIF   ('&ADT' EQ 'NO').PRL0100                                 13050002
         IHBERMAC 1001,ADT,&ADT                                         13100002
         AGO   .PRL0100                                                 13150002
.PRL0090 ANOP                                                           13200002
         ORG   PRLADTRL                                                 13250002
         MNOTE '         IECDSECS DSMADT,EXPAND=YES'                    13300002
         IECDSECS DSMADT,EXPAND=YES                                     13350002
.PRL0100 ANOP                                                           13400002
         SPACE 1                                                        13450002
*********************************************************************** 13500002
PRLWAEND EQU   *                        PARTIAL RELEASE WORK AREA END   13550002
PWALNGTH EQU   PRLWAEND-PWTGTBL         WORK AREA LENGTH FOR IECRES GET 13600002
*********************************************************************** 13650002
         MEND                                                           13700002
