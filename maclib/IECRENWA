         MACRO                                                          00050000
&LABEL   IECRENWA &EP,&F4,&D1=(0),&ADT=NO                               00100000
.*                                                                      00150000
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 038                    00160000
.*                                                                      00170000
.* $01=OZ65137,EDM1102,B820312,STLMDB: FIX FOR ENQ LOCKOUT PROB    @01A 00180000
.*                                                                      00190000
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 2                      00200000
.*0000                                                           YM1337 00210000
.*0000                                                           YM7036 00220000
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 3             @ZM30032 00230000
.*0000                                                         @Y30LSTG 00240000
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 3.7           @ZA29709 00250000
.*                                                             @ZA29709 00260000
.*       SU 32 ADDTIONS                                        @G32DSPD 00270000
.*0000009000-010000,022000-023000,029000               DELETED @ZA31909 00271000
.*0000009000-009600,019060-019120,022000-022600        ADDED   @ZA31909 00277000
.*0000029000-029100,152040-152400                      ADDED   @ZA31909 00284000
.*                                                                      00300000
.*        SU60 DELETIONS/CHANGES                               @G60ASBJ 00310000
.*A 159200                                                     @G60ASBJ 00320000
.*C 159200                                                     @ZA43918 00324900
.*                                                             @G60ASBJ 00330000
.********************************************************************** 00350000
.*                                                                      00400000
.* MODULE NAME = IECRENWA (VS2-2)                                       00450000
.*                                                                      00500000
.* DESCRIPTIVE NAME = DADSM RENAME WORK AREA ADDRESSING MACRO           00550000
.*                                                                      00600000
.* COPYRIGHT = NONE                                                     00650000
.*                                                                      00700000
.* STATUS = CHANGE LEVEL 000                                            00750000
.*                                                                      00800000
.* FUNCTION = DADSM RENAME MACRO WHICH PROVIDES ADDRESSABILITY          00850000
.*            TO THE 720 DECIMAL BYTE WORK AREA (NOT INCLUDING @ZA31909 00900000
.*            THE 8 WORD PREFIX OR THE 6 WORD RECOVERY ROUTINE @ZA31909 00930000
.*            PARAMETER LIST) USED BY RENAME.                  @ZA31909 00960000
.*                                                                      01050000
.* NOTES = THIS MACRO IS AN INTERNAL MACRO USED IN THE FOLLOWING        01100000
.*         DADSM FUNCTIONS:                                             01150000
.*                                                                      01200000
.*             RENAME                                                   01250000
.*             SCRATCH AND RENAME VOLUME MOUNTING ROUTINE               01300000
.*                                                                      01350000
.*         TO REFERENCE THIS WORK AREA WITHIN THE FUNCTIONS LISTED      01400000
.*         ABOVE, CODE A USING STATEMENT ON THE LABEL 'RENAMWKA'.       01450000
.*                                                                      01500000
.*      DEPENDENCIES = THIS MACRO IS DESIGNED TO BE USED OPTIONALLY     01550000
.*                     WITH THE IECSDSL1 DSCB MAPPING MACRO, THE        01600000
.*                     PREFX MACRO (DEFINED IN THE IECDSECS MACRO)      01650000
.*                     WHICH DEFINES THE 8 WORD WORK AREA PREFIX,       01700000
.*                     THE 20 WORD EXTENDED WORK AREA PREFIX, AND       01750000
.*                     THE 4 WORD DADSM WTG TABLE, AND THE DSMADT       01800000
.*                     MACRO (ALSO DEFINED IN THE IECDSECS MACRO)       01850000
.*                     WHICH DEFINES THE 6 WORD DADSM AUDIT TRAIL.      01900000
.*                     THE AUDIT TRAIL MUST APPEAR AS THE      @ZA31909 01906000
.*                     FINAL 6 WORDS OF THE RENAME WORK AREA.  @ZA31909 01912000
.*                                                                      01920000
.*                     THE FIRST 364 BYTES OF THE SCRATCH AND RENAME    01950000
.*                     WORK AREAS (ADDRESSED RESPECTIVELY BY MAPPING    02000000
.*                     MACROS IECSCRWA AND IECRENWA) ARE MAPPED         02050000
.*                     IDENTICALLY - 8 WORD PREFIX, 20 WORD EXTENDED    02100000
.*                     PREFIX, 4 WORD DADSM WTG TABLE, 96 BYTE DSCB     02150000
.*                     AREA, AND 140 BYTE DSCB AREA.  THE 250  @ZA31909 02200000
.*                     BYTES FROM LABELS 'WKADEB' THROUGH      @ZA31909 02230000
.*                     'PVOLSER' ARE ALSO MAPPED IDENTICALLY.  @ZA31909 02260000
.*                                                                      02350000
.*      RESTRICTIONS = NONE                                             02400000
.*                                                                      02450000
.*      REGISTER CONVENTIONS = NOT APPLICABLE                           02500000
.*                                                                      02550000
.* PATCH LABEL = NOT APPLICABLE                                         02600000
.*                                                                      02650000
.* MODULE TYPE = MACRO                                                  02700000
.*                                                                      02750000
.*      PROCESSOR = ASSEMBLER LANGUAGE                                  02800000
.*                                                                      02850000
.*      MODULE SIZE = 744 DECIMAL BYTES (NOT INCLUDING THE     @ZA31909 02900000
.*                    PREFIX)                                  @ZA31909 02910000
.*                                                                      02950000
.*      ATTRIBUTES = NOT APPLICABLE                                     03000000
.*                                                                      03050000
.* ENTRY POINT = RENPREFX                                               03100000
.*                                                                      03150000
.*      PURPOSE = THIS MACRO IS CALLED AT ASSEMBLY TIME BY THE          03200000
.*                FUNCTIONS LISTED UNDER 'NOTES' TO EXPAND THE          03250000
.*                RENAME WORK AREA.                                     03300000
.*                                                                      03350000
.*      LINKAGE =                                                       03400000
.*                                                                      03450000
.*                LABEL IECRENWA EP,F4,D1=(N),ADT=YES                   03500000
.*                                                                      03550000
.*      INPUT =                                                         03600000
.*                                                                      03650000
.*                OPTIONAL LABEL SPECIFIED AS AN ACCEPTABLE ASSEMBLER   03700000
.*                LABEL WHICH CAN BE USED BY THE CALLING CSECT TO       03750000
.*                ESTABLISH ADDRESSABILITY TO THE ENTIRE WORK AREA.     03800000
.*                                                                      03850000
.*                EP IS THE FIRST POSITIONAL PARAMETER, SPECIFYING      03900000
.*                THAT THE LABELS FOR EACH OF THE FIELDS IN THE 8       03950000
.*                WORD PREFIX, THE 20 WORD EXTENDED PREFIX, AND THE 4   04000000
.*                WORD DADSM WTG TABLE ARE TO BE EXPANDED.  IF THESE    04050000
.*                FIELDS ARE NOT TO BE DEFINED, A COMMA MUST BE CODED   04100000
.*                TO INDICATE THE ABSENCE OF THIS PARAMETER.            04150000
.*                                                                      04200000
.*                F4 IS THE SECOND POSITIONAL PARAMETER, SPECIFYING     04250000
.*                THAT THE FIELDS OF THE FORMAT 4 DSCB ARE TO BE        04300000
.*                EXPANDED USING THE IECSDSL1 DSCB MAPPING MACRO.       04350000
.*                IF THESE FIELDS ARE NOT TO BE DEFINED AND A           04400000
.*                KEYWORD PARAMETER FOLLOWS, A COMMA MUST BE            04450000
.*                CODED TO INDICATE THE ABSENCE OF THIS PARAMETER.      04500000
.*                                                                      04550000
.*                D1=(N) IS A KEYWORD PARAMETER THAT SPECIFIES THE      04600000
.*                FORMAT(S) OF THE DSCB(S) TO BE EXPANDED IN THE        04650000
.*                140 BYTE DSCB AREA, USING THE IECSDSL1 MACRO.         04700000
.*                N CAN BE A NUMBER FROM 0 - 6.  IF N IS 0 OR 4 OR      04750000
.*                IF THIS KEYWORD IS OMITTED, NO LABELS ARE EXPANDED.   04800000
.*                IF N IS 1, 2, 3, 5, OR 6, THE CORRESPONDING FORMAT    04850000
.*                OF THE DSCB WILL BE EXPANDED.  MORE THAN ONE DSCB     04900000
.*                FORMAT CAN BE MAPPED IN THE 140 BYTE DSCB AREA        04950000
.*                BY SEPARATING THE FORMAT NUMBERS BY COMMAS.           05000000
.*                                                                      05050000
.*                ADT=YES IS A KEYWORD PARAMETER WHICH SPECIFIES THAT   05100000
.*                THE LABELS FOR EACH OF THE FIELDS WITHIN THE 6 WORD   05150000
.*                DADSM AUDIT TRAIL ARE TO BE EXPANDED.                 05200000
.*                                                                      05250000
.*      OUTPUT = DADSM RENAME WORK AREA EXPANSION                       05300000
.*                                                                      05350000
.* EXIT-NORMAL = MEND STATEMENT                                         05400000
.*                                                                      05450000
.* EXIT-ERROR = ERROR MESSAGES:                                         05500000
.*                                                                      05550000
.*      MNOTE 12,***  IHB002 INVALID EP OPERAND SPECIFIED-&EP           05600000
.*      MNOTE 12,***  IHB002 INVALID F4 OPERAND SPECIFIED-&F4           05650000
.*      MNOTE 12,***  IHB002 INVALID ADT OPERAND SPECIFIED-&ADT         05700000
.*                                                                      05750000
.*      THE MACRO EXPANSION CONTINUES AS IF THE INVALID PARAMETER       05800000
.*      WERE NOT SPECIFIED.                                             05850000
.*                                                                      05900000
.* EXTERNAL REFERENCES = NONE                                           05950000
.*                                                                      06000000
.*      ROUTINES = NONE                                                 06050000
.*                                                                      06100000
.*      DATA AREAS = RENAME WORK AREA                                   06150000
.*                                                                      06200000
.*      CONTROL BLOCKS = DSCB, ECB, IOB, DCB, DEB                       06250000
.*                                                                      06300000
.* TABLES = NONE                                                        06350000
.*                                                                      06400000
.* MACROS = IECDSECS DSMADT                                             06450000
.*          IECDSECS PREFX                                              06500000
.*          IECSDSL1                                                    06550000
.*          IHBERMAC                                                    06600000
.*                                                                      06650000
.* CHANGE ACTIVITY = SEE DELETIONS/CHANGES PRECEDING THE MACRO PROLOGUE 06700000
.*                                                                      06750000
.********************************************************************** 06800000
.*                                                                      06850000
         LCLA  &C                                                       06900000
&LABEL   DSECT                                                          06950000
*********************************************************************** 07000000
*                                                                       07050000
*        RENAME WORK AREA                                               07100000
*                                                                       07150000
*********************************************************************** 07200000
         SPACE 1                                                        07250000
*********************************************************************** 07300000
*        PREFIX, EXTENDED PREFIX, AND WTG TABLE AREA                    07350000
*********************************************************************** 07400000
         SPACE 1                                                        07450000
RENPREFX DS    8F                       WORK AREA PREFIX                07500000
RENAMWKA DS    20F                      EXTENDED PREFIX                 07550000
FIRST    EQU   RENAMWKA                                                 07600000
RWTGTBL  DS    4F                       DADSM WTG TABLE                 07650000
         AIF   ('&EP' EQ 'EP').REN0010                                  07700000
         AIF   ('&EP' EQ '').REN0020                                    07750000
         IHBERMAC 1001,EP,&EP                                           07800000
         AGO   .REN0020                                                 07850000
.REN0010 ANOP                                                           07900000
         ORG   RENPREFX                                                 07950000
*        IECDSECS PREFX,EXPAND=YES                                      08000000
         IECDSECS PREFX,EXPAND=YES                                      08050000
.REN0020 ANOP                                                           08100000
         SPACE 1                                                        08150000
*********************************************************************** 08200000
*        FORMAT 4 DSCB AREA                                             08250000
*********************************************************************** 08300000
         SPACE 1                                                        08350000
VTOCDSCB DS    XL96                     FORMAT 4 DSCB                   08400000
ENQAREA  EQU   VTOCDSCB+4               ENQ PARAMETER LIST              08500000
MJNAME   EQU   ENQAREA+16               MAJOR ELEMENT NAME              08550000
MINAME   EQU   ENQAREA+24               MINOR ELEMENT NAME              08600000
         AIF   ('&F4' EQ 'F4').REN0030                                  08650000
         AIF   ('&F4' EQ '').REN0040                                    08700000
         IHBERMAC 1001,F4,&F4                                           08750000
         AGO   .REN0040                                                 08800000
.REN0030 ANOP                                                           08850000
         ORG   VTOCDSCB                                                 08900000
*        IECSDSL1 (4)                                                   08950000
         IECSDSL1 (4)                                                   09000000
.REN0040 ANOP                                                           09050000
         SPACE 1                                                        09100000
*********************************************************************** 09150000
*        140-BYTE DSCB AREA                                             09200000
*********************************************************************** 09250000
         SPACE 1                                                        09300000
F1DSCB   DS    XL140                    140-BYTE DSCB AREA              09350000
&C       SETA  1                                                        09400000
.REN0050 AIF   (&C GT N'&D1).REN0060                                    09450000
         AIF   (&D1(&C) EQ 0 OR &D1(&C) EQ 4).REN0055                   09500000
         ORG   F1DSCB                                                   09550000
         MNOTE '         IECSDSL1 (&D1(&C))'                            09600000
         IECSDSL1 (&D1(&C))                                             09650000
.REN0055 ANOP                                                           09700000
&C       SETA  &C+1                                                     09750000
         AGO   .REN0050                                                 09800000
.REN0060 ANOP                                                           09850000
         DS    F                        UNUSED FULLWORD                 09900000
         SPACE 1                                                        09950000
*********************************************************************** 10000000
*        RENAME INTERNAL DEB/DCB AREA                                   10050000
*********************************************************************** 10100000
         SPACE 1                                                        10150000
WKADEB   DS    F                        DEB                             10200000
DCB      DS    11F                      DCB                             10250000
DEBPTR   DS    F                        DCB POINTER TO DEB              10300000
         SPACE 1                                                        10350000
*********************************************************************** 10400000
*        RENAME INTERNAL ECB/IOB AREA                                   10450000
*********************************************************************** 10500000
         SPACE 1                                                        10550000
ECB      DS    F                        ECB                             10600000
IOB      DS    8F                       IOB                             10650000
SEEK     DS    D                        DIRECT ACCESS ADDRESS           10700000
         SPACE 1                                                        10750000
*********************************************************************** 10800000
*        RENAME CHANNEL CONTROL WORDS AREA                              10850000
*********************************************************************** 10900000
         SPACE 1                                                        10950000
CCW1     DS    D                                                        11000000
CCW2     DS    D                                                        11050000
CCW3     DS    D                                                        11100000
CCW4     DS    D                                                        11150000
CCW5     DS    D                                                        11200000
CCW6     DS    D                                                        11250000
CCW7     DS    D                                                        11300000
CCW8     DS    D                                                        11350000
CCW9     DS    D                                                        11400000
CCW10    DS    D                                                        11450000
CCW11    DS    D                                                        11500000
CCW12    DS    D                                                        11550000
         SPACE 1                                                        11600000
*********************************************************************** 11650000
*        RENAME WORK AREA FIELDS                                        11700000
*********************************************************************** 11750000
         SPACE 1                                                        11800000
FIELD1   DS    D                        PASSWORD WORK AREA FIELD        11850000
OLDPLPTR DS    F                        PARAMETER LIST ADDRESS          11900000
PRUCBPTR DS    F                        PRIMARY (DEMOUNTABLE) UCB ADDR  11950000
VOLPTR   DS    F                        NEXT VOLUME LIST ENTRY ADDRESS  12000000
TCBADDR  DS    F                        POINTER TO THE TCB              12050000
DSABADDR DS    F                        DSAB ADDRESS OF CURRENT VOLUME  12100000
PRDSABAD DS    F                        DSAB ADDRESS OF PRIMARY UCB     12150000
VOLNUM   DS    H                        NUMBER OF VOLUMES REMAINING     12200000
         CNOP  2,4                      ALIGN DSABQDB TO FULLWORD       12250000
TIOTMNR  DS    CL6                      ASID/DSAB QDB ADDRESS           12300000
DSABQDB  EQU   TIOTMNR+2                DSAB QDB ADDRESS                12350000
VOLMINOR DS    CL6                      VOLSER FOR DEQ ON SYSZVOLS      12400000
ERCODE   DS    H                        ERROR RETURN CODE               12450000
SISW1    DS    XL1                      SECURITY/INTEGRITY SWITCHES     12500000
*                                                                       12550000
*        DEFINITION OF SECURITY/INTEGRITY SWITCH 1 BIT SETTINGS         12600000
*                                                                       12650000
*        BIT 0 = ENQ ON SYSTIOT ISSUED                                  12700000
*        BIT 1 = ENQ ON SYSDSN ISSUED                                   12750000
*        BIT 2 = ENQ ON SYSZVOLS ISSUED                                 12800000
*        BIT 3 = DEQ ON SYSZVOLS TO BE ISSUED                           12850000
*        BIT 4 = UCB DYNAMICALLY ALLOCATED                              12900000
*        BIT 5 = DEB SEARCH REQUIRED                                    12950000
*        BIT 6 = NO SMF RECORD TO BE BUILT                              13000000
*        BIT 7 = RACHECK SAID USER HAS VOLUME AUTHORIZATION    @G32DSPD 13020000
*                                                                       13050000
TIOTENQ  EQU   X'80'                    TIOT ENQ'ED INDICATOR           13100000
DSNENQ   EQU   X'40'                    DSN ENQ'ED INDICATOR            13150000
VOLENQ   EQU   X'20'                    VOLUME ENQ'ED INDICATOR         13200000
VOLDEQ   EQU   X'10'                    VOLUME TO BE DEQ'ED INDICATOR   13250000
UCBALLOC EQU   X'08'                    UCB ALLOCATED INDICATOR         13300000
DEBSRCH  EQU   X'04'                    DEB SEARCH REQUIRED INDICATOR   13350000
NOSMFRCD EQU   X'02'                    NO SMF RECORD INDICATOR         13400000
VOLRACHK EQU   X'01'                    VOL RACHECKED OK       @G32DSPD 13420000
*                                                                       13450000
SISW2    DS    XL1                      SECURITY/INTEGRITY SWITCHES     13500000
*                                                                       13550000
*        DEFINITION OF SECURITY/INTEGRITY SWITCH 2 BIT SETTINGS         13600000
*                                                                       13650000
*        BIT 0 = VOLUME TO BE DEMOUNTED                                 13700000
*        BIT 1 = VOLUME TO BE MOUNTED AND VERIFIED                      13750000
*        BIT 2 = VOLUME TO BE VERIFIED                                  13800000
*        BIT 3 = ALLOCATED UNIT HAS BEEN FOUND                          13810000
*        BIT 4 = MOUNT WAS ISSUED FOR VIRTUAL PACK             @Y30LSTG 13820000
*                                                                       13850000
DEMNTSW  EQU   X'80'                    VOLUME DEMOUNT INDICATOR        13900000
MOUNTSW  EQU   X'40'                    VOLUME MOUNT/VERIFY INDICATOR   13950000
VERIFYSW EQU   X'20'                    VOLUME VERIFY INDICATOR         14000000
UCBFNDSW EQU   X'10'                    ALLOCATED UCB FOUND INDICATOR   14010000
VIRTMNT  EQU   X'08'                    VIRTUAL MOUNT ISSUED   @Y30LSTG 14020000
*                                                                       14050000
         DS    H                        UNUSED                          14060000
PVOLSER  DS    CL6                      PROTECTED VOLUME SERIAL         14070000
DSRENOPT DS    F                        RENAME OPTION CODE     @ZA29709 14080000
DSRENOAD DS    F                        POINT TO RENAME OLD DSN@ZA29709 14081000
DSRENNAD DS    F                        POINT TO RENAME NEW DSN@ZA29709 14082000
DSRENVAD DS    F                        POINT TO RENAME VOLIST @ZA29709 14083000
PDSNAME  DS    CL44                     PROTECTED DS OLD NAME  @ZA29709 14088000
PDSNNAME DS    CL44                     PROTECTED DS NEW NAME  @ZA29709 14090000
         DS    XL1                      UNUSED BYTE                     14100000
RTYPEFLG DS    XL1                      RENAME SWITCHES                 14150000
*                                                                       14200000
*        DEFINITION OF RENAME SWITCH BIT SETTINGS                       14250000
*                                                                       14300000
*        BIT 1 = RACF SAYS OK TO RENAME - REDRIVE FLAG             @01A 14325000
*        BIT 3 = NO VOLUME MOUNTED AND NO DEVICE ALLOCATED              14350000
*        BIT 5 = PASSWORD CHECKED FOR PASSWORD-PROTECTED DATA SET       14400000
*        BIT 7 = EXIT BIT (LAST VOLUME LIST ENTRY PROCESSED)            14450000
*                                                                       14500000
RACRENOK EQU   X'40'                    REDRIVE FLAG               @01A 14525000
NOMNTDEV EQU   X'10'                    NO DEVICE INDICATOR             14550000
PSWDCHKD EQU   X'04'                    PASSWORD CHECKED INDICATOR      14600000
EXITBIT  EQU   X'01'                    EXIT INDICATOR                  14650000
*                                                                       14700000
         SPACE 1                                                        14750000
*********************************************************************** 14800000
*        ABSOLUTE TRACK ADDRESSES                                       14850000
*********************************************************************** 14900000
         SPACE 1                                                        14950000
VTOCADR  DS    XL5                      FORMAT 4 CCHHR                  15000000
INCCHHR  DS    XL5                      CHANNEL PROGRAM WORK AREA       15050000
OUTCCHHR DS    XL5                      CHANNEL PROGRAM WORK AREA       15100000
NEWCCHHR DS    XL5                      CHANNEL PROGRAM WORK AREA       15150000
         SPACE 1                                                        15200000
***************************************************************@ZA31909 15204000
*        PROTECTED COPY OF VOLUME COUNT AND                    @ZA31909 15208000
*        POINTER TO PROTECTED COPY OF VOLUME LIST              @ZA31909 15212000
***************************************************************@ZA31909 15216000
         SPACE 1                                               @ZA31909 15220000
PVOLNUM  DS    H                        VOLUME COUNT           @ZA31909 15224000
VOL1ADDR DS    F                        ADDRESS OF FIRST       @ZA31909 15228000
*                                       VOLUME ID IN VOLUME    @ZA31909 15232000
*                                       LIST WORK AREA         @ZA31909 15236000
RENSAVE  DS    F                        REGISTER 14 SAVE AREA  @ZA43918 15237900
         SPACE 1                                               @ZA31909 15240000
*********************************************************************** 15250000
*        RENAME RECOVERY ROUTINE AUDIT TRAIL AREA                       15300000
*********************************************************************** 15350000
         SPACE 1                                                        15400000
RENADTRL DS    6F                       RENAME AUDIT TRAIL WORDS        15450000
         AIF   ('&ADT' EQ 'YES').REN0090                                15500000
         AIF   ('&ADT' EQ 'NO').REN0100                                 15550000
         IHBERMAC 1001,ADT,&ADT                                         15600000
         AGO   .REN0100                                                 15650000
.REN0090 ANOP                                                           15700000
         ORG   RENADTRL                                                 15750000
*        IECDSECS DSMADT,EXPAND=YES                                     15800000
         IECDSECS DSMADT,EXPAND=YES                                     15850000
.REN0100 ANOP                                                           15900000
         SPACE 1                                                        15950000
*********************************************************************** 16000000
RENWAEND EQU   *                        RENAME WORK AREA END            16050000
RWALNGTH EQU   RENWAEND-RWTGTBL         WORK AREA LENGTH FOR IECRES GET 16100000
*********************************************************************** 16150000
         MEND                                                           16200000
