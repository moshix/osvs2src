         MACRO                                                          00050000
&LABEL   IECSCRWA &EP,&F4,&D1=(0),&D2=(0),&ADT=NO                       00100000
.*                                                                      00150000
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 038                    00160000
.*                                                                      00170000
.* $01=OZ65137,EDM1102,B820313,STLMDB: FIX FOR ENQ LOCKOUT PROB    @01A 00180000
.*                                                                      00190000
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 2                      00200000
.*0000                                                           YM1337 00210000
.*0000                                                           YM7036 00220000
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 3             @ZM30032 00230000
.*0000                                                         @Y30LSTG 00240000
.*       MACRO DELETIONS/ADDITIONS/CHANGES FOR VS2 RELEASE 3.7 @ZA31933 00243000
.*0000021500-022500                                    DELETED @ZA31933 00244000
.*0000018560-018620,021500-021700,175944-175996        ADDED   @ZA31933 00245000
.*0000009000,028500                                    CHANGED @ZA31933 00246000
.*                                                                      00300000
.*       SU32 ADDITIONS                                        @G32DSPD 00310000
.*                                                                      00320000
.*       SU60 DELETIONS/CHANGES                                @G60ASBJ 00327000
.*A 182700                                                     @G60ASBJ 00334000
.*C 182700                                                     @ZA43917 00337400
.*                                                             @G60ASBJ 00341000
.********************************************************************** 00350000
.*                                                                      00400000
.* MODULE NAME = IECSCRWA (VS2-2)                                       00450000
.*                                                                      00500000
.* DESCRIPTIVE NAME = DADSM SCRATCH WORK AREA ADDRESSING MACRO          00550000
.*                                                                      00600000
.* COPYRIGHT = NONE                                                     00650000
.*                                                                      00700000
.* STATUS = CHANGE LEVEL 000                                            00750000
.*                                                                      00800000
.* FUNCTION = DADSM SCRATCH MACRO WHICH PROVIDES ADDRESSABILITY         00850000
.*            TO THE 896 DECIMAL BYTE WORK AREA (EXCLUDING THE @ZA31933 00900000
.*            8 WORD PREFIX OR THE 6 WORD RECOVERY ROUTINE PARAMETER    00930000
.*            LIST) USED BY SCRATCH.                                    00960000
.*                                                                      01050000
.* NOTES = THIS MACRO IS AN INTERNAL MACRO USED IN THE FOLLOWING        01100000
.*         DADSM FUNCTIONS:                                             01150000
.*                                                                      01200000
.*             SCRATCH                                                  01250000
.*                                                                      01300000
.*         TO REFERENCE THIS WORK AREA WITHIN THE FUNCTIONS LISTED      01350000
.*         ABOVE, CODE A USING STATEMENT ON THE LABEL 'SCRTHWKA'.       01400000
.*                                                                      01450000
.*      DEPENDENCIES = THIS MACRO IS DESIGNED TO BE USED OPTIONALLY     01500000
.*                     WITH THE IECSDSL1 DSCB MAPPING MACRO, THE        01550000
.*                     PREFX MACRO (DEFINED IN THE IECDSECS MACRO)      01600000
.*                     WHICH DEFINES THE 8 WORD WORK AREA PREFIX,       01650000
.*                     THE 20 WORD EXTENDED WORK AREA PREFIX, AND       01700000
.*                     THE 4 WORD DADSM WTG TABLE, AND THE DSMADT       01750000
.*                     MACRO (ALSO DEFINED IN THE IECDSECS MACRO)       01800000
.*                     WHICH DEFINES THE 6 WORD DADSM AUDIT TRAIL.      01850000
.*                     THE AUDIT TRAIL MUST APPEAR AS THE      @ZA31933 01856000
.*                     FINAL 6 WORDS OF THE SCRATCH WORK AREA. @ZA31933 01862000
.*                                                                      01870000
.*                     THE FIRST 364 BYTES OF THE SCRATCH AND RENAME    01900000
.*                     WORK AREAS (ADDRESSED RESPECTIVELY BY MAPPING    01950000
.*                     MACROS IECSCRWA AND IECRENWA) ARE MAPPED         02000000
.*                     IDENTICALLY - 8 WORD PREFIX, 20 WORD EXTENDED    02050000
.*                     PREFIX, 4 WORD DADSM WTG TABLE, 96 BYTE DSCB     02100000
.*                     AREA, AND 140 BYTE DSCB AREA.  THE 250  @ZA31933 02150000
.*                     BYTES FROM LABELS 'WKADEB' THROUGH      @ZA31933 02160000
.*                     'PVOLSER' ARE ALSO MAPPED IDENTICALLY.  @ZA31933 02170000
.*                                                                      02180000
.*      RESTRICTIONS = NONE                                             02350000
.*                                                                      02400000
.*      REGISTER CONVENTIONS = NOT APPLICABLE                           02450000
.*                                                                      02500000
.* PATCH LABEL = NOT APPLICABLE                                         02550000
.*                                                                      02600000
.* MODULE TYPE = MACRO                                                  02650000
.*                                                                      02700000
.*      PROCESSOR = ASSEMBLER LANGUAGE                                  02750000
.*                                                                      02800000
.*      MODULE SIZE = 920 DECIMAL BYTES (EXCLUDING THE PREFIX) @ZA31933 02850000
.*                                                                      02900000
.*      ATTRIBUTES = NOT APPLICABLE                                     02950000
.*                                                                      03000000
.* ENTRY POINT = SCRPREFX                                               03050000
.*                                                                      03100000
.*      PURPOSE = THIS MACRO IS CALLED AT ASSEMBLY TIME BY THE          03150000
.*                FUNCTIONS LISTED UNDER 'NOTES' TO EXPAND THE          03200000
.*                SCRATCH WORK AREA.                                    03250000
.*                                                                      03300000
.*      LINKAGE =                                                       03350000
.*                                                                      03400000
.*                LABEL IECSCRWA EP,F4,D1=(N),D2=(N),ADT=YES            03450000
.*                                                                      03500000
.*      INPUT =                                                         03550000
.*                                                                      03600000
.*                OPTIONAL LABEL SPECIFIED AS AN ACCEPTABLE ASSEMBLER   03650000
.*                LABEL WHICH CAN BE USED BY THE CALLING CSECT TO       03700000
.*                ESTABLISH ADDRESSABILITY TO THE ENTIRE WORK AREA.     03750000
.*                                                                      03800000
.*                EP IS THE FIRST POSITIONAL PARAMETER, SPECIFYING      03850000
.*                THAT THE LABELS FOR EACH OF THE FIELDS IN THE 8       03900000
.*                WORD PREFIX, THE 20 WORD EXTENDED PREFIX, AND THE 4   03950000
.*                WORD DADSM WTG TABLE ARE TO BE EXPANDED.  IF THESE    04000000
.*                FIELDS ARE NOT TO BE DEFINED, A COMMA MUST BE CODED   04050000
.*                TO INDICATE THE ABSENCE OF THIS PARAMETER.            04100000
.*                                                                      04150000
.*                F4 IS THE SECOND POSITIONAL PARAMETER, SPECIFYING     04200000
.*                THAT THE FIELDS OF THE FORMAT 4 DSCB ARE TO BE        04250000
.*                EXPANDED USING THE IECSDSL1 DSCB MAPPING MACRO.       04300000
.*                IF THESE FIELDS ARE NOT TO BE DEFINED AND A           04350000
.*                KEYWORD PARAMETER FOLLOWS, A COMMA MUST BE            04400000
.*                CODED TO INDICATE THE ABSENCE OF THIS PARAMETER.      04450000
.*                                                                      04500000
.*                D1=(N) IS A KEYWORD PARAMETER THAT SPECIFIES THE      04550000
.*                FORMAT(S) OF THE DSCB(S) TO BE EXPANDED IN THE        04600000
.*                FIRST 140 BYTE DSCB AREA, USING THE IECSDSL1 MACRO.   04650000
.*                N CAN BE A NUMBER FROM 0 - 6.  IF N IS 0 OR 4 OR      04700000
.*                IF THIS KEYWORD IS OMITTED, NO LABELS ARE EXPANDED.   04750000
.*                IF N IS 1, 2, 3, 5, OR 6, THE CORRESPONDING FORMAT    04800000
.*                OF THE DSCB WILL BE EXPANDED.  MORE THAN ONE DSCB     04850000
.*                FORMAT CAN BE MAPPED IN THE FIRST 140 BYTE DSCB       04900000
.*                AREA BY SEPARATING THE FORMAT NUMBERS BY COMMAS.      04950000
.*                                                                      05000000
.*                D2=(N) IS A KEYWORD PARAMETER THAT SPECIFIES          05050000
.*                THE FORMAT(S) OF THE DSCB(S) TO BE EXPANDED IN THE    05100000
.*                SECOND 140 BYTE DSCB AREA, USING THE IECSDSL1 MACRO.  05150000
.*                N CAN BE A NUMBER FROM 0 - 6.  IF N IS 0 OR 4 OR      05200000
.*                IF THIS KEYWORD IS OMITTED, NO LABELS ARE EXPANDED.   05250000
.*                IF N IS 1, 2, 3, 5, OR 6, THE CORRESPONDING FORMAT    05300000
.*                OF THE DSCB WILL BE EXPANDED.  MORE THAN ONE DSCB     05350000
.*                FORMAT CAN BE MAPPED IN THE SECOND 140 BYTE DSCB      05400000
.*                AREA BY SEPARATING THE FORMAT NUMBERS BY COMMAS.      05450000
.*                                                                      05500000
.*                ADT=YES IS A KEYWORD PARAMETER WHICH SPECIFIES THAT   05550000
.*                THE LABELS FOR EACH OF THE FIELDS WITHIN THE 6 WORD   05600000
.*                DADSM AUDIT TRAIL ARE TO BE EXPANDED.                 05650000
.*                                                                      05700000
.*      OUTPUT = DADSM SCRATCH WORK AREA EXPANSION                      05750000
.*                                                                      05800000
.* EXIT-NORMAL = MEND STATEMENT                                         05850000
.*                                                                      05900000
.* EXIT-ERROR = ERROR MESSAGES:                                         05950000
.*                                                                      06000000
.*      MNOTE 12,***  IHB002 INVALID EP OPERAND SPECIFIED-&EP           06050000
.*      MNOTE 12,***  IHB002 INVALID F4 OPERAND SPECIFIED-&F4           06100000
.*      MNOTE 12,***  IHB002 INVALID ADT OPERAND SPECIFIED-&ADT         06150000
.*                                                                      06200000
.*      THE MACRO EXPANSION CONTINUES AS IF THE INVALID PARAMETER       06250000
.*      WERE NOT SPECIFIED.                                             06300000
.*                                                                      06350000
.* EXTERNAL REFERENCES = NONE                                           06400000
.*                                                                      06450000
.*      ROUTINES = NONE                                                 06500000
.*                                                                      06550000
.*      DATA AREAS = SCRATCH WORK AREA                                  06600000
.*                                                                      06650000
.*      CONTROL BLOCKS = DSCB, ECB, IOB, DCB, DEB                       06700000
.*                                                                      06750000
.* TABLES = NONE                                                        06800000
.*                                                                      06850000
.* MACROS = IECDSECS DSMADT                                             06900000
.*          IECDSECS PREFX                                              06950000
.*          IECSDSL1                                                    07000000
.*          IHBERMAC                                                    07050000
.*                                                                      07100000
.* CHANGE ACTIVITY = SEE DELETIONS/CHANGES PRECEDING THE MACRO PROLOGUE 07150000
.*       EXTENDED MVS CVOL SUPPORT                             @Z40CSRC 07160000
.*                                                                      07200000
.********************************************************************** 07250000
.*                                                                      07300000
         LCLA  &C                                                       07350000
&LABEL   DSECT                                                          07400000
*********************************************************************** 07450000
*                                                                       07500000
*        SCRATCH WORK AREA                                              07550000
*                                                                       07600000
*********************************************************************** 07650000
         SPACE 1                                                        07700000
*********************************************************************** 07750000
*        PREFIX, EXTENDED PREFIX, AND WTG TABLE AREA                    07800000
*********************************************************************** 07850000
         SPACE 1                                                        07900000
SCRPREFX DS    8F                       WORK AREA PREFIX                07950000
SCRTHWKA DS    20F                      EXTENDED PREFIX                 08000000
FIRST    EQU   SCRTHWKA                                                 08050000
RACDEF   EQU   SCRTHWKA                 RACDEF PARM LIST           @01A 08075000
SWTGTBL  DS    4F                       DADSM WTG TABLE                 08100000
         AIF   ('&EP' EQ 'EP').SCR0010                                  08150000
         AIF   ('&EP' EQ '').SCR0020                                    08200000
         IHBERMAC 1001,EP,&EP                                           08250000
         AGO   .SCR0020                                                 08300000
.SCR0010 ANOP                                                           08350000
         ORG   SCRPREFX                                                 08400000
         MNOTE '         IECDSECS PREFX,EXPAND=YES'                     08450000
         IECDSECS PREFX,EXPAND=YES                                      08500000
.SCR0020 ANOP                                                           08550000
         SPACE 1                                                        08600000
*********************************************************************** 08650000
*        FORMAT 4 DSCB AREA                                             08700000
*********************************************************************** 08750000
         SPACE 1                                                        08800000
VTOCDSCB DS    XL96                     FORMAT 4 DSCB                   08850000
         AIF   ('&F4' EQ 'F4').SCR0030                                  08900000
         AIF   ('&F4' EQ '').SCR0040                                    08950000
         IHBERMAC 1001,F4,&F4                                           09000000
         AGO   .SCR0040                                                 09050000
.SCR0030 ANOP                                                           09100000
         ORG   VTOCDSCB                                                 09150000
         MNOTE '         IECSDSL1 (4)'                                  09200000
         IECSDSL1 (4)                                                   09250000
.SCR0040 ANOP                                                           09300000
         SPACE 1                                                        09350000
*********************************************************************** 09400000
*        FIRST 140-BYTE DSCB AREA                                       09450000
*********************************************************************** 09500000
         SPACE 1                                                        09550000
INDSCB   DS    XL140                    140-BYTE DSCB AREA              09600000
&C       SETA  1                                                        09650000
.SCR0050 AIF   (&C GT N'&D1).SCR0060                                    09700000
         AIF   (&D1(&C) EQ 0 OR &D1(&C) EQ 4).SCR0055                   09750000
         ORG   INDSCB                                                   09800000
         MNOTE '         IECSDSL1 (&D1(&C))'                            09850000
         IECSDSL1 (&D1(&C))                                             09900000
.SCR0055 ANOP                                                           09950000
&C       SETA  &C+1                                                     10000000
         AGO   .SCR0050                                                 10050000
.SCR0060 ANOP                                                           10100000
         SPACE 1                                                        10150000
*********************************************************************** 10200000
*        SECOND 140-BYTE DSCB AREA                                      10250000
*********************************************************************** 10300000
         SPACE 1                                                        10350000
OUTDSCB  DS    XL140                    140-BYTE DSCB AREA              10400000
ENQAREA  EQU   OUTDSCB+4                ENQ PARAMETER LIST              10500000
MJNAME   EQU   ENQAREA+16               MAJOR ELEMENT NAME              10550000
MINAME   EQU   ENQAREA+24               MINOR ELEMENT NAME              10600000
&C       SETA  1                                                        10650000
.SCR0070 AIF   (&C GT N'&D2).SCR0080                                    10700000
         AIF   (&D2(&C) EQ 0 OR &D2(&C) EQ 4).SCR0075                   10750000
         ORG   OUTDSCB                                                  10800000
         MNOTE '         IECSDSL1 (&D2(&C))'                            10850000
         IECSDSL1 (&D2(&C))                                             10900000
.SCR0075 ANOP                                                           10950000
&C       SETA  &C+1                                                     11000000
         AGO   .SCR0070                                                 11050000
.SCR0080 ANOP                                                           11100000
         SPACE 1                                                        11150000
*********************************************************************** 11200000
*        THE CONTROL BLOCK AREAS THAT FOLLOW (DEB/DCB, ECB/IOB, AND     11250000
*        THE CCW'S) ARE USED BY THE SCRATCH ROUTINES TO READ AND WRITE  11260000
*        THE DSCB'S.                                                    11270000
*********************************************************************** 11300000
         SPACE 1                                                        11350000
WKADEB   DS    F                        DEB                             11400000
DCB      DS    11F                      DCB                             11450000
DEBPTR   DS    F                        DCB POINTER TO DEB              11500000
ECB      DS    F                        ECB                             11800000
IOB      DS    8F                       IOB                             11850000
SEEK     DS    D                        DIRECT ACCESS ADDRESS           11900000
         SPACE 1                                                        11950000
*********************************************************************** 12000000
*        SCRATCH CHANNEL CONTROL WORDS AREA                             12050000
*********************************************************************** 12100000
         SPACE 1                                                        12150000
CCW1     DS    D                                                        12200000
CCW2     DS    D                                                        12250000
CCW3     DS    D                                                        12300000
CCW4     DS    D                                                        12350000
CCW5     DS    D                                                        12400000
CCW6     DS    D                                                        12450000
CCW7     DS    D                                                        12500000
CCW8     DS    D                                                        12550000
CCW9     DS    D                                                        12600000
CCW10    DS    D                                                        12650000
CCW11    DS    D                                                        12700000
CCW12    DS    D                                                        12750000
         SPACE 1                                                        12800000
*********************************************************************** 12850000
*        SCRATCH WORK AREA FIELDS                                       12900000
*********************************************************************** 12950000
         SPACE 1                                                        13000000
FIELD1   DS    D                        PASSWORD WORK AREA FIELD        13050000
OLDPLPTR DS    F                        PARAMETER LIST ADDRESS          13100000
PRUCBPTR DS    F                        PRIMARY (DEMOUNTABLE) UCB ADDR  13150000
VOLPTR   DS    F                        NEXT VOLUME LIST ENTRY ADDRESS  13200000
TCBADDR  DS    F                        POINTER TO THE TCB              13250000
DSABADDR DS    F                        DSAB ADDRESS OF CURRENT VOLUME  13300000
PRDSABAD DS    F                        DSAB ADDRESS OF PRIMARY UCB     13350000
VOLNUM   DS    H                        NUMBER OF VOLUMES REMAINING     13400000
         CNOP  2,4                      ALIGN DSABQDB TO FULLWORD       13450000
TIOTMNR  DS    CL6                      ASID/DSAB QDB ADDRESS           13500000
DSABQDB  EQU   TIOTMNR+2                DSAB QDB ADDRESS                13550000
VOLMINOR DS    CL6                      VOLSER FOR DEQ ON SYSZVOLS      13600000
ERCODE   DS    H                        ERROR RETURN CODE               13650000
SISW1    DS    XL1                      SECURITY/INTEGRITY SWITCHES     13700000
*                                                                       13750000
TIOTENQ  EQU   X'80'                    ENQ ON SYSTIOT ISSUED           14300000
DSNENQ   EQU   X'40'                    ENQ ON SYSDSN ISSUED            14350000
VOLENQ   EQU   X'20'                    ENQ ON SYSZVOLS ISSUED          14400000
VOLDEQ   EQU   X'10'                    DEQ ON SYSZVOLS TO BE ISSUED    14450000
UCBALLOC EQU   X'08'                    UCB DYNAMICALLY ALLOCATED       14500000
DEBSRCH  EQU   X'04'                    DEB SEARCH REQUIRED INDICATOR   14550000
NOSMFRCD EQU   X'02'                    NO SMF RECORD INDICATOR         14600000
VOLRACHK EQU   X'01'                    VOL RACHECKED OK       @G32DSPD 14620000
*                                                                       14650000
SISW2    DS    XL1                      SECURITY/INTEGRITY SWITCHES     14700000
*                                                                       14750000
DEMNTSW  EQU   X'80'                    VOLUME DEMOUNT INDICATOR        15100000
MOUNTSW  EQU   X'40'                    VOLUME MOUNT/VERIFY INDICATOR   15150000
VERIFYSW EQU   X'20'                    VOLUME VERIFY INDICATOR         15200000
UCBFNDSW EQU   X'10'                    ALLOCATED UCB FOUND INDICATOR   15210000
VIRTMNT  EQU   X'08'                    VIRTUAL MOUNT ISSUED   @Y30LSTG 15220000
DSNENQCV EQU   X'04'                    ENQ ON SYSCTLG.V------ @Z40CSRC 15230000
DORACDEF EQU   X'02'                    DO A DACDEF TYPE=DELETE    @01A 15240000
*                                                                       15250000
         DS    H                        UNUSED                          15260000
PVOLSER  DS    CL6                      VOLUME SERIAL NUMBER IN         15270000
*                                       PROTECTED STORAGE               15272000
PDSNAME  DS    CL44                     DATA SET NAME IN PROTECTED      15280000
*                                       STORAGE                         15290000
         DS    H                        UNUSED HALFWORD                 15300000
         SPACE 1                                                        15350000
*********************************************************************** 15400000
*        DADSM EXTENT TABLE                                             15450000
*********************************************************************** 15500000
         SPACE 1                                                        15550000
DADSMTBL DS    0XL68                    DADSM EXTENT TABLE              15600000
STYPEFLG DS    XL1                      SCRATCH SWITCHES                15650000
*                                                                       15700000
ERXTENT  EQU   X'80'                    OVERLAPPING EXTENTS FOUND       16300000
*                                       IN THE FORMAT 5 DSCB            16310000
VTOCENQ  EQU   X'40'                    RESERVE ON SYSVTOC ISSUED       16350000
VIOBIT   EQU   X'20'                    VIO DATA SET INDICATOR          16400000
NOMNTDEV EQU   X'10'                    NO VOLUME MOUNTED AND           16450000
*                                       NO DEVICE ALLOCATED             16460000
SHRCYL   EQU   X'08'                    SHARED CYLINDERS (SPLIT         16500000
*                                       CYLINDER EXTENT)                16510000
PSWDCHKD EQU   X'04'                    PASSWORD CHECKED FOR            16550000
*                                       PASSWORD-PROTECTED DATA SET     16560000
DOSPACK  EQU   X'02'                    DOS VOLUME INDICATOR            16600000
EXITBIT  EQU   X'01'                    EXIT INDICATOR (LAST VOLUME     16650000
*                                       LIST ENTRY PROCESSED)           16660000
*                                                                       16700000
EXTNUM   DS    XL1                      NUMBER OF EXTENT ENTRIES        16750000
*                                       IN DADSMTBL                     16760000
HOLENUM  DS    H                        NUMBER OF F0 DSCB'S THAT HAVE   16800000
*                                       BEEN USED                       16810000
ENTRIES  DS    16F                      SIXTEEN EXTENT ENTRIES          16850000
         SPACE 1                                                        16900000
*********************************************************************** 16950000
*        ABSOLUTE TRACK ADDRESSES                                       17000000
*********************************************************************** 17050000
         SPACE 1                                                        17100000
VTOCADR  DS    XL5                      FORMAT 4 CCHHR                  17150000
DADSMADR DS    XL5                      FORMAT 5 CCHHR                  17200000
INCCHHR  DS    XL5                      CHANNEL PROGRAM WORK AREA       17250000
OUTCCHHR DS    XL5                      CHANNEL PROGRAM WORK AREA       17300000
         CNOP  0,4                      ALIGN HOLD TO FULLWORD          17350000
HOLD     DS    XL5                      CHANNEL PROGRAM WORK AREA       17400000
NEXTDADS DS    XL5                      CHANNEL PROGRAM WORK AREA       17450000
INLINESK DS    CL6                      SEEK ADDRESS                    17500000
         SPACE 1                                                        17550000
***************************************************************@Y30LSTG 17560000
*        VIRTUAL DEVICE POINTER TO RELINQUISH WORK FIELD       @Y30LSTG 17570000
***************************************************************@Y30LSTG 17580000
         SPACE 1                                               @Y30LSTG 17590000
RELQADR  DS   1F                        PTR TO ICBACREL LIST   @Y30LSTG 17592000
         SPACE 1                                               @Y30LSTG 17594000
***************************************************************@ZA31933 17594400
*        PROTECTED COPIES OF PARAMETERS PASSED BY CALLER       @ZA31933 17594800
*        AND POINTER TO PROTECTED COPY OF VOLUME LIST          @ZA31933 17595200
***************************************************************@ZA31933 17595600
         SPACE 1                                               @ZA31933 17596000
OPTIONS  DS    XL4                      SCRATCH OPTION CODE    @ZA31933 17596400
PDSNADDR DS    F                        DATA SET NAME ADDRESS  @ZA31933 17596800
VOLISTAD DS    F                        CALLER'S VOLUME LIST   @ZA31933 17597200
*                                       ADDRESS                @ZA31933 17597600
PVOLNUM  DS    H                        VOLUME COUNT           @ZA31933 17598000
VOL1ADDR DS    F                        ADDRESS OF FIRST       @ZA31933 17598400
*                                       VOLUME ID IN VOLUME    @ZA31933 17598800
*                                       LIST WORK AREA         @ZA31933 17599200
SCRSAVE  DS    F                        REGISTER 14 SAVE AREA  @ZA43917 17599300
         SPACE 1                                               @ZA31933 17599600
*********************************************************************** 17600000
*        SCRATCH RECOVERY ROUTINE AUDIT TRAIL AREA                      17650000
*********************************************************************** 17700000
         SPACE 1                                                        17750000
SCRADTRL DS    6F                       SCRATCH AUDIT TRAIL WORDS       17800000
         AIF   ('&ADT' EQ 'YES').SCR0090                                17850000
         AIF   ('&ADT' EQ 'NO').SCR0100                                 17900000
         IHBERMAC 1001,ADT,&ADT                                         17950000
         AGO   .SCR0100                                                 18000000
.SCR0090 ANOP                                                           18050000
         ORG   SCRADTRL                                                 18100000
         MNOTE '         IECDSECS DSMADT,EXPAND=YES'                    18150000
         IECDSECS DSMADT,EXPAND=YES                                     18200000
.SCR0100 ANOP                                                           18250000
         SPACE 1                                                        18300000
*********************************************************************** 18350000
SCRWAEND EQU   *                        SCRATCH WORK AREA END           18400000
SWALNGTH EQU   SCRWAEND-SWTGTBL         WORK AREA LENGTH FOR IECRES GET 18450000
*********************************************************************** 18500000
         MEND                                                           18550000
