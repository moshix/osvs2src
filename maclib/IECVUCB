         MACRO                                                          00050002
         IECVUCB &UCBNO,&DEVAD,&TYPE,&CPRFX,&ERRTAB,&STATAB,&LCHN,     *00100002
               &DEVNDX,&TYPEA,&FL5,&SYSRES,&DDTNM,&OFF,&GR,&TMSK,      *00150064
               &EXT,&NOP2,&DEVADE,&SHR,&SHRUP,&FLP1            @G64HPLP 00180064
         GBLA  &IECIZS1,&IECIZCT,&PRFX                                  00250002
         LCLA  &COUNT,&A                                                00300002
         LCLC  &CUHEXAD,&DEVADDR                                        00350002
         LCLA  &BITS          FOR BYTES OF COMMON EXT          @Y40MPLG 00355004
         AIF   (&EXT EQ 1).UCBEXT                                       00360002
.********************************************************************** 00400002
.*                                                                    * 00450002
.*                   THIS SETS UP THE VS2-2 PREFIX.                   * 00500002
.*                   UCBIOQ                                           * 00700002
.*                   UCBLOCK                                          * 00750002
.*                                                                    * 00800002
.********************************************************************** 00850002
         DC    D'0'                     UCBIOQ AND UCBLOCK              00852002
.********************************************************************** 00860002
.*                                                                    * 00870002
.*    THIS SECTION SETS UP FOR GRAPHICS OR TP IN THE STANDARD PORTION * 00880002
.*                                                                    * 00890002
.********************************************************************** 00892002
&DEVADDR SETC  '&DEVADE'                 SET CAR ADDRESS                00894002
         AIF   ('&TYPE' NE 'TP' AND '&TYPE' NE 'GR').ST GRAPHIC OR TP?  00896002
&DEVADDR SETC  '&DEVAD'                SET TP CAR ADDRESS               00898002
.********************************************************************** 00950402
.*                                                                    * 00950802
.*        THIS SETS UP THE STANDARD PORTION OF ALL UCBS.              * 00951202
.*             UCBJBNR,UCBFL5,UCBID,UCBSTAT                           * 00951602
.*             UCBCHAN(UCBCHA/UCBUA),UCBFLA,UCBFLB                    * 00951702
.*                                                                    * 00954302
.********************************************************************** 00957002
.ST      ANOP                                                           00957402
UCB&UCBNO  DC  AL1(0),AL1(&FL5),X'FF'   JOBNO,UCBFL5,ID                 00959002
         AIF   (&OFF EQ 0).ST1          TEST FOR DEVICE OFFLINE         00959402
         DC    X'&SYSRES'               UCBSTAT FOR DEVICE ONLINE       00959502
         AGO   .ST2                                                     00959602
.ST1     DC    X'8&SYSRES'              UCBSTAT                         00959702
.ST2     DC    X'0&DEVAD',XL4'0'        UCBCHAN,UCBFLA,UCBFLB           00959802
.*                                      UCBCHANMASK,UCBCNT            * 00960002
         AIF   ('&TYPE' NE 'ZS').REG    IF NOT A 2305, BRANCH           00962002
         DC    AL1(&LCHN),XL2'0008'     UCB-(LCI,CPU,WGT)               00964002
         AGO   .UCB3                                                    00964102
.REG     DC    AL1(&LCHN),XL2'0'        UCB-(LCI,CPU,WGT)               00964402
.UCB3    DC    CL3'&DEVADDR',XL4'&TYPEA' UCBNAME,UCBTYPE                00966002
         DC    XL1'0'                   UCBFLC                          00968002
         DC    AL3(UCBX&UCBNO)          POINTER TO UCB EXTENSION        00968402
.********************************************************************** 01008002
.*   TEST FOR A PRINTER AND THE UCS FEATURE OR A 3505 OR A 3525 DEVICE* 01009302
.*   THE 'WR' DEVICE TYPE ACRONYM IS USED FOR THE 3505 CARD PUNCH AND * 01011302
.*   THE 3525 CARD PRINT. ONLY THE 3211, A UCS 1403 PRINTER, A 3540   * 01011904
.*   DISKET READER AND A 3800 PRINTER GET A UCB EXTENSION.    @Y40OPLG* 01012104
.*                                                            @Y40MPLG* 01012304
.*   SET A SENSE LENGTH OF TWO BYTES FOR 1403 PRINTER.                * 01012702
.*   SET A SENSE LENGTH OF FOUR BYTES FOR A 3505 AND A 3525.          * 01013102
.*   SET A SENSE LENGTH OF SIX BYTES FOR A 3211 PRINTER.              * 01013202
.*   SET A SENSE LENGTH OF SIX BYTES FOR A 3540 DISKET RDR/WTR@Y30OPLA* 01013803
.********************************************************************** 01014303
         AIF   ('&TYPEA'(6,3) EQ '842').URDSM DSM DEVICE       @Y30LPLC 01021203
         AIF   ('&TYPEA'(6,3) EQ '844').UR3540 3540 DISKET     @Y30OPLA 01021403
         AIF   ('&TYPE' EQ 'WR').UC3211 TEST FOR 3211 OR A 3505/3525    01021603
         AIF   ('&TYPE' NE 'UR').NOTPRT TEST FOR UR                     01022303
         AIF   (NOT(('&TYPEA'(6,3) EQ '808') AND ('&TYPEA'(3,1) EQ '8')*01022403
               )).NOTPRT                IT IS NOT A PRINTER WITH UCS    01023503
         DC    A(UCBW&UCBNO)                                            01023603
         AGO   .END                                                     01024703
.UC3211  AIF   ('&TYPEA'(7,2) NE '09' AND '&TYPEA'(7,2) NE '0E').END    01024804
.*                                                             @Y40MPLG 01025304
.UR3540  DC    A(UCBW&UCBNO)       3211,3540,3800              @Y30OPLA 01025904
         AGO   .END                                                     01026003
.URDSM   ANOP                      DSM DEVICE                  @Y30LPLC 01026903
         DC    2F'0'               2 WORD DEVICE DEPEND. SECT. @Y30LPLC 01027003
         AGO   .END                                            @Y30LPLC 01027103
.********************************************************************** 01027403
.*                                                                    * 01027703
.*       GRAPHICS AND ANR GRAPHICS SEGMENT SECTION                    * 01028103
.*                                                                    * 01028803
.********************************************************************** 01029303
.NOTPRT  AIF   ('&TYPE' NE 'GR').URCHECK  TEST FOR GRAPHICS             01029803
  AIF  ('&TYPEA'(7,2) NE '09' AND '&TYPEA'(7,2) NE '0A' AND '&TYPEA'(7,X01030303
               2) NE '0B' AND '&TYPEA'(7,2) NE '0C' AND '&TYPEA'(7,2)  X01030700
               NE '0D').NOTANR                                 @G51APLK 01031100
         DC    XL2'&TMSK',XL2'0'        ADDITIONAL OPTIONS BYTE         01031403
         DC    3F'0'                    IRB ADD,IOB ADD,D.I.            01032003
         AGO   .END                                                     01032603
.NOTANR  ANOP                                                           01033203
         AIF   ('&TYPEA'(8,1) EQ '7' OR '&TYPEA'(8,1) EQ '8').M3066     01033803
         DC    3F'0'                    SENSE,DUC,GCB                   01034403
         AIF   ('&ERRTAB' EQ '66').SDA0 IS THIS 2260                    01035003
         DC    XL1'&DEVNDX'             DEVICE INDEX           @ZA40477 01035646
         AIF   (&GR EQ 1).GRF           TEST FOR GAM                    01036203
         DC    XL3'00'                  NO GAM SUPPORT                  01036803
         AGO   .END                                                     01037403
.GRF     AIF   ('&TYPEA'(1,2) EQ '31').MOD1  CHECK FOR 2250-1           01038003
&CUHEXAD SETC  '&DEVAD'(1,2)            CONVERT TO CU ADDRESS           01038603
         DC    VL3(IFFB&NOP2)           BUFFER TABLE NAME               01039203
         AGO   .END                                                     01039803
.MOD1    DC    VL3(IFFB&DEVAD)          BUFFER TABLE NAME               01040403
         AGO   .END                                                     01041003
.M3066   AIF   ('&TYPEA'(8,1) NE '8').END                       @YM8410 01041603
         DC    3F'0'                                                    01042203
.SDA0    DC    F'0'                     2260 OR 1053                    01042803
         AGO   .END                                                     01043403
.********************************************************************** 01044003
.*                                                                    * 01044603
.*            UNIT RECORD,TELE-PROCESSING AND DUMMY UCB SEGMENTS      * 01045203
.********************************************************************** 01045803
.URCHECK AIF   ('&TYPE' NE 'UR' ).DUMMY                                 01046403
         AIF   ('&TYPEA'(6,3) NE '84C').UR3886 IS IT A 3838    @G29ANLO 01046541
         DC    2F'0'                 3838 DEVICE DEPEND. SECT. @G29ANLO 01046641
         AGO   .END                                            @G29ANLO 01046741
.UR3886  ANOP                                                  @G29ANLO 01046841
         AIF   ('&TYPEA'(6,3) NE '817').END  BRANCH IF IT IS NOT A 3886 01047003
         DC    F'0'                     FOUR BYTES FOR THE 3886         01047603
         AGO   .END                                                     01048203
.DUMMY   AIF   ('&TYPE' NE 'DUM').TP                                    01048803
         DC    F'0'                     EXTRA WORD FOR DUM UCB USE      01049403
         AGO   .END                                                     01050003
.TP      AIF   (NOT('&TYPE' EQ 'TP')).TAPE IS IT TP                     01050803
   AIF   (NOT('&TYPEA'(8,1) EQ '5' OR '&TYPEA'(8,1) EQ '6'  OR         *01051703
               '&TYPEA'(5,4) EQ '4100' OR '&TYPEA'(7,2) EQ 'F1' )).END  01052104
         DC    2F'0'                    3704/3705 SEGMENT               01052503
*                                  ALSO CTC & 3791             @Z40KPLG 01052704
         AGO   .END                                                     01053003
.********************************************************************** 01053603
.*                                                                    * 01054303
.*             THIS SECTION SETS UP THE TAPE SEGMENT                  * 01054903
.*             3420 HAS 24 SENSE BYTES                                * 01055603
.*             34XX HAS 09 SENSE BYTES                                * 01056203
.*             ALL OTHERS HAVE 6 SENSE BYTES                          * 01056903
.********************************************************************** 01057503
.TAPE    AIF   ('&TYPEA'(5,2) NE '80').DASD BR IF NOT TAPE              01058203
         AIF   ('&TYPEA'(5,4) EQ '8001').NOASP  CHECK FOR 2400          01058803
         AIF   ('&TYPE' NE 'AT').NOASP  NOT AN ASPEN TAPE               01059503
         AIF   (&SHR EQ 1).NOASP        3400 SHARABLE?                  01059903
         DC    H'0',H'0',XL6'0',XL2'8000',2F'0'   VOLSER                01060303
*          FILE SEQ CT, FILE SEQ NO,NOT SHARABLE/STATAB & DMCT,DS SERNO 01060803
         AGO   .REST1                                                   01061403
.NOASP   DC    H'0',H'0',XL6'0',XL2'0',2F'0'   VOLSER                   01062103
*               FILE SEQ CT, FILE SEQ NO,STATAB & DMCT,DS SERNO         01062703
.REST1   DC    AL1(&TMSK),AL3(UCBW&UCBNO) UCBVOPT AND UCBXTNB           01063403
         AGO   .END                     END OF TAPE UCB                 01064003
.********************************************************************** 01064703
.*                                                                    * 01065303
.*             DIRECT ACCESS DEVICES SEGMENTS.                        * 01066003
.*             THE 2305 AND 3330'S HAVE 24 SENSE BYTES.               * 01066603
.*             A 2314 HAS 6 SENSE BYTES.                              * 01067303
.********************************************************************** 01067703
.*             ML (MERLIN) TEST INCLUDES 3330,3340,3350        @Z30RSTA 01067804
.*             REFERENCE MACRO SGIEC202 IN AGENLIB             @Z30RSTA 01067904
.DASD    AIF   ('&TYPE' EQ 'ML' OR '&TYPE' EQ 'ZS').SPEED               01068103
.DABS    DC    XL6'0',XL2'0',F'0',XL4'0'   VOLSER                       01068603
*               VTOC, STATB & DMCT, UCBSQC,UCBFL4,UCBUSER,RESERVED      01069203
         AGO   .END                                                     01069903
.SPEED   DC    F'0',XL6'0',XL2'0',XL4'0'                                01070503
*               VTOC, STATB & DMCT, UCBSQC,UCBFL4,UCBUSER,RESERVED      01071103
         AIF   ('&TYPE' EQ 'ML').END                                    01071703
         AIF   (&IECIZCT  NE 0).ZSET    TEST FOR 1ST ZEUS UCB           01072303
&IECIZS1 SETA  &UCBNO                   SET POINTER TO FIRST UCB        01072903
.ZSET    ANOP                                                           01073503
&IECIZCT   SETA  &IECIZCT+1             UPDATE COUNTER                  01074103
         AIF   (&IECIZCT  EQ 8).ZSET1   TEST FOR LAST                   01074703
&A       SETA  &UCBNO+1                 SET PTR TO NEXT UCB             01075303
         DC    A(UCB&IECIZS1-512)       POINTER TO FIRST ZEUS UCB       01075903
         DC    A(UCB&A-512)             POINTER TO NEXT IN RING         01076503
         AGO   .END                     NOT LAST 2305                   01077103
.ZSET1   ANOP                           LAST DEVICE ON 2305 CHAIN       01077703
&A       SETA  &IECIZS1+1               SET LAST TO POINT TO SECOND     01078303
         DC    A(UCB&IECIZS1-512)       POINTER TO FIRST ZEUS UCB       01078903
         DC    A(UCB&A-512)             POINTER TO NEXT IN RING         01079902
&IECIZCT   SETA  0                      CLEAR COUNTER                   01081902
         AGO   .END                     LAST 2305                       01082302
.UCBEXT  ANOP                                                           01083902
UCBX&UCBNO DC  XL1'&ERRTAB',AL1(&STATAB)                                01084302
.********************************************************************** 01134302
.*   TEST FOR A PRINTER AND THE UCS FEATURE OR A 3505 OR A 3525 DEVICE* 01144302
.*   THE 'WR' DEVICE TYPE ACRONYM IS USED FOR THE 3505 CARD PUNCH AND * 01154302
.*   THE 3525 CARD PRINT. ONLY THE 3211 AND A UCS 1403 PRINTER GET    * 01164302
.*   A UCB EXTENSION.                                                 * 01174302
.*   SET A SENSE LENGTH OF TWO BYTES FOR 1403 PRINTER.                * 01176302
.*   SET A SENSE LENGTH OF FOUR BYTES FOR A 3505 AND A 3525.          * 01178302
.*   SET A SENSE LENGTH OF SIX BYTES FOR A 3211 PRINTER.              * 01180302
.*   SET A SENSE LENGTH OF 24 BYTES FOR A 3800 PRINTER         @Y40MPLG 01180804
.*   SET A SENSE LENGTH OF TWO BYTES FOR A 3895 RDR-INSCRIBER  @G30HNLO 01181041
.*   SET A SENSE LENGTH OF SIX BYTES FOR A 3540 DISKET RDR/WTR @Y30OPLA 01181303
.********************************************************************** 01182302
         AIF   ('&TYPE' EQ 'WR').UC3211A TEST FOR 3211 OR A 3505/3525   01182402
         AIF   ('&TYPE' NE 'UR').NOPRT TEST FOR UR                      01188002
         AIF   ('&TYPEA'(6,3) EQ '844').UR3540A 3540 DISKET    @Y30OPLA 01188603
         AIF   ('&TYPEA'(6,3) EQ '842').URDSMA DSM DEVICE      @Y30LPLC 01189203
         AIF   (NOT(('&TYPEA'(6,3) EQ '808') AND ('&TYPEA'(3,1) EQ '8')*01190002
               )).NOPRT                 IT IS NOT A PRINTER WITH UCS    01192002
         DC    XL2'0'                   UCBDTI,ATI/UCS PRINTER          01192402
         DC    X'02'                    SENSE COUNT/UCS PRINTER         01192502
         AGO   .UCB5                                                    01193202
.UC3211A AIF   ('&TYPEA'(7,2) NE '09').NEWUR  IT IS NOT A 3211          01193602
.UR3540A DC    XL2'0'              UCBDTI,ATI/UCS PRINTER,3540 @Y30OPLA 01193703
         DC    X'06'               SENSE COUNT/3211 PRNTR,3540 @Y30OPLA 01195403
         AGO   .UCB5                                                    01197703
.NEWUR   AIF   ('&TYPEA'(7,2) EQ '0E').UCB3800  3800 PRINTER   @Y40MPLG 01198304
         AIF   ('&TYPEA'(7,2) EQ '19').UCB3895  3895           @G30HNLO 01198641
         DC    XL2'0'                   UCBDTI,ATI/NEW UR               01198904
         DC    X'04'                    SENSE COUNT/NEW UR              01200303
         AGO   .UCB5                                                    01200803
.UCB3800 DC    XL2'0'               UCBDTI,ATI/3800 PRT        @Y40MPLG 01200904
         DC    X'18'                SENSE COUNT/3800 PRT       @Y40MPLG 01201004
         AGO   .UCB5                                                    01201104
.UCB3895 DC    XL2'0'               UCBDTI,ATI/3895            @G30HNLO 01201241
         DC    X'02'                SENSE COUNT/3895           @G30HNLO 01201341
         AGO   .UCB5                                           @G30HNLO 01201741
.URDSMA  ANOP                                                  @Y30LPLC 01201841
         DC    X'0038'                  UCBDTI,ATI/ DSM        @G64HPLP 01202264
         DC    X'20'                    SENSE COUNT/ DSM       @Y30LPLC 01202641
         AGO   .UCB5                                           @Y30LPLC 01202803
.********************************************************************** 01203303
.*                                                                    * 01204403
.*       GRAPHICS AND ANR GRAPHICS SEGMENT SECTION                    * 01204803
.*                                                                    * 01205803
.********************************************************************** 01206803
.NOPRT  AIF   ('&TYPE' NE 'GR').URDUMM TEST FOR GRAPHICS                01207803
  AIF  ('&TYPEA'(7,2) NE '09' AND '&TYPEA'(7,2) NE '0A' AND '&TYPEA'(7,X01208803
               2) NE '0B' AND '&TYPEA'(7,2) NE '0C' AND '&TYPEA'(7,2)  X01209300
               NE '0D').NOANR      ANR?                        @G51APLK 01209800
         DC    X'001C'                  UCBDTI,ATI/ANR GRAPHIC @G64HPLP 01210864
         DC    X'06'                    SENSE COUNT/ANR GRAPHICS        01211464
         AGO   .UCB5                                                    01212064
.NOANR   ANOP                                                           01212664
         DC    X'0010'                  UCBDTI,ATI/NON-ANR     @G64HPLP 01213264
*                                       GRAPHICS               @G64HPLP 01213864
         DC    X'06'                    SENSE COUNT/NON-ANR GRAPHICS    01215803
         AGO   .UCB5                                                    01216803
.********************************************************************** 01221402
.*                                                                    * 01221802
.*            UNIT RECORD,TELE-PROCESSING AND DUMMY UCB SEGMENTS      * 01221902
.********************************************************************** 01223302
.URDUMM  AIF   (NOT('&TYPE' EQ 'UR' OR '&TYPE' EQ 'DUM')).TPC           01225302
         AIF   ('&TYPEA'(6,3) EQ '817').OCR3886 CHECK FOR 3886 @ZA05374 01225800
         AIF   ('&TYPEA'(6,3) NE '84C').URDUM1 IS IT 3838      @G29ANLO 01226341
         DC    X'0000'                  UCBDTI,ATI/3838        @G64HPLP 01226864
         DC    X'20'                    SENSE COUNT/3838       @G29ANLO 01227341
         AGO   .UCB5                                           @G29ANLO 01227841
.OCR3886 ANOP                                                  @ZA05374 01227900
         DC    XL2'0'                   UCBDTI,ATI             @ZA05374 01228000
         DC    X'06'                    SENSE COUNT FOR 3886.  @ZA05374 01228100
         AGO   .UCB5                                           @ZA05374 01228200
.URDUM1  ANOP                                                  @G29ANLO 01228341
         DC    XL2'0'                   UCBDTI,ATI/UR OR DUMMY UCB'S    01229541
         DC    X'02'                    SENSE COUNT/UR OR DUMMY UCB'S   01229641
         AGO   .UCB5                                                    01229741
.TPC     AIF   (NOT('&TYPE' EQ 'TP')).TAPEC IS IT TP                    01230302
         AIF   ('&TYPEA'(5,4) EQ '4100').TPCTC                 @YM08445 01230402
         DC    X'0000'                  UCBDTI,ATI/TP UCB'S    @G64HPLP 02230464
         DC    X'02'                    SENSE COUNT/TP UCB'S            02280464
         AGO   .UCB5                                                    02330464
.TPCTC   ANOP                                                           02380464
         DC    X'002C'                  UCBDTI,ATI/TP CTC UCB  @G64HPLP 02430464
*                                                              @ZM30469 06730403
         DC    X'01'                    SENSE COUNT/TP UCB   DCRR21211  07230402
         AGO   .UCB5                                                    08230402
.********************************************************************** 09230402
.*                                                                    * 10230402
.*       THIS SECTION SETS UP THE TAPE UCB AND SEGMENTS TO IT         * 11230402
.*             3420 HAS 24 SENSE BYTES                                * 12230402
.*             34XX HAS 09 SENSE BYTES                                * 13230402
.*             ALL OTHERS HAVE 6 SENSE BYTES                          * 14230402
.********************************************************************** 15230402
.TAPEC   AIF   ('&TYPEA'(5,2) NE '80').DASDC BR IF NOT TAPE             16230402
         AIF   ('&TYPEA'(5,4) EQ '8001').C2400  CHECK FOR 2400          17230402
         AIF   ('&TYPE' NE 'AT').C34XX  NOT AN ASPEN TAPE               18230402
         DC    X'0000'                  UCBDTI,ATI/TAPE UCB'S  @G64HPLP 19230464
         DC    X'18'                    SENSE COUNT/TAPE UCB'S          19280464
         AGO   .UCB5                                                    19330464
.C34XX   DC    X'0000'                  UCBDTI,ATI/ASPEN-TAPE  @G64HPLP 19380464
*                                       UCB,S                  @G64HPLP 19430464
         DC    X'09'                    SENSE COUNT/ASPEN-TAPE UCB'S    19480464
         AGO   .UCB5                                                    19530464
.C2400   DC    X'0000'                  UCBDTI,ATI/TAPE UCB'S  @G64HPLP 19580464
         DC    X'06'                    SENSE COUNT/TAPE UCB'S          26230402
         AGO   .UCB5                                                    27230402
.********************************************************************** 28230402
.*                                                                    * 29230402
.*       DIRECT ACCESS DEVICES UCB SENSE COUNTS AND SEGMENTS.         * 30230402
.*             THE 2305 AND 3330'S HAVE 24 SENSE BYTES.               * 31230402
.*             A 2314 HAS 6 SENSE BYTES.                              * 32230402
.********************************************************************** 33230402
.*             ML (MERLIN) TEST INCLUDES 3330,3340,3350        @Z30RSTA 33280404
.*             REFERENCE MACRO SGIEC202 IN AGENLIB             @Z30RSTA 33330404
.DASDC   AIF   ('&TYPE' EQ 'ML' OR '&TYPE' EQ 'ZS').MLZSC               34230402
         DC    X'0000'                  UCBDTI,ATI/DA UCB'S    @G64HPLP 35230464
         DC    X'06'                    SENSE COUNT/DA UCB'S            36230402
         AGO   .UCB5                                                    37230402
.MLZSC   AIF   (NOT('&TYPE' EQ 'ML')).ZSDTI                             38230402
.* NOTE: THIS TEST ASSUMES NO OTHER BIT FOR V3330 IN HALF BYTE @ZM30SS1 38330403
         AIF   ('&TYPEA'(4,1) NE '8').NVML  IF NOT VIRT 3330   @ZM30SS1 38430403
         DC    X'0000'             DTI,ATI V3330               @G64HPLP 38480464
         DC    X'1C'               SENSE COUNT 28 FOR V3330    @ZM30SS1 38530464
         AGO   .UCB5                                           @ZM30SS1 38580464
.NVML    DC    X'0000'                  UCBDTI,ATI/DA MERLIN   @G64HPLP 38630464
*                                       UCB'S                  @G64HPLP 38680464
         DC    X'18'                    SENSE COUNT/DA MERLIN UCB'S     38730464
         AGO   .UCB5                                                    38780464
.ZSDTI   DC    X'0000'                  UCBDTI/ATI             @G64HPLP 38830464
         DC    X'18'                    SENSE COUNT/DA ZEUS UCB'S       43230402
.UCB5    ANOP                                                           44230403
&BITS    SETA  0              INIT BYTE5 CNTR                  @Y40MPLG 44250404
         AIF   ('&SHRUP' EQ '').UCB6                           @ZM30409 44280403
         AIF   (&SHRUP EQ 0).UCB6  IS IT SHARED IN UP, NO BRANCH        44330404
&BITS    SETA  &BITS+64            X'40' SHARED UP BIT         @Y40MPLG 44380404
.UCB6    AIF   ('&FLP1' EQ '').UCB65                           @Y40MPLG 44480404
&BITS    SETA  &BITS+&FLP1         ERROR LOG                   @Y40MPLG 44660404
.UCB65   DC    AL1(&BITS),XL2'0'        UCBFLP1,RESVD HALFWORD @Y40MPLG 44690441
.UCB7    ANOP                                                           44830403
&PRFX    SETA  &CPRFX                   CONVERT DBL WRD OFFSET @ZA16150 45230400
*                                       TO CCW PREFIX TO BYTE           46230402
         DC    H'&PRFX',XL6'0'          UCBCCWOF OFFSET,UCBPMSK         47230402
*                                       UCBMFCNT,UCBASID                48230402
         DC    X'00',XL3'00'            UCBMIHBY,UCBWTOID      @G64HPLP 48280464
.* IF NO DDT IS SPECIFIED OR THE PARAMETER SPECIFIED IS 0 THEN @ZM48987 48330464
.* GENERATE AN ADCON OF 0 RATHER THAN A V-CON.                 @ZM48987 48380464
         AIF   ('&DDTNM' EQ '' OR '&DDTNM' EQ '0').NODDT       @ZM48987 48430464
         DC    V(&DDTNM)                DDT POINTER            @ZM48987 48480464
         AGO   .END                                            @ZM48987 48530464
.NODDT   ANOP                                                  @ZM48987 48580464
         DC    A(0)                     DDT POINTER            @ZM48987 48630464
.END     ANOP                                                  @ZM48987 48680464
         MEND                                                           48730464
