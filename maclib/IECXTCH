         MACRO                                                          00300018
         IECXTCH &LCHNO,&TYPE,&PCHA,&CUA                                00400002
         GBLB  &IECIACU                 GLOBALS                         00900002
         GBLA  &IECIOPT(255)            FOR TEST CHANNEL       @ZA34236 01100003
         LCLB  &CUSW,&SECSW             BINARY SWITCHES            IMP5 01500018
         LCLA  &RCTR,&INDX,&PATHN(4),&X,&PATHS                          01800002
         LCLA  &NUMCH,&NUMCH2,&NUM,&HICHAN(4)                           02000002
         LCLC  &LABEL,&CHAN,&VARY,&CU                                   02400002
.*                                                               BBBBBB 03100019
.*  DEFINITION OF INPUT PARAMETERS                                 IMP5 03900018
.*  LCHNO    LOGICAL CHANNEL NUMBER OR SUBLIST OF LCH/CHAN         IMP5 04200018
.*  TYPE     0=MULTIPLEXOR,1=SELECTOR                              IMP5 04500018
.*  PCHA     PHYSICAL CHANNEL ADDRESS                              IMP5 04800018
.*  CUA      CONTROL UNIT ADDRESS                                  IMP5 05100018
.*                                                                19022 06600019
         AIF   (N'&LCHNO EQ 1).OPTCH    REAL MACRO CALL, SKIP     19022 06700002
         TITLE 'TEST CHANNEL PATH TABLES'                               06750002
*****************************************************************       06800002
*                                                               *       06850002
*        TEST CHANNEL PATH TABLES                               *       06860002
*        THIS TABLE IS USED IN CONJUNCTION WITH THE LOGICAL     *       06870002
*        CHANNEL WORD TO DETERMINE WHAT PATHS ARE AVAILABLE     *       06880002
*        TO THE DEVICES ON A LOGICAL CHANNEL.                   *       06890002
*                                                               *       06892002
*****************************************************************       06894002
         SPACE 2                                                        06896002
.SAVE    ANOP                                                           06900018
&NUM     SETA  &NUM+1                                                   07200018
&IECIOPT(&NUM) SETA  &LCHNO(&NUM)                                       07500018
         AIF   (&NUM NE N'&LCHNO).SAVE                                  07800018
         SPACE 1                                                   ROTN 08100018
         MEXIT                                                     ROTN 08400018
.OPTCH   ANOP                                                           09600018
&NUM     SETA  &NUM+1                                                   09900018
         AIF   (N'&PCHA EQ 1).CHAN00    CHECK IF CHAN 0        @YM05098 09950002
         AIF   (&PCHA(&NUM) EQ 0).LOOP                                  10200018
&PATHS   SETA  &PATHS+1                                                 10500018
&HICHAN(1) SETA  &NUM                                             19022 10800019
&PATHN(&PATHS) SETA &NUM                SAVE CHAN NO. IN TABLE     ROTN 11100018
.LOOP    AIF   (N'&PCHA NE &NUM).OPTCH                                  11400018
&CU      SETC  '0'                                                      11450002
         AIF   (&PATHS EQ 0).CHAN0A                                     11500002
         AIF   (&PATHS LT 2).CULBL                                      11700018
&HICHAN(1) SETA  &PATHN(1)                                        19022 11800019
&HICHAN(2) SETA  &PATHN(2)                                        19022 11900019
&HICHAN(3) SETA  &PATHN(3)                                        M1186 11930020
&HICHAN(4) SETA  &PATHN(4)                                        M1186 11960020
&RCTR    SETA  &PATHS                   SET NUMBER TO ROTATE =PTHS ROTN 12300018
         AGO   .SETLBL                                                  12600018
.CHAN00  ANOP                                                           12601002
         AIF   (&PCHA EQ 0).CHAN0       CHANNEL ZERO BRANCH    @YM05098 12602002
&HICHAN(1) SETA &PCHA                   SET CHANNEL            @YM05098 12603002
&RCTR    SETA  &NUM                     INITIALIZE COUNTER     @YM05098 12604002
&PATHS   SETA  &NUM                     INITIALIZE PATH        @YM05098 12605002
&PATHN(&PATHS) SETA &PCHA               INITIALIZE PATH TABLE  @YM05098 12606002
&CU      SETC  '0'                      SET CONTROL UNIT       @YM05098 12607002
         AGO   .CULBL                                          @YM05098 12608002
.CHAN0A  ANOP                                                           12610002
&NUM     SETA  1                                                        12620002
.CHAN0   ANOP                                                           12650002
&PATHS   SETA  &NUM                                                     12700002
.CULBL   ANOP                                                           12900018
         AIF   (T'&CUA EQ 'O').TCHLP4A                                  12950002
&X       SETA &CUA(1)+&CUA(2)+&CUA(3)+&CUA(4)                           13200002
         AIF   (&X LT 2).TCHLP4A                                        13500018
.* THERE ARE ALTERNATE C.U.'S IN THIS LOGICAL CHANNEL                   13800018
&CUSW    SETB 1                         SET C.U. SWITCH            MCS1 14400018
.TCHLP4A ANOP                                                           14450002
&CHAN    SETC  '&HICHAN(1)'             PICK UP CHAN NUMBER       19022 14700019
.SETLBL  ANOP                                                           15000018
&LABEL   SETC  'TCH&LCHNO.LST'                                          15600002
&LABEL   DS    0H                       TCH PATH TABLE                  15900002
&NUM     SETA  0                                                        16200018
.TCHLP1  ANOP                                                           16500018
&NUM     SETA  &NUM+1                                                   16800018
         AIF   (&PATHS LT 2 AND &X GT 1).TCHLP2 NO OPT CHANS, SKIP      17100002
         AIF   (&SECSW EQ 1).ROTN2                                 IMP5 17400018
         AIF   (&NUM EQ &PATHS OR &NUM LT &PATHS).ROTN6                 17700018
&SECSW   SETB  1                                                        18000018
&NUM     SETA  0                                                        18300018
         AGO   .TCHLP1                                             IMP5 18600018
.ROTN6   ANOP                                                      IMP5 18900018
&INDX    SETA  &PATHN(&NUM)+1           PT AT POSITION IN SUBLIST  ROTN 19200018
&RCTR    SETA  &RCTR-1                  ONE LESS TO ROTATE         ROTN 20100018
&PATHN(&NUM) SETA 0                     ZERO OUT ENTRY             ROTN 20400018
         AGO   .ROTN7                   GO SET UP TABLE ENTRY    BBBBBB 20700019
.ROTN2   AIF   (&RCTR LT 2).ROTN5       NONE LEFT TO ROTATE        ROTN 21000018
&NUMCH2  SETA  &RCTR*2-2                NO. OF BYTES TO MOVE       ROTN 21300018
.ROTN5   AIF   (&RCTR EQ 0).TCHLP2                                 ROTN 21600018
         AIF   (&NUM GT &PATHS).TCHLP2A                                 21900018
         AIF   (&PATHN(&NUM) EQ 0).ROTN4                           IMP5 22200018
&INDX     SETA  &PATHN(&NUM)+1          PT AT SUBLIST ENTRY        ROTN 22500018
.ROTN7   ANOP                           SET UP TABLE ENTRY       BBBBBB 22570019
&NUMCH   SETA  &NUMCH+1                 INCREASE PATH COUNT      BBBBBB 22640019
&VARY    SETC  '8421'(&NUM,1)           PICK UP PATH DESIGNATION BBBBBB 22710019
&CHAN    SETC   '0123456789ABCDEF'(&INDX,1)                        ROTN 22800018
         AGO   .TABLE                                              ROTN 23100018
.ROTN4   AIF   (&NUM NE &PATHS).TCHLP1                                  23400018
         AGO   .TCHLP2A                 COMPLETE TABLE             ROTN 23700018
.TCHLP2  AIF   (&NUM GT N'&CUA).TCHLP2A                                 24000018
         AIF   (&CUA(&NUM) EQ 0).TCHLP2A                                24300018
&CU      SETC  'CDEF'(&NUM,1)                                           24600018
&NUMCH   SETA  &NUMCH+1                                                 25200018
&VARY    SETC  '8421'(&NUMCH,1)                                         25500018
.TABLE   DC    X'0&CHAN.&CU.&VARY' CHAN ADR AND PATH DESIGNATION BBBBBB 25800019
         AGO   .TCHLP1                                                  26400018
.TCHLP2A AIF   (&NUM LT N'&CUA).TCHLP1                                  26700018
         DC    X'FFFF'                  END OF TABLE INDICATOR          26750002
         SPACE 1                                                        26800002
&IECIOPT(&LCHNO) SETA &PATHS                                            26850002
         MEND                                                      IMP5 88500018
