         MACRO                                                          00050002
         IEECRDCM    &DEV=,&A=,&KEY=,&DSECT=                            00100002
         SPACE 1                                               @G64UPSS 00110064
*********************************************************************** 00120064
*                                                              @G64UPSS 00130064
*          IEECRDCM - RESIDENT DISPLAY CONTROL MODULE          @G64UPSS 00140064
*                                                              @G64UPSS 00150064
* STATUS     - OS/VS2 SYSTEM CONTROL PROGRAM 2                 @G64UPSS 00160064
*              06/15/78   LEVEL=05                             @G64UPSS 00170064
*                                                              @G64UPSS 00180064
* INVOCATION - SPECIFY: IEECRDCM DEV=,A=,KEY=,DSECT=           @G64UPSS 00190064
*              WHERE:                                          @G64UPSS 00200064
*                DEV=   A - 3066 DEVICE                        @G64UPSS 00210064
*                       B - 2250 DEVICE                        @G64UPSS 00220064
*                       C - 2260 DEVICE                        @G64UPSS 00230064
*                       D - 3277 MOD 2 TYPE DEVICE (3158 3036) @G64UPSS 00240064
*                       E - 3277 MOD 1 DEVICE                  @G64UPSS 00250064
*                A=     NUMBER OF DISPLAY AREAS AND THE        @G64UPSS 00260064
*                       LENGTHS OF EACH.                       @G64UPSS 00270064
*                       EXAMPLE: A=(6,4) WHERE TWO AREAS ARE   @G64UPSS 00280064
*                       BEING DEFINED. THE FIRST CONSISTS OF   @G64UPSS 00290064
*                       6 LINES; THE SECOND OF 4 LINES.        @G64UPSS 00300064
*                KEY=   FULL NAME OF THE PFK AREA FOR THIS     @G64UPSS 00310064
*                       DEVICE. EXAMPLE: "IEECVK10"            @G64UPSS 00320064
*                DSECT= Y ONLY ONE SACB WILL BE GENERATED.     @G64UPSS 00330064
*                       ENTRY STATEMENTS WILL NOT BE           @G64UPSS 00340064
*                       GENERATED. DO NOT SPECIFY DSECT= IF    @G64UPSS 00350064
*                       DSECT=Y IS NOT WANTED.                 @G64UPSS 00360064
*                                                              @G64UPSS 00370064
* FUNCTION   - THIS MACRO IS INVOKED AT SYSGEN TO BUILD AN     @G64UPSS 00380064
*              RDCM WITH SACBS IN THE NUCLEUS. THE MAPPING     @G64UPSS 00390064
*              MACRO FOR THIS CONTROL BLOCK IS IEERDCM.        @G64UPSS 00400064
*                                                              @G64UPSS 00410064
* CHANGE-ACTIVITY -                                            @G64UPSS 00420064
*              G64DPSS                                         @G64DPSS 00430064
*              G64UPSS                                         @G64UPSS 00440064
*                                                              @G64UPSS 00450064
*********************************************************************** 00460064
         SPACE 2                                               @G64UPSS 00470064
         LCLA        &N,&S,&E                                           00480064
         LCLC  &TABLE,&C,&D,&F,&G,&H                           @G64DPSS 00490064
         AIF   ('&DSECT' EQ '').NODEC                                   00750002
&S       SETA   1                                                       00800002
&E       SETA   1                                                       00850002
&D       SETC   '1'                                                     00900002
         AGO   .POS                                                     00950002
.NODEC   ANOP                                                           01000002
         AIF   ('&A'  EQ  '').NEG                                       01050002
&S       SETA    N'&A                                                   01100002
&E       SETA  1                                                        01150002
&D       SETC  '&A(&E)'                                                 01200002
         AGO   .POS                                                     01250002
.NEG     ANOP                                                           01300002
&S       SETA  0                                                        01350002
.POS     ANOP                                                           01400002
&N       SETA  1                                                        01450002
&F       SETC  '0'                                                      01500002
&TABLE   SETC  'ABCDEFGHIJK'                                            01550002
         AIF   (&S  EQ  0).NUN                                          01600002
&C       SETC  '&TABLE'(&N,1)           COMPUTE AREA ID                 01650002
&F       SETC  '&TABLE'(&S,1)                                           01700002
.NUN     ANOP                                                           01750002
DCMADTRN DC    F'0'                POINTER TO PAGEABLE DCM              01800002
         DC    X'00'               RESERVED                             01850002
DCMRFLGS DC    X'00'               FLAGS                                01900002
DCMDOM   EQU   X'10'               DOM MUST BE TRIED                    01950002
DCMNIPP  EQU   X'08'               CONSOLE WAS USED BY NIP     @G64UPSS 01960064
DCMLEN   DC    H'0'                LENGTH OF TRANS DCM                  02000002
DCMADKP  DC    A(0)                ADDRESS OF ROUTED K COMMAND          02050002
*                                       PARAMETER LIST                  02100002
         AIF   ('&F' EQ '0').GGG                                        02150002
DCMTOPAR DC    C'&F'               TOP DISPLAY AREA DEFINED             02200002
         AGO   .HHH                                                     02250002
.GGG     ANOP                                                           02300002
DCMTOPAR DC    X'00'                                                    02350002
.HHH     ANOP                                                           02400002
DCMTOPDS DC    X'00'               TOP DISPLAY ON SCREEN                02450002
         DC    X'00'               RESERVED                             02500002
DCMDEVTY DC    X'00'               DEVICE TYPE FLAGS                    02550002
DCMTY60  EQU   X'80'               USABLE FOR SD              MC X01958 02600002
DCMTY50  EQU   X'40'               UNUSABLE FOR SD            MC X01958 02650002
         AIF   (&S EQ 0).NONE      IF NO SDS AREAS REQUESTED            02700002
DCMADSDS DC    A(DCMACB)           POINTER TO FIRST SDS SUPPORT AREA    02750002
         AGO   .RMS                                                     02800002
.NONE    ANOP                                                           02850002
DCMADSDS DC    F'0'                POINTER TO FIRST SDS SUPPORT AREA    02900002
.RMS     ANOP                                                           02950002
         AIF   ('&DEV' EQ '').EEE                                       03000002
&H       SETC  '03'                RMS CCW INDEX IN IEECSUB    @G64DPSS 03050064
         AIF   ('&DEV' NE 'A').AAA                                      03070064
&G       SETC  '3066'                                                   03090064
&H       SETC  '04'                RMS CCW INDEX FOR 3066      @G64DPSS 03110064
         AGO   .XXX                                                     03150002
.AAA     ANOP                                                           03200002
         AIF   ('&DEV' NE 'B').BBB                                      03250002
&G       SETC  '2250'                                                   03300002
         AGO   .XXX                                                     03350002
.BBB     ANOP                                                           03400002
         AIF   ('&DEV' NE 'C').CCC                                      03450002
&G       SETC  '2260'                                                   03500002
         AGO   .XXX                                                     03550002
.CCC     ANOP                                                           03600002
         AIF   ('&DEV' NE 'D').DDD                                      03650002
&G       SETC  'AN19'                                                   03700002
         AGO   .XXX                                                     03750002
.DDD     ANOP                                                           03800002
         AGO   .EEE                NO RMS CCWS FOR 3277 MOD 1  @G64DPSS 03850064
.XXX     ANOP                                                           03890064
DCMRMS   DC    X'&H'               INDEX OF CCW FOR USER MSG   @G64DPSS 03930064
DCMADRMS DC    VL3(SUB&G)          POINTER TO RMS CCWS                  03970064
         AGO   .YYY                                                     04010064
.EEE     ANOP                                                           04050064
DCMRMS   DC    X'00'               INDEX OF CCW FOR USER MSG   @G64DPSS 04090064
DCMADRMS DC   XL3'00'                                                   04250002
.YYY     ANOP                                                           04300002
DCMWLAST DC    F'0'                PT CON Q ENTRY LAST OUT (O-O-L)      04350002
DCMRMSAL DC    H'0'                NUMBER LINES IN MSG AREA             04400002
DCMDOMKY DC    H'0'                CONSOLE DOM ELEMENT        MC XM5812 04450002
DCMSUBAD DC    V(IEECVSUB)         POINTER TO SUB                       04500002
         AIF   ('&KEY' EQ '').MARA                                      04550002
DCMADPFK DC    V(&KEY)             POINTER TO RESIDENT PFK AREA         04600002
         AGO   .MARB                                                    04650002
.MARA    ANOP                                                           04700002
DCMADPFK DC    F'0'                POINTER TO RESIDENT PFK AREA         04750002
.MARB    ANOP                                                           04800002
DCMINTVL DC    H'0'                INTERVAL FOR THIS DCM                04850002
DCMTMCTR DC    H'0'                TIME COUNTER FOR THIS DCM            04900002
DCMR2FLG DC    X'00'               TIMER FLAGS                          04950002
DCMRXSFL EQU   X'80'               FULL SCREEN FLAG                     05000002
DCMRXUNV EQU   X'40'               UNVIEWABLE MESSAGE DISPLAYED         05050002
DCMRXTMR EQU   X'20'               TIMER FLAG                           05100002
DCMRXRLL EQU   X'10'               READY TO ROLL                        05150002
DCMRXDEL EQU   X'08'               PENDING DELETE REQUEST               05200002
DCMRXTIM EQU   X'02'               TIMER ELAPSED FOR THIS DISPLAY       05250002
DCMRXDCM EQU   X'01'               T DCM IS IN CORE                     05300002
DCMR3FLG DC    X'00'               MISC FLAGS                           05350002
DCMSTSWT EQU   X'80'               CHANGING STATUS OF OUTPUT ONLY CON   05400002
DCMKVIP  EQU   X'40'               ENTRY FOR K VARY COMMAND             05450002
DCMCLPR  EQU   X'20'               CLOSE IN PROCESS                     05500002
DCMRXSCN EQU   X'10'               ASY ERROR MESSAGE ON SCREEN          05550002
DCMR3PKA EQU   X'08'               DA I/O COMPLETE FLAG                 05600002
DCMRXHMT EQU   X'04'               FULL SCREEN SIMULATED?     MC YM4102 05610002
         DC    XL2'0'              RESERVED                             05650002
DCMRQDEL DC    XL8'40'             BUFFER FOR PENDING DELETE REQUESTS   05700002
DCMRSV01 DC    F'0'                RESERVED                    @G64UPSS 05702064
         AIF   (&S EQ 0).END       IF NO SDS AREAS, DEPART              05722064
         SPACE 3                                                        05742064
*********************************************************************** 05762064
* SCREEN AREA CONTROL BLOCK - SACB                                      05782064
*********************************************************************** 05802064
         SPACE                                                          05900002
DCMPACB  EQU   *                   SACB PREFIX                          05950002
DCMPLN   DC    FL2'&D'                  SYSGEN LENGTH OF AREA           06000002
DCMPLNPR DC    H'04'               LENGTH OF SACB PREFIX-IN BYTES       06050002
DCMPRSZ  EQU   *-DCMPACB           SIZE OF SACB PREFIX IN BYTES         06100002
         SPACE                                                          06150002
DCMACB   EQU   *                   SCREEN AREA CONTROL BLOCK            06200002
         AIF   (&N EQ &S).ZERO     BRANCH IF LAST AREA                  06250002
DCMACBNX DC    A(*+DCMACBSZ+DCMPRSZ) POINTER TO NEXT SACB               06300002
         AGO   .AID                                                     06350002
.ZERO    ANOP                                                           06400002
DCMACBNX DC    A(0)                POINTER TO NEXT SACB                 06450002
.AID     ANOP                                                           06500002
DCMAID   DC    C'&C'               AREA ID                              06550002
DCMASACB DC    X'80'               SACB FLAGS                           06600002
DCMAUSE  EQU   X'80'               AREA PRESENTLY DEFINED     MB Y02958 06650002
DCMAGM   EQU   X'40'               GETMAINED SACB                       06700002
DCMALN   DC    FL2'&D'                  LENGTH OF AREA                  06750002
DCMATOP  DC    X'00'               TOP ROW OF AREA                      06800002
DCMACLR  EQU   *                   REINITIALIZED PORTION      MB Y02958 06810002
DCMAROW  DC    X'00'               ROW TO BE WRITTEN NEXT               06850002
DCMAFR   DC    H'00'               FRAME ON SCREEN                      06900002
DCMAMJWQ DC    A(0)                POINTER TO CON Q ENTRY FOR MAJOR     06950002
DCMAMIN  DC    A(0)                POINTER TO MINOR WQE                 07000002
DCMATIME DC    F'0'                TIME CONTROL LINE WAS WRITTEN        07050002
DCMAMT   DC    XL2'0'              RESERVED                   MB Y02958 07100002
DCMAFLG1 DC    X'00'               AREA FLAGS1                          07200002
DCMADISP EQU   X'40'               DISPLAY IN AREA                      07250002
DCMADEND EQU   X'20'               END OF DISPLAY ON SCREEN             07300002
DCMAFRPR EQU   X'10'               FRAMING IN PROGRESS                  07350002
DCMAFULL EQU   X'08'               FRAME FULL                           07400002
DCMABL   EQU   X'04'               BLANKING TO BE DONE                  07450002
DCMAFLG2 DC    X'00'               AREA FLAGS 2                         07500002
DCMALMIN EQU   X'80'               SAVED POINTER TO LAST MINOR OUTPUT   07550002
DCMAWCON EQU   X'40'               WRITE CONTROL LINE                   07600002
DCMARCON EQU   X'20'               REWRITE CONTROL LINE                 07650002
DCMAMJFR EQU   X'10'               MAJOR WQE HAS BEEN FOUND             07700002
DCMADFLG DC    X'00'               DYNAMIC DISPLAY FLAGS      MB Y02958 07750002
DCMADD   EQU   X'80'               TRACK IN AREA              MB Y02958 07800002
DCMAHOLD EQU   X'40'               TRACK IN HOLD MODE         MB Y02958 07850002
DCMACSIB EQU   X'20'               TRACK WITH CONTINUATION LINE IN     *07900002
                                   SCREEN IMAGE BUFFER        MB Y02958 07950002
DCMATRCK DC    X'00'               TRACK REQUEST INDICATORS   MB Y02958 07960002
DCMATJOB EQU   X'80'               TRACK JOBS                 MB Y02958 07970002
DCMATJBL EQU   X'40'               TRACK JOBS,LIST            MB Y02958 07980002
DCMATTS  EQU   X'08'               TRACK TS                   MB Y02958 07990002
DCMATTSL EQU   X'04'               TRACK TS,LIST              MB Y02958 07992002
DCMATA   EQU   X'88'               TRACK A                    MB Y02958 07994002
DCMATAL  EQU   X'44'               TRACK A,LIST               MB Y02958 07996002
DCMAUTME DC    H'0'                UTME=NNN VALUE             MB Y02958 07998002
DCMATECB DC    F'0'                TRACK CANCEL ECB ADDRESS   MB Y02958 07998402
DCMAPAD  DC    F'0'                RESERVED                   MB Y02958 07998802
*                                  NOTE: WHEN THE SACB IS               08000002
*                                       REINITIALIZED, FIELDS           08050002
*                                       DCMAROW THROUGH THE END         08100002
*                                       (INCLUDING THE RESERVED         08150002
*                                       FIELD) ARE ZERO'D               08200002
DCMACBND EQU   *                   END OF SACB                          08250002
DCMCLRLN EQU   *-DCMACLR           LENGTH FOR REINITIALIZING  MB Y02958 08260002
DCMACBSZ EQU   *-DCMACB            SIZE OF SACB IN BYTES                08300002
.NEXT    AIF   (&N EQ &S).END      BRANCH IF LAST AREA IS DONE          08350002
&N       SETA  &N+1                                                     08400002
&E       SETA  &E+1                                                     08450002
&C       SETC  '&TABLE'(&N,1)           COMPUTE AREA ID                 08500002
&D       SETC  '&A(&E)'                                                 08550002
         SPACE                                                          08600002
*********************************************************************** 08650064
* SCREEN AREA CONTROL BLOCK - SACB                                      08660064
*********************************************************************** 08670064
         SPACE 1                                               @G64UPSS 08680064
*                                  SACB PREFIX                          08700002
         DC    FL2'&D'             SYSGEN LENGTH OF AREA                08750002
         DC    H'04'               LENGTH OF SACB PREFIX-IN BYTES       08800002
*                                  SACB                                 08850002
         AIF   (&N EQ &S).Z        BRANCH IF LAST AREA                  08900002
         DC    A(*+DCMACBSZ+DCMPRSZ) POINTER TO NEXT SACB               08950002
         AGO   .ID                                                      09000002
.Z       ANOP                                                           09050002
         DC    A(0)                POINTER TO NEXT SACB                 09100002
.ID      ANOP                                                           09150002
         DC    C'&C'               AREA ID                              09200002
         DC    X'80'               SACB FLAGS                           09250002
         DC    FL2'&D'             LENGTH OF AREA                       09300002
         DC    X'00'               TOP ROW OF AREA                      09350002
         DC    X'00'               ROW TO BE WRITTEN NEXT               09400002
         DC    H'00'               FRAME ON SCREEN                      09450002
         DC    A(0)                POINTER TO CON Q ENTRY FOR MAJOR     09500002
         DC    A(0)                POINTER TO MINOR WQE                 09550002
         DC    F'0'                TIME CONTROL LINE WAS WRITTEN        09600002
         DC    XL2'00'             RESERVED                   MB Y02958 09650002
         DC    X'00'               AREA FLAGS1                          09700002
         DC    X'00'               AREA FLAGS2                          09750002
         DC    X'00'               DISPLAY FLAGS                        09800002
         DC    X'00'               TRACK REQUEST INDICATORS   MB Y02958 09850002
         DC    H'0'                UTME=NNN VALUE             MB Y02958 09860002
         DC    F'0'                CANCEL TRACK ECB ADDRESS   MB Y01958 09870002
         DC    F'0'                RESERVED                   MB Y02958 09880002
         AGO   .NEXT               RETURN TO GENERATE NEXT AREA         09900002
.END     MEND                                                           09950002
