         MACRO                                                          02000002
         IEEDUCM &A1,&B1,&C1,&E1,&G1,&H1,&ROUTCD1=,&AUTH1=,            +04000002
               &ALTCON1=,&COMPCN1=,&ALTCOM1=                            06000002
         GBLA  &UCMINDX       TOTAL CONSOLE COUNT                       08000002
         GBLC  &UCMA(99),&UCMB(99),&UCMC(99)                            10000002
         GBLC  &UCME(99),&UCMG(99),&UCMH(99)                            11000002
         GBLC  &ROUTCDE(99),&AUTH(99),&ALTCON(99)                       12000002
         GBLC  &COMPCON(99),&ALTCOMP(99)                                14000002
         LCLA  &INDEX         PARAMETER INDEX                           16000002
         LCLA  &N             PARAMETER COUNT                           18000002
.*                                                                      20000002
.*       OS/VS2 RELEASE 3, 07/03/74, LEVEL=2                            22000003
.*                                                                      24000002
.*       THIS MACRO PUTS THE PARAMETERS PASSED IN ONE OR TWO CALLS TO   26000002
.*       IEEDUCM INTO GLOBAL DATA ARRAYS FOR PROCESSING BY IEECUCM.     28000002
.*       UP TO 50 ENTRIES MAY BE IN EACH SUBLIST PER CALL.              30000002
.*                                                                      32000002
&N       SETA  N'&A1          GET NUMBER OF ENTRIES IN SUBLIST          34000002
.DUCM100 ANOP                                                           36000002
&UCMINDX SETA  &UCMINDX+1     STEP TOTAL COUNTER                        38000002
&INDEX   SETA  &INDEX+1       STEP SUBLIST ENTRY COUNTER                40000002
&UCMA(&UCMINDX) SETC '&A1(&INDEX)' SET DEVICE ADDRESS                   42000002
&UCMB(&UCMINDX) SETC '&B1(&INDEX)' SET 2-BYTE NAME MODIFIER             43000002
&UCMC(&UCMINDX) SETC '&C1(&INDEX)' SET INITIAL ATTRIBUTES (WAXA)        44000002
&UCME(&UCMINDX) SETC '&E1(&INDEX)' SET DEVICE TYPE INDICATOR            45000002
&UCMG(&UCMINDX) SETC '&G1(&INDEX)' SET CONSOLE'S I/O CAPABILITY         46000002
.*                                 INDICATOR                            47000002
&UCMH(&UCMINDX) SETC '&H1(&INDEX)' SET INTEGRATED OPERATOR'S CONSOLE    52000002
.*                                 INDICATOR                            53000002
&ROUTCDE(&UCMINDX) SETC '&ROUTCD1(&INDEX)' SET ROUTING CODE             54000002
&AUTH(&UCMINDX) SETC '&AUTH1(&INDEX)' SET AUTHORIZATION CODE            56000002
&ALTCON(&UCMINDX) SETC '&ALTCON1(&INDEX)' SET ALTERNATE CONSOLE ADDRESS 58000002
&COMPCON(&UCMINDX) SETC '&COMPCN1(&INDEX)' SET COMPOSITE CONSOLE ADDR   60000002
&ALTCOMP(&UCMINDX) SETC '&ALTCOM1(&INDEX)' SET ALTERNATE COMPOSITE ADDR 62000002
         AIF   (&INDEX NE &N).DUCM100                                   64000002
         MEND                                                           66000002
