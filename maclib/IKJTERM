         MACRO                                                          00050001
&LABEL   IKJTERM &TYPNAM,&OPTION,&OPTION2,&OPTION3,&OPTION4,&PROMPT=,  *00100001
               &RSVWD=,&DEFAULT=,&TYPE=ANY,&SBSCRPT=,&VALIDCK=,&HELP=   00150001
.********************************************************************   00160001
.*                                                                      00170001
.* TITLE  -  IKJTERM MACRO (SYMBOLIC DEBUG PROJECT)                     00172001
.*                                                                      00174001
.* FUNCTION  -  THE IKJTERM MACRO IS USED BY COMMAND PROCESSORS TO      00180001
.*              DEFINE TO PARSE THE EXPECTED FORMAT OF A TERM-TYPE      00190001
.*              PARAMETER.                                              00190101
.*                                                                      00190401
.* INPUT  -  THE PROTOTYPE STATEMENT IS AS DEFINED ABOVE. IT'S          00192001
.*           PARAMETERS REPRESENT THE FOLLOWING:                        00192401
.*               &LABEL   - STATEMENT LABEL IS REQUIRED                 00194001
.*               &TYPNAM  - A PARAMETER TYPE IS REQUIRED                00196001
.*               &OPTION,&OPTION2,OPTION3, AND &OPTION4  -              00198001
.*                          FOUR POSSIBLE POSITIONAL PARAMETERS,        00198401
.*                          LIST,RANGE,SBSCRPT, AND ASIS/UPPERCASE.     00198801
.*               &RSVWD   - SPECIFIES THE PCE-LABEL WHICH WILL          00199201
.*                          DESCRIBE POSSIBLE LITERAL CONSTANTS.        00199601
.*               &TYPE    - INDICATES THE TYPE OF TERM TO BE            00199701
.*                          SCANNED FOR - VARIABLE,CONSTANT,            00199801
.*                          STATEMENT NUMBER, OR ANY(VARIABLE OR        00199901
.*                          CONSTANT). DEFAULT IS ANY.                  00216601
.*               &SBSCRPT - SPECIFIES THE PCE-LABEL WHICH WILL          00226601
.*                          DESCRIBE THE POSSIBLE SUBSCRIPT ON          00228601
.*                          THIS TERM.                                  00230601
.*               &HELP    - SUPPLIES SECOND-LEVEL MESSAGES.             00232601
.*               &VALIDCK - SPECIFIES THE ADDRESS OF A USER-SUPPLIED    00233001
.*                          VALIDITY CHECK EXIT ROUTINE.                00233101
.*               &PROMPT  - SUPPLIES DATA TO BE USED IF PROMPTING FOR   00233201
.*                          A MISSING PARAMETER.                        00238801
.*               &DEFAULT - SUPPLIES VALUE TO BE USED IF THE PARAMETER  00240801
.*                          IS NOT ENTERED.                             00242801
.*                                                                      00243201
.* OUTPUT  -  THIS MACRO PRODUCES A PARAMETER CONTROL ENTRY (PCE) TO BE 00243601
.*            PASSED TO THE PARSE SERVICE ROUTINE.                      00244001
.*            IF AN ERROR IS ENCOUNTERED IN PROCESSING THE MACRO        00244101
.*            INSTRUCTION ENTERED BY THE CP, AN APPROPRIATE ERROR       00244201
.*            MESSAGE IS PRINTED AND THE PCE IS NOT GENERATED.          00244301
.*                                                                      00258201
.********************************************************************   00268201
.*                                                                      00270201
.* THE FOLLOWING CODE DEFINES VARIABLES TO BE USED IN THE MACRO         00270601
.*     EXPANSION.                                                       00271001
.*        GLOBALS   -   IKJCSNM - CSECT NAME FROM IKJPARM MCARO LABEL   00271401
.*                      IKJDSNM - DSECT NAME FOR PDL                    00271801
.*        LOCALS(ARITHMETIC)  -  T - USED AS COUNTER FOR HELP MSG.      00271901
.*                               END - SECOND COUNTER FOR HELP MSG.     00272001
.*                               PDESIZE - AMOUNT OF SPACE FOR PDE      00272101
.*              (BINARY)      -  FLAGS FOR VARIOUS OPTIONS AND TO       00276701
.*                               INDICATE THAT NO PCE IS TO BE          00278701
.*                               GENERATED.                             00280701
.*              (CHARACTER)   -  QUOTE - USED WHEN QUOTES ARE MISSING   00281101
.*                               FROM &TYPNAM                           00281201
.*                                                                      00281301
         GBLC  &IKJCSNM,&IKJDSNM                                        00281501
         LCLA  &T,&PDESIZE,&END                                         00286101
         LCLB  &FLIST,&FRANGE,&FASIS,&FPRPT,&FDFLT,&FHELP,&FVALID       00300001
         LCLB  &FENDBIT,&FSUBSC,&FRSVWD,&FSTMT,&FVAR,&FCNST,&FANY       00350001
         LCLB  &SUBTRM                                                  00360001
         LCLC  &QUOTE                                                   00400001
&FENDBIT SETB  0                                                        00450001
.*                                                                      00460001
.* DETERMINE IF EXTRANEOUS POSITIONAL PARAMETERS HAVE BEEN SPECIFIED.   00470001
.*                                                                      00480001
         AIF   (N'&SYSLIST LT 6).GOOD                                   00500001
         MNOTE 8,'NO MORE THAN 5 POSITIONAL PARAMETERS ALLOWED'         00550001
&FENDBIT SETB  1                                                        00550401
.*                                                                      00552001
.* DETERMINE IF TYPE NAME HAS BEEN ENTERED.                             00560001
.*                                                                      00580001
.GOOD    AIF   ('&TYPNAM' EQ 'LIST' OR '&TYPNAM' EQ 'RANGE' OR         *00600001
               '&TYPNAM' EQ 'ASIS' OR '&TYPNAM' EQ 'UPPERCASE' OR      *00650001
               '&TYPNAM' EQ '').BAD                                     00700001
         AGO   .GOOD1                                                   00750001
.BAD     MNOTE 8,'PARAMETER TYPE MISSING BUT REQUIRED'                  00800001
&FENDBIT SETB  1                                                        00850001
.*                                                                      00852001
.* DETERMINE IF CSECT NAME IS MISSING.                                  00860001
.*                                                                      00870001
.GOOD1   AIF   ('&IKJCSNM' NE '').GOOD2                                 00900001
         MNOTE 8,'CSECT NAME (FROM IKJPARM MACRO LABEL) IS MISSING'     00950001
&FENDBIT SETB  1                                                        01000001
.*                                                                      01010001
.* DETERMINE IF STATEMENT LABEL WAS ENTERED.                            01020001
.*                                                                      01030001
.GOOD2   AIF   ('&LABEL' NE '').GOOD3                                   01050001
         MNOTE 8,'STATEMENT LABEL MISSING BUT REQUIRED'                 01100001
&FENDBIT SETB  1                                                        01150001
.GOOD3   ANOP                                                           01200001
.*                                                                      01210001
.* THE FOLLOWING CODE SETS FLAGS INDICATING THE OPTIONS SPECIFIED       01220001
.*   ON POSITIONAL PARAMETERS.                                          01230001
.*                                                                      01240001
&FLIST   SETB  ('&OPTION' EQ 'LIST' OR '&OPTION2' EQ 'LIST' OR         *01250001
               '&OPTION3' EQ 'LIST' OR '&OPTION4' EQ 'LIST')            01300001
&FRANGE  SETB  ('&OPTION' EQ 'RANGE' OR '&OPTION2' EQ 'RANGE' OR       *01350001
               '&OPTION3' EQ 'RANGE' OR '&OPTION4' EQ 'RANGE')          01400001
&FASIS   SETB  ('&OPTION' EQ 'ASIS' OR '&OPTION2' EQ 'ASIS' OR         *01450001
               '&OPTION3' EQ 'ASIS' OR '&OPTION4' EQ 'ASIS')            01500001
&SUBTRM   SETB  ('&OPTION' EQ 'SBSCRPT' OR '&OPTION2' EQ 'SBSCRPT' OR  *01502001
               '&OPTION3' EQ 'SBSCRPT' OR '&OPTION4' EQ 'SBSCRPT')      01504001
.*                                                                      01510001
.* THE FOLLOWING CODE DETERMINES IF INCORRECT POSITIONAL PARAMETERS     01520001
.*   HAVE BEEN ENTERED.                                                 01530001
.*                                                                      01540001
         AIF   ('&OPTION' EQ 'LIST' OR '&OPTION' EQ 'RANGE' OR         *01550001
               '&OPTION' EQ 'ASIS' OR '&OPTION' EQ 'UPPERCASE' OR      *01600001
               '&OPTION' EQ '' OR '&OPTION' EQ 'SBSCRPT').OPT1          01650001
         MNOTE 8,'''&OPTION'' IS AN INVALID POSITIONAL PARAMETER'       01700001
&FENDBIT SETB  1                                                        01710001
.OPT1    AIF   ('&OPTION2' EQ 'LIST' OR '&OPTION2' EQ 'RANGE' OR       *01750001
               '&OPTION2' EQ 'ASIS' OR '&OPTION2' EQ 'UPPERCASE' OR    *01800001
               '&OPTION2' EQ '' OR '&OPTION2' EQ 'SBSCRPT').OPT2        01850001
         MNOTE 8,'''&OPTION2'' IS AN INVALID POSITIONAL PARAMETER'      01900001
&FENDBIT SETB  1                                                        01950001
.OPT2    AIF   ('&OPTION3' EQ 'LIST' OR '&OPTION3' EQ 'RANGE' OR       *02000001
               '&OPTION3' EQ 'ASIS' OR '&OPTION3' EQ 'UPPERCASE' OR    *02010001
               '&OPTION3' EQ '' OR '&OPTION3' EQ 'SBSCRPT').OPT3        02020001
         MNOTE 8,'''&OPTION3'' IS AN INVALID POSITIONAL PARAMETER'      02030001
&FENDBIT SETB  1                                                        02040001
.OPT3    AIF   ('&OPTION4' EQ 'LIST' OR '&OPTION4' EQ 'RANGE' OR       *02043401
               '&OPTION4' EQ 'ASIS' OR '&OPTION4' EQ 'UPPERCASE' OR    *02045401
               '&OPTION4' EQ '' OR '&OPTION4' EQ 'SBSCRPT').OPT4        02047401
         MNOTE 8,'''&OPTION4'' IS AN INVALID POSITIONAL PARAMETER'      02047801
&FENDBIT SETB  1                                                        02048201
.*                                                                      02048701
.* UPPERCASE AND ASIS CANNOT BOTH BE SPECIFIED.                         02052101
.*                                                                      02055501
.OPT4    AIF   (('&OPTION' EQ 'UPPERCASE' OR '&OPTION2' EQ             *02058901
               'UPPERCASE' OR '&OPTION3' EQ 'UPPERCASE') AND           *02062301
               &FASIS).ERROR3                                           02065701
         AGO   .A1                                                      02069101
.ERROR3  MNOTE 8,'BOTH ''UPPERCASE'' AND ''ASIS'' CANNOT BE SPECIFIED'  02072501
&FENDBIT SETB  1                                                        02075901
.A1      ANOP                                                           02079301
.*                                                                      02082701
.* THE FOLLOWING CODE SETS FLAGS INDICATING KEYWORD OPTIONS.            02086101
.*                                                                      02089501
&FPRPT   SETB  ('&PROMPT' NE '')                                        02092901
&FDFLT   SETB  ('&DEFAULT' NE '')                                       02096301
&FHELP   SETB  ('&HELP' NE '')                                          02099701
&FVALID  SETB  ('&VALIDCK' NE '')                                       02103101
&FSUBSC  SETB  ('&SBSCRPT' NE '')                                       02106501
&FRSVWD  SETB  ('&RSVWD' NE '')                                         02109901
&FSTMT   SETB  ('&TYPE' EQ 'STMT')                                      02111901
&FVAR    SETB  ('&TYPE' EQ 'VAR')                                       02113901
&FCNST   SETB  ('&TYPE' EQ 'CNST')                                      02115901
&FANY    SETB  ('&TYPE' EQ 'ANY')                                       02117901
.*                                                                      02119301
         AIF  (&FRANGE AND &SUBTRM).BAD3                                02121301
         AGO   .GOODI4                                                  02121401
.BAD3    ANOP                                                           02121501
         MNOTE 4,'RANGE OPTION ILLEGAL WITH "SBSCRPT". IGNORED'         02121601
&FRANGE  SETB  0                                                        02122001
.GOODI4  AIF   (&FLIST AND &SUBTRM).BAD4                                02122101
         AGO   .GOODI5                                                  02122201
.BAD4    ANOP                                                           02122301
         MNOTE 4,'LIST OPTION ILLEGAL WITH "SBSCRPT". IGNORED.'         02122701
&FLIST   SETB  0                                                        02123101
.GOODI5  AIF (NOT(&SUBTRM AND &FSUBSC)).GOODI6                          02123201
         MNOTE 8,'"SBSCRPT" OPTION ILLEGAL WITH "SBSCRPT=LABEL" OPTION' 02123301
&FENDBIT SETB  1                                                        02123401
.GOODI6  ANOP                                                           02124501
.*                                                                      02126501
.* THE ONLY VALID 'TYPE' PARAMETERS ARE VAR,STMT,CNST, AND ANY          02127601
.*                                                                      02128701
         AIF   ('&TYPE' NE 'STMT' AND '&TYPE' NE 'VAR' AND '&TYPE' NE  *02129801
               'CNST' AND '&TYPE' NE 'ANY').ERROR                       02130901
         AGO   .GOOD9                                                   02132001
.ERROR   MNOTE 8,'INVALID ''TYPE'' PARAMETER'                           02133101
&FENDBIT SETB  1                                                        02134201
.*                                                                      02135301
.* PROMPT AND DEFAULT CANNOT BOTH BE SPECIFIED.                         02136401
.*                                                                      02137501
.GOOD9   AIF   (NOT(&FDFLT AND &FPRPT)).GOOD4                           02138601
         MNOTE 8,'PARAMETER CANNOT BOTH DEFAULT AND REQUIRE PROMPTING'  02139701
&FENDBIT SETB  1                                                        02140801
.*                                                                      02141901
.* RSVWD IS IGNORED IF TYPE=VAR OR TYPE=STMT.                           02143001
.*                                                                      02144101
.GOOD4   AIF   (('&TYPE' EQ 'VAR' OR '&TYPE' EQ 'STMT') AND            *02145201
               &FRSVWD).ERROR1                                          02146301
         AGO   .GOOD5                                                   02147401
.ERROR1  MNOTE 4,'''RSVWD'' IGNORED WITH ''&TYPE'''                     02148501
&FRSVWD  SETB  0                                                        02149601
.*                                                                      02150701
.* SBSCRPT IS IGNORED IF TYPE=STMT OR TYPE=CNST.                        02151801
.*                                                                      02152901
.GOOD5   AIF   (('&TYPE' EQ 'STMT' OR '&TYPE' EQ 'CNST') AND           *02154001
               &FSUBSC).ERROR2                                          02155101
         AGO   .GOOD6                                                   02156201
.ERROR2  MNOTE 4,'''SBSCRPT'' IGNORED WITH ''&TYPE'''                   02157301
&FSUBSC  SETB  0                                                        02158401
.*                                                                      02159501
.* IF AN ERROR HAS BEEN ENCOUNTERED DO NOT GENERATE PCE.                02169501
.*                                                                      02169601
.GOOD6   AIF   (NOT &FENDBIT).GEN                                       02169801
         MEXIT                                                          02179801
.GEN     ANOP                                                           02189801
&IKJCSNM CSECT ,                                                        02199801
* FLAGS - BIT3=PROMPT,    BIT4=DEFAULT, BIT6=HELP,                      02209801
*         BIT7=VALIDCK,   BIT8=LIST,    BIT9=ASIS,    BIT10=RANGE,      02219801
*         BIT11=SBSCRPT,  BIT12=RSVWD                                   02229801
DENT&SYSNDX EQU *                                                       02239801
&LABEL   EQU   *                                                        02240201
         DC    BL2'110&FPRPT&FDFLT.0&FHELP&FVALID&FLIST&FASIS&FRANGE&FS*02241801
               UBSC&FRSVWD.000'      FLAGS                              02243801
         DC    AL2(DEND&SYSNDX-DENT&SYSNDX)      LENGTH OF PCE          02245801
         AIF   (&SUBTRM).GOODI7                                         02246201
         DC    AL2(LAB&SYSNDX-&IKJDSNM)          OFFSET TO PDE          02247801
         AGO   .GOODI8                                                  02247901
.GOODI7  DC   AL2(0)        NO PDE OFFSET - PREVIOUSLY ALLOCATED        02248001
.GOODI8  ANOP                                                           02248101
* TYPES - BIT0=STMT, BIT1=VAR, BIT2=CNST, BIT3=ANY,BIT4=SBSCRPT         02248201
         DC    BL1'&FSTMT&FVAR&FCNST&FANY&SUBTRM.000'   TYPE SPECIFIED  02248601
.*                                                                      02249001
.* THE FOLLOWING CODE SUPPLIES QUOTES TO &TYPNAM IF THEY ARE MISSING.   02249401
.*                                                                      02249501
&QUOTE   SETC  '&TYPNAM'(1,1)                                           02249601
         AIF   ('&QUOTE' EQ '''').QUOT                                  02249701
&QUOTE   SETC  ''''                                                     02299701
         DC    AL2(IKJ@&SYSNDX-*),AL2(18),C&QUOTE&TYPNAM&QUOTE    PARAM*02309701
               ETER TYPE MESSAGE SEGMENT                                02319701
         AGO   .EQUGEN1                                                 02329701
.QUOT    ANOP                                                           02339701
         DC    AL2(IKJ@&SYSNDX-*),AL2(18),C&TYPNAM    PARAMETER TYPE ME*02341701
               SSAGE SEGMENT                                            02343701
.EQUGEN1 ANOP                                                           02345701
IKJ@&SYSNDX EQU *                                                       02347701
         AIF   (&FPRPT).PRPT      PROMPT SPECIFIED                      02349301
         AIF   (NOT &FDFLT).NPRDF                                       02349401
         DC    AL1(IKJ$&SYSNDX-*-2),C&DEFAULT        DEFAULT DATA       02349501
         AGO   .EQUGEN                                                  02349601
.PRPT    DC     AL1(IKJ$&SYSNDX-*-2),C&PROMPT        PROMPT DATA        02399601
.EQUGEN  ANOP                                                           02409601
IKJ$&SYSNDX EQU *                                                       02419601
.NPRDF   ANOP                                                           02429601
         AIF   (NOT &FSUBSC).NOSUBSC                                    02439601
         DC    AL2(&SBSCRPT-&IKJCSNM)    OFFSET TO SUBSCRIPT PCE        02441601
.NOSUBSC AIF   (NOT &FRSVWD).NORSWD                                     02443601
         DC    AL2(&RSVWD-&IKJCSNM)       OFFSET TO RESERVE WORD PCE    02445601
.NORSWD  ANOP                                                           02447601
IKJA&SYSNDX EQU *                                                       02448001
         AIF   (NOT &FHELP).NOHELP     IS HELP SPECIFIED                02448401
         DC    AL2(IKJB&SYSNDX-*)      LENGTH OF HELP DATA              02448801
&T       SETA  1                                                        02449201
.HELPLP  ANOP                                                           02449301
&T       SETA  &T+1                                                     02449401
         AIF   (&T EQ 256).HELPER                                       02449501
         AIF   (&T LE N'&HELP).HELPLP                                   02499501
         AGO   .HELPCT                                                  02509501
.HELPER  MNOTE 4,'ONLY 255 LEVELS OF HELP AVAILABLE. OTHERS IGNORED'    02519501
&T       SETA  &T+1                                                     02529501
.HELPCT  ANOP                                                           02539501
&T       SETA  &T-1                                                     02541501
         DC    AL1(&T)             NUMBER OF HELP MESSAGES              02543501
&END     SETA  &T                                                       02545501
&T       SETA  1                                                        02547501
.HELPLPA ANOP                                                           02547901
         DC    AL2(IKJ&SYSNDX.&T-*),AL2(00),C&HELP(&T) HELP MSG SEGMENT 02548301
IKJ&SYSNDX.&T EQU *                                                     02548701
&T       SETA  &T+1                                                     02548801
         AIF   (&T LE &END).HELPLPA                                     02548901
IKJB&SYSNDX EQU *                                                       02549001
.NOHELP  AIF   (NOT &FVALID).NOVALID     IS VALIDCK SPECIFIED           02549101
         DC    AL3(&VALIDCK)       VALIDCK ROUTINE ADDRESS              02549201
.NOVALID ANOP                                                           02549301
DEND&SYSNDX EQU *                                                       02582701
         AIF (&SUBTRM).NOALLOC                                          02592701
&IKJDSNM DSECT                                                          02599401
&PDESIZE SETA  41                                                       02601401
         AIF   (&FLIST AND &FRANGE AND &FSUBSC).DSECT                   02603401
&PDESIZE SETA  40                                                       02605401
         AIF   (&FRANGE AND &FSUBSC).DSECT                              02607401
&PDESIZE SETA  21                                                       02607801
         AIF   (&FLIST AND &FSUBSC).DSECT                               02608201
&PDESIZE SETA  20                                                       02608601
         AIF   (&FSUBSC).DSECT                                          02609001
&PDESIZE SETA  11                                                       02609401
         AIF   (&FLIST AND &FRANGE).DSECT  IS LIST AND RANGE SPECIFIED  02619401
&PDESIZE SETA  10                                                       02629401
         AIF   (&FRANGE).DSECT                                          02639401
&PDESIZE SETA  6                                                        02641401
         AIF   (&FLIST).DSECT                                           02643401
&PDESIZE SETA  5                                                        02645401
.DSECT   ANOP                                                           02647401
LAB&SYSNDX   DS    &PDESIZE.F      ALLOCATE PDE SPACE                   02647801
.NOALLOC  ANOP                                                          02647901
&SYSECT  CSECT ,               RESUME USERS CSECT                       02648201
         MEXIT                                                          02648601
         MEND                                                           02649001
