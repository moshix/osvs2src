         MACRO                                                          00050000
         ISTCBACB &AM=,&MF=,&CALLER=,                          @ZA34940C00100000
               &ACBLEN=,&ATRB=,&AVSPAC=,&BUFND=,   *** ACB KEYWORDS ***C00150000
               &BUFNI=,&BUFNO=,&BUFSP=,&CINV=,&DDNAME=,&ENDRBA=,       C00200000
               &ERROR=,&EXLST=,&FS=,&KEYLEN=,&LRECL=,                  C00250000
               &MACRF=,&NCIS=,&NDELR=,&NEXCP=,&NEXT=,&NINSR=,&NIXL=,   C00300000
               &NLOGR=,&NRETR=,&NSSS=,&NUPDR=,&OFLAGS=,&PASSWD=,&RKP=, C00350000
               &BSTRNO=,&CRA=,&OPENOBJ=,&AIXRKP=,               X04SVHSC00400000
               &MAREA=,&MLEN=,&JFCB=,                          @XM01140*00450000
               &USERPTR=,                                      @XM01129*00500000
               &STRNO=,&STMST=,&CATALOG=,&APPLID=                       00550000
.*                                                                      00600000
.*       CHANGE LEVEL = 0                                      @ZA34940 00650000
.*       DATE OF LAST CHANGE =  5 OCT  78 ( 78*278 )           @ZA34940 00700000
.*                                                                      00750000
.* FUNCTION    THIS MACRO ACTS AS A CONTROLLER FOR GENCB, MODCB, AND    00800000
.*             TESTCB FOR PROCESSING AN ACB.                            00850000
.*             IT IS CALLED BY IDACB2 TO BUILD VARIOUS ARRAYS TO BE     00900000
.*             USED BY IDACB2 TO BUILD A PARAMETER LIST OF ARGUMENT     00950000
.*             CONTROL ENTRIES THAT WILL BE PASSED TO THE CONTROL       01000000
.*             BLOCK MANIPULATION MODULE AT EXECUTE TIME.               01050000
.*             FOR NON-BIT KEYWORDS, THIS MACRO                @ZA34940 01100000
.*             CALLS ISTCB3 WHICH ACTUALLY                     @ZA34940 01150000
.*             SETS VALUES INTO ALL THE ARRAYS THAT ARE        @ZA34940 01200000
.*             REQUIRED.                                                01250000
.*                                                                      01300000
.*             FOR BIT KEYWORDS, ISTCB3 IS CALLED,             @ZA34940 01350000
.*             BUT THIS MACRO                                  @ZA34940 01400000
.*             SETS THE VALUE IN THE &IDAVALC ARRAY.                    01450000
.*             IT ALSO VALIDITY CHECKS BIT OPTIONS AND IF MUTUALLY      01500000
.*             EXCLUSIVE OPTIONS ARE SPECIFIED, AN ERROR MESSAGE IS     01550000
.*             ISSUED.                                                  01600000
.*                                                                      01650000
.*             IT IS ALSO CALLED BY THE ACB DECLARATIVE MACRO TO        01700000
.*             PROCESS THE BIT KEYWORDS MACRF, CATALOG, AND CRA.        01750000
.*             WHEN CALLED BY ACB, DIFFERENT GLOBALS ARE SET THAN       01800000
.*             THOSE USED BY THE CBMM. THESE GLOBALS ARE SET IN SUCH    01850000
.*             A MANNER TO ALLOW THE DECLARATIVE MACRO TO GENERATE      01900000
.*             A 'DC' USING THE GLOBALS DIRECTLY.                       01950000
.*                                                             @ZA34940 02000000
.* CHANGE ACTIVITY                                             @ZA34940 02050000
.*      CBMM SPLIT SUPPORT                                     @ZA34940 02100000
.*                                                             @ZA34940 02150000
.* GLOBALS                                                              02200000
         GBLB  &IDACB01                ERROR INDICATOR                  02250000
         GBLA  &IDAENO                 NUMBER OF ELEMENTS               02300000
         GBLA  &IDAELEN(50)            ELEMENT FIELD LENGTH ARRAY       02350000
         GBLC  &IDAVALC(50)            ELEMENT CHARACTER VALUE ARRAY    02400000
         GBLC  &IDAMAC1,&IDAMAC2,&IDAMAC3                       X04SVHS 02450000
.*             MACRF VALUES FOR ACB DECLARATIVE MACRO           X04SVHS 02500000
         GBLC  &IDAIFL1                 CATALOG/CRA VALUES FOR  X04SVHS 02550000
.*                                      ACB DECLARATIVE MACRO   X04SVHS 02600000
.* LOCALS                                                               02650000
         LCLA  &I,&A,&B,&C                       WORK VARIABLES         02700000
         LCLB  &ESDS,&KSDS,&WCK,&SSWD,&REPL      ATRB CODES             02750000
         LCLB  &RRDS,&SPAN,&UNQ                  ATRB CODES     X04SVHS 02800000
         LCLB  &PATH,&BASE,&OAIX                 OPENOBJ CODES  X04SVHS 02850000
         LCLB  &SCRA,&UCRA                       CRA CODES      X04SVHS 02900000
         LCLB  &MKEY,&MADR,&MCNV,&MSEQ,&MSKP     MACRF CODES            02950000
         LCLB  &MDIR,&IN,&OUT,&NUB,&UBF          MACRF CODES            03000000
         LCLB  &LOGONF,&NLOGON                   MACRF CODES     X03004 03050000
         LCLB  &NFX,&CFX,&RST,&NRS,&DDN,&DSN     MACRF CODES   @XM01125 03100000
         LCLB  &NRM,&AIX,&NSR,&LSR,&GSR          MACRF CODES    X04SVHS 03150000
         LCLB  &NCI,&ICI,&NDF,&DFR,&NIS,&SIS     MACRF CODES    X04SVHS 03200000
         LCLB  &OPEN                             OFLAGS CODE    X04SVHS 03250000
         LCLB  &VSAM,&VTAM                       AM CODES        X03004 03300000
         LCLB  &YES,&NO                          CATALOG CODES  X04SVHS 03350000
         LCLC  &Z16                     UNUSED PORTION OF BIT STRING    03400000
.* INITIALIZATION                                                       03450000
&Z16     SETC  '0000000000000000'       SET TO 16 ZEROES                03500000
.* SCAN ACB KEYWORDS ************************************************** 03550000
.*                                      BUFND                           03600000
         ISTCB3 ARG=&BUFND,ARGNAME=BUFND,KWTC=X'04',MF=&MF     @ZA34940 03650000
.********************************************************************** 03700000
.*                                      BUFNI                   X04SVHS 03750000
         ISTCB3 ARG=&BUFNI,ARGNAME=BUFNI,KWTC=X'05',MF=&MF     @ZA34940 03800000
.********************************************************************** 03850000
.*                                      BUFSP                   X04SVHS 03900000
         ISTCB3 ARG=&BUFSP,ARGNAME=BUFSP,KWTC=X'07',MF=&MF     @ZA34940 03950000
.********************************************************************** 04000000
.*                                      DDNAME                          04050000
         AIF   ('&DDNAME' EQ '').A000300  *** DDNAME ***                04100000
         AIF   ('&DDNAME(1)' EQ '*').A000200                            04150000
.*             IF INDIRECT ADDRESS SPECIFIED, BYPASS VALIDITY CHECKS    04200000
         AIF   (K'&DDNAME GT 8 OR '&DDNAME'(1,1) LT 'A' OR             C04250000
               '&DDNAME'(1,1) GT 'Z').A000100                           04300000
.*             DDNAME MUST BE 8 OR FEWER CHARS, AND FIRST CHAR MUST     04350000
.*                                      BE ALPHABETIC                   04400000
         AGO   .A000200                                                 04450000
.A000100 ISTERMAC 3,DDNAME,&DDNAME      ISSUE ERROR MESSAGE    @ZA34940 04500000
&IDACB01 SETB  1                        SET ERROR SWITCH                04550000
         AGO   .A000300                 SKIP PROCESSING                 04600000
.A000200 ANOP                                                           04650000
         ISTCB3 ARG=&DDNAME,ARGNAME=DDNAME,KWTC=X'09',MF=&MF,  @ZA34940*04700000
               INDRECT=YES                                      X04SVHS 04750000
&IDAELEN(&IDAENO) SETA 8                ELEMENT LENGTH IS 8 BYTES       04800000
.********************************************************************** 04850000
.A000300 ANOP                                                           04900000
.*                                      EXLST                           04950000
         ISTCB3 ARG=&EXLST,ARGNAME=EXLST,KWTC=X'0C',MF=&MF     @ZA34940 05000000
.********************************************************************** 05050000
.*                                      JFCB                   @XM01140 05100000
         ISTCB3 ARG=&JFCB,ARGNAME=JFCB,KWTC=X'8D',MF=&MF       @ZA34940 05150000
.********************************************************************** 05200000
.*                                      CATALOG                         05250000
         AIF   ('&CATALOG' EQ '').A000500 *** CATALOG ***               05300000
         ISTCB3 ARG=&CATALOG,ARGNAME=CATALOG,KWTC=X'22',       @ZA34940*05350000
               MF=&MF,INDRECT=YES,BIT=YES                      @ZA34940 05400000
         AIF   ('&CATALOG(1)' EQ '*').A000500                   X04SVHS 05450000
.*             IF INDIRECT ADDRESS SPECIFIED, VALUE ARRAY(&IDAVALC)     05500000
.*                                      WAS SET IN ISTCB3      @ZA34940 05550000
&YES     SETB  ('&CATALOG' EQ 'YES')                            X04SVHS 05600000
&NO      SETB  ('&CATALOG' EQ 'NO')                             X04SVHS 05650000
         AIF   (&YES OR &NO).A000400                            X04SVHS 05700000
         ISTERMAC 3,CATALOG,&CATALOG  ISSUE ERROR MESSAGE      @ZA34940 05750000
&IDACB01 SETB  1                        SET ERROR SWITCH        X04SVHS 05800000
.A000400 ANOP                                                   X04SVHS 05850000
         AIF   ('&CALLER' EQ 'ACB').A000500                     X04SVHS 05900000
.*             IF CALLER IS ACB, VALUE WILL BE SET IN CRA RTN   X04SVHS 05950000
&IDAVALC(&IDAENO) SETC '&YES&NO.00000000000000&Z16'             X04SVHS 06000000
.********************************************************************** 06050000
.*                                      MACRF                           06100000
.A000500 AIF   ('&MACRF' EQ '').A001750   *** MACRF ***         X04SVHS 06150000
         ISTCB3 ARG=&MACRF,ARGNAME=MACRF,KWTC=X'12',MF=&MF,    @ZA34940*06200000
               BIT=YES,INDRECT=YES                             @ZA34940 06250000
         AIF   ('&MACRF(1)' EQ '*').A001800                             06300000
.*             IF INDIRECT ADDRESS SPECIFIED, VALUE ARRAY(&IDAVALC)     06350000
.*                                      WAS SET IN ISTCB3      @ZA34940 06400000
&I       SETA  0                        CLEAR                   X04SVHS 06450000
&A       SETA  0                        WORK                    X04SVHS 06500000
&B       SETA  0                        VARIABLES               X04SVHS 06550000
&C       SETA  0                        ZERO ERROR COUNTER      X04SVHS 06600000
.A000600 ANOP                                                           06650000
&I       SETA  &I+1                     BUMP INDEX INTO LIST    X04SVHS 06700000
         AIF   (&I GT N'&MACRF).A000800 HAVE ALL OPTIONS BEEN CHECKED   06750000
&MKEY    SETB  ('&MACRF(&I)' EQ 'KEY' OR &MKEY)                 X04SVHS 06800000
&MADR    SETB  ('&MACRF(&I)' EQ 'ADR' OR &MADR)                 X04SVHS 06850000
&MCNV    SETB  ('&MACRF(&I)' EQ 'CNV' OR &MCNV)                 X04SVHS 06900000
&MSEQ    SETB  ('&MACRF(&I)' EQ 'SEQ' OR &MSEQ)                 X04SVHS 06950000
&MSKP    SETB  ('&MACRF(&I)' EQ 'SKP' OR &MSKP)                 X04SVHS 07000000
&MDIR    SETB  ('&MACRF(&I)' EQ 'DIR' OR &MDIR)                 X04SVHS 07050000
&OUT     SETB  ('&MACRF(&I)' EQ 'OUT' OR &OUT)                  X04SVHS 07100000
&IN      SETB  ('&MACRF(&I)' EQ 'IN' OR &IN)                    X04SVHS 07150000
&NUB     SETB  ('&MACRF(&I)' EQ 'NUB' OR &NUB)                  X04SVHS 07200000
&UBF     SETB  ('&MACRF(&I)' EQ 'UBF' OR &UBF)                  X04SVHS 07250000
&NFX     SETB  ('&MACRF(&I)' EQ 'NFX' OR &NFX)                  X04SVHS 07300000
&CFX     SETB  ('&MACRF(&I)' EQ 'CFX' OR &CFX)                  X04SVHS 07350000
&DDN     SETB  ('&MACRF(&I)' EQ 'DDN' OR &DDN)                  X04SVHS 07400000
&DSN     SETB  ('&MACRF(&I)' EQ 'DSN' OR &DSN)                  X04SVHS 07450000
&NRM     SETB  ('&MACRF(&I)' EQ 'NRM' OR &NRM)                  X04SVHS 07500000
&AIX     SETB  ('&MACRF(&I)' EQ 'AIX' OR &AIX)                  X04SVHS 07550000
&NSR     SETB  ('&MACRF(&I)' EQ 'NSR' OR &NSR)                  X04SVHS 07600000
&LSR     SETB  ('&MACRF(&I)' EQ 'LSR' OR &LSR)                  X04SVHS 07650000
&GSR     SETB  ('&MACRF(&I)' EQ 'GSR' OR &GSR)                  X04SVHS 07700000
&NCI     SETB  ('&MACRF(&I)' EQ 'NCI' OR &NCI)                  X04SVHS 07750000
&ICI     SETB  ('&MACRF(&I)' EQ 'ICI' OR &ICI)                  X04SVHS 07800000
&NDF     SETB  ('&MACRF(&I)' EQ 'NDF' OR &NDF)                  X04SVHS 07850000
&DFR     SETB  ('&MACRF(&I)' EQ 'DFR' OR &DFR)                  X04SVHS 07900000
&NIS     SETB  ('&MACRF(&I)' EQ 'NIS' OR &NIS)                  X04SVHS 07950000
&SIS     SETB  ('&MACRF(&I)' EQ 'SIS' OR &SIS)                  X04SVHS 08000000
&RST     SETB  ('&MACRF(&I)' EQ 'RST' OR &RST)                 @XM01125 08050000
&NRS     SETB  ('&MACRF(&I)' EQ 'NRS' OR &NRS)                 @XM01125 08100000
&A       SETA  &MKEY+&MADR+&MCNV+&MSEQ+&MSKP+&MDIR+&OUT+&IN+&NUB+&UBF+&*08150000
               NFX+&CFX+&DDN+&DSN+&NRM+&AIX+&NSR+&LSR+&GSR+&NCI+&ICI+&N*08200000
               DF+&DFR+&NIS+&SIS+&RST+&NRS                     @XM01125 08250000
.*             &A CONTAINS THE NUMBER OF VSAM OPTIONS SPECIFIED         08300000
.*             &B CONTAINS THE NUMBER OF VTAM OPTIONS SPECIFIED         08350000
.*             &C CONTAINS THE NUMBER OF ERRORS                         08400000
.*             IF THEY EQUAL &I AT THIS POINT, THEN THE LAST            08450000
.*             OPTION SPECIFIED WAS A VALID VSAM OPTION                 08500000
         AIF   (&A+&B+&C EQ &I).A000600 IF NEW VSAM BIT SET CHK X04SVHS 08550000
.*                                      NEXT SUBPARAMETER               08600000
.*                                      ELSE CHECK VTAM OPTIONS X04SVHS 08650000
&LOGONF  SETB  ('&MACRF(&I)' EQ 'LOGON' OR &LOGONF)              X03004 08700000
&NLOGON  SETB  ('&MACRF(&I)' EQ 'NLOGON' OR &NLOGON)             X03004 08750000
&B       SETA  &LOGONF+&NLOGON                                  X04SVHS 08800000
         AIF   (&A+&B+&C EQ &I).A000700 IF NEW VTAM BIT SET CHK X04SVHS 08850000
.*                                      FOR AM=VTAM             X04SVHS 08900000
.*             IF &A + &B + &C DO NOT EQUAL &I AT THIS POINT, THEN THE  08950000
.*             LAST OPTION CHECKED WAS INVALID OR DUPLICATED A          09000000
.*             PREVIOUSLY CHECKED OPTION                                09050000
         ISTERMAC 5,MACRF,&MACRF(&I)    ISSUE ERROR MESSAGE    @ZA34940 09100000
&IDACB01 SETB  1                        SET ERROR SWITCH        X04SVHS 09150000
&C       SETA  &C+1                     BUMP ERROR COUNTER      X04SVHS 09200000
         AGO   .A000600                                                 09250000
.A000700 AIF   ('&AM' EQ 'VTAM').A000600 IF VTAM NOT SPECIFIED,         09300000
         ISTERMAC 20,&MACRF(&I),MACRF    ISSUE ERROR MESSAGE   @ZA34940 09350000
&IDACB01 SETB  1                         SET ERROR SWITCH        X03004 09400000
         AGO   .A000600                  CHECK NEXT OPTION      X04SVHS 09450000
.*                                                                      09500000
.*             THE FOLLOWING CODE CHECKS FOR MUTUALLY EXCLUSIVE         09550000
.*             OPTIONS SPECIFIED,AND IF ANY ARE FOUND, AN ERROR         09600000
.*             MESSAGE IS ISSUED AND THE ERROR SWITCH IS SET            09650000
.A000800 AIF   (NOT(&UBF AND &NUB)).A000900                     X04SVHS 09700000
         ISTERMAC 8,MACRF,UBF,NUB                              @ZA34940 09750000
&IDACB01 SETB  1                                                        09800000
.A000900 AIF   (NOT(&LOGONF AND &NLOGON)).A001000               X04SVHS 09850000
         ISTERMAC 8,MACRF,LOGON,NLOGON                         @ZA34940 09900000
&IDACB01 SETB  1                                                 X03004 09950000
.A000950 AIF   (NOT(&RST AND &NRS)).A001000                    @XM01125 10000000
         ISTERMAC 8,MACRF,RST,NRS                              @ZA34940 10050000
&IDACB01 SETB  1                        SET ERR SWITCH         @XM01125 10100000
.A001000 AIF   (NOT(&NFX AND &CFX)).A001100                     X04SVHS 10150000
         ISTERMAC 8,MACRF,NFX,CFX                              @ZA34940 10200000
&IDACB01 SETB  1                        SET ERR SWITCH          X04SVHS 10250000
.A001100 AIF   (NOT(&DDN AND &DSN)).A001200                     X04SVHS 10300000
         ISTERMAC 8,MACRF,DDN,DSN                              @ZA34940 10350000
&IDACB01 SETB  1                        SET ERR SWITCH          X04SVHS 10400000
.A001200 AIF   (NOT(&NRM AND &AIX)).A001300                     X04SVHS 10450000
         ISTERMAC 8,MACRF,NRM,AIX                              @ZA34940 10500000
&IDACB01 SETB  1                        SET ERR SWITCH          X04SVHS 10550000
.A001300 AIF   ((NOT(&NSR AND &GSR)) AND (NOT(&GSR AND &LSR)) AND      *10600000
               (NOT(&LSR AND &NSR))).A001400                    X04SVHS 10650000
         ISTERMAC 25,MACRF,&NSR,&LSR,&GSR                      @ZA34940 10700000
&IDACB01 SETB  1                        SET ERR SWITCH          X04SVHS 10750000
.A001400 AIF   (NOT(&NCI AND &ICI)).A001500                     X04SVHS 10800000
         ISTERMAC 8,MACRF,NCI,ICI                              @ZA34940 10850000
&IDACB01 SETB  1                        SET ERROR SWITCH        X04SVHS 10900000
.A001500 AIF   (NOT(&NDF AND &DFR)).A001600                     X04SVHS 10950000
         ISTERMAC 8,MACRF,NDF,DFR                              @ZA34940 11000000
&IDACB01 SETB  1                        SET ERR SWITCH          X04SVHS 11050000
.A001600 AIF   (NOT(&NIS AND &SIS)).A001700                     X04SVHS 11100000
         ISTERMAC 8,MACRF,NIS,SIS                              @ZA34940 11150000
&IDACB01 SETB  1                        SET ERR SWITCH          X04SVHS 11200000
.A001700 ANOP                                                   X04SVHS 11250000
         AIF   ('&CALLER' EQ 'ACB').A001750                     X04SVHS 11300000
.*             IF CALLED BY DECLARATIVE, BYPASS CBMM PROCESSING         11350000
.*                                                                      11400000
&IDAVALC(&IDAENO) SETC '&MKEY&MADR&MCNV&MSEQ&MSKP&MDIR&IN&OUT&NUB&UBF&L*11450000
               OGONF&NLOGON.0&DDN&DSN&NRM&AIX&NSR&LSR&GSR&NCI&ICI&NDF&D*11500000
               FR&NIS&SIS&NFX&CFX&RST&NRS.00'                  @XM01139 11550000
.*             SET GLOBAL ARRAY TO BE USED BY IDACB2 TO BUILD           11600000
.*             PARAMETER LIST                                           11650000
         AGO   .A001800                 BYPASS DECLARATIVE PROC X04SVHS 11700000
.A001750 ANOP                                                   X04SVHS 11750000
         AIF   ('&CALLER' NE 'ACB').A001800                     X04SVHS 11800000
.*             THE FOLLOWING CODE SETS DEFAULT VALUES FOR ACB           11850000
.*             DECLARATIVE.  THE DEFAULTS FOR CBM WILL BE SET AT        11900000
.*             EXECUTE TIME.                                            11950000
.*                                                                      12000000
&MKEY    SETB  (NOT(&MADR OR &MCNV) OR &MKEY)                   X04SVHS 12050000
.*             MKEY IS DEFAULT IF MADR OR MCNV ARE NOT SPECIFIED        12100000
&MSEQ    SETB  (NOT(&MSKP OR &MDIR) OR &MSEQ)                   X04SVHS 12150000
.*             MSEQ IS DEFAULT IF MSKP OR MDIR ARE NOT SPECIFIED        12200000
&IN      SETB  (NOT(&OUT) OR &IN)                               X04SVHS 12250000
.*             IN IS DEFAULT IF OUT IS NOT SPECIFIED            X04SVHS 12300000
.*                                                                      12350000
.*             SET MACRF VALUES IN GLOBALS FOR ACB DECLARATIVE  X04SVHS 12400000
&IDAMAC1 SETC  '&MKEY&MADR&MCNV&MSEQ&MDIR&IN&OUT&UBF'           X04SVHS 12450000
&IDAMAC2 SETC  '000&MSKP&NLOGON&RST&DSN&AIX'                    X04SVHS 12500000
&IDAMAC3 SETC  '0&LSR&GSR&ICI&DFR&SIS&CFX.0'                    X04SVHS 12550000
         AGO   .A002200                 PROCESS CRA             X04SVHS 12600000
.********************************************************************** 12650000
.A001800 ANOP                                                   X04SVHS 12700000
.*                                      PASSWD                          12750000
         ISTCB3 ARG=&PASSWD,ARGNAME=PASSWD,KWTC=X'1E',MF=&MF   @ZA34940 12800000
.********************************************************************** 12850000
.*                                      STRNO                           12900000
         ISTCB3 ARG=&STRNO,ARGNAME=STRNO,KWTC=X'20',MF=&MF     @ZA34940 12950000
.********************************************************************** 13000000
.*                                      USERPTR                @XM01129 13050000
         AIF ('&USERPTR' EQ '' OR '&USERPTR' EQ '0').A001850   @XM01129 13100000
         ISTCB3 ARG=&USERPTR,ARGNAME=USERPTR,KWTC=X'8C',MF=&MF @ZA34940 13150000
         AIF   ('&AM' NE 'VSAM' AND '&AM' NE '').A001825       @XM01129 13200000
.*                                   IF AM NOT VSAM OR NULL, ERROR      13250000
         AGO   .A001850                                        @XM01129 13300000
.A001825 ISTERMAC 24,USERPTR                                   @ZA34940 13350000
&IDACB01 SETB  1                                               @XM01129 13400000
.********************************************************************** 13450000
.A001850  ANOP                                                          13500000
.*                                      APPLID                          13550000
         ISTCB3 ARG=&APPLID,ARGNAME=APPLID,KWTC=X'41',MF=&MF,  @ZA34940*13600000
               CHKAM=VTAM,AM=&AM                                X04SVHS 13650000
.********************************************************************** 13700000
.*                                       AM                             13750000
         AIF   ('&AM' EQ '' OR '&CALLER' NE 'GEN').A002000      X04SVHS 13800000
.*             ARGUMENT CONTROL ENTRY IS ONLY BUILT FOR GENCB FOR AM    13850000
         ISTCB3 ARG=&AM,ARGNAME=AM,KWTC=X'40',BIT=YES          @ZA34940 13900000
&VSAM    SETB  ('&AM' EQ 'VSAM')                                X04SVHS 13950000
&VTAM    SETB  ('&AM' EQ 'VTAM')                                X04SVHS 14000000
         AIF   (&VSAM OR &VTAM).A001900                         X04SVHS 14050000
.*             CHECK FOR VALID OPTION                           X04SVHS 14100000
         ISTERMAC 5,AM,&AM                                     @ZA34940 14150000
&IDACB01 SETB  1                         SET ERROR SWITCH       X04SVHS 14200000
.A001900    ANOP                                                X04SVHS 14250000
&IDAVALC(&IDAENO) SETC '&VSAM&VTAM.00000000000000&Z16'          X04SVHS 14300000
.********************************************************************** 14350000
.A002000 ANOP                                                   X04SVHS 14400000
.*                                      MAREA                           14450000
         ISTCB3 ARG=&MAREA,ARGNAME=MAREA,KWTC=X'0E',MF=&MF     @ZA34940 14500000
         AIF   ('&AM' NE 'VSAM' AND '&AM' NE '' AND '&MAREA' NE        *14550000
               '').A002050                                      X04SVHS 14600000
.*                                      IF AM NOT VSAM OR NULL  X04SVHS 14650000
.*                                      ERROR                   X04SVHS 14700000
         AGO   .A002100                                         X04SVHS 14750000
.A002050 ANOP                                                   X04SVHS 14800000
         ISTERMAC 24,MAREA                                     @ZA34940 14850000
&IDACB01 SETB  1                        SET ERR SWITCH          X04SVHS 14900000
.********************************************************************** 14950000
.A002100 ANOP                                                   X04SVHS 15000000
.*                                        MLEN                  X04SVHS 15050000
         ISTCB3 ARG=&MLEN,ARGNAME=MLEN,KWTC=X'0F',MF=&MF       @ZA34940 15100000
         AIF   ('&AM' NE 'VSAM' AND '&AM' NE '' AND '&MLEN' NE         *15150000
               '').A002150                                      X04SVHS 15200000
.*                                      IF AM NOT EQUAL TO VSAM X04SVHS 15250000
.*                                      OR NULL, ERROR          X04SVHS 15300000
         AGO   .A002200                                         X04SVHS 15350000
.A002150 ANOP                                                   X04SVHS 15400000
         ISTERMAC 24,MLEN                                      @ZA34940 15450000
&IDACB01 SETB  1                        SET ERROR SWITCH        X04SVHS 15500000
.********************************************************************** 15550000
.A002200 ANOP                                                   X04SVHS 15600000
.*                                      CRA                             15650000
         AIF   ('&CRA' EQ '').A002350 *** CRA ***               X04SVHS 15700000
.*             IF CRA IS NULL, CHECK FOR CALLER = ACB           X04SVHS 15750000
&SCRA    SETB  ('&CRA' EQ 'SCRA')                              @XM07755 15800000
&UCRA    SETB  ('&CRA' EQ 'UCRA')                              @XM07755 15850000
         AIF   (&SCRA OR &UCRA).A002300 CHK FOR VALID OPTION    X04SVHS 15900000
         ISTERMAC 5,CRA,&CRA            ISSUE ERROR MESSAGE    @ZA34940 15950000
&IDACB01 SETB  1                        SET ERR SWITCH          X04SVHS 16000000
         AGO   .A002400                 BYPASSING PROCESSING KEYWORD    16050000
.*                                                              X04SVHS 16100000
.A002300 ANOP                                                   X04SVHS 16150000
         AIF   ('&CALLER' EQ 'ACB').A002375                     X04SVHS 16200000
.*             IF CALLER IS ACB BYPASS CBM PROCESSING           X04SVHS 16250000
         ISTCB3 ARG=&CRA,KWTC=X'59',BIT=YES                    @ZA34940 16300000
&IDAVALC(&IDAENO) SETC '&SCRA&UCRA.00000000000000&Z16'          X04SVHS 16350000
         AGO   .A002400                 BYPASS DECLARATIVE PROC X04SVHS 16400000
.A002350 ANOP                                                   X04SVHS 16450000
.*             AT THIS POINT, EITHER CRA IS NOT SPECIFIED, OR   X04SVHS 16500000
.*             CALLER = ACB                                     X04SVHS 16550000
         AIF   ('&CALLER' NE 'ACB').A002400                     X04SVHS 16600000
.*             IF CALLER NOT ACB, BYPASS DECLARATIVE PROCESSING X04SVHS 16650000
.A002375 ANOP                                                   X04SVHS 16700000
&IDAIFL1 SETC  '000&YES&SCRA&UCRA.00'   SET ACB INFLGS BYTE 1   X04SVHS 16750000
.*             ALL BIT KEYWORDS HAVE BEEN PROCESSED FOR DECLARATIVE     16800000
         MEXIT                                                  X04SVHS 16850000
.********************************************************************** 16900000
.A002400 ANOP                                                   X04SVHS 16950000
.*                                      BSTRNO                          17000000
         ISTCB3 ARG=&BSTRNO,ARGNAME=BSTRNO,KWTC=X'24',MF=&MF   @ZA34940 17050000
.********************************************************************** 17100000
.*                                                                      17150000
         AIF   ('&CALLER' NE 'TEST').A003800                            17200000
.*                                                                      17250000
.*             THE FOLLOWING KEYWORDS ARE VALID FOR TESTCB ONLY         17300000
.*                                                                      17350000
.********************************************************************** 17400000
.*                                      ATRB                            17450000
         AIF   ('&ATRB' EQ '').A002800    *** ATRB ***                  17500000
         ISTCB3 ARG=&ATRB,ARGNAME=ATRB,KWTC=X'01',MF=&MF,      @ZA34940*17550000
               INDRECT=YES,BIT=YES                             @ZA34940 17600000
         AIF   ('&ATRB(1)' EQ '*').A002800                      X04SVHS 17650000
.*             IF INDIRECT ADDRESS WAS SPECIFIED, VALUE ARRAY(&IDAVALC) 17700000
.*             WAS SET IN ISTCB3                               @ZA34940 17750000
&I       SETA  0                        ZERO INDEX INTO LIST            17800000
&C       SETA  0                        ZERO ERROR COUNTER      X04SVHS 17850000
.A002500 ANOP                                                           17900000
&I       SETA  &I+1                     BUMP INDEX INTO LIST            17950000
         AIF   (&I GT N'&ATRB).A002600  HAVE ALL OPTIONS BEEN CHECKED   18000000
&ESDS    SETB  ('&ATRB(&I)' EQ 'ESDS' OR &ESDS)                 X04SVHS 18050000
&KSDS    SETB  ('&ATRB(&I)' EQ 'KSDS' OR &KSDS)                 X04SVHS 18100000
&WCK     SETB  ('&ATRB(&I)' EQ 'WCK' OR &WCK)                   X04SVHS 18150000
&SSWD    SETB  ('&ATRB(&I)' EQ 'SSWD' OR &SSWD)                 X04SVHS 18200000
&REPL    SETB  ('&ATRB(&I)' EQ 'REPL' OR &REPL)                 X04SVHS 18250000
&RRDS    SETB  ('&ATRB(&I)' EQ 'RRDS' OR &RRDS)                 X04SVHS 18300000
&SPAN    SETB  ('&ATRB(&I)' EQ 'SPAN' OR &SPAN)                 X04SVHS 18350000
&UNQ     SETB  ('&ATRB(&I)' EQ 'UNQ' OR &UNQ)                   X04SVHS 18400000
         AIF   (&ESDS+&KSDS+&WCK+&SSWD+&REPL+&RRDS+&SPAN+&UNQ+&C EQ &I)*18450000
               .A002500                 CHK FOR VALID OPTION    X04SVHS 18500000
.*             IF A NEW BIT WAS NOT SET, THEN LAST OPTION CHECKED       18550000
.*             IS INVALID OR DUPLICATES A PREVIOUSLY SPEC OPTION        18600000
         ISTERMAC 5,ATRB,&ATRB(&I)      ISSUE ERROR MESSAGE    @ZA34940 18650000
&IDACB01 SETB  1                        SET ERROR SWITCH        X04SVHS 18700000
&C       SETA  &C+1                     BUMP ERROR COUNTER      X04SVHS 18750000
         AGO   .A002500                                         X04SVHS 18800000
.A002600 AIF   (NOT(&ESDS AND &KSDS)).A002700                           18850000
.*             CHECK FOR MUTUALLY EXCLUSIVE OPTIONS @ZA34940            18900000
         ISTERMAC 8,ATRB,ESDS,KSDS      ISSUE ERROR MESSAGE    @ZA34940 18950000
&IDACB01 SETB  1                        SET ERROR SWITCH                19000000
.A002700 ANOP                                                           19050000
&IDAVALC(&IDAENO) SETC '&KSDS&ESDS&WCK&SSWD&REPL&RRDS&SPAN&UNQ.00000000*19100000
               &Z16'                                                    19150000
.********************************************************************** 19200000
.A002800 ANOP                                                           19250000
.*                                      AVSPAC                          19300000
         ISTCB3 ARG=&AVSPAC,ARGNAME=AVSPAC,KWTC=X'02',MF=&MF   @ZA34940 19350000
.********************************************************************** 19400000
.*                                      BUFNO                           19450000
         ISTCB3 ARG=&BUFNO,ARGNAME=BUFNO,KWTC=X'06',MF=&MF     @ZA34940 19500000
.********************************************************************** 19550000
.*                                      CINV                            19600000
         ISTCB3 ARG=&CINV,ARGNAME=CINV,KWTC=X'08',MF=&MF       @ZA34940 19650000
.********************************************************************** 19700000
.*                                      ENDRBA                          19750000
         ISTCB3 ARG=&ENDRBA,ARGNAME=ENDRBA,KWTC=X'0A',MF=&MF   @ZA34940 19800000
.********************************************************************** 19850000
.*                                      ERROR                           19900000
         ISTCB3 ARG=&ERROR,ARGNAME=ERROR,KWTC=X'0B',MF=&MF     @ZA34940 19950000
.********************************************************************** 20000000
.*                                      FS                              20050000
         ISTCB3 ARG=&FS,ARGNAME=FS,KWTC=X'0D',MF=&MF           @ZA34940 20100000
.********************************************************************** 20150000
.*                                      KEYLEN                          20200000
         ISTCB3 ARG=&KEYLEN,ARGNAME=KEYLEN,KWTC=X'10',MF=&MF   @ZA34940 20250000
.********************************************************************** 20300000
.*                                      LRECL                           20350000
         ISTCB3 ARG=&LRECL,ARGNAME=LRECL,KWTC=X'11',MF=&MF     @ZA34940 20400000
.********************************************************************** 20450000
.*                                      NCIS                            20500000
         ISTCB3 ARG=&NCIS,ARGNAME=NCIS,KWTC=X'13',MF=&MF       @ZA34940 20550000
.********************************************************************** 20600000
.*                                      NDELR                           20650000
         ISTCB3 ARG=&NDELR,ARGNAME=NDELR,KWTC=X'14',MF=&MF     @ZA34940 20700000
.********************************************************************** 20750000
.*                                      NEXCP                           20800000
         ISTCB3 ARG=&NEXCP,ARGNAME=NEXCP,KWTC=X'15',MF=&MF     @ZA34940 20850000
.********************************************************************** 20900000
.*                                      NEXT                            20950000
         ISTCB3 ARG=&NEXT,ARGNAME=NEXT,KWTC=X'16',MF=&MF       @ZA34940 21000000
.********************************************************************** 21050000
.*                                      NINSR                           21100000
         ISTCB3 ARG=&NINSR,ARGNAME=NINSR,KWTC=X'17',MF=&MF     @ZA34940 21150000
.********************************************************************** 21200000
.*                                      NIXL                            21250000
         ISTCB3 ARG=&NIXL,ARGNAME=NIXL,KWTC=X'18',MF=&MF       @ZA34940 21300000
.********************************************************************** 21350000
.*                                      &NLOGR                          21400000
         ISTCB3 ARG=&NLOGR,ARGNAME=NLOGR,KWTC=X'19',MF=&MF     @ZA34940 21450000
.********************************************************************** 21500000
.*                                      NRETR                           21550000
         ISTCB3 ARG=&NRETR,ARGNAME=NRETR,KWTC=X'1A',MF=&MF     @ZA34940 21600000
.********************************************************************** 21650000
.*                                      NSSS                            21700000
         ISTCB3 ARG=&NSSS,ARGNAME=NSSS,KWTC=X'1B',MF=&MF       @ZA34940 21750000
.********************************************************************** 21800000
.*                                      NUPDR                           21850000
         ISTCB3 ARG=&NUPDR,ARGNAME=NUPDR,KWTC=X'1C',MF=&MF     @ZA34940 21900000
.********************************************************************** 21950000
.*                                                                      22000000
         AIF   ('&OFLAGS' EQ '').A003000 *** OFLAGS ***                 22050000
         ISTCB3 ARG=&OFLAGS,ARGNAME=OFLAGS,KWTC=X'1D',MF=&MF,  @ZA34940*22100000
               BIT=YES,INDRECT=YES                             @ZA34940 22150000
         AIF   ('&OFLAGS(1)' EQ '*').A003000                            22200000
.*             IF INDIRECT ADDRESS WAS SPECIFIED, VALUE ARRAY(&IDAVALC) 22250000
.*             WAS SET IN ISTCB3                               @ZA34940 22300000
&OPEN    SETB  ('&OFLAGS' EQ 'OPEN')                            X04SVHS 22350000
         AIF   (&OPEN).A002900                                  X04SVHS 22400000
.*             CHECK FOR VALID OPTION SPECIFIED                         22450000
         ISTERMAC 3,OFLAGS,&OFLAGS                             @ZA34940 22500000
&IDACB01 SETB  1                                                        22550000
.A002900 ANOP                                                           22600000
&IDAVALC(&IDAENO) SETC '&OPEN.000000000000000&Z16'              X04SVHS 22650000
.********************************************************************** 22700000
.A003000 ANOP                                                   X04SVHS 22750000
.*                                      RKP                             22800000
         ISTCB3 ARG=&RKP,ARGNAME=RKP,KWTC=X'1F',MF=&MF         @ZA34940 22850000
.********************************************************************** 22900000
.*                                      AIXRKP                          22950000
         ISTCB3 ARG=&AIXRKP,ARGNAME=AIXRKP,KWTC=X'3C',MF=&MF   @ZA34940 23000000
.********************************************************************** 23050000
.*                                      STMST                           23100000
         AIF   ('&STMST' EQ '').A003300  *** STMST ***                  23150000
         AIF   ('&STMST(1)' EQ '*').A003100                             23200000
.*                                      STMST MUST BE INDIRECT ADDRESS  23250000
         ISTERMAC 3,STMST,&STMST        ELSE, ISSUE ERROR MSG  @ZA34940 23300000
         AGO   .A003300                                                 23350000
.A003100 ANOP                                                           23400000
         ISTCB3 ARG=&STMST,ARGNAME=STMST,KWTC=X'23',MF=&MF,    @ZA34940*23450000
               INDRECT=YES                                     @ZA34940 23500000
&IDAELEN(&IDAENO) SETA 8                                        X04SVHS 23550000
.********************************************************************** 23600000
.*                                      OPENOBJ                         23650000
.A003300 ANOP                                                   X04SVHS 23700000
         AIF   ('&OPENOBJ' EQ '').A003800 *** OPENOBJ ***       X04SVHS 23750000
&I       SETA  0                        CLEAR SUBPARM CTR       X04SVHS 23800000
&C       SETA  0                        ZERO ERROR COUNTER      X04SVHS 23850000
.A003400 ANOP                                                   X04SVHS 23900000
&I       SETA  &I+1                     BUMP SUBPARM CTR        X04SVHS 23950000
         AIF   (&I GT N'&OPENOBJ).A003500 CHK FOR END OF LIST   X04SVHS 24000000
&PATH    SETB  ('&OPENOBJ(&I)' EQ 'PATH' OR &PATH)              X04SVHS 24050000
&BASE    SETB  ('&OPENOBJ(&I)' EQ 'BASE' OR &BASE)              X04SVHS 24100000
&AIX     SETB  ('&OPENOBJ(&I)' EQ 'AIX' OR &AIX)                X04SVHS 24150000
         AIF   (&PATH+&BASE+&AIX+&C EQ &I).A003400              X04SVHS 24200000
.*                                      CHK FOR NEW BIT SET     X04SVHS 24250000
         ISTERMAC 5,OPENOBJ,&OPENOBJ(&I)                       @ZA34940 24300000
&IDACB01 SETB  1                        SET ERROR SWITCH        X04SVHS 24350000
&C       SETA  &C+1                     BUMP ERROR COUNTER      X04SVHS 24400000
         AGO   .A003400                                         X04SVHS 24450000
.*             CHECK FOR MUTUALLY EXCLUSIVE OPTIONS             X04SVHS 24500000
.A003500 AIF   (NOT(&PATH AND &BASE)).A003600                   X04SVHS 24550000
         ISTERMAC 8,OPENOBJ,PATH,BASE                          @ZA34940 24600000
&IDACB01 SETB  1                        SET ERROR SWITCH        X04SVHS 24650000
.A003600 AIF   (NOT(&PATH AND &AIX)).A003700                    X04SVHS 24700000
         ISTERMAC 8,OPENOBJ,PATH,AIX                           @ZA34940 24750000
&IDACB01 SETB  1                        SET ERROR SWITCH        X04SVHS 24800000
.A003700 ANOP                                                   X04SVHS 24850000
         ISTCB3 ARG=&OPENOBJ,KWTC=X'21',BIT=YES                @ZA34940 24900000
&IDAVALC(&IDAENO) SETC '&AIX&PATH&BASE.0000000000000&Z16'      @XM07772 24950000
.********************************************************************** 25000000
.A003800 ANOP                                                   X04SVHS 25050000
         MEND                                                           25100000
