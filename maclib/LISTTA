         MACRO                                                          00300005
&NM     LISTTA &RLN=,&ADDR=,&SCREEN=,&FULLSCR=,&SESSION=       @Y17XAYP 00600000
.*                                                                      00900005
.*       TCAM LEVEL 10.0                                       @Y17XAYP 01200000
.*                                                                      01500005
.*                                                                      01800005
.*FUNCTION -- THIS MACRO GIVES THE USER THE CAPABILITY TO SPECIFY       02100005
.*   ATTRIBUTES OF HIS CONFIGURATION WHICH VARY AMONG LINES OR          02400005
.*   TERMINALS WITHIN A LINE GROUP.                                     02700005
.*                                                                      03000005
.*ENTRY POINTS -- NOT APPLICABLE                                        03300005
.*                                                                      03600005
.*INPUT -- MACRO CALL PARAMETERS                                        03900005
.*                                                                      04200005
.*OUTPUT -- VALUES IN GLOBAL VARIABLES TO BE USED LATER BY THE          04500005
.*   'TSOMCP' MACRO.  MNOTE MESSAGES IF INVALID PARAMETERS ARE          04800005
.*   DETECTED.                                                          05100005
.*                                                                      05400005
.*EXTERNAL REFERENCES - NOT APPLICABLE                                  05700005
.*                                                                      06000005
.*EXITS,NORMAL -- NORMAL EXIT OCCURS IF NO SERIOUS ERRORS IN THE        06300005
.*   CALLER'S PARAMETERS ARE DETECTED.                                  06600005
.*                                                                      06900005
.*EXITS, ERROR -- UPON DETECTION OF A SERIOUS ERROR, THE 'QUITSW'       07200005
.*   IS SET TO 1 AND FURTHER PROCESSING IS BYPASSED.                    07500005
.*                                                                      07800005
.*TABLES/WORK AREAS -- SEE DESCRIPTIONS ACCOMPANYING VARIABLE           08100005
.*   DECLARATIONS.                                                      08400005
.*                                                                      08700005
.*ATTRIBUTES -- NOT APPLICABLE                                          09000005
.*                                                                      09300005
.*CHARACTER CODE DEPENDENCY - NONE                                      09600005
.*                                                                      09900005
.*NOTES -- NONE                                                         10200005
.*                                                                      10500005
.*                                                                      10800005
.*       GLOBAL VARIABLES                                               11100005
.*                                                                      11400005
         GBLA  &NLG                NUMBER OF LINEGRP MACROS ISSUED      11700005
         GBLA  &LNO(63)            NUMBER OF LINES/LINE GROUP           12000005
         GBLA  &ADRPT              POINTER TO LAST ADDR PARM GROUP      12300005
         GBLA  &SCRPT              POINTER TO LAST SCREEN PARM GROUP    12600005
         GBLA  &SCRH(2500)         CONTAINS SCREEN HEIGHT               12900005
         GBLA  &SCRW(2500)         CONTAINS SCREEN WIDTH                13200005
         GBLA  &TRMNO(2500)        NUMBER OF TERMINALS/LINE             13500005
.*                                                                      13800005
         GBLB  &LGRPSW             INDICATES LINEGRP ISSUED             14100005
         GBLB  &MCPSW              INDICATES TSOMCP ISSUED              14400005
         GBLB  &QUITSW             GENERATION QUIT SWITCH               14700005
         GBLB  &DIALSW(63)         ON MEANS SWITCHED LINE               15000005
         GBLB  &FSCR               FULL SCREEN REQUEST FLAG      Y06327 15300005
         GBLB  &SCRALL(2500)       FULL SCREEN READ SWITCHES     Y06327 15600005
         GBLB  &LUSB(2500)         LU/LU SESSION SWITCHES      @Y17XAYP 15700000
.*                                                                      15900005
.*                                                                      16200005
         GBLC  &GRPNAME(63)        CONTAINS GROUP= PARAMETER     Y06327 16500005
         GBLC  &T(63)              CONTAINS TERM= PARAMETER VALUES      16800005
         GBLC  &ADCHAR(2500)       CONTAINS ADDR= PARAMETER VALUES      17100005
.*                                                                      17400005
.*       LOCAL VARIABLES                                                17700005
.*                                                                      18000005
         LCLA  &B                  WORK VARIABLE               @Y17XAYP 18040000
         LCLA  &C                  WORK VARIABLE               @Y17XAYP 18080000
         LCLA  &D                  WORK VARIABLE               @Y17XAYP 18120000
         LCLA  &E                  WORK VARIABLE               @Y17XAYP 18160000
         LCLA  &G                  WORK VARIABLE               @Y17XAYP 18200000
         LCLA  &K                  WORK VARIABLE               @Y17XAYP 18240000
         LCLA  &P                  POINTER TO SCREEN/TERMNO ARRAYS      18300005
         LCLA  &I                  WORK VARIABLE                        18600005
         LCLA  &J                  WORK VARIABLE                        18900005
         LCLA  &M                  WORK VARIABLE               @Y17XAYP 19000000
         LCLC  &H                                                       19200005
         LCLC  &W                                                       19500005
         LCLA  &SH                 TEMPORARY STORAGE             Y06327 19800005
         LCLA  &SW                 TEMPORARY STORAGE             Y06327 20100005
         LCLB  &OLD                OLD SCREEN FORMAT FLAG        Y06327 20400005
.*                                                                      20700005
.*       TEST FOR MACRO CALL OUT OF ORDER                               21000005
.*                                                                      21300005
         AIF   (&LGRPSW AND NOT &MCPSW).INORDER                         21600005
         MNOTE 16,'IKJ54120I LISTTA MACRO OUT OF ORDER -- IGNORED'      21900005
         AGO   .GIVEUP             GO SET QUIT SW & EXIT                22200005
.*                                                                      22800005
.*       TEST IF CALL IS CONSISTANT WITH LAST LINEGRP CALL              23100005
.INORDER AIF   (N'&RLN EQ 1 AND T'&RLN EQ 'N').RLNOK           @Y17XAYP 23700000
         MNOTE 16,'IKJ54121I RLN OPERAND MISSING OR INVALID'            24000005
         AGO   .GIVEUP                                           Y06327 24300005
.RLNOK   AIF   (&RLN LE &LNO(&NLG) AND &RLN NE 0).REFOK        @Y17XAYP 24900000
         MNOTE 16,'IKJ54122I RLN OPERAND REFERS TO NON-EXISTENT LINE'   25200005
         AGO   .GIVEUP             GO SET QUIT SWITCH & EXIT            25500005
.REFOK   AIF   ('&T(&NLG)'(1,3) EQ '226').TERMOK               @Y17XAYP 26400000
         AIF   ('&T(&NLG)'(1,3) EQ '327').TERMOK                        26700005
         AIF   ('&T(&NLG)' EQ 'PUNT').TERMOK                            26800000
         AIF   ('&T(&NLG)' EQ '1050').TERMOK                            27000005
         AIF   ('&T(&NLG)' EQ '5041').TERMOK                            27300005
         MNOTE 16,'IKJ54123I LISTTA MACRO NOT ALLOWED WITH TERMINAL TYP-27600005
               E OF PREVIOUS LINEGRP'                                   27900005
         AGO   .GIVEUP             GO SET QUIT SW & EXIT                28200005
.TERMOK  ANOP                                                           28500005
.CKRLN   AIF   (&B EQ &RLN-1).SETPT IS THIS THE CORRECT RLN    @Y17XAYP 28520000
&C       SETA  &TRMNO(&SCRPT+&B+&D) GET TERMNO VALUE           @Y17XAYP 28540000
&E       SETA  &E+&C               ACCUMULATE NO. LU ENTRIES   @Y17XAYP 28560000
.LULOP   ANOP                                                  @Y17XAYP 28580000
&D       SETA &D+1                 INCREMENT LUNO SELECTOR     @Y17XAYP 28600000
&K       SETA &K+&TRMNO(&SCRPT+&B+&D) ACCUMLATE NO. LU         @Y17XAYP 28620000
&G       SETA  &G+1                INCREMENT LUNO COUNT/TERMNO @Y17XAYP 28640000
         AIF   (&G LT &C).LULOP    MORE LUNO ENTRIES/TERMNO    @Y17XAYP 28660000
&B       SETA  &B+1                SELECT NEXT TERMNO ENTRY    @Y17XAYP 28680000
&G       SETA  0                   RESET LUNO/TERMNO COUNT     @Y17XAYP 28700000
         AGO   .CKRLN                                          @Y17XAYP 28720000
.SETPT   ANOP                                                  @Y17XAYP 28740000
&P       SETA  &SCRPT+&B+&E        SET TRMNO PTR FOR THIS RLN  @Y17XAYP 28760000
&J       SETA  &ADRPT+&K           SET ADDCHAR PTR FOR THIS RLN@Y17XAYP 29700000
&D       SETA  0                                               @Y17XAYP 29710000
&E       SETA  0                                               @Y17XAYP 29720000
&I       SETA  &TRMNO(&P)          GET TERMNO VALUE THSI RLN   @Y17XAYP 29730000
.LUCNT   ANOP                                                  @Y17XAYP 29740000
&D       SETA  &D+1                INCREMENT LUNO SELECTOR     @Y17XAYP 29750000
&E       SETA  &E+&TRMNO(&P+&D)    ACCUMULATE LU COUNT         @Y17XAYP 29760000
         AIF   (&D LT &I).LUCNT    MORE LUNO TO ADD            @Y17XAYP 29770000
&I       SETA  0                                               @Y17XAYP 29780000
         AIF   (N'&SESSION EQ 0).ADDR BR IF OPERAND OMITTED    @Y17XAYP 29790000
         AIF   ('&GRPNAME(&NLG)' EQ '').SESERR  NOT ALLOWED    @Y17XAYP 29800000
         AIF   ('&T(&NLG)' EQ 'PUNT').MOVSES  VALID OPERAND    @Y17XAYP 29810000
.SESERR  MNOTE 4,'IKJ54114I SESSION OPERAND NOT ALLOWED FOR THIS TERMINX29820000
               AL TYPE'                                        @Y17XAYP 29830000
         AGO   .ADDR                                           @Y17XAYP 29840000
.MOVSES  AIF   (N'&SESSION LE &E).SETSES VALID NO. OPERANDS    @Y17XAYP 29850000
         MNOTE 4,'IKJ54124I SESSION OPERAND REFERS TO NON-EXISTENT TERMX29860000
               INAL(S)'                                        @Y17XAYP 29870000
         AGO   .ADDR                                           @Y17XAYP 29880000
.SETSES  AIF   ('&SESSION(&I+1)' EQ '').LUSES SKP NULL OPERAND @Y17XAYP 29890000
         AIF   ('&SESSION(&I+1)' EQ 'YES').LUSES SET ARRAY BIT @Y17XAYP 29900000
         AIF   ('&SESSION(&I+1)' EQ 'NO').NOSES                @Y17XAYP 29910000
         MNOTE 4,'IKJ60004I SESSION SUBLIST OPERAND &SESSION(&I+1) INCOX29920000
               RRECT -- IGNORED'                               @Y17XAYP 29930000
         AGO   .LUSES                                          @Y17XAYP 29940000
.NOSES   ANOP                                                  @Y17XAYP 29950000
&LUSB(&J+&I)   SETB 0                                          @Y17XAYP 29960000
.LUSES   ANOP                                                  @Y17XAYP 29970000
&I       SETA  &I+1                CHECK NEXT OPERAND          @Y17XAYP 29980000
         AIF   (&I LT N'&SESSION).SETSES CHECK NEXT OPERAND    @Y17XAYP 29990000
.ADDR    AIF   (N'&ADDR EQ 0).SCREEN  BR IF OPERAND OMITTED    @Y17XAYP 31500000
         AIF   ('&GRPNAME(&NLG)' EQ '').ADDR0  BYPASS IF NOT 3705Y06327 31800005
         AIF   ('&T(&NLG)' EQ '226L').ADDER1  BR IF 226L                32100005
         MNOTE 4,'IKJ60002I ADDR OPERAND NOT USED FOR 3705. IGNORED'    32400005
.*                                                               Y06327 32700005
         AGO   .SCREEN                                           Y06327 33000005
.ADDR0   AIF   ('&T(&NLG)' NE '327L').NOTLOCL BR IF NOT 327L   @Y17XAYP 33600000
.ADDER1  MNOTE 4,'IKJ54114I ADDR OPERAND NOT ALLOWED FOR THIS TERMINAL -34200000
                TYPE'                                                   34500005
         AGO   .SCREEN             SKIP ADDR PROCESSING                 34800005
.NOTLOCL AIF   (N'&ADDR LE &E).PUTADDR VALID NO.ADDR OPERANDS  @Y17XAYP 35400000
         MNOTE 4,'IKJ54124I ADDR OPERAND REFERS TO NON-EXISTENT TERMINA-35700005
               L(S)'                                                    36000005
         AGO   .SCREEN                                                  36300005
.PUTADDR ANOP                                                           37500005
.MVADDR  ANOP                      LOOP EXECUTED N'&ADDR TIMES          41100005
&I       SETA  0                   SET LOOP CONTROL VARIABLE            41400005
.ADDR2   AIF   ('&ADDR(&I+1)' EQ '').ADDRLT  SKIP NULL SUBOPERANDS      42000000
         AIF   (K'&ADDR(&I+1) EQ 2 AND '&T(&NLG)' EQ '1050').ADSZOK     42300005
         AIF   (K'&ADDR(&I+1) EQ 2 AND '&T(&NLG)' EQ '5041').ADSZOK     42600005
         AIF   (K'&ADDR(&I+1) EQ 4 AND '&T(&NLG)' EQ '226R').ADSZOK     42900005
         AIF   (K'&ADDR(&I+1) EQ 4 AND '&T(&NLG)' EQ '2265').ADSZOK     43200005
         AIF   (K'&ADDR(&I+1) EQ 4 AND '&T(&NLG)' EQ '327R').ADSZOK     43500005
         AIF   (K'&ADDR(&I+1) EQ 4 AND '&T(&NLG)' EQ '327S').ADSZOK     43800005
         MNOTE 4,'IKJ54118I ADDR=&ADDR(&I+1) INCORRECT LENGTH -- IGNORE-44100005
               D'                                                       44400005
         AGO   .ADDRLT                                                  44700005
.ADSZOK  ANOP                                                           45000005
&ADCHAR(&J+&I) SETC '&ADDR(&I+1)'  PUT IN ARRAY                         45300005
.ADDRLT  ANOP                                                           45600005
&I       SETA  &I+1                BUMP LOOP CONTROL VARIABLE           45900005
         AIF   (&I LT N'&ADDR).ADDR2   BR UNTIL ALL OPERANDS HANDLED    46200005
.SCREEN  AIF   ('&SCREEN' EQ '' AND '&FULLSCR' EQ '').EXIT     @Y17XAYP 48300000
         AIF   ('&SCREEN' EQ '').CHKFSCR                         Y06327 48600005
         AIF   ('&T(&NLG)'(1,3) EQ '226').CHKDSPY 2260 VALID     Y06327 48900005
         AIF   ('&T(&NLG)'(1,3) EQ '327').CHKDSPY 3270 VALID     Y06327 49200005
         AIF   ('&T(&NLG)' EQ 'PUNT').CHKDSPY PUNT VALID       @Y17XAYP 49300000
         MNOTE 4,'IKJ54114I SCREEN OPERAND NOT ALLOWED FOR THIS TERMINAL49500005
               L TYPE'                                                  49800005
         MEXIT                                                   Y06327 50100005
.********************************************************************** 50700005
.*       FOR COMPATIBILITY PURPOSES THE OLD FORMAT OF SCREEN            51000005
.*       SCREEN=(HEIGHT,WIDTH) IS STILL VALID.                          51300000
.********************************************************************** 51900005
.CHKDSPY AIF   (N'&SCREEN EQ 2).OLDSCR                         @Y17XAYP 52800000
         AIF   (N'&SCREEN LE &E).CHKFSCR                       @Y17XAYP 53100000
         MNOTE 4,'IKJ54124I SCREEN OPERAND REFERS TO NON-EXISTENT TERMIX53400005
               NAL(S)'                                           Y06327 53700005
         MEXIT                                                   Y06327 54000005
.OLDSCR  AIF   (K'&SCREEN(1) EQ 2 AND K'&SCREEN(2) EQ 2).OLDSCR1 Y06327 54600000
         AIF   (K'&SCREEN(1) EQ 4 AND K'&SCREEN(2) EQ 4).CHKFSCR Y06327 54900005
         AIF   (K'&SCREEN(1) EQ 0 AND K'&SCREEN(2) EQ 4).CHKFSCR Y06327 55200005
         AIF   (K'&SCREEN(1) EQ 4 AND K'&SCREEN(2) EQ 0).CHKFSCR Y06327 55500005
         MNOTE 4,'IKJ54116I SCREEN OPERAND MUST BE A TWO-OPERAND SUBLISX55800005
               T'                                                       56100005
         MEXIT                                                          56400005
.OLDSCR1 ANOP                                                    Y06327 56700005
&OLD     SETB  1                                                        57000005
.CHKFSCR AIF   ('&FULLSCR' EQ '').SCR1                         @Y17XAYP 57600000
         AIF   ('&T(&NLG)'(1,3) EQ '327').CHKNO                  Y06327 57900005
         AIF   ('&T(&NLG)' EQ 'PUNT').CHKNO FULLSCR VALID PUNT @Y17XAYP 58000000
         MNOTE 4,'IKJ54114I FULLSCR OPERAND NOT ALLOWED FOR THIS TERMINX58200005
               AL TYPE'                                          Y06327 58500005
         MEXIT                                                   Y06327 58800005
.CHKNO   AIF   (N'&FULLSCR LE &E).SCR1                         @Y17XAYP 59400000
         MNOTE 4,'IKJ54124I FULLSCR OPERAND REFERS TO NON-EXISTENT TERMX59700005
               INAL(S)'                                          Y06327 60000005
         MEXIT                                                   Y06327 60300005
.SCR1    AIF   ('&SCREEN' EQ '').MVFSCR                        @Y17XAYP 63600000
&I       SETA  0                   INITIALIZE SUBLIST POINTER    Y06327 63900005
.SCREEN2 AIF   (&OLD).SCR2                                     @Y17XAYP 64500000
         AIF   ('&SCREEN(&I+1)' EQ '').SCRNULL DEFAULT           Y06327 64800005
         AIF   (K'&SCREEN(&I+1) EQ 4).SCR2                       Y06327 65100005
         MNOTE 4,'IKJ60004I SCREEN SUBLIST OPERAND &SCREEN(&I+1) INCORRX65400005
               ECT -- IGNORED'                                   Y06327 65700005
         AGO   .MVFSCR                                           Y06327 66000005
.SCR2    AIF   (NOT &OLD).SCR3                                   Y06327 66600000
&SH      SETA  &SCREEN(1)                                        Y06327 66900005
&SW      SETA  &SCREEN(2)                                        Y06327 67200005
         AGO   .SCR4                                             Y06327 67500005
.SCR3    ANOP                                                    Y06327 67800005
&H       SETC  '&SCREEN(&I+1)'(1,2)                                     68100005
&W       SETC  '&SCREEN(&I+1)'(3,2)                                     68400005
&SH      SETA  &H                                                       68700005
&SW      SETA  &W                                                       69000005
.SCR4    AIF   (&SH EQ 12 AND &SW EQ 40).PUTSCR                @Y17XAYP 69600000
         AIF   ('&T(&NLG)'(1,3) EQ '327').LGSCRN CK 24*80 SCR  @Y17XAYP 69800000
         AIF   ('&T(&NLG)' EQ 'PUNT').LGSCRN     CK 24*80 SCR  @Y17XAYP 70000000
         AIF   (&SH EQ 12 AND &SW EQ 80).PUTSCR                  Y06327 70200005
         AIF   (&SH EQ 6 AND &SW EQ 40).PUTSCR                   Y06327 70500005
         AIF   (&SH EQ 15 AND &SW EQ 64).PUTSCR                  Y06327 70800005
         AGO   .SCRER                                            Y06327 71100005
.LGSCRN  AIF   (&SH EQ 24 AND &SW EQ 80).PUTSCR                @Y17XAYP 71700000
.SCRER   ANOP                                                    Y06327 72000005
         MNOTE 4,'IKJ54117I NON-STANDARD SCREEN DIMENSIONS -- ACCEPTED' 72300005
.PUTSCR  ANOP                                                    Y06327 72600005
&SCRH(&J+&I) SETA &SH              PUT INTO ARRAY                Y06327 72900005
&SCRW(&J+&I) SETA &SW              PUT INTO ARRAY                Y06327 73200005
.SCRNULL ANOP                                                    Y06327 73500005
&I       SETA  &I+1                UPDATE SUBLIST POINTER        Y06327 73800005
         AIF   (NOT &OLD).SCR5                                   Y06327 74100005
         AIF   (&I LT &TRMNO(&P)).SCREEN2                        Y06327 74400005
         AGO   .MVFSCR                                           Y06327 74700005
.SCR5    AIF   (&I LT N'&SCREEN).SCREEN2 GET THE NEXT ENTRY    @Y17XAYP 75300000
.MVFSCR  AIF   ('&FULLSCR' EQ '').EXIT                         @Y17XAYP 75900000
&FSCR    SETB  1                   REQUEST FULL SCREEN           Y06327 76200005
&I       SETA  0                   INITIALIZE SUBLIST POINTER    Y06327 76500005
.FULLS2  AIF   ('&FULLSCR(&I+1)' EQ '').FNULL DEFAULT          @Y17XAYP 77100000
         AIF   ('&FULLSCR(&I+1)' EQ 'YES').FSCRY                 Y06327 77400005
         AIF   ('&FULLSCR(&I+1)' EQ 'NO').FSCRN                  Y06327 77700005
         MNOTE 4,'IKJ60004I FULLSCR SUBLIST OPERAND &FULLSCR(&I+1) INCOX78000005
               RRECT -- IGNORED'                                 Y06327 78300005
         MEXIT                                                   Y06327 78600005
.FSCRY   ANOP                                                    Y06327 78900005
&SCRALL(&J+&I) SETB 1              SET FULLSCR FLAG IN ARRAY     Y06327 79200005
         AGO   .FNULL                                            Y06327 79500005
.FSCRN   ANOP                                                    Y06327 79800005
&SCRALL(&J+&I) SETB 0              CLEAR FULLSCR FLAG IN ARRAY   Y06327 80100005
.FNULL   ANOP                                                    Y06327 80400005
&I       SETA  &I+1                UPDATE SUBLIST POINTER        Y06327 80700005
         AIF   (&I LT N'&FULLSCR).FULLS2                         Y06327 81000005
         MEXIT                                                   Y06327 81300005
.*                                                                      81600005
.GIVEUP  ANOP                                                           81900005
&QUITSW  SETB  1                        SERIOUS ERROR - DON'T GENERATE  82200005
.EXIT    ANOP                                                    Y06327 82500005
         MEXIT                                                          82800005
         MEND                                                           83100005
