         MACRO                                                          00050000
&NAME    LOGCHAR &APPLID=,&SEQNCE=                                      00100000
.* GLOBAL CMZELOG IS USED TO CONTROL THE ORDER OF THE MACROS            00150000
.* LOCAL IN1      IS A GENERAL STRING INDEX                             00200000
.* LOCAL COUNT    IS  THE NUMBER OF CHARACTERS IN SEQNCE STRING         00250000
.* LOCAL ERRBIT   IS A GENERAL ERROR BIT. IS 1 IF ANY ERRORS ARE        00300000
.*                DETECTED BY THE MACRO                                 00350000
.* LOCAL ROUTBIT  IS 1 FOR ROUTINES, AND 0 FOR APPILICIDS. AS           00400000
.*                SPECIFIED IN THE APPLID PARAMETER                     00450000
.* LOCAL TRANBIT  IS 0 SINCE TSEQNCE WAS DELETED - DCR K1769            00500000
.* LOCAL ERRCHAR  IS USED AS A GENERAL ERROR MESSAGE STRING             00550000
.* LOCAL QUOTE    IS SET TO NULL OR TO ONE QUOTE DEPENDING ON           00600000
.*                WHETHER SEQNCE IS A QUOTED STRING                     00650000
.* LOCAL ONEQUOT  IS USED AS A SINGLE QUOTE CONSTANT                    00700000
.* LOCAL ONEAMPR  IS SET TO A CONSTANT OF ONE AMPERSAND                 00750000
.* LOCAL OPARM    IS USED AS A LEFT PARENTHESIS CONSTANT                00800000
.* LOCAL CPARM    IS USED AS A RIGHT PARENTHESIS CONSTANT               00850000
.* LOCAL K1       IS SET TO THE CHARACTER STRING 'BL1'                  00900000
.* LOCAL K2       IS SET TO THE CHARACTER STRING '0'                    00950000
.* LOCAL K3       IS SET TO THE CHARACTER STRING '00000'      @ZTR5599  01000000
.* LOCAL K4       IS SET TO THE CHARACTER STRING 'CL'                   01050000
.* LOCAL K5       IS SET TO THE CHARACTER STRING 'CL8' OR 'VL3'         01100000
         GBLB  &CMZELOG(2)                                              01150000
         LCLA  &IN1,&COUNT                                              01200000
         LCLB  &ERRBIT,&ROUTBIT,&TRANBIT                                01250000
         LCLC  &ERRCHAR,&QUOTE,&ONEQUOT,&ONEAMPR,&OPAREN,&CPAREN        01300000
         LCLC  &K1,&K2,&K3,&K4,&K5                                      01350000
&ONEQUOT SETC  ''''                                                     01400000
&ONEAMPR SETC  '&&'                                                     01450000
&OPAREN  SETC  '('                                                      01500000
&CPAREN  SETC  ')'                                                      01550000
         AGO   .ONW00                                                   01600000
* /* START OF SPECIFICATIONS ****                                       01650000
*                                                                       01700000
*01*  MODULE-NAME = LOGCHAR                                             01750000
*                                                                       01800000
*01*  DESCRIPTIVE-NAME = LOGON CHARACTERISTICS TABLE ELEMENT MACRO      01850000
*                                                                       01900000
*01*  COPYRIGHT = XXXX-XXX COPYRIGHT IBM CORP 1973                      01950000
*                                                                       02000000
*01*  STATUS = RELEASE 1                                                02050000
*                                                                       02100000
*01*  FUNCTION = CREATES INDIVIDUAL ENTRIES IN A LOGON CHARACTERISTICS  02150000
*     TABLE. IT IS USED WITH INTAB AND ENDINTAB MACROS.                 02200000
*                                                                       02250000
*01*  NOTES = THE FOLLOWING MNOTES ARE ISSUED                           02300000
*        8,'CARDS OUT OF SEQUENCE'                                      02350000
*        8,'NAME ''&NAME'' IS LONGER THAN EIGHT CHARACTERS'             02400000
*        8,'''&ERRCHAR IS INVALID CHARACTER IN NAME ''&NAME'''          02450000
*        8,'LOGCHAR SHOULD HAVE NO POSITIONAL OPERANDS'                 02500000
*        8,'APPLID IS A REQUIRED KEYWORD'                               02550000
*        8,'MISMATCHED QUOTES IN SEQNCE'                                02600000
*        8,'MISMATCHED AMPERSANDS IN SEQNCE'                            02650000
*        8,'TOTAL SEQNCE LENGTH OF ''&IN1'' EXCEEDS 225'                02700000
*        8,'APPLID SHOULD BE A SUBLIST WITH TWO ARGUMENTS'              02750000
*        8,'''&APPLID(1)'' IS AN INVALID APPLID VALUE'                  02800000
*        8,'NAME IN APPLID CANNOT BE BLANK'                             02850000
*        8,'''&ERRCHAR'' IS INVALID CHARACTER IN APPLID NAME'           02900000
*        8,'APPLID NAME IS LONGER THAN EIGHT CHARACTERS'                02950000
*        8,'MISMATCHED QUOTES IN TSEQNCE'                               03000000
*        8,'MISMATCHED AMPERSANDS IN TSEQNCE'                           03050000
*        8,'TOTAL TSEQNCE LENGTH OF ''&IN1'' EXCEEDS 225'               03100000
*                                                                       03150000
*02*    CHARACTER-CODE-DEPENDENCIES = EBCDIC                            03200000
*                                                                       03250000
*02*    DEPENDENCIES = NONE                                             03300000
*                                                                       03350000
*02*    RESTRICTIONS = VTAM MAPPING MACRO                               03400000
*                                                                       03450000
*02*    REGISTER-CONVENTIONS = NONE, LIST FORM                          03500000
*                                                                       03550000
*02*    PATCH-LABEL = NONE                                              03600000
*                                                                       03650000
*01*  MODULE-TYPE = MACRO                                               03700000
*                                                                       03750000
*02*    PROCESSOR = ASSEMBLER                                           03800000
*                                                                       03850000
*02*    MACRO-SIZE = 210 STATEMENTS                                     03900000
*                                                                       03950000
*02*    ATTRIBUTES = LIST                                               04000000
*                                                                       04050000
*01*  ENTRY-POINT = NA                                                  04100000
*                                                                       04150000
*01*  EXIT-NORMAL = NA                                                  04200000
*                                                                       04250000
*01*  EXIT-ERROR = NA                                                   04300000
*                                                                       04350000
*01*  EXTERNAL-REFERENCES = AS-FOLLOWS                                  04400000
*                                                                       04450000
*02*    ROUTINES = NONE                                                 04500000
*                                                                       04550000
*02*    DATA-SETS = NONE                                                04600000
*                                                                       04650000
*02*    DATA-AREA = CMZELOG IS A GLOBAL WHICH PASSES SEQUENCING         04700000
*       INFORMATION BETWEEN INTAB, LOGCHAR AND ENDINTAB MACROS.         04750000
*                                                                       04800000
*02*    CONTROL-BLOCKS-SYSTEM = REQ: COMMENTS:,                         04850000
*                            COMMENTS: ENTER NONE OR LIST SYSTEM        04900000
*                                      CONTROL BLOCKS WITH FIELDS       04950000
*                                      REFERENCED OR SET, THIS          05000000
*                                      SECTION MAY BE EXPANDED;         05050000
*                                                                       05100000
*02*    CONTROL-BLOCKS-ATCAM = REQ: CHOOSE: NONE|                       05150000
*                                        ISTATCVT=SPCATCVT|             05200000
*                                        ISTACB=SPCACB|                 05250000
*                                        ISTACDEB=SPCACDEB|             05300000
*                                        ISTRPL=SPCRPL|                 05350000
*                                        ISTPREFX=SPCPREFX|             05400000
*                                        ISTQAB=SPCQAB|                 05450000
*                                        ISTDFNOD=SPCDFNOD|             05500000
*                                        ISTEXLST=SPCEXLST|             05550000
*                                        ISTLDO=SPCLDO|                 05600000
*                                        ISTDNCB=SPCDNCB|               05650000
*                                        ISTRPLH=SPCRPLH|               05700000
*                                        ISTRDT=SPCRDT|                 05750000
*                                        ISTBTU=SPCBTU|                 05800000
*                                        ISTDVT=SPCDVT|                 05850000
*                                        ISTAPB=SPCAPB|                 05900000
*                                        ISTICB=SPCICB|                 05950000
*                                        ISTICEX=SPCICEX|               06000000
*                                        ISTFSB=SPCFSB|                 06050000
*                                        ISTFDB=SPCFDB,                 06100000
*                           COMMENTS: ENTER NAME OF CONTROL BLOCK       06150000
*                                     USED, COPY THIS SECTION IF        06200000
*                                     MORE THAN ONE;                    06250000
*                                                                       06300000
*01*  TABLES = NONE                                                     06350000
*                                                                       06400000
*01*  INNER-MACROS = NONE                                               06450000
*                                                                       06500000
*01*  SYSTEM-MACROS = NONE                                              06550000
*                                                                       06600000
*01*  CHANGE-ACTIVITY = NONE                                            06650000
*     APAR OZ11987                                             @ZA11987 06700000
*     APAR OZ35348                                             @ZA35348 06720000
*                                                                       06750000
**** END OF SPECIFICATIONS ***/                                         06800000
*/*LOGCHAR: CHART (FMODE) */                                            06850000
*/* HEADER                                                              06900000
*/*PROGRAM NAME:                                                        06950000
*/*DATE:                                                                07000000
*/*PROGRAMMER:                                                      */  07050000
*/*1A2 E LOGCHAR */                                                     07100000
*/*1B2 P GLOBAL: CMZELOG(2), LOCALS: IN1, COUNT, COUNT2, ERRBIT,        07150000
*/*ROUTBIT */                                                           07200000
*/*1C2 P LOCALS: TRANBIT, ERRCHAR, QUOTE, ONEQUOT, ONEAMPR, QUOTE2 */   07250000
*/*1D2 P LOCALS: K1, K2, K3, K4, K5 */                                  07300000
*/*ONW00:1E2 D (NO,ERR0010:RL,YES,:BT) IS CMZELOG(1) = 1 */             07350000
*/*ONW10:1F2 P (,2B2)  SET CMZELOG(2) = 1 */                            07400000
*/*1K1 COMMENT (1,1,) NEW DCRS */                                       07450000
*/*COMMENT (2,1) 1K1547 */                                              07500000
*/*COMMENT (3,1) 1K1769 */                                              07550000
*/*2B2:2B2 D (NO,ONW30:LT,YES,:BT) WAS A NAME SPECIFIED */              07600000
*/*ONW20:2C2 P SET IN1 = IN1 + 1 */                                     07650000
*/*2D2 D (YES,ONW30:LT,NO,:BT) IS IN1 > NAME LENGTH */                  07700000
*/*2E2 D (YES,ERR0020:BT,NO,:RT) IS IN1 > 8 */                          07750000
*/*2F4 D (YES,ONW20:RT,INVL,ERR0030:LT,NO,:BT) IS NAME(IN1) FROM A-Z */ 07800000
*/*2G4 D (YES,ERR0030:LT,NO,:BT) IS IN1 = 1 */                          07850000
*/*2H4 D (NO,ERR0030:BT,YES,ONW20:RT) IS NAME(IN1) FROM 0-9 */          07900000
*/*ONW30:2G2 D (YES,ERR0040:LR,NO,:BT) ANY POSITIONAL OPERANDS */       07950000
*/*ONW40:3B2 D (NO,ONW140:BT,YES,:RL) WAS SEQNCE OPERAND SPECIFIED */   08000000
*/*3B3 S (,ONW140) CHART 2: SEQNCE CHECK */                             08050000
*/*5A2 E CHART 2: SEQNCE CHECK */                                       08100000
*/*5B2 P SET IN1 TO 0 */                                                08150000
*/*ONW50:5C2 P SET QUOTE ON AND IN1 = IN1+1 */                          08200000
*/*5D2 D (YES,ONW120:LT,NO,:BT) WAS ENTIRE STRING SCANNED */            08250000
*/*5E2 D (NO,%ONW100:BT,YES,:RL) IS SEQNCE(IN1) A QUOTE */              08300000
*/*5E3 P (,:RT) SET QUOTE NULL AND IN1 = IN1+1 */                       08350000
*/*5F4 D (NO,%ONW70:BT,YES,:RL) IS SEQNCE(IN1) A QUOTE */               08400000
*/*5F5 P (,ONW50:TT) COUNT = COUNT+1 */                                 08450000
*/*%ONW100:5G2 S (AMPR,ERR0070:LR,QUOT,ERR0060:BL,GOOD,ONW120:RT)       08500000
*/*CHART 3:EVEN QUOTE CHECK */                                          08550000
*/*%ONW70:5G4 S (AMPR,ERR0070:RR,QUOT,ERR0060:BL,GOOD,ONW120:LT)        08600000
*/*CHART 4:ODD QUOTE CHECK  */                                          08650000
*/*8A2 E (,ONW70) CHART 4: ODD QUOTE CHECK */                           08700000
*/*ONW60:8G4 P (,:RT) SET COUNT = COUNT+1, IN1 = IN1+1 */               08750000
*/*ONW70:8B2 D (YES,ONW80:RL,NO,:BT) IS SEQNCE(IN1) AN '&' */           08800000
*/*8C2 D (NO,ONW60:RT,YES,:BT) IS SEQNCE(IN1) A QUOTE */                08850000
*/*8D2 D (YES,ONW120:LT,NO,:BT) IS THIS LAST CHARACTER */               08900000
*/*8E2 P SET IN1 = IN1+1 */                                             08950000
*/*8F2 D (NO,ERR0060:BT,YES,ONW60:RT) IS QUOTE DOUBLED */               09000000
*/*ONW80:8B3 P SET IN1 = IN1 +1 */                                      09050000
*/*8C3 D (NO,ERR0070:BT,YES,ONW60:RT) WAS AMPERSAND DOUBLED */          09100000
*/*7A2 E (,ONW100) CHART 3: EVEN QUOTE CHECK */                         09150000
*/*ONW90:7F4 P SET COUNT = COUNT+1 , IN1 = IN1+1 */                     09200000
*/*7G4 D (YES,ONW120:BT,NO,:RT) END OF STRING */                        09250000
*/*ONW100:7B2 D (NO,ONW110:BT,YES,:RL) IS SEQNCE(IN1) A '&' */          09300000
*/*7B3 P SET IN1 = IN1+1 */                                             09350000
*/*7C3 D (NO,ERR0070:BT,YES,ONW90:LT) IS AMPERSAND DOUBLED */           09400000
*/*ONW110:7C2 D (NO,ONW90:RT,YES,:BT) IS SEQNCE(IN1) A QUOTE */         09450000
*/*7D2 P SET IN1 = IN1+1 */                                             09500000
*/*7E2 D (NO,ERR0060:BT,YES,ONW90:RT) IS QUOTE DOUBLED */               09550000
*/*ONW120:6B3 P SET IN1 = STRING LENGTH */                              09600000
*/*6C3 D (NO,ONW130:BT,YES,:RL) WAS STRING QUOTED */                    09650000
*/*6C4 P SET IN1 = IN1-2 */                                             09700000
*/*ONW130:6D3 D (YES,ERR0080:RL,NO,:BT) IS IN1 > 225 */                 09750000
*/*%ONW140:6G3 R END SEQNCE CHECK */                                    09800000
*/*ONW140:3C2 D (NO,ERR0050:BT,YES,:RL) WAS APPLID SPECIFIED */         09850000
*/*3D3 S (,ONW290) CHART 5: APPLID CHECK */                             09900000
*/*9A2 E CHART 5: APPLID CHECK */                                       09950000
*/*ONW150:9B2 D (NO,ERR0090:LT,YES,:BT) SUBLIST WITH 2 ARGS */          10000000
*/*9C2 D (YES,ONW160:BT,NO,:RT) APPLID(1) = APPLICID */                 10050000
*/*9D3 D (NO,ERR0100:RL,YES,:BT) APPLID(1) = ROUTINE */                 10100000
*/*9E3 P SET ROUTBIT = 1 */                                             10150000
*/*ONW160:9F3 D (NO,ERR0110:LR,YES,:BT) WAS APPLID(2) SPECIFIED */      10200000
*/*9G3 S CHART 6: APPLID (2) CHECK */                                   10250000
*/*%ONW180:9H3 R END APPLID CHECK */                                    10300000
*/*10A2 E CHART 6: APPLID(2) CHECK */                                   10350000
*/*10B2 P SET IN1 = 0 */                                                10400000
*/*ONW170:10C2 P SET IN1 = IN1+1 */                                     10450000
*/*10D2 D (YES,%ONW181:LT,NO,:BT) IN1 > APPLID NAME */                  10500000
*/*10E2 D (YES,ERR0130:BT,NO,:RT) IN1 > 8 */                            10550000
*/*10F4 D (YES,ONW170:RT,INVL,ERR0120:LT,NO,:BT) CHAR FROM A-Z */       10600000
*/*10G4 D (YES,ERR0120:LT,NO,:BT) IN1 = 1 */                            10650000
*/*10H4 D (NO,ERR0120:BT,YES,ONW170:RT) CHAR FROM 0-9 */                10700000
*/*%ONW181:10G2 R END APPID(2) CHECK */                                 10750000
*/*ONW290:4B2 D (YES,ONW320:LT,NO,:BT) IS ERRBIT = 1 */                 10800000
*/*4C2 D (NO,ONW300:BT,YES,:RL) WAS NAME SPECIFIED */                   10850000
*/*4C3 P GENERATE NAME STMT */                                          10900000
*/*ONW300:4D2 P GENERATE DC'S UP TO SEQNCE */                           10950000
*/*4E2 P GENERATE APPLID DC */                                          11000000
*/*4F2 D (NO,ONW320:BT,YES,:RL) WAS SEQNCE SPECIFIED */                 11050000
*/*4F3 P GENERATE SEQNCE DC */                                          11100000
*/*ONW320:4J2 R MEXIT */                                                11150000
*/*ERR0010:1E3 P (,ONW10) MNOTE ERROR, SET ERRBIT TO 1 */               11200000
*/*ERR0020:2F2 P (,ONW30) MNOTE ERROR, SET ERRBIT TO 1 */               11250000
*/*ERR0030:2J4 P (,ONW20:RT) MNOTE ERROR, SET ERRBIT TO 1 */            11300000
*/*ERR0040:2G1 P (,ONW40) MNOTE ERROR, SET ERRBIT TO 1 */               11350000
*/*ERR0050:3D2 P (,ONW290) MNOTE ERROR, SET ERRBIT TO 1 */              11400000
*/*ERR0060:6F2 P (,%ONW140:RT) MNOTE ERROR, SET ERRBIT TO 1 */          11450000
*/*ERR0070:6F4 P (,%ONW140:LT) MNOTE ERROR, SET ERRBIT TO 1 */          11500000
*/*ERR0080:6D4 P (,%ONW140) MNOTE ERROR, SET ERRBIT TO 1 */             11550000
*/*ERR0090:9C1 P (,%ONW180) MNOTE ERROR, SET ERRBIT TO 1 */             11600000
*/*ERR0100:9D4 P (,ONW160) MNOTE ERROR, SET ERRBIT TO 1 */              11650000
*/*ERR0110:9F2 P (,%ONW180) MNOTE ERROR, SET ERRBIT TO 1 */             11700000
*/*ERR0120:10J4 P (,ONW170:RT) MNOTE ERROR, SET ERRBIT TO 1 */          11750000
*/*ERR0130:10F2 P (,%ONW181) MNOTE ERROR, SET ERRBIT TO 1 */            11800000
*/*LOGCHAR: END */                                                      11850000
.* IF THE LOGCHAR MACRO IS INCORRECTLY SEQUENCED, MNOTE AN ERROR        11900000
.ONW00   AIF   (&CMZELOG(1) NE 1 ).ERR0010                              11950000
.ONW10   ANOP                                                           12000000
.* SET THE LOGCHAR WAS ISSUED FLAG                                      12050000
&CMZELOG(2) SETB 1                                                      12100000
.* IF THERE IS NO NAME, SKIP THE NAME PROCESSING                        12150000
         AIF   ('&NAME' EQ '' ).ONW30                                   12200000
.ONW20   ANOP                                                           12250000
.* SCAN THE NAME FOR VALID EBCDIC CHARACTERS, AND CHECK FOR NAME        12300000
.* LENGTH GREATER THAN EIGHT. PRINT APPROPRIATE MNOTES IF FOUND.        12350000
&IN1     SETA  &IN1+1                                                   12400000
         AIF   (&IN1 GT K'&NAME).ONW30                                  12450000
         AIF   (&IN1 GT 8 ).ERR0020                                     12500000
         AIF   ('&NAME'(&IN1,1) GE 'A' ).ONW27                @ZA35348  12550000
         AIF   ('&NAME'(&IN1,1) EQ '@' ).ONW20                @ZA35348  12560000
         AIF   ('&NAME'(&IN1,1) EQ '#' ).ONW20                @ZA35348  12570000
         AIF   ('&NAME'(&IN1,1) EQ '$' ).ONW25                @ZA35348  12580000
         AGO   .ERR0030                                       @ZA35348  12590000
.* A "$" IS INVALID AS THE 1ST CHARACTER OF A NAME            @ZA35348  12600000
.ONW25   AIF   (&IN1 EQ 0).ERR0030                            @ZA35348  12610000
         AGO   .ONW20                                         @ZA35348  12620000
.ONW27   AIF   ('&NAME'(&IN1,1) LE 'I' ).ONW20                @ZA35348  12630000
         AIF   ('&NAME'(&IN1,1) LT 'J' ).ERR0030                        12650000
         AIF   ('&NAME'(&IN1,1) LE 'R' ).ONW20                          12700000
         AIF   ('&NAME'(&IN1,1) LT 'S' ).ERR0030                        12750000
         AIF   ('&NAME'(&IN1,1) LE 'Z' ).ONW20                          12800000
         AIF   (&IN1 EQ 1 ).ERR0030                                     12850000
         AIF   ('&NAME'(&IN1,1) LT '0' ).ERR0030                        12900000
         AIF   ('&NAME'(&IN1,1) GT '9' ).ERR0030                        12950000
         AGO   .ONW20                                                   13000000
.* CHECK IF ANY POSITIONAL OPERANDS WERE SUPPLIED. IF SO ERROR.         13050000
.ONW30   AIF   (N'&SYSLIST NE 0 ).ERR0040                               13100000
.* IF THERE IS NO SEQNCE, SKIP TO APPLID SECTION.                       13150000
.ONW40   AIF   ('&SEQNCE' EQ '' ).ONW140                                13200000
&IN1     SETA  0                       RESET STRING COUNTER             13250000
.ONW50   ANOP                          EVEN/ODD QUOTE COUNT             13300000
&QUOTE    SETC  ''''                    ASSUME EVEN NUMBER OF QUOTES    13350000
&IN1     SETA  &IN1+1                  POINT TO NEXT CHARACTER TO TEST  13400000
.* IF THIS IS THE END OF THE STRING, THE STRING IS TOTALLY AN EVEN      13450000
.* NUMBER OF CONSECUTIVE QUOTES.                                        13500000
         AIF   (&IN1 GT K'&SEQNCE ).ONW55                      @ZA11987 13550000
.* IF THIS CHARACTER IS NOT A QUOTE, THERE ARE AN EVEN NUMBER OF        13600000
.* LEADING QUOTES, COUNT MULTIPLE AMPERSANDS AND QUOTES.                13650000
         AIF   ('&SEQNCE'(&IN1,1) NE '&ONEQUOT').ONW100                 13700000
.* THE TOTAL NUMBER OF LEADING QUOTES IS NOW ODD                        13750000
&QUOTE   SETC  ''                      SET FOR ODD NUMBER OF QUOTES     13800000
&IN1     SETA  &IN1+1                  POINT TO NEXT CHARACTER          13850000
.* IF THIS CHARACTER IS NOT A QUOTE, THE LEADING TOTAL IS ODD, START    13900000
.* COUNTING MULTIPLE QUOTES AND AMPERSANDS.                             13950000
         AIF   ('&SEQNCE'(&IN1,1) NE '&ONEQUOT').ONW70                  14000000
.* THE COUNT IS STILL EVEN CONTINUE THE LOOP. WE ARE COUNTING EACH      14050000
.* DOUBLE QUOTE AS ONE QUOTE, AND EACH DOUBLE AMPERSAND AS ONE.         14100000
&COUNT   SETA  &COUNT+1                INCREMENT THE CHARACTER COUNT    14150000
         AGO   .ONW50                  CONTINUE COUNTING LEADING QUOTES 14200000
.ONW55   ANOP                                                  @ZA11987 14250000
&COUNT   SETA  &COUNT-1  ADJUST COUNT FOR EVEN # DOUBLE QUOTES @ZA11987 14300000
         AGO   .ONW120                                         @ZA11987 14350000
.* AT THIS POINT WE HAVE FOUND AN ODD NUMBER OF LEADING QUOTES. WE      14400000
.* MUST NOW CHECK THAT EACH QUOTE AND EACH AMPERSAND IS DOUBLED,        14450000
.* AND THAT THE STRING ENDS WITH ONE UNMATCHED QUOTE.                   14500000
.ONW60   ANOP                          CONTINE LOOP                     14550000
&COUNT   SETA  &COUNT+1                INCREMENT CHARACTER COUNT        14600000
&IN1     SETA  &IN1+1                  POINT TO NEXT CHARACTER          14650000
.* EVEN NUMBER OF AMPERSANDS, ODD NUMBER OF QUOTES                      14700000
.* IF THE NEXT CHARACTER IS AN AMPERSAND, CHECK FOR DOUBLE.             14750000
.ONW70   AIF   ('&SEQNCE'(&IN1,1) EQ '&ONEAMPR'(1,1)).ONW80             14800000
.*IF THIS CHARACTER IS NOT A QUOTE, CONTINUE THE LOOP                   14850000
         AIF   ('&SEQNCE'(&IN1,1) NE '&ONEQUOT').ONW60                  14900000
.* IF THIS QUOTE WAS THE LAST QUOTE, SUCCESS, SO LEAVE                  14950000
         AIF   (&IN1 EQ K'&SEQNCE ).ONW120                              15000000
.* CHECK FOR DOUBLED QUOTE                                              15050000
&IN1     SETA  &IN1+1                  POINT TO NEXT CHARACTER          15100000
.* IF THE QUOTE IS NOT DOUBLED, THERE IS AN ERROR.                      15150000
         AIF   ('&SEQNCE'(&IN1,1) NE '&ONEQUOT').ERR0060                15200000
.* BUMP COUNTS AND CONTINUE THE LOOP                                    15250000
         AGO   .ONW60                                                   15300000
.* AT THIS POINT THERE SHOULD BE A SECOND AMPERSAND. IF NOT. EROOR.     15350000
.ONW80   ANOP                                                           15400000
&IN1     SETA  &IN1+1                  POINT TO NEXT CHARACTER          15450000
         AIF   ('&SEQNCE'(&IN1,1) NE '&ONEAMPR'(1,1)).ERR0070           15500000
.* STILL GOOD. BUMP COUNTS AND CONTINUE.                                15550000
         AGO   .ONW60                                                   15600000
.* THIS SECTION DEALS WITH AN EVEN NUMBER OF LEADING QUOTES. EACH       15650000
.* AMPERSAND AND EACH QUOTE SHOULD BE DOUBLED.                          15700000
.ONW90   ANOP                                                           15750000
&COUNT   SETA  &COUNT+1                INCREMENT CHARACTER COUNT        15800000
&IN1     SETA  &IN1+1                  INCREMENT STRING POINTER         15850000
.* IF THE STRING LENGTH IS EXCEEDED, SUCCESS, SO LEAVE                  15900000
         AIF   (&IN1 GT K'&SEQNCE ).ONW120                              15950000
.* IF THE FIRST CHARACTER IS NOT AN AMPERSAND, CHECK FOR QUOTE          16000000
.ONW100  AIF   ('&SEQNCE'(&IN1,1) NE '&ONEAMPR'(1,1)).ONW110            16050000
.* IF THE NEXT CHARACTER IS NOT AN AMPERSAND, THERE IS AN ERROR         16100000
&IN1     SETA  &IN1+1                  INCREMENT STRING POINTER         16150000
         AIF   ('&SEQNCE'(&IN1,1) NE '&ONEAMPR'(1,1)).ERR0070           16200000
.* ALL IS FINE CONTINUE LOOP                                            16250000
         AGO   .ONW90                                                   16300000
.* IF THIS CHARACTER IS NOT A QUOTE, CONTINUE LOOP                      16350000
.ONW110  AIF   ('&SEQNCE'(&IN1,1) NE '&ONEQUOT').ONW90                  16400000
.* IF THE NEXT CHARACTER IS NOT ALSO A QUOTE THERE IS AN ERROR          16450000
&IN1     SETA  &IN1+1                  INCREMENT STRING POINTER         16500000
         AIF   ('&SEQNCE'(&IN1,1) NE '&ONEQUOT').ERR0060                16550000
.* OTHERWISE ALL IS FINE CONTINUE THE LOOP                              16600000
         AGO   .ONW90                                                   16650000
.* WE SHALL NOW CHECK THAT THE STRING LENGTH FOR SEQNCE IS LESS TAHN    16700000
.* 225 CHARACTERS                                                       16750000
.ONW120  ANOP                                                           16800000
&IN1     SETA  K'&SEQNCE               SET TEMPORARY VALUE              16850000
.* IF THE SEQNCE STRING IS QUOTED, SUBTRACT TWO FROM THE TOTAL LENGTH.  16900000
         AIF   ('&QUOTE' NE '' ).ONW130                                 16950000
&IN1     SETA  &IN1-2                  SUBTRACT TWO                     17000000
.ONW130  AIF   (&IN1 GT 225 ).ERR0080                                   17050000
.* IF APPLID WAS NOT SPECIFIED, MNOTE ERROR                             17100000
.ONW140  AIF   ('APPLID' EQ '' ).ERR0050                                17150000
.* IF APPLID DOES NOT HAVE TWO SUBPARAMETERS, MNOTE AN ERROR            17200000
.ONW150  AIF   (N'&APPLID NE 2 ).ERR0090                                17250000
.* IF THE FIRST PARAMETER IS NOT 'ROUTINE' OR 'APPLICID', ERROR         17300000
         AIF   ('&APPLID(1)' EQ 'APPLICID' ).ONW160                     17350000
         AIF   ('&APPLID(1)' NE 'ROUTINE' ).ERR0100                     17400000
&ROUTBIT SETB  1                       SET ROUTINE INDICATOR            17450000
.* NEXT CHECK THE NAME FOR A VALID 8 CHARACTER NAME                     17500000
.ONW160  AIF   ('&APPLID(2)' EQ '' ).ERR0110                            17550000
&IN1     SETA  0                       SET INDEX TO ZERO                17600000
.ONW170  ANOP                                                           17650000
&IN1     SETA  &IN1+1                  INCREMENT INDEX                  17700000
         AIF   (&IN1 GT K'&APPLID(2) ).ONW290                           17750000
         AIF   (&IN1 GT 8 ).ERR0130                                     17800000
         AIF   ('&APPLID(2)'(&IN1,1) GE 'A' ).ONW200    @ZA35348        17850000
         AIF   ('&APPLID(2)'(&IN1,1) EQ '@' ).ONW170    @ZA35348        17860000
         AIF   ('&APPLID(2)'(&IN1,1) EQ '#' ).ONW170    @ZA35348        17870000
         AIF   ('&APPLID(2)'(&IN1,1) EQ '$' ).ONW190    @ZA35348        17880000
         AGO   .ERR0120                                 @ZA35348        17890000
.* A ''$'' IS INVALID AS 1ST CHARACTER OF A NAME        @ZA35348        17900000
.ONW190  AIF   (&IN1 EQ 0).ERR0120                      @ZA35348        17910000
         AGO   .ONW170                                  @ZA35348        17920000
.ONW200  AIF   ('&APPLID(2)'(&IN1,1) LE 'I' ).ONW170    @ZA35348        17930000
         AIF   ('&APPLID(2)'(&IN1,1) LT 'J' ).ERR0120                   17950000
         AIF   ('&APPLID(2)'(&IN1,1) LE 'R' ).ONW170                    18000000
         AIF   ('&APPLID(2)'(&IN1,1) LT 'S' ).ERR0120                   18050000
         AIF   ('&APPLID(2)'(&IN1,1) LE 'Z' ).ONW170                    18100000
         AIF   (&IN1 EQ 1 ).ERR0120                                     18150000
         AIF   ('&APPLID(2)'(&IN1,1) LT '0' ).ERR0120                   18200000
         AIF   ('&APPLID(2)'(&IN1,1) GT '9' ).ERR0120                   18250000
         AGO   .ONW170                                                  18300000
.* SINCE THERE IS NO TSEQNCE, SKIP TO TEST ERROR SECTION. (DCR K1769)   18350000
.ONW290  AIF   (&ERRBIT EQ 1 ).ONW320                                   18400000
.* IF THERE IS NO NAME, SKIP NAME CARD                                  18450000
         AIF   ('&NAME' EQ '' ).ONW300                                  18500000
&NAME    EQU   *                                                        18550000
.ONW300  ANOP                                                           18600000
.* SET GENERATION CHARACTER CONSTANTS                                   18650000
&K1      SETC  'BL1'                                                    18700000
&K2      SETC  '0'                                                      18750000
&K3      SETC  '00000'                                    @ZTR5599      18800000
&K4      SETC  'CL'                                                     18850000
&K5      SETC  'CL8'                                                    18900000
.* GENERATE THE REQUIRED DC'S                                           18950000
         DC    &K1&ONEQUOT&K2&ROUTBIT&TRANBIT&K3&ONEQUOT OPTION FLAGS   19000000
.* IF ROUTINE SPECIFIED, GENERATE VCON                                  19050000
         AIF   (&ROUTBIT EQ 1).ONW305                          DCR1547  19100000
.* GENERATE APPLICATION NAME                                            19150000
         DC    &K5&ONEQUOT&APPLID(2)&ONEQUOT               APPLID NAME  19200000
         AGO   .ONW307                                                  19250000
.* GENERATE EXTERNAL ADDRESS CONSTANT (VCON)                            19300000
.ONW305  ANOP                                                           19350000
&K5      SETC  'VL4'                                           DCR1547  19400000
         DC    &K5&OPAREN&APPLID(2)&CPAREN             ROUTINE ADDRESS  19450000
         DC    XL4'00'                                 RESERVED         19500000
.ONW307  ANOP                                                           19550000
         DC    AL1(&COUNT)                       SEQNCE CHARACTER COUNT 19600000
.* IF THE SEQNCE COUNT IS ZERO THERE IS NO SEQNCE                       19650000
         AIF   (&COUNT EQ 0 ).ONW310                                    19700000
         DC    &K4&COUNT&QUOTE&SEQNCE&QUOTE     SEQNCE CHARACTER STRING 19750000
.ONW310  ANOP                                                           19800000
.ONW320  ANOP                                                           19850000
         MEXIT                                                          19900000
.ERR0010 MNOTE 8,'CARDS OUT OF SEQUENCE'                                19950000
&ERRBIT  SETB  1                                                        20000000
         AGO   .ONW10                                                   20050000
.ERR0020 MNOTE 8,'NAME ''&NAME'' IS LONGER THAN EIGHT CHARACTERS'       20100000
&ERRBIT  SETB  1                                                        20150000
         AGO   .ONW30                                                   20200000
.ERR0030 ANOP                                                           20250000
&ERRCHAR SETC  '&NAME'(&IN1,1)                                          20300000
         MNOTE 8,'''&ERRCHAR'' IS INVALID CHARACTER IN NAME ''&NAME'''  20350000
&ERRBIT  SETB  1                                                        20400000
         AGO   .ONW20                                                   20450000
.ERR0040 MNOTE 8,'LOGCHAR SHOULD HAVE NO POSITIONAL OPERANDS'           20500000
&ERRBIT  SETB  1                                                        20550000
         AGO   .ONW40                                                   20600000
.ERR0050 MNOTE 8,'APPLID IS A REQUIRED KEYWORD'                         20650000
&ERRBIT  SETB  1                                                        20700000
         AGO   .ONW290                                                  20750000
.ERR0060 MNOTE 8,'MISMATCHED QUOTES IN SEQNCE'                          20800000
&ERRBIT  SETB  1                                                        20850000
         AGO   .ONW140                                                  20900000
.ERR0070 MNOTE 8,'MISMATCHED AMPERSANDS IN SEQNCE'                      20950000
&ERRBIT  SETB  1                                                        21000000
         AGO   .ONW140                                                  21050000
.ERR0080 MNOTE 8,'TOTAL SEQNCE LENGTH OF ''&IN1'' EXCEEDS 225'          21100000
&ERRBIT  SETB  1                                                        21150000
         AGO   .ONW140                                                  21200000
.ERR0090 MNOTE 8,'APPLID SHOULD BE A SUBLIST WITH TWO ARGUMENTS'        21250000
&ERRBIT  SETB  1                                                        21300000
         AGO   .ONW290                                                  21350000
.ERR0100 MNOTE 8,'''&APPLID(1)'' IS AN INVALID APPLID VALUE'            21400000
&ERRBIT  SETB  1                                                        21450000
         AGO   .ONW160                                                  21500000
.ERR0110 MNOTE 8,'NAME IN APPLID CANNOT BE BLANK'                       21550000
&ERRBIT  SETB  1                                                        21600000
         AGO   .ONW290                                                  21650000
.ERR0120 ANOP                                                           21700000
&ERRCHAR SETC  '&APPLID(2)'(&IN1,1)                                     21750000
         MNOTE 8,'''&ERRCHAR'' IS INVALID CHARACTER IN APPLID NAME'     21800000
&ERRBIT  SETB  1                                                        21850000
         AGO   .ONW170                                                  21900000
.ERR0130 MNOTE 8,'APPLID NAME IS LONGER THAN EIGHT CHARACTERS'          21950000
&ERRBIT  SETB  1                                                        22000000
         AGO   .ONW290                                                  22050000
         MEND                                                           22100000
