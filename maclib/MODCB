         MACRO                                                          01000038
&LBL     MODCB &ACB=,&EXLST=,&RPL=,&MF=,&BUFND=,&BUFNI=,               C02000038
               &BUFSP=,&DDNAME=,&MACRF=,&PASSWD=,&STRNO=,              C03000038
               &AREA=,&AREALEN=,&ARG=,&ECB=,&KEYLEN=,&MSGAREA=,        C04000038
               &MSGLEN=,&NXTRPL=,&OPTCD=,&RECLEN=,&CATALOG=,&EODAD=,   C05000038
               &JRNAD=,&LERAD=,&SYNAD=,&TRANSID=,&BSTRNO=,      X04SVHSC06000038
               &CRA=,&MAREA=,&MLEN=,&JFCB=,                    @XM01140*06010038
               &USERPTR=,                                      @XM01129*06020038
               &AM=,&APPLID=,&LOGON=,        *** VTAM KEYWORDS***X03004C06050038
               &LOSTERM=,&RELREQ=,&ATTN=,                      @Z40BHUC*06100038
               &TPEND=,&AAREA=,&AAREALN=,&ARECLEN=,&EXIT=,       X03004C06150038
               &NSEXIT=,                                       @G40AKCM*06170000
               &NIB=,&BRANCH=,&NAME=,                           Y30ASHSC06200038
               &USERFLD=,&MODE=,&LISTEND=,&CONDN=,&PROC=,        X03004C06250038
               &CHNGDIR=,&STYPE=,&RTYPE=,&CHAIN=,&POST=,        X3004BSC06300038
               &RESPOND=,&CONTROL=,&OBSQAC=,&IBSQAC=,           X3004BSC06350038
               &OBSQVAL=,&IBSQVAL=,&SEQNO=,&SSENSEO=,           X3004BSC06400038
               &USENSEO=,&RESPLIM=,&SDT=,&SCIP=,&DFASY=,        X3004BSC06450038
               &RESP=,&BRACKET=,&SSENSMO=,                     @Z40BHUC*06500038
               &LOGMODE=,&BNDAREA=,&SIGDATA=,&CODESEL=,        @Z40BHUC*06550048
               &CRYPT=,&ENCR=                                  @G40AKSL 06600000
.* GLOBALS                                                              07000038
         GBLB  &IDACB01                ERROR INDICATOR                  08000038
         GBLA  &IDACB02                NUMBER OF FIELDS IN HEADER ACE   09000038
         GBLA  &IDACB03                LENGTH OF HEADER ACE             10000038
         GBLA  &IDACB04(5)             HEADER TYPE ARRAY                11000038
         GBLC  &IDACB05(5)             HEADER VALUE ARRAY               12000038
         GBLA  &IDACB06(5)             HEADER OFFSET ARRAY              13000038
         GBLA  &IDACB07(5)             HEADER FIELD LENGTH ARRAY        14000038
         GBLA  &IDACB11                OPERAND TYPE - SET BY IDACB1     15000038
         GBLC  &IDACB13                OPERAND VALUE - SET BY IDACB1    16000038
.* LOCALS                                                               17000038
         LCLA  &RS,&BA,&I              ARRAY INDICES                    18000038
         LCLC  &BLKTYPE                TYPE OF BLOCK TO BE MODIFIED     19000038
.* INITIALIZATION                                                       20000038
&IDACB01 SETB  0                       CLEAR ERROR INDICATOR            21000038
&IDACB02 SETA  2                       NUMBER OF HEADER FIELDS          22000038
&IDACB03 SETA  8                       HEADER LENGTH                    23000038
&RS      SETA  1                       RESERVED FIELD INDEX             24000038
&IDACB04(3) SETA 0                     CLEAR UNUSED PORTION             25000038
&IDACB04(4) SETA 0                       OF HEADER TYPE ARRAY           26000038
&BA      SETA  2                       BLOCK ADDRESS INDEX              27000038
.* TEST FOR POSITIONAL PARAMETERS                                       28000038
&I       SETA  N'&SYSLIST+1                                             29000038
.L10     ANOP                                                           30000038
&I       SETA  &I-1                                                     31000038
         AIF   (&I LE 0).L20                                            32000038
         IDAERMAC 1,&SYSLIST(&I)                                        33000038
         AGO   .L10                                                     34000038
.* VALIDITY CHECK AND TRANSFORM REQUEST KEYWORDS                        35000038
.L20     ANOP                          *** BLOCK TYPE AND ADDRESS ***   36000038
&IDACB04(&BA) SETA 0                                                    37000038
&IDACB06(&BA) SETA 4                                                    38000038
&IDACB07(&BA) SETA 4                                                    39000038
         AIF   ('&RPL' NE '').L30           CHECKING MUST BE DONE       40000038
         AIF   ('&ACB' NE '').L40           IN THIS ORDER BECAUSE       41000038
         AIF   ('&NIB' NE '').L60           OF USE OF BLOCK NAMESX03004 41050038
         AIF   ('&EXLST' NE '').L50         AS KEYWORDS                 42000038
         AIF   ('&MF(1)' EQ 'E').L80                                    43000038
         IDAERMAC 2,BLOCK-TYPE                                          44000038
&IDACB01 SETB  1                                                        45000038
         AGO   .L80                                                     46000038
.L30     IDACB1 ARG=&RPL,ARGNAME=RPL,MF=&MF                             47000038
&BLKTYPE SETC  'RPL'                                                    48000038
         AGO   .L70                                                     49000038
.L40     IDACB1 ARG=&ACB,ARGNAME=ACB,MF=&MF                             50000038
&BLKTYPE SETC  'ACB'                                                    51000038
         AGO   .L70                                                     52000038
.L50     IDACB1 ARG=&EXLST,ARGNAME=EXLST,MF=&MF                         53000038
&BLKTYPE SETC  'EXLST'                                                  54000038
         AGO   .L70                                              X03004 54050038
.L60     IDACB1 ARG=&NIB,ARGNAME=NIB,MF=&MF                      X03004 54100038
&BLKTYPE SETC  'NIB'                                             X03004 54150038
.L70     ANOP                                                           55000038
&IDACB04(&BA) SETA &IDACB11                                             56000038
&IDACB05(&BA) SETC '&IDACB13'                                           57000038
.L80     ANOP                          *** RESERVED FIELD ***           58000038
&IDACB04(&RS) SETA 0                                                    59000038
&IDACB06(&RS) SETA 2                                                    60000038
&IDACB07(&RS) SETA 2                                                    61000038
.L100    IDACB2 CALLER=MOD,BLKTYPE=&BLKTYPE,LABEL=&LBL,MF=&MF,         C62000038
               BUFND=&BUFND,BUFNI=&BUFNI,BUFSP=&BUFSP,DDNAME=&DDNAME,  C63000038
               EXLST=&EXLST,MACRF=&MACRF,PASSWD=&PASSWD,STRNO=&STRNO,  C64000038
               ACB=&ACB,AREA=&AREA,AREALEN=&AREALEN,                   C65000038
               ARG=&ARG,ECB=&ECB,KEYLEN=&KEYLEN,MSGAREA=&MSGAREA,      C66000038
               MSGLEN=&MSGLEN,NXTRPL=&NXTRPL,OPTCD=&OPTCD,             C67000038
               RECLEN=&RECLEN,CATALOG=&CATALOG,EODAD=&EODAD,           C68000038
               JRNAD=&JRNAD,LERAD=&LERAD,SYNAD=&SYNAD,           X03004C69000038
               USERPTR=&USERPTR,JFCB=&JFCB,                            *69020000
               APPLID=&APPLID,LOGON=&LOGON, *** VTAM KEYWORDS ***X03004C69050038
               LOSTERM=&LOSTERM,RELREQ=&RELREQ,ATTN=&ATTN,     @Z40BHUC*69100038
               TPEND=&TPEND,                                   @Z40BHUC*69150038
               NSEXIT=&NSEXIT,                                 @G40AKCM*69170000
               AAREA=&AAREA,AAREALN=&AAREALN,ARECLEN=&ARECLEN,   X03004C69200038
               EXIT=&EXIT,                                      Y30ASHSC69250038
               NIB=&NIB,BRANCH=&BRANCH,NAME=&NAME,               X03004C69300038
               USERFLD=&USERFLD,MODE=&MODE,LISTEND=&LISTEND,     X03004C69350038
               CONDN=&CONDN,PROC=&PROC,AM=&AM,                   X03004C69400038
               CHNGDIR=&CHNGDIR,STYPE=&STYPE,RTYPE=&RTYPE,      X3004BSC69500038
               CHAIN=&CHAIN,POST=&POST,RESPOND=&RESPOND,        X3004BSC69550038
               CONTROL=&CONTROL,OBSQAC=&OBSQAC,IBSQAC=&IBSQAC,  X3004BSC69600038
               OBSQVAL=&OBSQVAL,IBSQVAL=&IBSQVAL,SEQNO=&SEQNO,  X3004BSC69650038
               SSENSEO=&SSENSEO,USENSEO=&USENSEO,               X3004BSC69700038
               RESPLIM=&RESPLIM,SDT=&SDT,SCIP=&SCIP,            X3004BSC69750038
               DFASY=&DFASY,RESP=&RESP,                        @Z40BHUC*69800038
               BRACKET=&BRACKET,SSENSMO=&SSENSMO,               X04SVHSC69850038
               TRANSID=&TRANSID,CRA=&CRA,BSTRNO=&BSTRNO,        X04SVHSC69900038
               MAREA=&MAREA,MLEN=&MLEN,                         X04SVHSC69950038
               LOGMODE=&LOGMODE,BNDAREA=&BNDAREA,              @Z40BHUCC70000038
               SIGDATA=&SIGDATA,CODESEL=&CODESEL,              @Z40BHUC*70050048
               CRYPT=&CRYPT,ENCR=&ENCR                         @G40AKSL 73050000
         MEND                                                           73100000
