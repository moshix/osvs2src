         MACRO                                                          00050000
&NAME    MODEENT &LOGMODE=,&FMPROF=0,&TSPROF=0,&PRIPROT=0,&SECPROT=0,  *00070000
               &COMPROT=0,&PSERVIC=X'00',&RUSIZES=X'0000',             *00080000
               &PSNDPAC=X'00',&SRCVPAC=X'00',&SSNDPAC=X'00'             00090000
.********************************************************************** 00100000
.*       DECLARE LOCALS AND GLOBALS                                     00110000
.********************************************************************** 00120000
         GBLB  &ERRCK                                                   00130000
         GBLA  &ENTRYCT                                                 00140000
         LCLC  &CHAR1,&CHAR2,&CHAR3(3),&CHAR4                @DM39835   00150000
         LCLA  &LEN,&I,&J,&K                                 @DM39835   00160000
.********************************************************************** 00170000
.*       IF MODETAB WAS NOT PREVIOUSLY INVOKED, INDICATE ERROR          00180000
.********************************************************************** 00190000
         AIF   ('&ERRCK' EQ '1').MODLINE  THIS IS A VALID MACRO CALL    00200000
         MNOTE 8,'MODETAB MACRO WAS NOT ISSUED MODEENT IS VOID'         00210000
.********************************************************************** 00220000
.*       IF LOGMODE WAS PASSED, GENERATE CL8 FOR ITS VALUE              00230000
.*          OTHERWISE, GENERATE A CL8 OF BLANKS,                        00240000
.*          ISSUING AN ERROR MESSAGE IF THIS IS NOT THE FIRST ENTRY     00250000
.********************************************************************** 00260000
.MODLINE ANOP                                                           00270000
         AIF   ('&LOGMODE' EQ '').NLOGMOD WAS LOGMODE SPECIFIED ?       00280000
         DC    CL8'&LOGMODE'                   LOGMODE                  00290000
         AGO   .FMPROF                                                  00500000
.NLOGMOD ANOP                                                           00550000
         AIF   ('&ENTRYCT' EQ '0').LOGMOD1 FIRST TABLE ENTRY ?          00560000
         MNOTE 0,'LOGMODE= BLANK ONLY VALID FOR FIRST ENTRY '           00562000
.LOGMOD1 ANOP                                                           00570000
         DC    CL8'        '                    LOGMODE DEFAULT         00600000
.********************************************************************** 00620000
.*       GENERATE CODE FOR THE FMPROF KEYWORD VALUE OR DEFAULT          00640000
.*                HEXIDECIMAL CHECKING                                  00660000
.********************************************************************** 00680000
.FMPROF  ANOP                                                           00700000
         DC    XL1'00'                          FORMAT TYPE    @ZA47729 00720000
&CHAR1   SETC  '&FMPROF'(1,2)                   FIRST 2 CHARS           00740000
&LEN     SETA  K'&FMPROF                        VALUE LENGTH            00760000
&CHAR2   SETC  '&FMPROF'(&LEN,1)                LAST CHAR               00780000
         AIF   ('&CHAR1' NE 'X''').MODCKQ1      IF NOT HEX, BRANCH      00800000
         AIF   ('&CHAR2' NE '''').FMPERR1       ENDING QUOTE MISSING    00820000
         AIF   (&LEN GT 5).FMPERR2              VALUE TOO LONG          00840000
&CHAR3(1) SETC  '&FMPROF'(3,&LEN-3)             GET VALUE               00860000
&CHAR1   SETC  '&CHAR1'(1,1)                    SET TO X                00880000
         DC    &CHAR1.L1&CHAR2&CHAR3(1)&CHAR2   FM PROFILE              00900000
         AGO   .TSPROF                                                  00920000
.********************************************************************** 00940000
.*       GENERATE CODE FOR THE FMPROF KEYWORD VALUE OR DEFAULT          00960000
.*                DECIMAL PROCESSING                                    00980000
.********************************************************************** 01000000
.MODCKQ1 ANOP                                                           01020000
&CHAR1   SETC  '&FMPROF'                        GET VALUE               01040000
         AIF   ('&CHAR1' LT '0').FMPERR3        INVALID, USE DEFAULT    01060000
         AIF   ('&CHAR1' GT '255').FMPERR4      INVALID, USE DEFAULT    01080000
         DC    AL1(&FMPROF)                     FM PROFILE - NUMERIC    01100000
         AGO   .TSPROF                                                  01120000
.********************************************************************** 01140000
.*       GENERATE CODE FOR THE FMPROF KEYWORD VALUE OR DEFAULT          01160000
.*                DEFAULT PROCESSING                                    01180000
.********************************************************************** 01200000
.FMPERR1 MNOTE 0,'FMPROF INVALID - NO ENDING QUOTE'                     01220000
         AGO   .NFMPROF                                                 01240000
.FMPERR2 MNOTE 0,'FMPROF INVALID - TOO MANY DIGITS'                     01260000
         AGO   .NFMPROF                                                 01280000
.FMPERR3 MNOTE 0,'FMPROF INVALID - VALUE LESS THAN 0'                   01300000
         AGO   .NFMPROF                                                 01320000
.FMPERR4 MNOTE 0,'FMPROF INVALID - VALUE GREATER THAN 255'              01340000
.NFMPROF ANOP                                                           01360000
         MNOTE 0,'FMPROF INVALID - DEFAULT VALUE USED'                  01380000
         DC    XL1'0'                           FM PROFILE DEFAULT      01400000
.********************************************************************** 01420000
.*       GENERATE CODE FOR THE TSPROF KEYWORD VALUE OR DEFAULT          01440000
.*                HEXIDECIMAL CHECKING                                  01460000
.********************************************************************** 01480000
.TSPROF  ANOP                                                           01500000
&CHAR1   SETC  '&TSPROF'(1,2)                   FIRST 2 CHARS           01520000
&LEN     SETA  K'&TSPROF                        VALUE LENGTH            01540000
&CHAR2   SETC  '&TSPROF'(&LEN,1)                LAST CHAR               01560000
         AIF   ('&CHAR1' NE 'X''').MODCKQ2      IF NOT HEX, BRANCH      01580000
         AIF   ('&CHAR2' NE '''').TSPERR1       ENDING QUOTE MISSING    01600000
         AIF   (&LEN GT 5).TSPERR2              VALUE TOO LONG          01620000
&CHAR3(1) SETC  '&TSPROF'(3,&LEN-3)             GET VALUE               01640000
&CHAR1   SETC  '&CHAR1'(1,1)                    SET TO X                01660000
         DC    &CHAR1.L1&CHAR2&CHAR3(1)&CHAR2   TS PROFILE              01680000
         AGO   .PRIPROT                                                 01700000
.********************************************************************** 01720000
.*       GENERATE CODE FOR THE TSPROF KEYWORD VALUE OR DEFAULT          01740000
.*                DECIMAL PROCESSING                                    01760000
.********************************************************************** 01780000
.MODCKQ2 ANOP                                                           01800000
&CHAR1   SETC  '&TSPROF'                        GET VALUE               01820000
         AIF   ('&CHAR1' LT '0').TSPERR3        INVALID, USE DEFAULT    01840000
         AIF   ('&CHAR1' GT '255').TSPERR4      INVALID, USE DEFAULT    01860000
         DC    AL1(&TSPROF)                     TS PROFILE - NUMERIC    01880000
         AGO   .PRIPROT                                                 01900000
.********************************************************************** 01920000
.*       GENERATE CODE FOR THE TSPROF KEYWORD VALUE OR DEFAULT          01940000
.*                DEFAULT PROCESSING                                    01960000
.********************************************************************** 01980000
.TSPERR1 MNOTE 0,'TSPROF INVALID - NO ENDING QUOTE'                     02000000
         AGO   .NTSPROF                                                 02020000
.TSPERR2 MNOTE 0,'TSPROF INVALID - TOO MANY DIGITS'                     02040000
         AGO   .NTSPROF                                                 02060000
.TSPERR3 MNOTE 0,'TSPROF INVALID - VALUE LESS THAN 0'                   02080000
         AGO   .NTSPROF                                                 02100000
.TSPERR4 MNOTE 0,'TSPROF INVALID - VALUE GREATER THAN 255'              02120000
.NTSPROF ANOP                                                           02140000
         MNOTE 0,'TSPROF INVALID - DEFAULT VALUE USED'                  02160000
         DC    XL1'0'                           TS PROFILE DEFAULT      02180000
.********************************************************************** 02200000
.*       GENERATE CODE FOR THE PRIPROT KEYWORD VALUE OR DEFAULT         02220000
.*                HEXIDECIMAL CHECKING                                  02240000
.********************************************************************** 02260000
.PRIPROT ANOP                                                           02280000
&CHAR1   SETC  '&PRIPROT'(1,2)                  FIRST 2 CHARS           02300000
&LEN     SETA  K'&PRIPROT                       VALUE LENGTH            02320000
&CHAR2   SETC  '&PRIPROT'(&LEN,1)               LAST CHAR               02340000
         AIF   ('&CHAR1' NE 'X''').MODCKQ3      IF NOT HEX, BRANCH      02360000
         AIF   ('&CHAR2' NE '''').PRIERR1       ENDING QUOTE MISSING    02380000
         AIF   (&LEN GT 5).PRIERR2              VALUE TOO LONG          02400000
&CHAR3(1) SETC  '&PRIPROT'(3,&LEN-3)            GET VALUE               02420000
&CHAR1   SETC  '&CHAR1'(1,1)                    SET TO X                02440000
         DC    &CHAR1.L1&CHAR2&CHAR3(1)&CHAR2   FM PROFILE              02460000
         AGO   .SECPROT                                                 02480000
.********************************************************************** 02500000
.*       GENERATE CODE FOR THE PRIPROT KEYWORD VALUE OR DEFAULT         02520000
.*                DECIMAL PROCESSING                                    02540000
.********************************************************************** 02560000
.MODCKQ3 ANOP                                                           02580000
&CHAR1   SETC  '&PRIPROT'                       GET VALUE               02600000
         AIF   ('&CHAR1' LT '0').PRIERR3        INVALID, USE DEFAULT    02620000
         AIF   ('&CHAR1' GT '255').PRIERR4      INVALID, USE DEFAULT    02640000
         DC    AL1(&PRIPROT)                    PRIPROT - NUMERIC       02660000
         AGO   .SECPROT                                                 02680000
.********************************************************************** 02700000
.*       GENERATE CODE FOR THE PRIPROT KEYWORD VALUE OR DEFAULT         02720000
.*                DEFAULT PROCESSING                                    02740000
.********************************************************************** 02760000
.PRIERR1 MNOTE 0,'PRIPROT INVALID - NO ENDING QUOTE'                    02780000
         AGO   .PRIPROF                                                 02800000
.PRIERR2 MNOTE 0,'PRIPROT INVALID - TOO MANY DIGITS'                    02820000
         AGO   .PRIPROF                                                 02840000
.PRIERR3 MNOTE 0,'PRIPROT INVALID - VALUE LESS THAN 0'                  02860000
         AGO   .PRIPROF                                                 02880000
.PRIERR4 MNOTE 0,'PRIPROT INVALID - VALUE GREATER THAN 255'             02900000
.PRIPROF ANOP                                                           02920000
         MNOTE 0,'PRIPROT INVALID - DEFAULT VALUE USED'                 02940000
         DC    XL1'0'                           PRIPROT DEFAULT         02960000
.********************************************************************** 02980000
.*       GENERATE CODE FOR THE SECPROT KEYWORD VALUE OR DEFAULT         03000000
.*                HEXIDECIMAL CHECKING                                  03020000
.********************************************************************** 03040000
.SECPROT ANOP                                                           03060000
&CHAR1   SETC  '&SECPROT'(1,2)                  FIRST 2 CHARS           03080000
&LEN     SETA  K'&SECPROT                       VALUE LENGTH            03100000
&CHAR2   SETC  '&SECPROT'(&LEN,1)               LAST CHAR               03120000
         AIF   ('&CHAR1' NE 'X''').MODCKQ4      IF NOT HEX, BRANCH      03140000
         AIF   ('&CHAR2' NE '''').SECERR1       ENDING QUOTE MISSING    03160000
         AIF   (&LEN GT 5).SECERR2              VALUE TOO LONG          03180000
&CHAR3(1) SETC  '&SECPROT'(3,&LEN-3)            GET VALUE               03200000
&CHAR1   SETC  '&CHAR1'(1,1)                    SET TO X                03220000
         DC    &CHAR1.L1&CHAR2&CHAR3(1)&CHAR2   FM PROFILE              03240000
         AGO   .COMPROT                                                 03260000
.********************************************************************** 03280000
.*       GENERATE CODE FOR THE SECPROT KEYWORD VALUE OR DEFAULT         03300000
.*                DECIMAL PROCESSING                                    03320000
.********************************************************************** 03340000
.MODCKQ4 ANOP                                                           03360000
&CHAR1   SETC  '&SECPROT'                       GET VALUE               03380000
         AIF   ('&CHAR1' LT '0').SECERR3        INVALID, USE DEFAULT    03400000
         AIF   ('&CHAR1' GT '255').SECERR4      INVALID, USE DEFAULT    03420000
         DC    AL1(&SECPROT)                    FM PROFILE - NUMERIC    03440000
         AGO   .COMPROT                                                 03460000
.********************************************************************** 03480000
.*       GENERATE CODE FOR THE SECPROT KEYWORD VALUE OR DEFAULT         03500000
.*                DEFAULT PROCESSING                                    03520000
.********************************************************************** 03540000
.SECERR1 MNOTE 0,'SECPROT INVALID - NO ENDING QUOTE'                    03560000
         AGO   .SECPROF                                                 03580000
.SECERR2 MNOTE 0,'SECPROT INVALID - TOO MANY DIGITS'                    03600000
         AGO   .SECPROF                                                 03620000
.SECERR3 MNOTE 0,'SECPROT INVALID - VALUE LESS THAN 0'                  03640000
         AGO   .SECPROF                                                 03660000
.SECERR4 MNOTE 0,'SECPROT INVALID - VALUE GREATER THAN 255'             03680000
.SECPROF ANOP                                                           03700000
         MNOTE 0,'SECPROT INVALID - DEFAULT VALUE USED'                 03720000
         DC    XL1'0'                           SECPROT DEFAULT         03740000
.********************************************************************** 03760000
.*       GENERATE CODE FOR THE COMPROT KEYWORD VALUE OR DEFAULT         03780000
.*                HEXIDECIMAL CHECKING                                  03800000
.********************************************************************** 03820000
.COMPROT ANOP                                                           03840000
&CHAR1   SETC  '&COMPROT'(1,2)                  FIRST 2 CHARS           03860000
&LEN     SETA  K'&COMPROT                       VALUE LENGTH            03880000
&CHAR2   SETC  '&COMPROT'(&LEN,1)               LAST CHAR               03900000
         AIF   ('&CHAR1' NE 'X''').MODCKQ5      IF NOT HEX, BRANCH      03920000
         AIF   ('&CHAR2' NE '''').COMERR1       ENDING QUOTE MISSING    03940000
         AIF   (&LEN GT 7).COMERR2              VALUE TOO LONG          03960000
&CHAR3(1) SETC  '&COMPROT'(3,&LEN-3)            GET VALUE               03980000
&CHAR1   SETC  '&CHAR1'(1,1)                    SET TO X                04000000
         DC    &CHAR1.L2&CHAR2&CHAR3(1)&CHAR2   COMPROT PROFILE         04020000
         AGO   .SSNDS                                                   04040000
.********************************************************************** 04060000
.*       GENERATE CODE FOR THE COMPROT KEYWORD VALUE OR DEFAULT         04080000
.*                DECIMAL PROCESSING                                    04100000
.********************************************************************** 04120000
.MODCKQ5 ANOP                                                           04140000
&CHAR1   SETC  '&COMPROT'                       GET VALUE               04160000
         AIF   ('&CHAR1' LT '0').COMERR3        INVALID, USE DEFAULT    04180000
         AIF   ('&CHAR1' GT '65535').COMERR4    INVALID, USE DEFAULT    04200000
         DC    AL2(&COMPROT)                    COMPROT - NUMERIC       04220000
         AGO   .SSNDS                                                   04240000
.********************************************************************** 04260000
.*       GENERATE CODE FOR THE COMPROT KEYWORD VALUE OR DEFAULT         04280000
.*                DEFAULT PROCESSING                                    04300000
.********************************************************************** 04320000
.COMERR1 MNOTE 0,'COMPROT INVALID - NO ENDING QUOTE'                    04340000
         AGO   .COMPROF                                                 04360000
.COMERR2 MNOTE 0,'COMPROT INVALID - TOO MANY DIGITS'                    04380000
         AGO   .COMPROF                                                 04400000
.COMERR3 MNOTE 0,'COMPROT INVALID - VALUE LESS THAN 0'                  04420000
         AGO   .COMPROF                                                 04440000
.COMERR4 MNOTE 0,'COMPROT INVALID - VALUE GREATER THAN 65535'           04460000
.COMPROF ANOP                                                           04480000
         MNOTE 0,'COMPROT INVALID - DEFAULT VALUE USED'                 04500000
         DC    XL2'00'                          COMPROT DEFAULT         04520000
.********************************************************************** 04540000
.*       GENERATE CODE FOR THE SSNDPAC KEYWORD VALUE OR DEFAULT         04560000
.*                HEXIDECIMAL CHECKING                                  04580000
.********************************************************************** 04600000
.SSNDS   ANOP                                                           04620000
&CHAR1   SETC  '&SSNDPAC'(1,2)                  FIRST 2 CHARS           04640000
&LEN     SETA  K'&SSNDPAC                       VALUE LENGTH            04660000
&CHAR2   SETC  '&SSNDPAC'(&LEN,1)               LAST CHAR               04680000
         AIF   ('&CHAR1' NE 'X''').MODCKQ6      IF NOT HEX, BRANCH      04700000
         AIF   ('&CHAR2' NE '''').SSNERR1       ENDING QUOTE MISSING    04720000
         AIF   (&LEN GT 5).SSNERR2              VALUE TOO LONG          04740000
&CHAR3(1) SETC  '&SSNDPAC'(3,&LEN-3)            GET VALUE               04760000
&CHAR1   SETC  '&CHAR1'(1,1)                    SET TO X                04780000
         DC    &CHAR1.L1&CHAR2&CHAR3(1)&CHAR2   SSNDPAC PROFILE         04800000
         AGO   .SRCVS                                                   04820000
.********************************************************************** 04840000
.*       GENERATE CODE FOR THE SSNDPAC KEYWORD VALUE OR DEFAULT         04860000
.*                DECIMAL PROCESSING                                    04880000
.********************************************************************** 04900000
.MODCKQ6 ANOP                                                           04920000
&CHAR1   SETC  '&SSNDPAC'                       GET VALUE               04940000
         AIF   ('&CHAR1' LT '0').SSNERR3        INVALID, USE DEFAULT    04960000
         AIF   ('&CHAR1' GT '63').SSNERR4       INVALID, USE DEFAULT    04980000
         DC    AL1(&SSNDPAC)                    SSNDPAC - NUMERIC       05000000
         AGO   .SRCVS                                                   05020000
.********************************************************************** 05040000
.*       GENERATE CODE FOR THE SSNDPAC KEYWORD VALUE OR DEFAULT         05060000
.*                DEFAULT PROCESSING                                    05080000
.********************************************************************** 05100000
.SSNERR1 MNOTE 0,'SSNDPAC INVALID - NO ENDING QUOTE'                    05120000
         AGO   .SSNPROF                                                 05140000
.SSNERR2 MNOTE 0,'SSNDPAC INVALID - TOO MANY DIGITS'                    05160000
         AGO   .SSNPROF                                                 05180000
.SSNERR3 MNOTE 0,'SSNDPAC INVALID - VALUE LESS THAN 0'                  05200000
         AGO   .SSNPROF                                                 05220000
.SSNERR4 MNOTE 0,'SSNDPAC INVALID - VALUE GREATER THAN 63'              05240000
.SSNPROF ANOP                                                           05260000
         MNOTE 0,'SSNDPAC INVALID - DEFAULT VALUE USED'                 05280000
         DC    XL1'0'                           SSNDPAC DEFAULT         05300000
.********************************************************************** 05320000
.*       GENERATE CODE FOR THE SRCVPAC KEYWORD VALUE OR DEFAULT         05340000
.*                HEXIDECIMAL CHECKING                                  05360000
.********************************************************************** 05380000
.SRCVS   ANOP                                                           05400000
&CHAR1   SETC  '&SRCVPAC'(1,2)                  FIRST 2 CHARS           05420000
&LEN     SETA  K'&SRCVPAC                       VALUE LENGTH            05440000
&CHAR2   SETC  '&SRCVPAC'(&LEN,1)               LAST CHAR               05460000
         AIF   ('&CHAR1' NE 'X''').MODCKQ7      IF NOT HEX, BRANCH      05480000
         AIF   ('&CHAR2' NE '''').SRCERR1       ENDING QUOTE MISSING    05500000
         AIF   (&LEN GT 5).SRCERR2              VALUE TOO LONG          05520000
&CHAR3(1) SETC  '&SRCVPAC'(3,&LEN-3)            GET VALUE               05540000
&CHAR1   SETC  '&CHAR1'(1,1)                    SET TO X                05560000
         DC    &CHAR1.L1&CHAR2&CHAR3(1)&CHAR2   SRCVPAC PROFILE         05580000
         AGO   .RUSZS                                                   05600000
.********************************************************************** 05620000
.*       GENERATE CODE FOR THE SRCVPAC KEYWORD VALUE OR DEFAULT         05640000
.*                DECIMAL PROCESSING                                    05660000
.********************************************************************** 05680000
.MODCKQ7 ANOP                                                           05700000
&CHAR1   SETC  '&SRCVPAC'                       GET VALUE               05720000
         AIF   ('&CHAR1' LT '0').SRCERR3        INVALID, USE DEFAULT    05740000
         AIF   ('&CHAR1' GT '63').SRCERR4       INVALID, USE DEFAULT    05760000
         DC    AL1(&SRCVPAC)                    SRCVPAC - NUMERIC       05780000
         AGO   .RUSZS                                                   05800000
.********************************************************************** 05820000
.*       GENERATE CODE FOR THE SRCVPAC KEYWORD VALUE OR DEFAULT         05840000
.*                DEFAULT PROCESSING                                    05860000
.********************************************************************** 05880000
.SRCERR1 MNOTE 0,'SRCVPAC INVALID - NO ENDING QUOTE'                    05900000
         AGO   .SRCPROF                                                 05920000
.SRCERR2 MNOTE 0,'SRCVPAC INVALID - TOO MANY DIGITS'                    05940000
         AGO   .SRCPROF                                                 05960000
.SRCERR3 MNOTE 0,'SRCVPAC INVALID - VALUE LESS THAN 0'                  05980000
         AGO   .SRCPROF                                                 06000000
.SRCERR4 MNOTE 0,'SRCVPAC INVALID - VALUE GREATER THAN 63'              06020000
.SRCPROF ANOP                                                           06040000
         MNOTE 0,'SRCVPAC INVALID - DEFAULT VALUE USED'                 06060000
         DC    XL1'0'                           SRCVPAC DEFAULT         06080000
.********************************************************************** 06100000
.*       GENERATE CODE FOR THE RUSIZES KEYWORD VALUE OR DEFAULT         06120000
.*                HEXIDECIMAL CHECKING                                  06140000
.********************************************************************** 06160000
.RUSZS   ANOP                                                           06180000
&CHAR1   SETC  '&RUSIZES'(1,2)                  FIRST 2 CHARS           06200000
&LEN     SETA  K'&RUSIZES                       VALUE LENGTH            06220000
&CHAR2   SETC  '&RUSIZES'(&LEN,1)               LAST CHAR               06240000
         AIF   ('&CHAR1' NE 'X''').RUSERR1      IF NOT HEX, BRANCH      06260000
         AIF   ('&CHAR2' NE '''').RUSERR2       ENDING QUOTE MISSING    06280000
         AIF   (&LEN GT 7).RUSERR3              VALUE TOO LONG          06300000
&CHAR3(1) SETC  '&RUSIZES'(3,&LEN-3)            GET VALUE               06320000
&CHAR1   SETC  '&CHAR1'(1,1)                    SET TO X                06340000
         DC    &CHAR1.L2&CHAR2&CHAR3(1)&CHAR2   RUSIZES PROFILE         06360000
         AGO   .RUSEND                                                  06380000
.********************************************************************** 06400000
.*       GENERATE CODE FOR THE RUSIZES KEYWORD VALUE OR DEFAULT         06420000
.*                DEFAULT PROCESSING                                    06440000
.********************************************************************** 06460000
.RUSERR1 MNOTE 4,'RUSIZES INVALID - NOT A HEX NUMBER'          @ZA43118 06480000
         AGO   .RUSPROF                                                 06500000
.RUSERR2 MNOTE 4,'RUSIZES INVALID - NO ENDING QUOTE'           @ZA43118 06520000
         AGO   .RUSPROF                                                 06540000
.RUSERR3 MNOTE 4,'RUSIZES INVALID - TOO MANY DIGITS'           @ZA43118 06560000
.RUSPROF ANOP                                                           06580000
         MNOTE 4,'RUSIZES INVALID - DEFAULT VALUE USED'        @ZA43118 06600000
         DC    XL2'00'                          RUSIZES DEFAULT         06620000
.********************************************************************** 06640000
.*       GENERATE CODE FOR THE PSNDPAC KEYWORD VALUE OR DEFAULT         06660000
.*                HEXIDECIMAL CHECKING                                  06680000
.********************************************************************** 06700000
.RUSEND  ANOP                                                           06720000
&CHAR1   SETC  '&PSNDPAC'(1,2)                  FIRST 2 CHARS           06740000
&LEN     SETA  K'&PSNDPAC                       VALUE LENGTH            06760000
&CHAR2   SETC  '&PSNDPAC'(&LEN,1)               LAST CHAR               06780000
         AIF   ('&CHAR1' NE 'X''').MODCKQ8      IF NOT HEX, BRANCH      06800000
         AIF   ('&CHAR2' NE '''').PSNERR1       ENDING QUOTE MISSING    06820000
         AIF   (&LEN GT 5).PSNERR2              VALUE TOO LONG          06840000
&CHAR3(1) SETC  '&PSNDPAC'(3,&LEN-3)            GET VALUE               06860000
&CHAR1   SETC  '&CHAR1'(1,1)                    SET TO X                06880000
         DC    &CHAR1.L1&CHAR2&CHAR3(1)&CHAR2   PSNDPAC PROFILE         06900000
         AGO   .PSERIC                                                  06920000
.********************************************************************** 06940000
.*       GENERATE CODE FOR THE PSNDPAC KEYWORD VALUE OR DEFAULT         06960000
.*                DECIMAL PROCESSING                                    06980000
.********************************************************************** 07000000
.MODCKQ8 ANOP                                                           07020000
&CHAR1   SETC  '&PSNDPAC'                       GET VALUE               07040000
         AIF   ('&CHAR1' LT '0').PSNERR3        INVALID, USE DEFAULT    07060000
         AIF   ('&CHAR1' GT '63').PSNERR4       INVALID, USE DEFAULT    07080000
         DC    AL1(&PSNDPAC)                    PSNDPAC - NUMERIC       07100000
         AGO   .PSERIC                                                  07120000
.********************************************************************** 07140000
.*       GENERATE CODE FOR THE PSNDPAC KEYWORD VALUE OR DEFAULT         07160000
.*                DEFAULT PROCESSING                                    07180000
.********************************************************************** 07200000
.PSNERR1 MNOTE 0,'PSNDPAC INVALID - NO ENDING QUOTE'                    07220000
         AGO   .PSNPROF                                                 07240000
.PSNERR2 MNOTE 0,'PSNDPAC INVALID - TOO MANY DIGITS'                    07260000
         AGO   .PSNPROF                                                 07280000
.PSNERR3 MNOTE 0,'PSNDPAC INVALID - VALUE LESS THAN 0'                  07300000
         AGO   .PSNPROF                                                 07320000
.PSNERR4 MNOTE 0,'PSNDPAC INVALID - VALUE GREATER THAN 63'              07340000
.PSNPROF ANOP                                                           07360000
         MNOTE 0,'PSNDPAC INVALID - DEFAULT VALUE USED'                 07380000
         DC    XL1'0'                           PSNDPAC DEFAULT         07400000
.********************************************************************** 07420000
.*       GENERATE RESERVED FIELD                                        07440000
.********************************************************************** 07460000
.PSERIC  ANOP                                                @D34EKMP   07480000
         DC    XL1'00'                       RESERVED        @D34EKMP   07500000
.********************************************************************** 07520000
.*       GENERATE CODE FOR THE PSERVIC KEYWORD VALUE OR DEFAULT         07540000
.*                HEXIDECIMAL CHECKING                                  07560000
.********************************************************************** 07580000
&CHAR1   SETC  '&PSERVIC'(1,2)                  FIRST 2 CHARS           07600000
&LEN     SETA  K'&PSERVIC                       VALUE LENGTH            07620000
&CHAR2   SETC  '&PSERVIC'(&LEN,1)               LAST CHAR               07640000
         AIF   ('&CHAR1' NE 'X''').PSEERR1      IF NOT HEX, BRANCH      07660000
         AIF   ('&CHAR2' NE '''').PSEERR2       ENDING QUOTE MISSING    07680000
         AIF   (&LEN GT 27).PSEERR3             VALUE TOO LONG          07700000
.********************************************************************** 07720000
.*       GENERATE CODE FOR THE PSERVIC KEYWORD VALUE OR DEFAULT         07740000
.*                LOOP TO LOAD LOCAL ARRAY                              07760000
.********************************************************************** 07780000
&LEN     SETA  &LEN-3                                                   07800000
&I       SETA  0                                             @DM39835   07820000
&J       SETA  3                                             @DM39835   07840000
&K       SETA  8                                             @DM39835   07860000
.PSLOOP  ANOP                                                @DM39835   07880000
&I       SETA  &I+1                                          @DM39835   07900000
         AIF   (&K LE &LEN).PSMOVE                           @DM39835   07920000
&K       SETA  &LEN                                          @DM39835   07940000
.PSMOVE  ANOP                                                @DM39835   07960000
&CHAR3(&I) SETC '&PSERVIC'(&J,&K)                            @DM39835   07980000
&J       SETA  &J+&K                                         @DM39835   08000000
&LEN     SETA  &LEN-&K                                       @DM39835   08020000
         AIF   (&LEN GT 0).PSLOOP                            @DM39835   08040000
&CHAR4   SETC  '&CHAR1'(2,1)                                            08060000
&CHAR1   SETC  '&CHAR1'(1,1)                                            08080000
         DC    &CHAR1.L12&CHAR4&CHAR3(1)&CHAR3(2)&CHAR3(3)&CHAR4        08100000
*                                            PRESENTATION    @DM39835   08120000
*                                            SERVICES        @DM39835   08140000
         AGO   .DEF                                                     08160000
.********************************************************************** 08180000
.*       GENERATE CODE FOR THE PSERVIC KEYWORD VALUE OR DEFAULT         08200000
.*                DEFAULT PROCESSING                                    08220000
.********************************************************************** 08240000
.PSEERR1 MNOTE 0,'PSERVIC INVALID - NOT A HEX NUMBER'                   08260000
         AGO   .PSERVD                                                  08280000
.PSEERR2 MNOTE 0,'PSERVIC INVALID - NO ENDING QUOTE'                    08300000
         AGO   .PSERVD                                                  08320000
.PSEERR3 MNOTE 0,'PSERVIC INVALID - TOO MANY DIGITS'                    08340000
.PSERVD  ANOP                                                           08360000
         MNOTE 0,'PSERVIC INVALID - DEFAULT VALUE USED'                 08380000
         DC    XL2'00'                          PSERVIC DEFAULT         08400000
.********************************************************************** 08420000
.*       GENERATE FINAL CODE                                            08440000
.********************************************************************** 08460000
.DEF     ANOP                                                           09250200
         DC    XL1'00'                       RESERVED                   09250300
         DC    XL9'00'                       RESERVED                   09250400
         DC    XL1'00'                       USER DATA LENGTH           09250800
         AIF   ('&ERRCK' NE '1').MODEEND                                09252000
&ENTRYCT SETA  &ENTRYCT+1         COUNT OF THE NUMBER OF MODE ENTRIES   09260000
.MODEEND ANOP                                                           09360000
         MEND                                                           09400000
