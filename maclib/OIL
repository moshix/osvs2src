         MACRO                                                          00050000
&NAME    OIL   &BYTE,&MASK,&REF=,&WREGS=(0,1,2)                         00100000
.*             OR IMMEDIATE LOCKED                                      00150000
.*             04/24/72, LEVEL=1                                        00200000
.*                                                                      00250000
.*       THIS MACRO IS USED IN A MULTIPROCESSING ENVIRONMENT TO         00300000
.*       PROVIDE A LOCK ON A WORD CONTAINING THE BYTE WHICH IS THE      00350000
.*       TARGET OF AN OI INSTRUCTION.                                   00400000
.*                                                                      00450000
.*       &BYTE      SYMBOL NAMING THE BYTE TO WHICH THE OR IMMEDIATE    00500000
.*                  FUNCTION IS TO BE APPLIED.                          00550000
.*       &MASK      A ONE-BYTE SELF-DEFINING TERM OR SYMBOL EQUATED     00600000
.*                  TO SUCH A TERM WHICH SPECIFIES A MASK AS IN         00650000
.*                  IMMEDIATE OPERAND OF OI INSTRUCTION.                00700000
.*       &REF       LABEL OF A FULL WORD IN STORAGE.  ADDRESS OF REF    00750000
.*                  LABEL MUST BE LESS THAN OR EQUAL TO THAT OF BYTE    00800000
.*                  (FIRST OPERAND).  LABEL MUST BE ADDRESSABLE WHEN    00850000
.*                  THE MACRO IS ISSUED, AND USING MUST HAVE BEEN       00900000
.*                  ISSUED ON A FULL-WORD LABEL.                        00950000
.* NOTE  &BYTE, &MASK AND &REF MUST BE SPECIFIED ON MACRO CALL.         01000000
.*       &WREGS     DECIMAL NUMBERS OR SYMBOLS EQUATED TO DECIMAL       01050000
.*                  NUMBERS OF THREE WORK REGISTERS TO BE USED BY THIS  01100000
.*                  MACRO.  REGISTERS 0, 1 AND 2 ARE DEFAULT VALUES.    01150000
         LCLC  &XREGS(3)      FOR WREGS VALUES                          01200000
         AIF   ('&BYTE' NE '').OIL10    BRANCH IF BYTE SPECIFIED        01250000
         IHBERMAC 1006,BYTE   BYTE REQUIRED BUT NOT SPECIFIED           01300000
         MEXIT                                                          01350000
.OIL10   AIF   ('&MASK' NE '').OIL20    BRANCH IF MASK SPECIFIED        01400000
         IHBERMAC 1006,MASK   MASK REQUIRED BUT NOT SPECIFIED           01450000
         MEXIT                                                          01500000
.OIL20   AIF   ('&REF' NE '').OIL30     BRANCH IF REF SPECIFIED         01550000
         IHBERMAC 1006,REF    REF REQUIRED BUT NOT SPECIFIED            01600000
         MEXIT                                                          01650000
.OIL30   AIF   ('&WREGS(1)' NE '').OIL35 BRANCH IF FIRST REG SPECIFIED  01700000
&XREGS(1) SETC '0'            USE REGISTER 0 AS DEFAULT                 01750000
         AGO   .OIL40                                                   01800000
.OIL35   ANOP                                                           01850000
&XREGS(1) SETC '&WREGS(1)'                                              01900000
.OIL40   AIF   ('&WREGS(2)' NE '').OIL45 BRANCH IF SECOND REG SPECIFIED 01950000
&XREGS(2) SETC '1'            USE REGISTER 1 AS DEFAULT                 02000000
         AGO   .OIL50                                                   02050000
.OIL45   ANOP                                                           02100000
&XREGS(2) SETC '&WREGS(2)'                                              02150000
.OIL50   AIF   ('&WREGS(3)' NE '').OIL55 BRANCH IF THIRD REG SPECIFIED  02200000
&XREGS(3) SETC '2'            USE REGISTER 2 AS DEFAULT                 02250000
         AGO   .OIL60                                                   02300000
.OIL55   ANOP                                                           02350000
&XREGS(3) SETC '&WREGS(3)'                                              02400000
.OIL60   ANOP  ,              ALL OPERANDS SPECIFIED                    02450000
&NAME    L     &XREGS(1),&REF+(((&BYTE-&REF)/4)*4)                      02500000
*                             LOAD WORD CONTAINING BYTE TO BE SET       02550000
         LA    &XREGS(3),&MASK.(0,0) PUT MASK IN LOW-ORDER BYTE OF REG  02600002
*                                                                YM1995 02620002
         SLL   &XREGS(3),24-(8*((&BYTE-&REF)-(((&BYTE-&REF)/4)*4)))(0)  02650002
*                             SHIFT MASK TO SAME POSITION IN WORD AS    02700000
*                             BYTE TO BE CHANGED                 YM1995 02750002
         LR    &XREGS(2),&XREGS(1) LOAD WORD CONTAINING BYTE TO BE SET  02800000
         OR    &XREGS(2),&XREGS(3) OR MASK INTO BYTE IN REGISTER        02850000
*                             USE COMPARE AND SWAP TO STORE UPDATED     02900000
*                             REGISTER IN STORAGE IF STORAGE WORD WAS   02950000
*                             NOT MODIFIED                              03000000
         CS    &XREGS(1),&XREGS(2),&REF+(((&BYTE-&REF)/4)*4)            03050000
         BNE   *-8            DO AGAIN FROM LR IF WORD WAS MODIFIED     03100000
         MEND                                                           03150000
