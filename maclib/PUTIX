         MACRO                                                          00050004
&NAME    PUTIX &RPL=                                                    00100004
         LCLA  &I                       WORK VARIABLE                   00110004
         AIF   (N'&SYSLIST EQ 0).NOPOS  GO IF NO POSITIONAL OPERANDS    00150004
.POSLOOP ANOP                                                           00160004
&I       SETA  &I+1                     POINT TO NEXT POS OPERAND       00170004
         IDAERMAC 1,&SYSLIST(&I)        ISSUE WARNING MNOTE             00200004
         AIF   (&I LT N'&SYSLIST).POSLOOP  GO IF MORE POS OPERANDS      00210004
.NOPOS   ANOP                                                           00250004
         AIF   ('&RPL' NE '').OKY       GO IF RPL IS SPECIFIED          00300004
         IDAERMAC 2,RPL                 ISSUE ERROR MNOTE               00350004
         MEXIT                          ..AND TERMINATE                 00400004
.OKY     ANOP                                                           00450004
         AIF   ('&NAME' EQ '').NAMEB    GO IF NO NAME                   00460004
&NAME    DS    0H                                                       00500004
.NAMEB   ANOP                                                           00510004
         AIF   ('&RPL'(1,1) EQ '(').RPLR  GO IF RPL SPECIFIED AS REG    00550004
         LA    1,&RPL                   LOAD RPL ADDRESS                00600004
         AGO   .AREG                    GO AROUND REG CODE              00650004
.RPLR    ANOP                                                           00700004
         AIF   ('&RPL' EQ '(1)').AREG   GO IF RPL ADDRESS ALREADY IN 1  00750004
         LR    1,&RPL(1)                LOAD RPL ADDRESS                00800004
.AREG    ANOP                                                           00850004
         LA    0,12                     INDICATE PUT INDEX              00900004
         L     15,24(,1)                LOAD ACB ADDRESS FROM RPL       00950004
         L     15,8(,15)                LOAD VSAM INTERFACE RTN ADDR    01000004
         BALR  14,15                    LINK TO INTERFACE ROUTINE       01050004
         MEND                                                           01100004
