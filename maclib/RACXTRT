********************************************************************    00050000
*                                                                       00100000
*  $MAC(RACXTRT) COMP(SC1BN) PROD(RACF):                                00150000
*                                                                       00200000
*  DESCRIPTIVE NAME = INTERFACE TO RACF EXTRACT FUNCTION                00250000
*                                                                       00300000
*  COPYRIGHT = 5740-XYN, 5750-XYS COPYRIGHT IBM CORP 1976, 1982         00350000
*              LICENSED MATERIAL - PROGRAM PROPERTY OF IBM              00400000
*              REFER TO COPYRIGHT INSTRUCTIONS FORM NUMBER G120-2083    00450000
*                                                                       00500000
*  STATUS = OS/VS2 JBB1326                                              00550000
*                                                                       00600000
*  FUNCTION = THE RACXTRT MACRO PROVIDES A STANDARD INTERFACE TO        00650000
*             THE RACF EXTRACT ROUTINE                                  00700000
*             (MODULE NAME - ICHRXT00)                                  00750000
*                                                                       00800000
*  INVOCATION = THE RACF EXTRACT ROUTINE IS INVOKED BY THE RACXTRT      00850000
*               MACRO VIA SVC 132.                                      00900000
*                                                                       00950000
*   REQUIREMENTS FOR USE - USER MUST BE AUTHORIZED                      01000000
*                                                                       01050000
*  MODULE TYPE = MACRO                                                  01100000
*                                                                       01150000
*  CHANGE ACTIVITY =                                                    01200000
*          $L0=HRF1602,RACF/160,830620,CNTDSD:                          01250000
*          UZ90283                                             @ZA90283 01300000
*          UZ90284                                             @ZA90284 01350000
*          UZ90285                                             @ZA90285 01400000
*                                                                       01450000
********************************************************************    01500000
         MACRO                                                          01550000
&NAME    RACXTRT &ACEE=,                                               *01600000
               &ENCRYPT=,                                              *01650000
               &ENTITY=,                                               *01700000
               &FIELDS=,                                               *01750000
               &SUBPOOL=,                                              *01800000
               &TYPE=,                                                 *01850000
               &MF=S                                                    01900000
.*  DEFINE LOCAL VARIABLES                                              01950000
         LCLA  &FN                 USED TO CONTAIN NUMERIC VALUE        02000000
.*                                 FOR FUNCTION                         02050000
         LCLA  &ENC                USED TO CONTAIN ADDRESS OF DATA      02100000
.*                                 TO BE ENCRYPTED                      02150000
         LCLA  &DFLTSP             USED TO SET DEFAULT SUBPOOL          02200000
.*                                 WHEN ENTERED AS A STRING             02250000
         LCLC  &METH               USED TO CONTAIN ENCRYPTION METHOD    02300000
         LCLC  &SP                 USED TO CONTAIN SUBPOOL VALUE        02350000
         LCLC  &NX                 USED TO CONTAIN SYSNDX FOR LABELS    02400000
.*  DEFINE GLOBAL VARIABLES                                             02450000
         GBLC  &ICHL               USED FOR ADDRESS OF EXTRACT PARM     02500000
         GBLA  &ICHCT              ICHCT - A COUNTER USED TO KEEP       02550000
.*                                 TRACK OF WHERE IN THE PARAMETER      02600000
.*                                 LIST THE PARAMETER IS TO BE          02650000
.*                                 PLACED. ICHCT IS INITIALIZED TO      02700000
.*                                 THE FIRST FULLWORD IN THE LIST       02750000
.*                                 TO BE FILLED IN BY ICHMM,            02800000
.*                                 AND INCREMENTED BY THE INNER         02850000
.*                                 MACRO (ICHMM).                       02900000
         GBLB  &ICHRAC             ICHRAC - A FLAG WHICH IS             02950000
.*                                 INITIALIZED IN THE RACROUTE MACRO    03000000
.*                                 TO COMMUNICATE TO RACXTRT THAT IT    03050000
.*                                 IS BEING INVOKED AS AN INNER MACRO   03100000
.*                                 TO THE RACROUTE MACRO.               03150000
         GBLB  &ICHLIST            ICHLIST - A FLAG WHICH IS SET TO     03200000
.*                                 INDICATE THE FORMAT OF THE MACRO.    03250000
.*                                 FOR LIST FORMAT, ICHLIST IS ON       03300000
.*                                 FOR EXECUTE FORMAT, ICHLIST IS       03350000
.*                                 OFF. FOR STANDARD FORMAT, THERE      03400000
.*                                 ARE TWO PASSES DONE BY THE MACRO     03450000
.*                                 A LIST PASS AND AN EXECUTE PASS.     03500000
.*                                 INITIALLY, FOR STANDARD, THE         03550000
.*                                 FLAG  IS ON - FOR THE LIST PASS.     03600000
.*                                 IT IS THEN TURNED OFF FOR THE        03650000
.*                                 EXECUTE PASS.                        03700000
         GBLB  &ICHNTFY            ICHNTFY - A GLOBAL INDICATOR         03750000
.*                                 WHICH IS USED TO COMMUNICATE WITH    03800000
.*                                 ICHMM - THE INNER MACRO. IF ICHNTFY  03850000
.*                                 IS ON, THE INNER MACRO WILL NOT      03900000
.*                                 ISSUE ITS OWN MNOTE IF IT FINDS AN   03950000
.*                                 ERROR-IT WILL MERELY TURN ON THE     04000000
.*                                 ICHINER FLAG AS NOTIFICATION TO THE  04050000
.*                                 OUTER MACRO, WHICH WILL TAKE THE     04100000
.*                                 APPROPRIATE ACTION.                  04150000
.*                                 IF ICHNTFY IS OFF, THE INNER MACRO   04200000
.*                                 WILL TAKE ACTION ON ITS OWN,         04250000
.*                                 IE. ISSUE AN MNOTE WITH A SEVERITY   04300000
.*                                 CODE OF ITS CHOOSING.                04350000
         GBLB  &ICHINER            ICHINER - A GLOBAL INDICATOR         04400000
.*                                 WHICH IS USED BY ICHMM - AN INNER    04450000
.*                                 MACRO - TO TELL THE OUTER MACRO IF   04500000
.*                                 AN ERROR HAS BEEN DETECTED IN A      04550000
.*                                 PARAMETER.  IF ICHINER IS ON, AN     04600000
.*                                 ERROR HAS BEEN DETECTED.             04650000
         GBLC  &ICHMF              ICHMF - AN INDICATOR OF THE          04700000
.*                                 FORMAT - L, E, OR S.                 04750000
.************************                                               04800000
.*   INITIALIZE VARIABLES                                               04850000
.************************                                               04900000
&NX      SETC  '&SYSNDX'                                                04950000
&DFLTSP  SETA  229                 DEFAULT SUBPOOL                      05000000
&ICHNTFY SETB  0                   WANT ICHMM TO ISSUE ERROR MSGS       05050000
&ICHINER SETB  0                   INITIALIZE ICHINER TO 0              05100000
.***********************************                                    05150000
.*   SET UP INDICATORS FOR MF FORMAT                                    05200000
.***********************************                                    05250000
.*  VALIDATE MF PARAMETER                                               05300000
         AIF   ('&MF' NE 'L' AND '&MF' NE 'S' AND '&MF(1)' NE          *05350000
               'E').ERR01                                               05400000
&ICHMF   SETC  '&MF(1)'        SET MF TYPE                              05450000
         AIF   ('&MF(1)' EQ 'E').EXT0030                                05500000
&ICHLIST SETB  1               MF LIST OR STANDARD                      05550000
         AGO   .CONT01                                                  05600000
.EXT0030 ANOP                                                           05650000
         AIF   ('&MF(2)' EQ '' OR N'&MF NE 2).ERR01                     05700000
&ICHLIST SETB  0               MF EXECUTE                               05750000
.****************************************************                   05800000
.*  DETERMINE WHETHER FUNCTION IS EXTRACT OR ENCRYPT                    05850000
.****************************************************                   05900000
.CONT01  ANOP                                                           05950000
         AIF   ('&TYPE' EQ '').ERR07     REQUIRED PARAMETER             06000000
         AIF   ('&TYPE' EQ 'ENCRYPT').EXT0100                           06050000
         AIF   ('&TYPE' NE 'EXTRACT').ERR05                             06100000
.****************************************************                   06150000
.*   VALIDATE PARAMETERS ALLOWABLE WITH                                 06200000
.*   TYPE=EXTRACT                                                       06250000
.****************************************************                   06300000
&FN      SETA  1                                                        06350000
         AIF   ('&ENCRYPT' NE '').ERR03                                 06400000
         AIF   ('&SUBPOOL' EQ '').CONT01A                               06450000
         AIF   (T'&SUBPOOL NE 'N').ERR09  SUBPOOL NON-NUMERIC           06500000
         AIF   (&SUBPOOL LT 0 OR &SUBPOOL GT 255).ERR09 INV. SUBPOOL    06550000
&SP      SETC  '&SUBPOOL'          COPY SUBPOOL VALUE TO LOCAL AREA     06600000
         AGO   .CONT02                                                  06650000
.CONT01A ANOP  ,                                                        06700000
         AIF   ('&ICHMF' EQ 'E').CONT02                                 06750000
&SP      SETC  '&DFLTSP'           SET DEFAULT SUBPOOL IN LOCAL AREA    06800000
         AGO   .CONT02                                                  06850000
.****************************************************                   06900000
.*   VALIDATE PARAMETERS ALLOWABLE WITH                                 06950000
.*   TYPE=ENCRYPT                                                       07000000
.****************************************************                   07050000
.EXT0100 ANOP                                                           07100000
&FN      SETA  2                                                        07150000
         AIF   ('&FIELDS&SUBPOOL' NE '').ERR03                          07200000
         AIF   ('&ENCRYPT' EQ '').ERR10                                 07250000
         AIF   ('&ENCRYPT(2)' NE '' AND '&ENCRYPT(2)' NE 'DES' AND     X07300000
               '&ENCRYPT(2)' NE 'HASH' AND                             X07350000
               '&ENCRYPT(2)' NE 'INST').ERR06 INVALID ENCR. METHOD      07400000
         AIF   (N'&ENCRYPT GT 2).ERR04  TOO MANY SUBPARAMETERS          07450000
         AIF   ('&ICHMF' NE 'S').CONT02                                 07500000
         AIF   ('&ENCRYPT(1)' EQ '' OR '&ENCRYPT(2)' EQ '').ERR08       07550000
.**************************************************************         07600000
.*  BEGIN THE PARAMETER LIST AND ESTABLISH ADDRESSABILITY TO IT         07650000
.**************************************************************         07700000
.CONT02  ANOP                                                           07750000
         AIF   ('&ICHMF' NE 'E').EXT0215                                07800000
.* MF=E                                                                 07850000
         AIF   (&ICHRAC).EXT0212                                        07900000
&NAME    IHBINNRA &MF(2)          POINT R1 AT PARAMETER LIST            07950000
         AGO   .CONT03                                                  08000000
.EXT0212 ANOP                                                           08050000
&NAME    A     1,36(1)            POINT R1 AT PARAMETER LIST            08100000
         AGO   .CONT03                                                  08150000
.EXT0215 ANOP                                                           08200000
.* MF=L/S                                                               08250000
         AIF   ('&NAME' EQ '').EXT0220                                  08300000
&NAME    DS    0F                                                       08350000
.EXT0220 ANOP                                                           08400000
         AIF   (NOT &ICHRAC).EXT0222  INVOKER NOT RACROUTE              08450000
&ICHL    DS    0F                                                       08500000
.EXT0222 ANOP                                                           08550000
         AIF   ('&MF' EQ 'L').EXT0224                                   08600000
.* MF=S                                                                 08650000
         CNOP  0,4                                                      08700000
         BAL   1,ICH&NX.C         POINT R1 AT PARAMETER LIST            08750000
.EXT0224 ANOP                                                           08800000
.* MF=L/S                                                               08850000
ICH&NX.A DS    0F                                                       08900000
         DC    AL2(ICH&NX.C-ICH&NX.A) LENGTH OF LIST IN BYTES           08950000
         DC    XL1'82'            FUNCTION CODE FOR ICHRSV00            09000000
         DC    AL1(&FN)           REQUEST TYPE                          09050000
         DC    AL1(0)             VERSION NUMBER                        09100000
         DC    AL1(0)             RESERVED                              09150000
         DC    AL2(ICH&NX.B-ICH&NX.A) OFFSET TO VARIABLE PART OF LIST   09200000
.CONT03  ANOP                                                           09250000
&ICHCT   SETA  2               INITIALIZE PARAMETER COUNTER             09300000
         ICHMM &ENTITY                                                  09350000
         AIF   (NOT &ICHLIST).EXT0231                                   09400000
ICH&NX.B EQU   *                  END OF FIXED PART                     09450000
.EXT0231 ANOP  ,                                                        09500000
         AIF   (&FN EQ 2).EXT0310   REQUEST IS ENCRYPT                  09550000
.***************************                                            09600000
.* GENERATE LIST FOR                                                    09650000
.* TYPE=EXTRACT                                                         09700000
.***************************                                            09750000
         ICHMM ,               RESERVED                                 09800000
         ICHMM &SP             SUBPOOL NUMBER                           09850000
         ICHMM &FIELDS         ADDRESS OF VARIABLE EXTRACT FIELDS       09900000
         AGO   .CONT04                                                  09950000
.************************************                                   10000000
.* GENERATE LIST FOR                                                    10050000
.* TYPE=ENCRYPT                                                         10100000
.************************************                                   10150000
.EXT0310 ANOP                                                           10200000
         ICHMM &ENCRYPT(1)   ADDRESS OF DATA TO BE ENCRYPTED            10250000
         AIF   ('&ENCRYPT(2)' EQ '').EXT0340                            10300000
         AIF   ('&ENCRYPT(2)' NE 'DES').EXT0330                         10350000
&METH    SETC  '1'           DES ENCRYPTION METHOD                      10400000
         AGO   .EXT0340                                                 10450000
.EXT0330 ANOP  ,                                                        10500000
         AIF   ('&ENCRYPT(2)' NE 'HASH').EXT0334                        10550000
&METH    SETC  '2'           HASHING ENCRYPTION METHOD                  10600000
         AGO   .EXT0340                                                 10650000
.EXT0334 ANOP  ,                                                        10700000
&METH    SETC  '3'           INSTALLATION-DEFINED METHOD                10750000
.EXT0340 ANOP  ,                                                        10800000
         ICHMM  &METH        ENCRYPTION METHOD                          10850000
.***************************************************************        10900000
.*  AT THIS POINT, THE LIST AND EXECUTE FORMS ARE COMPLETED.  IF        10950000
.*  THE LIST PASS OF THE STANDARD FORM HAS JUST BEEN COMPLETED,         11000000
.*  RETURN FOR AN EXECUTE PASS.                                         11050000
.***************************************************************        11100000
.CONT04  ANOP                                                           11150000
         AIF   (NOT &ICHLIST).CONT05  SKIP END-OF-LIST LABEL            11200000
ICH&NX.C DS    0H                 END OF PARM LIST                      11250000
.CONT05  ANOP                                                           11300000
         AIF   ('&ICHMF' NE 'S' OR NOT &ICHLIST).CONT06                 11350000
&ICHLIST SETB  0                                                        11400000
         AGO   .CONT03           DO EXECUTE PATH OF STANDARD FORM       11450000
.*****************************************************                  11500000
.*  THE SVC INSTRUCTION IS GENERATED IF RACXTRT IS                      11550000
.*  NOT INVOKED FROM THE RACROUTE MACRO OR IF THIS                      11600000
.*  IS NOT A LIST PASS.                                                 11650000
.*****************************************************                  11700000
.CONT06  ANOP                                                           11750000
         AIF   (&ICHRAC OR &ICHLIST).EROUTE                             11800000
         SVC   132                INVOKE EXTRACT ROUTINE                11850000
         AGO   .EROUTE                                                  11900000
.*****************                                                      11950000
.*  ERROR MESSAGES                                                      12000000
.*****************                                                      12050000
.ERR01   ANOP                                                           12100000
         MNOTE 12,'*** INVALID VALUE FOR ''MF'' PARAMETER'              12150000
         AGO   .EROUTE                                                  12200000
.ERR03   ANOP                                                           12250000
         MNOTE 12,'*** EXTRANEOUS PARAMETERS SPECIFIED FOR ''TYPE=&TYPEX12300000
               '''                                                      12350000
         AGO   .EROUTE                                                  12400000
.ERR04   ANOP                                                           12450000
         MNOTE 12,'*** TOO MANY PARAMETERS SPECIFIED FOR ''ENCRYPT'' PAX12500000
               RAMETER'                                                 12550000
         AGO   .EROUTE                                                  12600000
.ERR05   ANOP                                                           12650000
         MNOTE 12,'*** INVALID VALUE FOR ''TYPE'' PARAMETER'            12700000
         AGO   .EROUTE                                                  12750000
.ERR06   ANOP                                                           12800000
         MNOTE 12,'*** INVALID METHOD SPECIFIED FOR ''ENCRYPT'' PARAMETX12850000
               ER'                                                      12900000
         AGO   .EROUTE                                                  12950000
.ERR07   ANOP                                                           13000000
         MNOTE 12,'*** REQUIRED PARAMETER ''TYPE'' NOT SPECIFIED'       13050000
         AGO   .EROUTE                                                  13100000
.ERR08   ANOP                                                           13150000
         MNOTE 12,'*** BOTH SUBPARAMETERS MUST BE SPECIFIED FOR ''ENCRYX13200000
               PT'' PARAMETER'                                          13250000
         AGO   .EROUTE                                                  13300000
.ERR09   ANOP                                                           13350000
         MNOTE 12,'*** INVALID VALUE FOR ''SUBPOOL'' PARAMETER'         13400000
         AGO   .EROUTE                                                  13450000
.ERR10   ANOP                                                           13500000
         MNOTE 12,'*** ''ENCRYPT'' PARAMETER REQUIRED WITH ''TYPE=ENCRYX13550000
               PT'''                                                    13600000
         AGO   .EROUTE                                                  13650000
.EROUTE  ANOP                                                           13700000
.* RESET GLOBALS                                                        13750000
&ICHL    SETC  ''                                                       13800000
         MEND                                                           13850000
