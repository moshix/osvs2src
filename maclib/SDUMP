         MACRO                                                          00050002
&NAME    SDUMP &HDR=,&HDRAD=,&DCB=,&ASID=,&ASIDLST=,&ECB=,&SDATA=,     X00070043
               &STORAGE=,&LIST=,&SUMLIST=,&BUFFER=,&QUIESCE=,&BRANCH=, X00100043
               &MF=                                            @G33VPHD 00150043
.*                                                                      00200002
.*       DEFINE SET SYMBOLS                                             00250002
.*                                                                      00300002
         LCLA  &SDFLAG0           SDFLAG0 FIELD OF PARAMETER LIST       00350002
         LCLA  &SDFLAG1           SDFLAG1 FIELD OF PARAMETER LIST       00400002
         LCLA  &SDATAF1,&SDATAF2  SDATA FIELD OF PARAMETER LIST         00450002
         LCLA  &CTR1,&CTR2        COUNTERS FOR LOOP CONTROL, INDEX      00500002
         LCLA  &HDRCTR            NUMBER OF PAIRED APOSTROPHES          00510002
         LCLA  &DISP              DISPLACEMENT FOR STORING ADDRESSES    00550002
         LCLC  &GNAME             SYMBOLIC NAME-BRANCH AROUND CONSTANTS 00600002
         LCLB  &SDDCB             SET IF DCB ADDRESS IS SPECIFIED       00650002
         LCLB  &SDBUFF            SET IF BUFFER IS TO BE DUMPED         00700002
         LCLB  &SDSTOR            SET IF STORAGE OR LIST IS SPECIFIED   00750002
         LCLB  &SDHDR             SET IF HDR OR HDRAD IS SPECIFIED      00800002
         LCLB  &SDECB             SET IF AN ECB ADDRESS IS SUPPLIED     00850002
         LCLB  &SDASID            SET IF AN ASID IS SPECIFIED           00900002
         LCLB  &SDASIDL           SET IF AN ASIDLIST IS SPECIFIED       00910043
.*                                                             @G33VPHD 00920043
         LCLB  &SDQUIET           SET IF SYSTEM IS TO BE QUIESCED       00950002
         LCLB  &SDBRNCH           SET IF INTERFACE IS BRANCH ENTRY      01000002
         LCLB  &DUMPTYP           ALWAYS SET FOR SVC DUMP               01050002
         LCLB  &SDALPSA           SET IF ALL PSA IN SYSTEM TO BE DUMPED 01100002
         LCLB  &SDPSA             SET IF CURRENT PSA IS TO BE DUMPED    01150002
         LCLB  &SDNUC             SET IF NUCLEUS IS TO BE DUMPED        01200002
         LCLB  &SDSQA             SET IF SQA IS TO BE DUMPED            01250002
         LCLB  &SDLSQA            SET IF LSQA IS TO BE DUMPED           01300002
         LCLB  &SDRGN             SET IF VIRTUAL MEMORY IS TO BE DUMPED 01350002
         LCLB  &SDLPA             SET IF ACTIVE LPA MOD FOR MEMORY REQ  01400002
         LCLB  &SDTRT             SET IF GTF INCORE TRACE BUFFERS  REQ  01450002
         LCLB  &SDCSA             SET IF CSA SUBPOOLS ARE TO BE DUMPED  01500002
         LCLB  &SDSWA             SET IF SWA FOR MEMORY TO BE DUMPED    01550002
.*                                                                      01600002
         LCLB  &SDSUMLT           SET IF SUMLIST SPECIFIED     @G33VPHD 01607043
         LCLB  &SDSMDMP           SET IF SUMDUMP SPECIFIED     @G33VPHD 01614043
         LCLB  &SDNSMDP           SET IF NO SUMDUMP SPECIFIED  @G33VPHD 01621043
         LCLB  &SDNSQA            SET IF NO SQA SPECIFIED      @G33VPHD 01628043
         LCLB  &SDNAPSA           SET IF NOALLPSA IS SPECIFIED @G33VPHD 01635043
         LCLB  &SDNEW             SET IF ENHANCED SVC DUMP     @G33VPHD 01642043
.*       INITIAL ERRORS                                                 01650002
.*                                                                      01700002
         AIF   ('&HDR' NE '' AND '&HDRAD' NE '').MESG2A     MUTUALLY    01750002
         AIF   ('&STORAGE' NE '' AND '&LIST' NE '').MESG2B   EXCLUSIVE  01800002
         AIF   ('&HDR' EQ '').SD3           HDR SPECIFIED               01802002
         AIF   ('&HDR'(1,1) NE '''').MESG9  IS FIRST CHAR APOSTROPHE    01802402
&CTR1    SETA  K'&HDR-1                     LENGTH OF USER DATA +1      01804002
&HDRCTR  SETA  0                            NUMBER OF PAIRED APOSTROHES 01804402
&CTR2    SETA  2                            INDEX FIRST CHAR-USER DATA  01804802
.SDLOOP  ANOP                                                           01805202
         AIF   (&CTR2 GE &CTR1).SD2              END OF SCAN            01805602
         AIF   ('&HDR'(&CTR2,2) EQ '''''').SD1   PAIRED APOSTROPHES     01805702
&CTR2    SETA  &CTR2+1                           UPDATE INDEX BY 1      01805802
         AGO   .SDLOOP                           CONTINUE LOOP          01805902
.SD1     ANOP                                                           01809402
&HDRCTR  SETA  &HDRCTR+1                    ONE MORE PAIRED APOSTROPHES 01811402
&CTR2    SETA  &CTR2+2                      UPDATE INDEX BY 2           01811802
         AGO   .SDLOOP                      CONTINUE LOOP               01812202
.SD2     ANOP                                                           01812602
&CTR1    SETA  K'&HDR-2-&HDRCTR             LENGTH OF USER DATA         01812702
         AIF   (&CTR1 GT 100).MESG8         USER DATA TOO LONG          01813002
.SD3     AIF   ('&MF' NE 'L').SD5           IS MF STANDARD OR EXECUTE   01816502
         AIF   ('&ASID' NE '').MESG5A       ASID INVALID W/MF=L         01820002
         AIF   ('&ECB' NE '').MESG5B        ECB INVALID W/MF=L          01830002
         AIF   ('&BRANCH' NE '').MESG5C     BRANCH INVALID W/MF=L       01840002
         AIF   ('&ASIDLST' NE '').MESG5D   ASID INV W/MF=L     @G33VPHD 01840343
         AIF   ('&SUMLIST' NE '').MESG5E   SUMLIST INV W/MF=L  @G33VPHD 01840643
         AGO   .SD8                 BYPASS STANDARD, EXEC FORM @G33VPHD 01841043
.SD5     AIF   ('&ASID' NE '').SD7          IS ASID SPECIFIED  @G33VPHD 01843043
         AIF   ('&ASIDLST' NE '').SD8      IS ASIDLIST SPECIFIED        01844043
.*                                                             @G33VPHD 01846043
         AIF   ('&BRANCH' EQ 'YES').MESG3A   BRANCH WITHOUT ASID OR     01846643
.*                                                     ASIDLIST INVALID 01848043
.*                                                             @G33VPHD 02250043
         AIF   ('&ECB' NE '').MESG3B        ECB WITHOUT ASID OR         02260043
.*                                                     ASIDLIST INVALID 02300043
.*                                                             @G33VPHD 02305043
.SD7     AIF   ('&ASIDLST' NE '').MESG10    ASID WITH ASSIDLIST INVALID 02310043
.*                                                             @G33VPHD 02315043
.SD8     AIF   ('&BUFFER' EQ '').SD9        IS BUFFER SPECIFIED         02320043
.*                                                             @G33VPHD 02325043
         AIF   ('&BUFFER' NE 'YES' AND '&BUFFER' NE 'NO').MESG4B        02330043
.*                                                             @G33VPHD 02335043
.SD9     AIF   ('&BRANCH' EQ '').SD11       IS BRANCH SPECIFIED         02350002
         AIF   ('&BRANCH' NE 'YES' AND '&BRANCH' NE 'NO').MESG4C        02400002
.SD11    AIF   ('&QUIESCE' EQ '').SD13      IS QUIESCE SPECIFIED        02450002
         AIF   ('&QUIESCE' NE 'YES' AND '&QUIESCE' NE 'NO').MESG4D      02500002
.SD13    AIF   ('&STORAGE' EQ '').SD15      IS STORAGE SPECIFIED        02550002
&CTR1    SETA  N'&STORAGE/2                 NUMBER OF ADDRESS PAIRS     02600002
&CTR2    SETA  (N'&STORAGE-1)/2             NUMBER OF PAIRS MINUS 1     02650002
         AIF   (&CTR1 EQ &CTR2).MESG6       ODD NUMBER OF ADDRESSES     02700002
.SD15    ANOP                                                           02750002
.*                                                                      02800002
.*       SDFLAG0 PROCESSING                                             02850002
.*                                                                      02900002
&SDDCB   SETB  ('&DCB' NE '')                IF DCB SPECIFIED           02950002
&SDBUFF  SETB  ('&BUFFER' EQ 'YES')          IF BUFFER TO BE DUMPED     03000002
&SDSTOR  SETB  ('&STORAGE' NE '' OR '&LIST' NE '')    STORAGE ADDR LIST 03050002
&SDHDR   SETB  ('&HDR' NE '' OR '&HDRAD' NE '')       USER DATA         03100002
&SDECB   SETB  ('&ECB' NE '')                IF ECB SPECIFIED           03150002
&SDASID  SETB  ('&ASID' NE '')               IF ASID SPECIFIED          03200002
&SDQUIET SETB  ('&QUIESCE' NE 'NO')          IF SYSTEM TO BE QUIESCED   03250002
&SDBRNCH SETB  ('&BRANCH' EQ 'YES')          IF LINKAGE BY BRANCH ENTRY 03300002
&SDFLAG0 SETA  128*&SDDCB+64*&SDBUFF+32*&SDSTOR+16*&SDHDR+8*&SDECB+4*&SX03350002
               DASID+2*&SDQUIET+1*&SDBRNCH                              03400002
.*                                                                      03450002
.*       SDFLAG1 PROCESSING                                             03500002
.*                                                                      03550002
&DUMPTYP SETB  1                  ALWAYS ON FOR SVC DUMP                03600002
&SDNEW   SETB  1                  ALWAYS SET TO 1 FOR NEW SDUMP MACRO   03620043
.*                                                             @G33VPHD 03650043
&SDASIDL SETB  ('&ASIDLST' NE '') IF ASIDLIST WAS SPECIFIED             03656043
.*                                                             @G33VPHD 03662043
&SDSUMLT SETB  ('&SUMLIST' NE '') IF SUMLIST WAS SPECIFIED              03668043
.*                                                             @G33VPHD 03674043
&SDFLAG1 SETA  128*&DUMPTYP+32*&SDNEW+16*&SDASIDL+8*&SDSUMLT            03680043
.*                                                             @G33VPHD 03686043
.*                                                                      03700002
.*       SDATA PROCESSING                                               03750002
.*                                                                      03800002
         AIF   ('&SDATA' EQ '').SD80        IS SDATA SPECIFIED          03850002
         AIF   ('&SDATA'(1,1) EQ '(').SD60  MORE THAN 1 OPTION, IN ()   03900002
&CTR1    SETA  0                                                        03950002
&SDALPSA SETB  ('&SDATA' EQ 'ALLPSA')       SET IF ALLPSA SPECIFIED     04000002
&SDPSA   SETB  ('&SDATA' EQ 'PSA')          SET IF PSA SPECIFIED        04050002
&SDNUC   SETB  ('&SDATA' EQ 'NUC')          SET IF NUC SPECIFIED        04100002
&SDSQA   SETB  ('&SDATA' EQ 'SQA')          SET IF SQA SPECIFIED        04150002
&SDLSQA  SETB  ('&SDATA' EQ 'LSQA')         SET IF LSQA SPECIFIED       04200002
&SDRGN   SETB  ('&SDATA' EQ 'RGN')          SET IF RGN SPECIFIED        04250002
&SDLPA   SETB  ('&SDATA' EQ 'LPA')          SET IF LPA SPECIFIED        04300002
&SDTRT   SETB  ('&SDATA' EQ 'TRT')          SET IF TRT SPECIFIED        04350002
&SDCSA   SETB  ('&SDATA' EQ 'CSA')          SET IF CSA SPECIFIED        04400002
&SDSWA   SETB  ('&SDATA' EQ 'SWA')          SET IF SWA SPECIFIED        04450002
&SDSMDMP SETB  ('&SDATA' EQ 'SUMDUMP' OR '&SDATA' EQ 'SUM')             04453043
.*                         SET IF SUMDUMP OR SUM ARE SPECIFIED @G33VPHD 04455043
&SDNSQA  SETB  ('&SDATA' EQ 'NOSQA')        SET IF NOSQA SPECIFIED      04459043
.*                                                             @G33VPHD 04462043
&SDNAPSA SETB  ('&SDATA' EQ 'NOALLPSA' OR '&SDATA' EQ 'NOALL')          04465043
.*                          SET IF NOALLPSA OR NOALL SPECIFIED @G33VPHD 04467043
&SDNSMDP SETB  ('&SDATA' EQ 'NOSUMDUMP' OR '&SDATA' EQ 'NOSUM')         04483043
.*          SET IF NOSUMDUMP OR NOSUM ARE SPECIFIED            @G33VPHD 04485043
&CTR1    SETA  &SDALPSA+&SDPSA+&SDNUC+&SDSQA+&SDLSQA+&SDRGN+&SDLPA+&SDTX04500002
               RT+&SDCSA+&SDSWA+&SDSMDMP+&SDNSMDP+&SDNSQA+&SDNAPSA      04520043
.*                                                             @G33VPHD 04550043
&CTR2    SETA  1                            IF INVALID, MESG PRINTS IT  04600002
         AIF   ('&CTR1' EQ '0').MESG4A      IS SPECIFIED OPTION VALID   04650002
         AGO   .SD80                        SDATA OPTION PROCESSED      04700002
.SD60    ANOP                     SDATA OPTIONS IN PARENTHESIS          04750002
&CTR1    SETA  N'&SDATA           NUMBER OF OPTIONS ON SDATA PARAMETER  04800002
&CTR2    SETA  0                  NUMBER BY POSITION OPERAND WORKING ON 04850002
.LOOPA1  ANOP                                                           04900002
&CTR2    SETA  &CTR2+1            NUMBER OF NEXT OPTION TO WORK ON      04950002
         AIF   ('&SDATA(&CTR2)' EQ 'ALLPSA').SETAPSA                    05000002
         AIF   ('&SDATA(&CTR2)' EQ 'PSA').SETPSA                        05050002
         AIF   ('&SDATA(&CTR2)' EQ 'NUC').SETNUC                        05100002
         AIF   ('&SDATA(&CTR2)' EQ 'SQA').SETSQA                        05150002
         AIF   ('&SDATA(&CTR2)' EQ 'LSQA').SETLSQA                      05200002
         AIF   ('&SDATA(&CTR2)' EQ 'RGN').SETRGN                        05250002
         AIF   ('&SDATA(&CTR2)' EQ 'LPA').SETLPA                        05300002
         AIF   ('&SDATA(&CTR2)' EQ 'TRT').SETTRT                        05350002
         AIF   ('&SDATA(&CTR2)' EQ 'CSA').SETCSA                        05400002
         AIF   ('&SDATA(&CTR2)' EQ 'SWA').SETSWA                        05450002
         AIF   ('&SDATA(&CTR2)' EQ 'SUMDUMP').SETSUM SUMDUMP SPEC       05452043
.*                                                             @G33VPHD 05454043
         AIF   ('&SDATA(&CTR2)' EQ 'SUM').SETSUM  SET IF SUM SPECIFIED  05456043
.*                                                             @G33VPHD 05458043
         AIF   ('&SDATA(&CTR2)' EQ 'NOSUMDUMP').SETNSUM SET IF          05460043
.*                                   NO SUMDUMP IS SPECIFIED   @G33VPHD 05461043
         AIF   ('&SDATA(&CTR2)' EQ 'NOSUM').SETNSUM  SET IF NOSUM IS    05466043
.*                                                   SPECIFIED          05468043
.*                                                             @G33VPHD 05470043
         AIF   ('&SDATA(&CTR2)' EQ 'NOSQA').SETNSQA  SET IF NOSQA SPEC  05472043
.*                                                             @G33VPHD 05474043
         AIF   ('&SDATA(&CTR2)' EQ 'NOALLPSA').SETNAPA                  05476043
.*                                                             @G33VPHD 05478043
         AIF   ('&SDATA(&CTR2)' EQ 'NOALL').SETNAPA                     05480043
.*                                                             @G33VPHD 05482043
         AGO   .MESG4A                 SDATA OPTION IS INVALID          05500002
.LOOPA2  AIF   (&CTR1 NE &CTR2).LOOPA1 MORE OPTIONS, CONTINUE LOOP      05650002
         AGO   .SD80                   ALL OPTIONS PROCESSED            05700002
.SETAPSA ANOP                          ALLPSA SPECIFIED                 05750002
         AIF   (&SDNAPSA EQ 1).MESG13  NOALLPSA AND ALLPSA IS ERROR     05760043
.*                                                             @G33VPHD 05770043
&SDALPSA SETB  1                       SET INDICATOR                    05800002
         AGO   .LOOPA2                 CONTINUE LOOP                    05850002
.SETPSA  ANOP                          PSA SPECIFIED                    05900002
&SDPSA   SETB  1                       SET INDICATOR                    05950002
         AGO   .LOOPA2                 CONTINUE LOOP                    06000002
.SETNUC  ANOP                          NUC SPECIFIED                    06050002
&SDNUC   SETB  1                       SET INDICATOR                    06100002
         AGO   .LOOPA2                 CONTINUE LOOP                    06150002
.SETSQA  ANOP                          SQA SPECIFIED                    06200002
         AIF   (&SDNSQA EQ 1).MESG12   SQA AND NOSQA IS ERROR           06210043
.*                                                             @G33VPHD 06220043
&SDSQA   SETB  1                       SET INDICATOR                    06250002
         AGO   .LOOPA2                 CONTINUE LOOP                    06300002
.SETLSQA ANOP                          LSQA SPECIFIED                   06350002
&SDLSQA  SETB  1                       SET INDICATOR                    06400002
         AGO   .LOOPA2                 CONTINUE LOOP                    06450002
.SETRGN  ANOP                          RGN SPECIFIED                    06500002
&SDRGN   SETB  1                       SET INDICATOR                    06550002
         AGO   .LOOPA2                 CONTINUE LOOP                    06600002
.SETLPA  ANOP                          LPA SPECIFIED                    06650002
&SDLPA   SETB  1                       SET INDICATOR                    06700002
         AGO   .LOOPA2                 CONTINUE LOOP                    06750002
.SETTRT  ANOP                          TRT SPECIFIED                    06800002
&SDTRT   SETB  1                       SET INDICATOR                    06850002
         AGO   .LOOPA2                 CONTINUE LOOP                    06900002
.SETCSA  ANOP                          CSA SPECIFIED                    06950002
&SDCSA   SETB  1                       SET INDICATOR                    07000002
         AGO   .LOOPA2                 CONTINUE LOOP                    07050002
.SETSWA  ANOP                          SWA SPECIFIED                    07100002
&SDSWA   SETB  1                       SET INDICATOR                    07150002
         AGO  .LOOPA2                  CONTINUE LOOP                    07151043
.SETSUM  ANOP                          SUMDUMP OR SUM SPECIFIED         07152043
.*                                                             @G33VPHD 07154043
         AIF   (&SDNSMDP EQ 1).MESG11  NOSUMDUMP AND SUMDUMP IS ERROR   07156043
.*                                                             @G33VPHD 07158043
&SDSMDMP SETB  1                       SET SUMDUMP INDICATOR            07160043
.*                                                             @G33VPHD 07162043
         AGO   .LOOPA2                 CONTINUE LOOP                    07164043
.*                                                             @G33VPHD 07166043
.SETNSUM ANOP                          NOSUMDUMP OR NOSUM SPECIFIED     07168043
.*                                                             @G33VPHD 07170043
         AIF   (&SDSMDMP EQ 1).MESG11  NOSUMDUMP AND SUMDUMP IS ERROR   07172043
.*                                                             @G33VPHD 07174043
&SDNSMDP SETB  1                       SET NOSUMDUMP INDICATOR          07176043
.*                                                             @G33VPHD 07178043
         AGO   .LOOPA2                 CONTINUE LOOP                    07180043
.*                                                             @G33VPHD 07182043
.SETNSQA ANOP                          NOSQA SPECIFIED                  07184043
.*                                                             @G33VPHD 07186043
         AIF   (&SDSQA EQ 1).MESG12    SQA AND NOSQA IS AN ERROR        07188043
.*                                                             @G33VPHD 07190043
&SDNSQA  SETB  1                       SET NOSQA INDICATOR              07192043
.*                                                             @G33VPHD 07194043
         AGO   .LOOPA2                 CONTINUE LOOP                    07196043
.*                                                             @G33VPHD 07198043
.SETNAPA ANOP                          NOALLPSA OR NOALL SPECIFIED      07200043
.*                                                             @G33VPHD 07205043
         AIF   (&SDALPSA EQ 1).MESG13  NOALLPSA AND ALLPSA IS ERROR     07210043
.*                                                             @G33VPHD 07215043
&SDNAPSA SETB  1                       SET NOALLPSA INDICATOR           07220043
.*                                                             @G33VPHD 07225043
         AGO   .LOOPA2                 CONTINUE LOOP                    07230043
.*                                                             @G33VPHD 07235043
         AGO   .LOOPA2                 CONTINUE LOOP                    07240043
.SD80    ANOP         ALL SDATA OPTIONS PROCESSED,BUILD SDATA  @G33VPHD 07286043
&SDATAF1 SETA  128*&SDALPSA+64*&SDPSA+32*&SDNUC+16*&SDSQA+8*&SDLSQA+4*&X07300002
               SDRGN+2*&SDLPA+1*&SDTRT                                  07350002
&SDATAF2 SETA  128*&SDCSA+64*&SDSWA+32*&SDSMDMP+16*&SDNSMDP+8*&SDNAPSA+X07370043
               4*&SDNSQA                                                07400043
.*                                                             @G33VPHD 07430043
.*                                                                      07500002
.*       WHICH FORM OF MACRO                                            07550002
.*                                                                      07600002
         AIF   ('&MF' EQ '').STFORM          IS IT STANDARD FORM        07650002
         AIF   ('&MF' EQ 'L').LSTFORM        IS IT LIST FORM            07700002
         AIF   ('&MF'(1,3) NE '(E,').MESG7   ELSE, IF NOT EXECUTE, ERR  07750002
.*                                                                      07800002
.*       EXECUTE FORM OF MACRO                                          07850002
.*                                                                      07900002
         AIF   ('&NAME' EQ '').SDEX2        IS NAME SPECIFIED           07950002
&NAME    DS    0H                                                       08000002
.SDEX2   ANOP                                                           08050002
         IHBSETR   &MF(2),1                 LOAD REG 1 WITH LIST ADDR   08100002
         AIF   ('&QUIESCE' NE '').SDEX3     IS QUIESCE SPECIFIED        08110002
&SDFLAG0 SETA  &SDFLAG0-2                   NO, LEAVE AS SPECIFIED ON L 08120002
.SDEX3   AIF   ('&SDFLAG0' EQ '0').SDEX3A   ANY OPTIONS TO TURN ON      08130002
         OI    0(1),&SDFLAG0                SET OPTION FLAGS            08150002
.SDEX3A  AIF   ('&BUFFER' EQ 'NO').SDEX4    BUFFER NOT REQUESTED        08160002
         AGO   .SDEX6                       GO CHECK FOR QUIESCE        08170002
.SDEX4   ANOP                                                           08180002
         NI    0(1),X'BF'                   TURN OFF BUFFER FLAG        08190002
.SDEX6   AIF   ('&QUIESCE' EQ 'NO').SDEX8   QUIESCE NOT REQUESTED       08192002
         AGO   .SDEX10                      GO SET SVC DUMP FLAG        08194002
.SDEX8   ANOP                                                           08196002
         NI    0(1),X'FD'                   TURN OFF QUIESCE FLAG       08198002
.SDEX10  ANOP                                                           08198402
         AIF   ('&SUMLIST' EQ '' AND '&ASIDLST' EQ '').SDEX11  @G33VPHD 08208443
         OI    1(1),&SDFLAG1                 SET OPTION BITS            08218443
.SDEX11  ANOP                                                  @G33VPHD 08228443
         AIF   ('&SDATA' EQ '').SDEX12      IS SDATA OPTIONS SPECIFIED  08250002
         MVI   2(1),&SDATAF1                SET OPTION FLAGS            08300002
         MVI   3(1),&SDATAF2                SET OPTION FLAGS            08350002
.SDEX12  AIF   ('&DCB' EQ '').SDEX15        IS DCB SPECIFIED            08400002
         IHBSETR   &DCB,0                   LOAD REG 0 WITH DCB ADDR    08450002
         ST    0,4(1,0)               ADDR OF DCB INTO PARAM LISTYM1995 08500002
.SDEX15  ANOP                                                           08550002
         AIF   ('&LIST' EQ '').SDEX20       IS LIST SPECIFIED           08600002
         IHBSETR   &LIST,0                  LOAD REG 0 WITH LIST ADDR   08650002
         ST    0,8(1,0)               ADDR OF LIST INTO PARAM LSTYM1995 08700002
         AGO   .SDEX25                      GO CHECK FOR USER DATA      08750002
.SDEX20  AIF   ('&STORAGE' EQ '').SDEX25    IS STORAGE SPECIFIED        08800002
         L     15,8(1,0)    ADDR OF STORAGE LIST FROM PARAM LIST YM1995 08850002
&DISP    SETA  -4                 DISPLACEMENT OF ADDR IN STORAGE LIST  08900002
&CTR1    SETA  N'&STORAGE         NUMBER OF ADDRESSES SPECIFIED         08950002
&CTR2    SETA  0                  POSITION NUMBER OF ADDR WORKING ON    09000002
.LOOPC1  ANOP                                                           09050002
&DISP    SETA  &DISP+4                 DISP OF NEXT ADDR IN STORAGE LST 09100002
&CTR2    SETA  &CTR2+1                 POSITION NUMBER OF NEXT ADDRESS  09150002
         IHBSETR   &STORAGE(&CTR2),0   PUT STORAGE ADDRESS IN REG 0     09200002
         ST    0,&DISP.(0,15)          STORAGE ADDR INTO STORAGE LIST   09250002
&CTR1    SETA  &CTR1-1                 NUMBER OF REMAINING ADDRESSES    09300002
         AIF   (&CTR1 NE 0).LOOPC1   ANY MORE ADDRESSES                 09350002
         OI    &DISP.(15),X'80'      SET END OF LIST INDICATOR          09400002
.SDEX25  AIF   ('&HDRAD' EQ '').SDEX30      IS HDRAD SPECIFIED          09450002
         IHBSETR   &HDRAD,0                 LOAD REG0 WITH HDR RCD ADDR 09500002
         ST    0,12(1,0)              ADDR OF USER DATA INTO LISTYM1995 09550002
         AGO   .SDEX35                      GO CHECK FOR ECB            09600002
.SDEX30  AIF   ('&HDR' EQ '').SDEX35        IS HDR SPECIFIED            09650002
&GNAME   SETC  'IHB'.'&SYSNDX'              GET A UNIQUE SYMBOL         09700002
         BAL   15,&GNAME                    BRANCH AROUND USER DATA     09750002
&CTR1    SETA  K'&HDR-2-&HDRCTR             LENGTH OF USER DATA         09800002
         DC    AL1(&CTR1)                   LENGTH OF USER DATA         09850002
         DC    C&HDR                        USER DATA                   09860002
         CNOP  2,4                                                      09900002
&GNAME   DS    0H                                                       09950002
         ST    15,12(1,0)             ADDR OF USER DATA INTO LISTYM1995 10000002
.SDEX35  AIF   ('&ECB' EQ '').SDEX40        IS ECB SPECIFIED            10050002
         AIF   ('&ECB'(1,1) EQ '(').SDEX37  IS ADDR OF ECB IN A REG     10100002
         L     15,&ECB                      ADDRESS OF ECB              10150002
         ST    15,16(1,0)             ADDR OF ECB INTO PARAM LISTYM1995 10200002
         AGO   .SDEX40                      GO CHECK FOR ASID           10250002
.SDEX37  ANOP                                                           10300002
         ST    &ECB,16(1,0)           ADDR OF ECB INTO PARAM LISTYM1995 10350002
.SDEX40  AIF   ('&ASID' EQ '').SDEX45       IS ASID SPECIFIED           10400002
         AIF   ('&ASID'(1,1) EQ '(').SDEX42 IS ASID IN A REGISTER       10450002
         LH    0,&ASID                      ASID OF MEMORY TO BE DUMPED 10500002
         STH   0,22(1,0)              ASID INTO PARAMETER LIST   YM1995 10550002
         AGO   .SDEX47                      GO CHECK FOR SUMLIST        10570043
.*                                                             @G33VPHD 10600043
.SDEX42  ANOP                                                           10650002
         STH   &ASID,22(1,0)          ASID INTO PARAMETER LIST   YM1995 10700002
         AGO   .SDEX47                      GO CHECK FOR SUMLIST        10703043
.*                                                             @ZA39011 10706043
.SDEX45  AIF   ('&ASIDLST' EQ '').SDEX47 IF ASIDLIST SPECIFIED          10709043
.*                                                             @G33VPHD 10712043
         IHBSETR   &ASIDLST,0           LOAD REG 0 WITH ASIDLIST ADDR   10715043
.*                                                             @G33VPHD 10718043
         ST    0,24(1)                  ADDR ASIDLIST INTO PARM LIST    10721043
.*                                                             @G33VPHD 10724043
.SDEX47  AIF   ('&SUMLIST' EQ '').SDEX49 IF SUMLIST IS SPECIFIED        10727043
.*                                                             @G33VPHD 10730043
         AIF   (&SDNSMDP EQ 1).MESG14 SUMLIST WITH NOSUMDUMP ERROR      10731043
.*                                                             @ZA39011 10732043
         IHBSETR   &SUMLIST,0           LOAD REG 0 WITH SUMLIST ADDR    10733043
.*                                                             @G33VPHD 10736043
         ST    0,28(1)                  ADDR SUMLIST INTO PARM LIST     10739043
.*                                                             @G33VPHD 10742043
.SDEX49  AIF   ('&BRANCH' EQ 'YES').SDEX50  IS BRANCH ENTRY REQUESTED   10745043
         SVC   51                           INVOKE SVC DUMP VIA SVC     10800002
         AGO   .SDEND                       GO END MACRO PROCESSING     10850002
.SDEX50  ANOP                                                           10900002
         L     15,CVTPTR(0,0)               ADDRESS OF CVT       YM1995 10950002
         L     15,CVTSDMP-CVTMAP(15,0)  ADDRESS OF SVC DUMP ENTRYYM1995 11000002
         BALR  14,15                        INVOKE SVC DUMP VIA BAL     11050002
         AGO   .SDEND                       GO END MACRO PROCESSING     11100002
.*                                                                      11150002
.*       LIST FORM OF MACRO                                             11200002
.*                                                                      11250002
.LSTFORM ANOP                                                           11260002
&NAME    DS    0F                           SDUMP PARAMETER LIST        11300002
         DC    AL1(&SDFLAG0)                FLAG BYTE                   11350002
         DC    AL1(&SDFLAG1)                FLAG BYTE                   11400002
         DC    AL1(&SDATAF1)                FLAG BYTE                   11450002
         DC    AL1(&SDATAF2)                FLAG BYTE                   11500002
         AIF   ('&DCB' EQ '').SDLST10       IS DCB SPECIFIED            11550002
         AIF   ('&DCB'(1,1) EQ '(').MESG1   IS DCB ADDRESS IN A REG     11600002
         DC    A(&DCB)                      ADDRESS OF DCB              11650002
         AGO   .SDLST15                     GO CHECK FOR STORAGE ADDR   11700002
.SDLST10 ANOP                                                           11750002
         DC    A(0)                         ADDRESS OF DCB              11800002
.SDLST15 AIF   ('&SDSTOR' EQ '0').SDLST24   IS STORAGE OR LIST SPECIFED 11850002
         AIF   ('&LIST' EQ '').SDLST20      IS LIST SPECIFIED           11900002
         AIF   ('&LIST'(1,1) EQ '(').MESG1  IS LIST ADDRESS IN A REG    11950002
         DC    A(&LIST)                     ADDRESS OF STORAGE LIST     12000002
         AGO   .SDLST25                     GO CHECK FOR USER DATA      12050002
.SDLST20 ANOP                                                           12100002
         DC    A(*+32)                 ADDRESS OF STORAGE LIST @G33VPHD 12120043
         AGO   .SDLST25                     GO CHECK FOR USER DATA      12200002
.SDLST24 ANOP                                                           12250002
         DC    A(0)                         ADDRESS OF STORAGE LIST     12300002
.SDLST25 ANOP                                                           12350002
         AIF   ('&SDHDR' EQ '0').SDLST38    IS USER DATA SPECIFIED      12400002
         AIF   ('&HDRAD' NE '').SDLST35     IS HDRAD SPECIFIED          12450002
&CTR1    SETA  0                            PROCESS FOR HDR             12500002
         AIF   ('&STORAGE' EQ '').SDLST28   IS STORAGE SPECIFIED        12550002
&CTR1    SETA  N'&STORAGE                   NUMBER OF STORAGE ADDRESSES 12600002
&CTR1    SETA  &CTR1*4                      LENGTH OF STORAGE ADDR LIST 12650002
.SDLST28 ANOP                                                           12700002
&DISP    SETA  28+&CTR1                     DISPL TO USER DATA @G33VPHD 12720043
         DC    A(*+&DISP)                   ADDRESS OF USER DATA        12800002
         AGO   .SDLST40                     GO DEFINE REST OF PARAM LST 12850002
.SDLST35 AIF   ('&HDRAD'(1,1) EQ '(').MESG1 IS ADDR OF USER DATA IN REG 12900002
         DC    A(&HDRAD)                    ADDRESS OF USER DATA        12950002
         AGO   .SDLST40                     GO DEFINE REST OF PARAM LST 13000002
.SDLST38 ANOP                                                           13050002
         DC    A(0)                         ADDRESS OF USER DATA        13100002
.SDLST40 ANOP                                                           13150002
         DC    A(0)                         ADDRESS OF ECB              13200002
         DC    AL2(0)                       CURRENT ASID                13250002
         DC    AL2(0)                       OTHER ASID                  13300002
         DC    A(0)                         ADDRESS OF ASID LIST        13322043
         DC    A(0)                         ADDRESS OF SUMLIST          13346043
.*                                                             @G33VPHD 13350043
.SDLST50 ANOP                                                           13355043
         DC    A(0)                      RESERVED              @G33VPHD 13360043
         DC    A(0)                      RESERVED              @G33VPHD 13368043
         AIF   ('&STORAGE' EQ '').SDLST60   BUILD STORAGE LIST          13385043
.*                                                             @G33VPHD 13390043
&CTR1    SETA  N'&STORAGE                   NUMBER OF STORAGE ADDRESSES 13400002
&CTR2    SETA   0                           POSITION NO. OF ADDRESS     13450002
.LOOPD1  ANOP                                                           13500002
&CTR2    SETA  &CTR2+1                      NUMBER OF NEXT ADDRESS      13550002
         AIF   ('&STORAGE(&CTR2)'(1,1) EQ '(').MESG1  ADDR IN A REG     13600002
         DC    A(&STORAGE(&CTR2))           STORAGE ADDRESS             13650002
&CTR1    SETA  &CTR1-1                      NUMBER OF REMAINING ADDR.S  13700002
         AIF   ('&CTR1' GT '1').LOOPD1      IF NOT LAST ADDRESS, LOOP   13750002
&CTR2    SETA  &CTR2+1                      NUMBER OF LAST ADDRESS      13800002
         AIF   ('&STORAGE(&CTR2)'(1,1) EQ '(').MESG1  ADDR IN A REG     13850002
         DC    AL4(X'80000000'+&STORAGE(&CTR2))  LAST ADDRESS           13900002
.SDLST60 ANOP                                                           13950043
.*                                                             @G33VPHD 14000043
         AIF   ('&HDR' EQ '').SDEND         IS USER DATA SPECIFIED      14050002
&CTR1    SETA  K'&HDR-2-&HDRCTR             LENGTH OF USER DATA         14100002
         DC    AL1(&CTR1)                   LENGTH OF USER DATA         14150002
         DC    C&HDR                        USER DATA                   14160002
         AGO   .SDEND                       GO END MACRO PROCESSING     14200002
.*                                                                      14250002
.*       STANDARD FORM                                                  14300002
.*                                                                      14350002
.STFORM  ANOP                                                           14400002
&GNAME   SETC  'IHB'.'&SYSNDX'              OBTAIN UNIQUE SYMBOL-LABEL  14450002
         CNOP  0,4                                                      14500002
         AIF   ('&NAME' EQ '').SDSTF10      IS NAME SPECIFIED           14550002
&NAME    DS    0F                                                       14600002
.SDSTF10 ANOP                                                           14650002
         BAL   1,&GNAME                     BRANCH AROUND PARAM LIST    14700002
         DC    AL1(&SDFLAG0)                FLAG BYTE                   14750002
         DC    AL1(&SDFLAG1)                FLAG BYTE                   14800002
         DC    AL1(&SDATAF1)                FLAG BYTE                   14850002
         DC    AL1(&SDATAF2)                FLAG BYTE                   14900002
         DC    A(0)                         ADDRESS OF DCB              14950002
         DC    A(0)                         ADDRESS OF STORAGE LIST     15000002
         DC    A(0)                         ADDRESS OF HEADER RECORD    15050002
         DC    A(0)                         ADDRESS OF ECB              15100002
         DC    AL2(0)                       CURRENT ASID                15150002
         DC    AL2(0)                       OTHER ASID                  15200002
         DC    A(0)                         ADDRESS OF ASID LIST        15205043
.*                                                             @G33VPHD 15210043
         DC    A(0)                         ADDRESS OF SUMLIST          15215043
         DC    A(0)                         RESERVED           @G33VPHD 15225043
         DC    A(0)                         RESERVED           @G33VPHD 15235043
         AIF   ('&STORAGE' EQ '').SDSTF20   IS STORAGE SPECIFIED        15250002
&CTR1    SETA  N'&STORAGE                   NUMBER OF STORAGE ADDRESSES 15300002
         DC    &CTR1.F'0'                   STORAGE ADDRESSES           15350002
.SDSTF20 ANOP                                                           15400002
         AIF   ('&HDR' EQ '').SDSTF30       IS HDR SPECIFIED            15450002
&CTR1    SETA  K'&HDR-2-&HDRCTR             LENGTH OF USER DATA         15500002
         DC    AL1(&CTR1)                   LENGTH OF USER DATA         15550002
         DC    C&HDR                        USER DATA                   15560002
         CNOP  2,4                                                      15600002
.SDSTF30 ANOP                                                           15650002
&GNAME   DS    0H                                                       15700002
         AIF   ('&DCB' EQ '').SDSTF35       IS DCB SPECIFIED            15750002
         IHBSETR   &DCB,0                   ADDRESS OF DCB IN REG 0     15800002
         ST    0,4(1,0)               ADDR OF DCB INTO PARAM LISTYM1995 15850002
.SDSTF35 AIF   ('&LIST' EQ '').SDSTF40      IS LIST SPECIFIED           15900002
         IHBSETR   &LIST,0                  ADDR OF STORAGE LIST IN R0  15950002
         ST    0,8(1,0)               ADDR OF LIST INTO PARAM LSTYM1995 16000002
         AGO   .SDSTF50                     GO CHECK FOR USER DATA      16050002
.SDSTF40 AIF   ('&STORAGE' EQ '').SDSTF50   STORAGE ADDRESSES SPECIFIED 16100002
         LA    15,40(1,0)             ADDR OF STORAGE LIST      G33VPHD 16120043
         ST    15,8(1,0)              INTO PARAM LIST            YM1995 16200002
&CTR1    SETA  N'&STORAGE          NUMBER OF STORAGE ADDRESSES          16250002
&CTR2    SETA  0                   NUMBER ADDRESSES BY POSITION         16300002
&DISP    SETA  36               DISPLACEMENT INTO STORAGE LIST  G33VPHD 16320043
.LOOPE1  ANOP                                                           16400002
&CTR2    SETA  &CTR2+1                 NUMBER OF ADDRESS WORKING ON     16450002
&DISP    SETA  &DISP+4                 DISP OF ADDRESS INTO LIST        16500002
         IHBSETR   &STORAGE(&CTR2),0   PUT ADDRESS INTO REGISTER 0      16550002
         ST    0,&DISP.(1,0)     STORAGE ADDRESS INTO LIST       YM1995 16600002
         AIF   ('&CTR1' NE '&CTR2').LOOPE1  ANY  MORE ADDRESSES         16650002
         OI    &DISP.(1),X'80'              END OF LIST INDICATOR       16700002
.SDSTF50 AIF   ('&HDRAD' EQ '').SDSTF60     IS HDRAD SPECIFIED          16750002
         IHBSETR   &HDRAD,0                 ADDR OF USER DATA IN REG 0  16800002
         ST    0,12(1,0)         ADDR OF USER DATA INTO PARAM LSTYM1995 16850002
         AGO   .SDSTF70                     GO CHECK FOR ECB            16900002
.SDSTF60 AIF   ('&HDR' EQ '').SDSTF70       IS HDR SPECIFIED            16950002
&CTR1    SETA  0                                                        17000002
         AIF   ('&STORAGE' EQ '').SDSTF62   IS STORAGE SPECIFIED        17050002
&CTR1    SETA  N'&STORAGE                   NUMBER OF STORAGE ADDRESSES 17100002
&CTR1    SETA  &CTR1*4                      LENGTH OF STORAGE LIST      17150002
.SDSTF62 ANOP                                                           17200002
&DISP    SETA  40+&CTR1             DISPLACEMENT TO USER DATA  @G33VPHD 17220043
         LA    0,&DISP.(1,0)          ADDRESS OF USER DATA       YM1995 17300002
         ST    0,12(1,0)              INTO PARAMETER LIST        YM1995 17350002
.SDSTF70 AIF   ('&ECB' EQ '').SDSTF80       IS ECB SPECIFIED            17400002
         AIF   ('&ECB'(1,1) EQ '(').SDSTF72 IS ECB ADDRESS IN A REG     17450002
         L     0,&ECB                       ADDRESS OF ECB              17500002
         ST    0,16(1,0)              ADDR OF ECB INTO PARAM LISTYM1995 17550002
         AGO   .SDSTF80                     GO CHECK FOR ASID           17600002
.SDSTF72 ANOP                                                           17650002
         ST    &ECB,16(1,0)           ADDR OF ECB INTO PARAM LISTYM1995 17700002
.SDSTF80 AIF   ('&ASID' EQ '').SDSTF90      IS ASID SPECIFIED           17750002
         AIF   ('&ASID'(1,1) EQ '(').SDSTF82     IS ASID IN A REGISTER  17800002
         LH    0,&ASID                      ASID OF MEMORY TO BE DUMPED 17850002
         STH   0,22(1,0)              ASID INTO PARAMETER LIST   YM1995 17900002
         AGO   .SDSTF90                     GO CHECK FOR BRANCH ENTRY   17950002
.SDSTF82 ANOP                                                           18000002
         STH   &ASID,22(1,0)          ASID INTO PARAMETER LIST   YM1995 18050002
.SDSTF90 AIF   ('&ASIDLST' EQ '').SDSTF93  IS ASIDLIST SPECIFIED        18053043
.*                                                             @G33VPHD 18056043
         IHBSETR   &ASIDLST,0           ADDR OF ASIDLIST IN R0          18059043
.*                                                             @G33VPHD 18062043
         ST    0,24(1)                  ADDR ASIDLIST INTO PARM LIST    18065043
.*                                                             @G33VPHD 18068043
.SDSTF93 AIF   ('&SUMLIST' EQ '').SDSTF98 IS SUMLIST SPECIFIED          18071043
.*                                                             @G33VPHD 18074043
         AIF   (&SDNSMDP EQ 1).MESG14   SUMLIST WITH  NOSUMDUMP ERROR   18077043
.*                                                             @ZA39011 18080043
         IHBSETR   &SUMLIST,0                                           18083043
.*                                                             @G33VPHD 18086043
         ST    0,28(1)                                                  18089043
.*                                                             @G33VPHD 18092043
.SDSTF98 AIF   ('&BRANCH' EQ 'YES').SDSTF99  IS BRANCH ENTRY REQUESTED  18095043
.*                                                             @G33VPHD 18100043
         SVC   51                           INVOKE SVC DUMP VIA SVC     18150002
         AGO   .SDEND                       GO END MACRO PROCESSING     18200002
.SDSTF99 ANOP                                                           18220043
.*                                                             @G33VPHD 18250043
         L     15,CVTPTR(0,0)               ADDRESS OF CVT       YM1995 18300002
         L     15,CVTSDMP-CVTMAP(15,0)  ADDRESS OF SVC DUMP ENTRYYM1995 18350002
         BALR  14,15                        INVOKE SVC DUMP VIA BAL     18400002
         AGO   .SDEND                       GO END MACRO PROCESSING     18450002
.*                                                                      18500002
.*       MESSAGES                                                       18550002
.*                                                                      18600002
.MESG1   MNOTE 12,'REGISTER NOTATION INVALID W/MF=L'                    18650002
         MEXIT                                                          18700002
.MESG2A  MNOTE 12,'HDR AND HDRAD MUTUALLY EXCLUSIVE'                    18750002
         MEXIT                                                          18800002
.MESG2B  MNOTE 12,'STORAGE AND LIST MUTUALLY EXCLUSIVE'                 18850002
         MEXIT                                                          18900002
.MESG3A  MNOTE 12,'MISSING PARAMETERS: ASID OR ASIDLST  REQUIRED WITH BX18920043
               RANCH=YES'                                     @G33VPHD  18950043
         MEXIT                                                          19000002
.MESG3B  MNOTE 12,'MISSING PARAMETERS ASID OR ASIDLST  REQUIRED WITH ECX19020043
               B'                                              @G33VPHD 19050043
         MEXIT                                                          19100002
.MESG4A  MNOTE 12,'INVALID SDATA OPERAND SPECIFIED &SDATA(&CTR2)'       19150002
         MEXIT                                                          19200002
.MESG4B  MNOTE 12,'INVALID BUFFER OPERAND SPECIFIED &BUFFER'            19250002
         MEXIT                                                          19300002
.MESG4C  MNOTE 12,'INVALID BRANCH OPERAND SPECIFIED &BRANCH'            19350002
         MEXIT                                                          19400002
.MESG4D  MNOTE 12,'INVALID QUIESCE OPERAND SPECIFIED &QUIESCE'          19450002
         MEXIT                                                          19500002
.MESG5A  MNOTE 12,'ASID OPERAND INVALID W/MF=L'                         19550002
         MEXIT                                                          19600002
.MESG5B  MNOTE 12,'ECB OPERAND INVALID W/MF=L'                          19650002
         MEXIT                                                          19700002
.MESG5C  MNOTE 12,'BRANCH OPERAND INVALID W/MF=L'                       19750002
         MEXIT                                                          19800002
.MESG5D  MNOTE 12,'ASIDLST OPERAND INVALID W/MF=L'                      19810043
         MEXIT                                                          19820043
.MESG5E  MNOTE 12,'SUMLIST OPERAND INVALID W/MF=L'                      19830043
         MEXIT                                                          19840043
.MESG6   MNOTE 12,'ODD NUMBER OF STORAGE ADDRESSES'                     19850002
         MEXIT                                                          19900002
.MESG7   MNOTE 12,'MACRO FORM IS INVALID'                               19950002
         MEXIT                                                          19952002
.MESG8   MNOTE 12,'USER DATA TOO LONG'                                  19960002
         MEXIT                                                          19970002
.MESG9   MNOTE 12,'INITIAL CHARACTER OF HDR OPERAND NOT AN APOSTROPHE'  19980002
         MEXIT                                                          19980943
.*                                                             @G33VPHD 19981843
.MESG10  MNOTE 12,'MUTUALLY EXCLUSIVE OPERANDS: ASID AND ASIDLST'       19982743
.*                                                             @G33VPHD 19983643
         MEXIT                                                          19984543
.*                                                             @G33VPHD 19985443
.MESG11  MNOTE 12,'MUTUALLY EXCLUSIVE OPERANDS: SUMDUMP/SUM AND NOSUMDUX19986343
               MP/NOSUM'                                                19987243
.*                                                             @G33VPHD 19988143
         MEXIT                                                          19989043
.*                                                             @G33VPHD 19989943
.MESG12  MNOTE 12,'MUTUALLY EXCLUSIVE OPERANDS:SQA AND NOSQA'           19990843
.*                                                             @G33VPHD 19991743
         MEXIT                                                          19992643
.*                                                             @G33VPHD 19993543
.MESG13  MNOTE 12,'MUTUALLY EXCLUSIVE OPERANDS: ALLPSA AND NOALLPSA'    19994443
.*                                                             @G33VPHD 19995343
         MEXIT                                                          19996243
.*                                                             @G33VPHD 19997143
.MESG14  MNOTE 12,'MUTUALLY EXCLUSIVE OPERANDS: SUMLIST AND NOSUMDUMP/NX19998043
               OSUM'                                                    20048043
.*                                                             @ZA39011 20098043
.SDEND   MEXIT                                                          20148043
         MEND                                                           20198043
