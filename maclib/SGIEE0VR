         MACRO                                                          00400019
         SGIEE0VR  &XXX                                          Y01012 00700002
         TITLE 'IEE3203D, VARY KEYWORD SCAN ROUTINE'              M4957 01000019
IEE3203D CSECT                                                          01200019
*********************************************************************** 10400019
*                                                                       10450002
* MODULE NAME       = IEE3203D (SGIEE0VR)                               10500002
*    CSECT NAME    = IEE3203D                                           10502002
*                                                                       10510002
* LOAD MODULE NAME  = IGC0003D                                          10512002
*                                                                       10514002
* DESCRIPTIVE NAME  = VARY KEYWORD SCAN MODULE                          10518402
*                                                                       10518802
* COPYRIGHT         = MISSING                                           10519702
*                                                                       10519802
* STATUS            = OS/VS/2 RELEASE 2                                 10519902
*                                                                       10539902
* FUNCTION          = THIS MODULE SCANS THE VARY PRIMARY KEYWORDS       10566602
*                     NET,CH,CPU,STOR,PATH AND GIVES CONTROL TO THE     10568602
*                     ATCAM PROCESSOR FOR NET OR IEE0803D FOR THE       10570602
*                     OTHER KEYWORDS.  IF THERE IS NO MATCH ON THE      10572602
*                     PRIMARY KEYWORDS THEN THIS ROUTINE SCANS FOR THE  10573002
*                     SECONDARY KEYWORDS IN THE COMMAND FIELD AND THEN  10573102
*                     COMPARES THIS KEYWORD TO EACH ENTRY IN A TABLE    10573202
*                     OF VALID VARY KEYWORDS.  A SECONDARY KEYWORD IS   10589902
*                     VALID ACCORDING TO THE OPTIONS SPECIFIED AT SYSGS 10599902
*                     TIME. FOR THIS SYSTEM, THESE KEYWORDS ARE VALID-  10601902
*                        ONLINE     VALID FOR ALL SYSTEM                10603902
*                        OFFLINE    VALID FOR ALL SYSTEMS               10605902
*                        CONSOLE    VALID FOR ALL SYSTEMS               10606302
*                        HARDCPY    VALID FOR ALL SYSTEMS               10606402
*                        MSTCONS    VALID FOR ALL SYSTEMS               10606502
*                        ONTP/OFFTP VALID FOR TCAM IN ALL SYSTEMS       10614102
*                                                                       10616902
* OPERATION         = IEE3203D FIRST SCANS THE VARY COMMAND OPERAND FOR 10617302
*                     THE PRIMARY KEYWORD WHICH IS DEFINED AS THE WORD  10617402
*                     FOLLOWING THE VERB 'VARY'.                        10617502
*                     THE PRIMARY KEYWORDS THAT ARE SUPPORTED ARE:      10617602
*                        V NET   FOR ATCAM                              10623202
*                        V PATH  FOR MP                                 10625202
*                        V CH    FOR MP                                 10627202
*                        V CPU   FOR MP                                 10627602
*                        V STOR  FOR MP                                 10628002
*                     IF NET IS FOUND THEN THIS ROUTINE WILL CONTROL    10628402
*                     TO THE ATCAM PROCESSOR TO SERVICE THE COMMAND.    10628502
*                     IF THE OTHERS ARE FOUND, THEN THIS ROUTINE WILL   10628602
*                     CHANGE THE VERB CODE IN THE XAN FOR THEM AS       10628702
*                     FOLLOWS:                                          10652902
*                        V PATH = X'50'                                 10654902
*                        V CH   = X'58'                                 10656902
*                        V CPU  = X'54'                                 10657302
*                        V STOR = X'5C'                                 10662002
*                     AND CONTROL IS PASSED TO IEE0803D TO CREATE A     10664002
*                     CSCB FOR THEM SO THAT THEY CAN EVENTUALLY BE      10666002
*                     ATTACHED.                                         10666402
*                     IF NONE OF THE ABOVE, THEN THIS ROUTINE SCANS FOR 10666802
*                     THE SECONDARY KEYWORD WHICH IS DEFINED AS THE     10671502
*                     WORD FOLLOWING THE FIRST COMMA NOT EMBEDDED IN    10673502
*                     PARENS.  R9 IS USED TO INDEX THROUGH THE OPERAND  10675502
*                     TO FIND THE ',KEYWORD'. COMMANDS OF THE FOLLOWING 10675902
*                     FORMS ARE ACCEPTABLE:                             10676302
*                        V UNIT,KEYWORD                                 10681002
*                        V UNIT(MULTIPLE UNITS),KEYWORD                 10683002
*                        V (MULTIPLE UNITS),KEYWORD                     10685002
*                        V (MULTIPLE UNITS)UNIT,KEYWORD                 10685402
*                     (ANY NUMBER OF BALANCED INNER/OUTER PARENS MAY BE 10685802
*                     SPECIFIED)                                        10698102
*                     WHEN THE COMMA PRECEDING THE KEYWORD IS FOUND, A  10708102
*                     POINTER TO THE START OF THE KEYWORD IS SAVED AND  10710102
*                     THE SCAN CONTINUES UNTIL A COMMA, BLANK OR THE    10710502
*                     END OF BUFFER IS FOUND.  WHEN THE COMPLETE        10710602
*                     KEYWORD IS FOUND, IT IS COMPARED TO EACH ENTRY    10710702
*                     IN THE VALID KEYWORD TABLE UNTIL A MATCH OR THE   10714802
*                     END OF THE TABLE IS FOUND. EACH ENTRY CONSISTS    10716802
*                     OF AN EIGHT (8) BYTE KEYWORD FOLLOWED BY THE      10718802
*                     ADDRESS OF THE RESPECTIVE PROCESSOR.              10718902
*                     THE TABLE SCAN IS DONE BY ESTABLISHING ADDRESS-   10723002
*                     ABILITY TO THE TABLE AND THEN INCREMENTING THE    10725002
*                     BASE REGISTER BY THE LENGTH OF AN ENTRY AS EACH   10727002
*                     NON-MATCH IS FOUND.  THIS METHOD INSURES FUTURE   10727102
*                     FLEXIBILITY BECAUSE NO MATTER HOW MANY NEW        10731202
*                     KEYWORDS ARE ADDED TO THE TABLE, THE LABELS       10733202
*                     'KEYTAB' AND 'BRANCH' WILL ALWAYS POINT TO THE    10735202
*                     CURRENT KEYWORD AND ITS EXITING POINT.  WHEN A    10735302
*                     MATCH IS FOUND, EXIT IS MADE BY BRANCHING TO THE  10739402
*                     MODULE INDICATED IN THE TABLE.  FOR AN ERROR      10741402
*                     EXIT IS MADE TO IEE0503D.                         10743402
*                                                                       10743502
* NOTES                                                                 10753502
*    DEPENDENCIES   = KEYWORDS SUCH AS ONLINE/OFFLINE/CONSOLE/HARDCPY/  10764802
*                     MSTCONS/ONTP/OFFTP ARE STANDARD.                  10771102
*                     THE NET (ATCAM), AND CH,CPU,STOR,AND PATH         10774102
*                     PRIMATY KEYWORDS ARE CONSIDERED STANDARD ALSO.    10775002
*    CHARACTER/CODE DEPENDENCIES                                        10775402
*                  = THIS MODULE CAN BE ASSEMBLED WITH ANY NEW          10775802
*                    CHARACTER/CODE SET.                                10776902
*                                                                       10781902
* RESTRICTIONS     = ON ENTRY, REG2 PTR TO THE PARAM LIST (XSA)         10782602
* REGISTER CONVENTIONS                                                  10783002
*                   = REGISTERS ARE DEFINED AT LABEL: DEFREG            10784102
*    PATCH LABEL   = N/A                                                10786102
*                                                                       10786502
* MODULE TYPE      = CSECT                                              10787102
*    PROCESSOR     = ASSEMBLER                                          10787202
*    MODULE SIZE   = VS/2 = '254 HEX '                                  10787302
*    ATTRIBUTES     = REENTRANT,REUSABLE,REFRESHABLE,PAGED LPA,         10787402
*                     ZERO PSW PROTECT KEY,SUPERVISOR MODE              10788502
*                                                                       10789602
* ENTRY POINTS      = IEE3203D FROM COMMAND ROUTER RTN IEE0403D         10792902
*   PURPOSE         = VARY COMMAND ROUTER                               10794002
*   LINKAGE         = BRANCH                                            10795102
*   INPUT           = XSA PTR IN REG 2                                  10796202
*                     THE XSA CONTAINS:                                 10797302
*                        PTR TO PARMLIST IN XAR                         10798402
*                        PTR TO LIST AND VERB CODE IN XAL               10799502
*                        VERB = VARY IN XAV                             10800602
*                        UCM ID IN XAU                                  10801702
*   REGISTERS SAVED = NONE                                              10802802
*   REGISTER USAGE  = R2  = XSA BASE                                    10805002
*                     R4  = COUNTER FOR KEYWORD CHARACTER               10805402
*                     R6  = PTR TO FIRST CHARACTER OF KEYWORD           10805802
*                     R7  = END OF BUFFER REG                           10805902
*                     R9  = PTR TO CURRENT CHARACTER OF OPERAND FIELD   10806002
*                     R11 = BASE FOR VALID DKYWORD TABLE                10806902
*                     R12 = ADDRESSABILITY                              10808902
*   REGISTERS RESTORED                                                  10809202
*                   = NONE                                              10810102
*                                                                       10811002
* EXITS - NORMAL    =  IN THIS SYSTEM IEE3203D EXITS TO:                10811902
*   CONDITIONS           IEE0803D FOR KEYWORDS - ONLINE, CONSOLE        10812802
*                                                AND OFFLINE            10813702
*                        IEE4703D FOR KEYWORDS - HARDCPY                10814602
*                        ISTCFF3D FOR KEYWORDS(PRIMARY) - NET           10815502
*                        IEE0803D FOR KEYWORDS(PRIMARY) - CH,CPU,       10816402
*                                     PATH, STOR                        10817302
*                        IEE4303D FOR KEYWORDS - MSTCONS                10818202
*                        IED1303D FOR KEYWORDS - ONTP,OFFTP             10819102
*   OUTPUT          = SAME AS INPUT TO THIS MODULE                      10820002
*   RETURN CODES    = NONE                                              10820402
*                                                                       10822602
* EXITS - ERROR     = EXIT TO MESSAGE MODULE IEE0503D                   10823702
*   CONDITIONS      = WHEN AN ERROR IS DETECTED                         10824802
*   OUTPUT          = REG 2 PTS TO THE XSA WHICH CONTAINS:              10825902
*                     ERROR MSG CODE IN THE XAE                         10827002
*                     VERB IN THE XAV                                   10828102
*                     UCM ID IN THE XAU                                 10829202
*   RETURN CODES    = NONE                                              10830302
*                                                                       10832502
* EXTERNAL REFERENCES                                                   10833602
*   ROUTINES        = ISTCFF3D  FOR NET IN ATCAM                        10834702
*                   = IEE0803D  FOR CH,CPU,STOR,PATH IN MP              10835802
*                     IEE0803D  FOR OFFLINE/ONLINE/CONSOLE              10836902
*                     IEE4703D  FOR HARDCPY                             10839102
*                     IEE4303D  FOR MSTCONS                             10840202
*                     IED1303D  FOR ONTP/OFFTP                          10841302
*                     IEE0503D  FOR ERROR MESSAGES                      10842402
*   DATA AREAS      = SEE EXITS - OUTPUT DATA                           10843502
*                                                                       10844602
* CONTROL BLOCKS    = EXTENDED SAVE AREA (XSA)                          10845702
*                        FOR FORMAT SEE - INPUT DATA AND                10855702
*                        OUTPUT DATA SECTIONS                           10857702
*                                                                       10859702
* TABLES            = NONE                                              10861702
*                                                                       10864202
* MACROS            = NONE                                              10864602
*                                                                       10865002
* MAPPING MACROS    = IEEXSA                                            10865402
*    SERIALIZATION = NONE REQUIRED IN THIS MODULE                       10866102
*                                                                       10868102
* CHANGE LEVEL      = Y02669,Y02674                                     10870202
*                                                                       10870902
* CHANGE ACTIVITY  = N/A                                                10871402
*                                                                       10871502
* MESSAGES          = 'IEE311I VARY PARAMETER MISSING'                  10871602
*                        WHEN THE POINTER TO THE COMMAND                10872302
*                        PARAM LIST IN XAL IS 0                         10873002
*                     'IEE307I VARY DELIMITER ERROR'                    10874002
*                        WHEN NO COMMA AFTER PARENS, UNBALANCED         10876002
*                        PARENS, OR NO COMMA BEFORE KEYWORD             10878002
*                     'IEE309I VARY UNIDENTIFIABLE KEYWORD'             10878402
*                        WHEN THE KEYWORD DOES NOT MATCH ANY            10878802
*                        ENTRY IN THE TABLE                             10879202
*                     'IEE908I VARY CMD LENGTH EXCEEDS MAX.'            10879302
*                        WHEN A V ONLINE/OFFLINE/OR CONSOLE COMMANDS'S  10879402
*                        PARAM LIST IS GREATER THAN 124 CHARACTERS.     10879502
*                                                                       10891102
* ABEND CODES       = NONE                                              10896102
*                                                                       10901102
* PACKAGING         = IEE3203D (SGIEE0VR) WILL BE PART OR LOAD MODULE   10906102
*                        = IGC0003D                                     10911102
*                     THIS LOAD MODULE ALIASES ARE                      10916102
*                        = NONE                                         10921102
*                     THIS LOAD MODULE ENTRY POINTS ARE                 10926102
*                        = IGC0003D                                     10931102
*                     MODULE WILL RESIDE IN: LPALIB                     10933102
*                                                                       10936102
* SYSGEN            = SUPPLIED BY SECTIONS:                             10941102
*                        LOAD MODULE NAME                               10946102
*                        PACKAGING                                      10951102
*                                                                       10956102
* SYSTEM LIBRARIES  = NONE                                              10961102
*                                                                       10966102
*********************************************************************** 10971102
          EJECT                                                         11021102
*  REGISTER ASSIGNMENTS                                                 11071102
*                                                                       11121102
DEFREG   EQU   *                                                 Y02669 11131102
R1        EQU   1                                                       11171102
R2        EQU   2             XSA BASE                                  11221102
R3        EQU   3                                                       11271102
R4        EQU   4             COUNTER FOR KEYWORD CHARC.                11321102
R5        EQU   5                                                       11371102
R6        EQU   6             PTR TO FIRST CHAR OF KEYWORD              11421102
R7       EQU   7                   POINTER TO END OF BUFFER             16480019
R8       EQU   8                   WORK REG                             16490019
R9       EQU   9                   PTR TO CURRENT CHAR OF OPERAND FIELD 16500019
R10      EQU   10                                                       16510019
R11      EQU   11                  BASE FOR VALID KEYWORD TABLE         16520019
R12      EQU   12                  PGM BASE                             16530019
R13      EQU   13                                                       16540019
R14      EQU   14                                                       16550019
R15      EQU   15                                                       16560019
*    EQUATES                                                            16570019
C0       EQU   0                                                        16580019
C1       EQU   1                                                        16590019
C2       EQU   2                                                        16600019
C3       EQU   3                                                        16610019
C4       EQU   4                                                        16610402
C6       EQU   6                                                 Y02669 16610802
C7       EQU   7                                                 Y01029 16612001
C8       EQU   8                                                        16620019
D124     EQU   124                                                      16622002
ERRX39   EQU   X'39'                                             Y02669 16624002
LENG     EQU   12           LENGTH OF KEYWORD-MODULE CODE ENTRY  Y01029 16630001
COMMA    EQU   C','                COMMA CHAR                           16640019
LFTPAREN EQU   C'('                LEFT PAREN CHAR                      16650019
RGTPAREN EQU   C')'                RIGHT PAREN CHAR                     16660019
BLANK    EQU   C' '                BLANK CHAR                           16670019
PARENSW  EQU   3                   SWITCH FOR BALANCED PARENS           16680019
ERR7     EQU   07                  ERR CODE   DELIM ERROR               16690019
ERR9     EQU   9                   ERR CODE   UNIDENT KEYWORD           16700019
ERR11    EQU   11                  ERR CODE   PARAM MISSING             16710019
CHCODE   EQU   X'58'                                             Y02669 16712002
CPUCODE  EQU   X'54'                                             Y02669 16714002
STORCODE EQU   X'5C'                                             Y02669 16716002
PATHCODE EQU   X'50'                                             Y02669 16718002
*********************************************************************** 16720019
         SPACE 10                                                       16730019
*********************************************************************** 16800019
* FUNCTIONS OF THIS ROUTINE-                                          * 17200019
*     1. ESTABLISHES PROGRAM AND XSA ADDRESSABILITY                   * 17600019
*     2. INITIALIZES A BALANCED PAREN SWITCH AND A CHARACTER COUNTER  * 18000019
*     3. CHECKS XAL FOR 0. (IF 0, BRANCHES TO ERROR ROUTINE.)         * 18400019
*     4. COMPUTES THE END OF BUFFER                                   * 18800019
*********************************************************************** 19200019
         BALR  R12,C0              ESTABLISH PGM ADDRESSABILITY         19600019
         USING *,R12                                                    20000019
         MODID BR=YES                                            Y01886 20050001
         USING XSA,R2              BASE FOR EXTENDED SAVE AREA          20400019
         L     R9,XAL              REG 9 POINTS TO OPERAND FIELD OF    *20800019
                                   VARY COMMAND                         21200019
         LA    R9,C0(R9)           ZERO OUT HIGH ORDER BYTE             21600019
         LTR   R9,R9               IS PARAMETER PTR 0 (NO KEYWORD)      22000019
         LA    R5,ERR11            SET ERR CODE FOR MSG 'PARAM MISSING' 22400019
         BZ    ERREXIT             YES,PTR IS 0, GO TO ERROR ROUTINE    22800019
         L     R7,XAR              LOAD POINTER TO THE BUFFER THAT      23200019
         LA    R7,C0(R7)           CONTAINS THE VARY COMMAND            23600019
         AH    R7,C0(R7)           COMPUTE END OF BUFFER                24000019
PRIMKEY  EQU   *                                                 Y02669 24050002
         CLC   C0(C3,R9),CNET        IS IT 'NET'FOR ATCAM        Y02674 24100002
         BE    ATCAMEXT              YES, EXIT TO ATCAM MOD.     Y02674 24150002
         CLC   C0(C2,R9),CCH         S IT CHANNEL FOR MP         Y02669 24200002
         BE    CHANNEL               YES,CHANGE CODE AND EXIT    Y02669 24250002
         CLC   C0(C3,R9),CCPU        IS IT CPU FOR MP            Y02669 24300002
         BE    CPU                   YES, CHANGE CODE AND EXIT   Y02669 24350002
         CLC   C0(C4,R9),CSTOR       IS IT STOR FOR MP           Y02669 24360002
         BE    STORAGE               YES, CHANGE CODE AND EXIT   Y02669 24370002
         CLC   C0(C4,R9),CPATH       IS IT PATH FOR MP           Y02669 24380002
         BE    PATH                  YES, CHANGE CODE AND EXIT   Y02669 24390002
* NO PRIMARY KEYWORD, SET UP AND CHECK THE SECONDARY KEYWORDS    Y02669 24392002
*                                                                Y02669 24394002
         SR    PARENSW,PARENSW     INITIALIZE PAREN SWITCH TO 0         24400019
         SR    R4,R4               INITIALIZE CHARACTER COUNTER TO 0    24600019
         SPACE 10                                                       24800019
*********************************************************************** 24850019
* THE COMMAND OPERAND IS SCANNED FOR A LEFT PAREN OR A COMMA. IF A    * 24900019
* LEFT PAREN IS FOUND, THE OPERAND IS SCANNED FOR BALANCED PARENS AND * 24950019
* THEN THE KEYWORD. IF THERE IS NO LEFT PAREN, AND A COMMA IS FOUND,  * 25000019
* THE KEYWORD SHOULD FOLLOW.                                          * 25050019
*********************************************************************** 25100019
PARENSCN EQU   *                                                        25200019
CHECK    CLI   C0(R9),LFTPAREN     IS FIRST CHAR A LEFT PAREN           25600019
         BE    INCSWTCH            YES,GO TO CHECK FOR BAL PARENS       26000019
         CLI   C0(R9),COMMA        IS CURRENT CHAR A COMMA              27000019
         BE    SAVEPTR             YES, GO TO SCAN KEYWORD              28400019
         LA    R9,C1(R9)           INCREMENT PTR TO NEXT CHAR           28800019
         CR    R9,R7               HAS END OF BUFFER BEEN REACHED       29200019
         BE    DELIMERR            YES,GO TO ERR RTN, COMMA MISSING     29600019
         B     CHECK               GO BACK TO CHECK NEXT CHAR           30000019
*********************************************************************** 30400019
* THIS ROUTINE SCANS THE PARAMETER LIST FOR BALANCED PARENS. WHEN A   * 30800019
* LEFT PAREN IS FOUND, PARENSW IS INCREMENTED BY 1, FOR A RIGHT PAREN * 31200019
* IT IS DECREMENTED BY 1. WHEN PARENSW IS 0, BALANCED PARENS HAVE BEEN* 31600019
* FOUND, AND A COMMA AND THE KEYWORD SHOULD FOLLOW.                   * 32000019
*********************************************************************** 32400019
INCSWTCH LA    PARENSW,C1(PARENSW) INCREMENT BY 1 FOR LEFT PAREN        32800019
CHARSCAN LA    R9,C1(R9)           INCREMENT PTR TO NEXT CHAR           33200019
         CLI   C0(R9),LFTPAREN     IS CURRENT CHAR A LEFT PAREN         33600019
         BE    INCSWTCH            YES,CHAR IS'(',SET SWTCH,CHECK AGAIN 34000019
         CLI   C0(R9),RGTPAREN     IS CURRENT CHAR A RIGHT PAREN        34400019
         BNE   CHKSWTCH            NO,CHECK PAREN SWITCH FOR 0          34800019
DECSWTCH BCTR  PARENSW,C0          DECREMENT BY 1 FOR RIGHT PAREN       35200019
CHKSWTCH LTR   PARENSW,PARENSW     IS SWITCH 0 (BALANCED PARENS)        35600019
         BZ    KEYSCAN             YES,GO TO SCAN KEYWORD AFTER PARENS  36000019
         CR    R9,R7               NO, CHECK FOR END OF BUFFER REACHED  36400019
         BNE   CHARSCAN            NO,GO BACK TO CHECK NEXT CHAR        36800019
DELIMERR LA    R5,ERR7             DELIM ERR UNBAL PARENS OR NO COMMA   37200019
         B     ERREXIT             GO TO ERROR RTN TO SET UP XCTL       37600019
         SPACE 10                                                       38000019
*********************************************************************** 38400019
* THIS ROUTINE CHECKS FOR A COMMA PRECEDING A KEYWORD AND IF THE COMMA* 38800019
* IS PRESENT, SCANS THE KEYWORD. A POINTER TO THE FIRST CHAR OF THE   * 39200019
* KEYWORD IS SAVED. (A KEYWORD IS DEFINED AS THE FIRST WORD FOLLOWING * 39600019
* A COMMA NOT EMBEDDED IN PARENS.) THE KEYWORD IS COMPLETE WHEN A     * 40000019
* COMMA, A BLANK, OR END OF BUFFER IS FOUND.                          * 40400019
KEYSCAN  EQU   *                                                        41200019
         LA    R9,C1(R9)           INCREMENT PTR TO NEXT CHAR           41600019
         CLI   C0(R9),COMMA        IS CURRENT CHAR A COMMA              42000019
         BNE   DELIMERR            NO.COMMA MUST PRECEDE KEYWORD        42400019
SAVEPTR  LA    R6,C1(R9)           SAVE PTR TO 1ST CHAR OF KEYWORD      42800019
SCAN1    LA    R9,C1(R9)           INCREMENT PTR TO CURR CHAR OF KEYWD  43200019
         CLI   C0(R9),COMMA        IS CURRENT CHAR A COMMA              44400019
         BE    TABSCAN             YES,COMMA MEANS END OF KEYWD FOUND   44800019
         CLI   C0(R9),BLANK        NO,CHECK CURRENT CHAR FOR BLANK      45200019
         BE    TABSCAN             YES,BLANK MEANS END OF KEYWD FOUND   45300019
         CR    R9,R7               NO,CHECK FOR END OF BUFFER           45400019
         BE    TABSCAN             GO TO SCAN TABLE                     45500019
         LA    R4,C1(R4)           INCREMENT KEYWORD CHAR COUNTER       45600019
         B     SCAN1               GO BACK TO CHECK NEXT CHAR OF KEYWD  46000019
         SPACE 10                                                       46400019
*********************************************************************** 46800019
* THIS ROUTINE COMPARES THE KEYWORD WITH EACH ENTRY IN A TABLE OF     * 47200019
* VALID VARY KEYWORDS UNTIL A MATCH, OR THE END OF THE TABLE IS FOUND.* 47600019
* WHEN A MATCH IS FOUND, THE ADDRESS FOLLOWING THE KEYWORD IN   Y01029* 48450001
* THE TABLE IS USED TO EXIT BY.                                 Y01029* 48500001
*********************************************************************** 48800019
TABSCAN  EQU   *                                                        49200019
         LA    R11,KEYTAB          PUT ADDRESS OF KEYWORD TABLE IN A    49600019
         USING KEYTAB,R11          REG AND ESTABLISH ADDRESSABILITY     50000019
         LA    R8,TABEND           PUT ADDRESS OF END OF TABLE IN A REG 50400019
         BCTR  R4,C0               DECREMENT CHAR COUNTER FOR EX INSTR  50600019
SCAN2    EQU   *                                                        50800019
         CLR   R11,R8              HAS END OF TABLE BEEN REACHED        51200019
         LA    R5,ERR9             PRESET CODE FOR UNIDENT KEYWORD MSG  51600019
         BH    ERREXIT             YES, THE VERB IS INVALID             52000019
         EX    R4,DOCLC            INSERT KEYWORD LENGTH IN CLC INSTR   52400019
         BE    XMOD                IF KEYWDS MATCH,GOTO SET UP EXIT MOD 52800002
INCPTR   LA    R11,LENG(R11)       IF NOT, INCREMENT PTR TO NEXT KEYWD  53200019
         B     SCAN2         GO TO CHECK NEXT ENTRY                     53250002
XMOD     LA    R8,C1(R11)          COMPUTE A POINTER TO THE FIRST       53680019
         AR    R4,R8               CHARACTER AFTER THE KEYWORD          53770019
         CLI   C7(R11),BLANK           IF THE EIGHTH CHARACTER   Y01029 53820001
*                      OF THE TABLE ENTRY IS NOT A BLANK, THE    Y01029 53830001
*                      KEYWORD IS EIGHT CHARACTERS.  IT IS A     Y01029 53832001
*                      MATCH AND EXIT SHOULD BE MADE TO THE      Y01029 53834001
*                      MODULE INDICATED IN THAT TABLE ENTRY      Y01029 53836001
         BNE   EXIT                    BRANCH TO APPROPRIATE RT  Y01029 53838001
         CLI   C0(R4),BLANK        IS THE CHARACTER BLANK               53840019
         BNE   INCPTR              IF NOT, A FALSE MATCH HAS BEEN FOUND 53920019
*                                  EG. KEYWORD 'ABC' WOULD MATCH TABLE  54000019
*                                  ENTRY 'ABCD' BECAUSE ONLY THE FIRST  54080019
*                                  3 CHARACTERS WERE COMPARED.          54160019
EXIT     EQU   *                                                 Y01029 54170001
*                                                                       54180002
*  THIS CODE WAS ADDED FOR THE ONLINE/OFFLINE/CONSOLE KEYWORDS   Y02669 54190002
*  BECAUSE SINCE THEY ARE ATTACHED TASKS, A CSCB IS CREATED FOR  Y02669 54200002
*  THEM WHOSE COMMAND BUFFER LENGTH IS ONLY 124 BYTES.           Y02669 54202002
*  IF THE COMMAND WAS ISSUED FROM A CONSOLE WHOSE CHARACTER SPAN Y02669 54204002
*  IS GREATED THAN 124 CHARACTERS, THE COMMAND WILL BE TRUNCATED Y02669 54206002
*  AND SUBSEQUENTLY, VARY PROCESSORS WILL THINK IT IS INVALID.   Y02669 54208002
*  TO PREVENT THIS EARLY, A SCAN IS DONE OF THE PARM LIST OF THE Y02669 54208402
*  BUFFER AND IF IT EXCEEDS 124 CHARACTERS, MSG 'IEE908I VARY    Y02669 54208802
*  CMD LENGTH EXCEEDS MAX.' WILL BE ISSUED IMMEDIATELY AND THE   Y02669 54209202
*  COMMAND WILL NOT BE PROCESSED.                                Y02669 54209602
*                                                                Y02669 54209702
         LR    R3,R11     SAVE REG11 PTR SINCE IT WILL BE USED   Y02669 54209802
         CLC   C0(C6,R11),ONL     IS IT AN ONLINE COMMAND        Y02669 54209902
         BE    BUFFCHK            YES, GO CHECK BUFFER LENGTH    Y02669 54210002
         CLC   C0(C7,R11),OFL     IS IT AN OFFLINE COMMAND       Y02669 54213202
         BE    BUFFCHK            YES, GO CHECK BUFFER LENGTH    Y02669 54215202
         CLC   C0(C7,R11),CONS    IS IT A CONSOLE COMMAND        Y02669 54217202
         BE    BUFFCHK            YES, GO CHECK BUFFER LENTH     Y02669 54219202
*  IT IS NOT THE ABOVE, THEREFORE CAN EXIT NORMALLY              Y02669 54219602
*                                                                Y02669 54219702
EXITOK   EQU   *                                                 Y02669 54219802
         LR    R11,R3             RESTORE REG11                  Y01029 54219902
         L     R15,C8(R11)             GET ADDRESS OF EXIT RT    Y01029 54220002
         BR    R15                     EXIT TO APPROPRIATE RT    Y01029 54260001
         SPACE 10                                                       61200019
BUFFCHK  EQU   *                                                 Y02669 61210002
         L     R9,XAL             PTR TO LIST POSITION OF PARAM  Y02669 61220002
         LA    R9,C0(R9)                                         Y02669 61222002
         LA    R10,C1             INCREMENTER                    Y02669 61230002
         L     R11,XAR            PTS TO COMMAND BUFFER          Y02669 61240002
         LA    R11,C0(R11)        ZERO HIGH ORDER BYTE           Y02669 61242002
         AH    R11,C0(R11)        COMPUTE END OF BUFFER          Y02669 61244002
         BCTR  R11,C0             MINUS ONE                      Y02669 61246002
EOBLOOP  EQU   *                                                 Y02669 61248002
         CLI   C0(R9),BLANK  IS THE NEXT CHAR AN ENDING BLANK    Y02669 61248402
         BE    EXITCHK            YES GO COMPUT LENTH OF CMD     Y02669 61248802
         BXLE  R9,R10,EOBLOOP     NO GO CHECK NEXT CHARACTER     Y02669 61249202
*                                                                Y02669 61339902
EXITCHK  EQU   *                                                 Y02669 61341902
         L     R10,XAL            POINTER TO BEGINNING           Y02669 61343902
         LA    R10,C0(R10)                                       Y02669 61344302
         SR    R9,R10             SIZE OF CMD IN REG9            Y02669 61345902
         LA    R10,D124           MAX SIZE                       Y02669 61347902
         CR    R9,R10             IS IT LONGER OF NOT?           Y02669 61348302
         BH    LONGERR            YEP, ERROR                     Y02669 61348702
         B     EXITOK             NO IT IS SAFE, GO PROCESS CMD  Y02669 61349102
*                                                                Y02669 61349502
LONGERR  EQU   *                                                 Y02669 61349602
         LA    R5,ERRX39          ERROR CODE                     Y02669 61349702
         B     ERREXIT            GO TO 0503D TO ISSUED MSG      Y02669 61349802
*                                                                Y02669 61399802
         SPACE 10                                                       61449802
*********************************************************************** 61600019
* EXIT ROUTINE-                                                       * 62000019
* THE CORRECT ERROR CODE IS PUT IN XAE IN THE XSA AND CONTROL   Y01029* 62850001
* IS PASSED TO IEE0503D                                         Y01029* 62900001
*********************************************************************** 63200019
ERREXIT  EQU   *                                                        63600019
         STC   R5,XAE              PUT ERROR CODE IN XAE                64000019
         L     R15,VCON05              GET ADDRESS OF IEE0503D   Y01029 64050001
         BR    R15                     BRANCH TO IEE0503D        Y01029 64100001
*                                                                       64150002
ATCAMEXT EQU   *                                                 Y02674 64200002
         L     R15,VATCAM         GET VCON ATCAM MOD             Y02674 64250002
         BR    R15                EXIT TO IT                     Y02674 64300002
CHANNEL  EQU   *                                                 Y02669 64350002
         MVI   XAN,CHCODE         CHANGE THE COMMAND CODE        Y02669 64400002
         B     EXIT08             GO TO 0803D EXIT               Y02669 64450002
CPU      EQU   *                                                 Y02669 64500002
        MVI   XAN,CPUCODE        CHANGE THE COMMAND CODE         Y02669 64550002
         B     EXIT08             GO TO 0803D EXIT               Y02669 64600002
STORAGE  EQU   *                                                 Y02669 64650002
         MVI   XAN,STORCODE       CHANGE THE COMMAND CODE        Y02669 64700002
         B     EXIT08             GO TO 0803D EXIT               Y02669 64750002
PATH     EQU   *                                                 Y02669 64800002
         MVI   XAN,PATHCODE       CHANGE THE COMMAND CODE        Y02669 64850002
EXIT08   EQU   *                                                 Y02669 64900002
         L     R15,VCON08         GET ADDRESS OF 0803D           Y02669 64950002
        BR    R15                EXIT TO IEE0803D                Y02669 65000002
*                                                                Y02669 65050002
         SPACE 10                                                       65200019
DOCLC    CLC   C0(C0,R11),C0(R6)   DOES CURRENT TABLE ENTRY EQU KEYWD   65400019
         DS    0F                  ALIGN ON FULL WORD BOUNDARY          65600019
VCON05   DC    V(IEE0503D)         ERROR MSG MODULE              Y01029 66000001
VATCAM   DC    V(ISTCFF3D)                                       Y02674 66050002
VCON08   DC   V(IEE0803D)                                        Y02669 66100002
ONL      DC    CL6'ONLINE'                                              66150002
OFL      DC    CL7'OFFLINE'                                             66160002
CONS     DC    CL7'CONSOLE'                                             66170002
*********************************************************************** 66200019
KEYTAB   EQU   *                   START OF VALID KEYWORD TABLE         66400019
*                                  ****KEYWORD FOLLOWED BY ITS****      66700019
*                                  ****    MODULE ADDR    ****   Y01029 67000001
         DC    CL8'ONLINE'                                       Y02669 67050002
BRANCH   EQU   *                                                 Y01029 67900001
         DC    V(IEE0803D)                 ..                    Y02669 68200002
         DC    CL8'OFFLINE'                ..                    Y02669 68500002
         DC    V(IEE0803D)                 ..                    Y02669 68800002
         DC    CL8'CONSOLE'                ..                    Y02669 69100002
         DC    V(IEE0803D)                 ..                    Y02669 69400002
         DC    CL8'HARDCPY'                ..                    Y01029 69700001
         DC    V(IEE4703D)                 ..                    Y01029 70000001
         DC    CL8'MSTCONS'                ..                    Y01029 70300001
         DC    V(IEE4303D)                 ..                    Y01029 70600001
         DC    CL8'ONTP'                   ..                    Y01029 73600001
         DC    V(IED1303D)                 ..                    Y01029 73900001
         DC    CL8'OFFTP'                  ..                    Y01029 74200001
         DC    V(IED1303D)                 ..                    Y01029 74500001
TABEND   EQU   *-LENG                                                   75100019
*                                                                       75150002
CNET     DC    C'NET'                                            Y02674 75300002
CCH      DC    C'CH'                                             Y02669 75350002
CCPU     DC    C'CPU'                                            Y02669 75360002
CSTOR    DC    C'STOR'                                           Y02669 75370002
CPATH    DC    C'PATH'                                           Y02669 75380002
*********************************************************************** 75400019
SETXSA   DSECT                                                          82400019
         IEEXSA                                                         82800019
         MEND                                                           83200019
