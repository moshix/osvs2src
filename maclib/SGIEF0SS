         MACRO                                                          00050002
         SGIEF0SS &PRMNAME,&JESNAME                                     00100002
.********************************************************************** 00150002
.*                                                                      00200002
.*  MACRO NAME = SGIEF0SS                                               00250002
.*                                                                      00300002
.*  DESCRIPTIVE NAME = GROUP 0 SYSGEN MACRO JOB ENTRY SUBSYSTEM         00350002
.*                     NAMES TABLE GENERATOR                            00400002
.*                                                                      00450002
.*  FUNCTION =  GENERATES TABLE OF JOB ENTRY SUBSYSTEM NAMES            00500002
.*                                                                      00550002
.*  OPERATION = GENERATES AN 4-CHARACTER TABLE ENTRY FOR:               00600002
.*                - THE PRIMARY JOB ENTRY SUBSYSTEM NAME                00650002
.*                  SPECIFIED IN THE SCHEDULR MACRO.                    00700002
.*                - THE MASTER SUBSYSTEM (MSTR)                         00710002
.*                - EACH JOB ENTRY SUBSYSTEM NAME SPECIFIED             00750002
.*                  IN THE SCHEDULR MACRO.                              00800002
.*                - AN ENTRY OF ZEROS DENOTING END OF TABLE.            00850002
.*                                                                      00900002
.*              ALL SUBSYSTEM NAMES ARE LESS THAN OR EQUAL TO           00950002
.*              FOUR CHARACTERS IN LENGTH.  NAMES ARE LEFT-             01000002
.*              JUSTIFIED AND PADDED WITH BLANKS.                       01050002
.*                                                                      01100002
.*  DEPENDENCIES = SCHEDULR SYSGEN MACRO (GROUP 1):                     01150002
.*                  -PROVIDE NAME OF PRIMARY JOB ENTRY SUBSYSTEM        01200002
.*                   AND NAMES FOR JOB ENTRY SUBSYSTEMS TO BE           01250002
.*                   USED IN SYSTEM CREATED BY THIS SYSGEN              01300002
.*                  -CALL MACRO SGIEF2SS                                01350002
.*                                                                      01400002
.*                 SGIEF2SS SYSGEN MACRO (GROUP 2):                     01450002
.*                  -CALL SGIEF0SS, PASSING THE PARAMETERS              01500002
.*                   SPECIFIED ON THE SCHEDULR MACRO.                   01550002
.*                                                                      01600002
.*  INPUT = &PRMNAME - PRIMARY JOB ENTRY SUBSYSTEM NAME                 01650002
.*          &JESNAME - ARRAY CONTAINING UP TO 15 JOB ENTRY              01700002
.*                     SUBSYSTEM NAMES.                                 01750002
.*                                                                      01800002
.*  OUTPUT = JOB ENTRY SUBSYSTEM NAMES TABLE                            01850002
.*                                                                      01900002
.********************************************************************** 01950002
.*  SET JESNAMES ARRAY COUNTERS                                         02000002
         LCLA  &I,&K                                                    02050002
.*  SET COUNT OF JOB ENTRY SUBSYSTEM NAMES PASSED                       02100002
&I       SETA  N'&JESNAME                                               02150002
&K       SETA  1                                                        02200002
         TITLE '  JOB ENTRY SUBSYSTEM NAMES TABLE'                      02250002
IEFJESNM CSECT                                                          02300002
         ENTRY IEFJTBNM                                                 02310002
*********************************************************************** 02350002
*                                                                       02400002
*  MODULE NAME = IEFJESNM                                               02450002
*                                                                       02500002
*  DESCRIPTIVE NAME = JOB ENTRY SUBSYSTEM NAMES TABLE                   02550002
*                                                                       02600002
*  COPYRIGHT = NONE                                                     02650002
*                                                                       02700002
*  STATUS = 0S/VS2 RELEASE 2                                            02750002
*                                                                       02800002
*  FUNCTION = TABLE OF JOB ENTRY SUBSYSTEM NAMES                        02850002
*                                                                       02900002
*    OPERATION = THIS TABLE CONTAINS THE NAMES OF THE PRIMARY JOB       02950002
*                ENTRY SUBSYSTEM AND THE JOB ENTRY SUBSYSTEMS           03000002
*                SPECIFIED AT SYSGEN TIME.  IT ALSO CONTAINS THE        03050002
*                NAME 'MSTR' FOR THE MASTER SUBSYSTEM.  THE MSTR        03060002
*                ENTRY IS WIRED IN - IT IS NOT SPECIFIED AT             03070002
*                SYSGEN TIME.  THESE NAMES ARE USED AT IPL TIME         03080002
*                TO INITIALIZE THE SUBSYSTEM CVT'S                      03100002
*                                                                       03150002
*                THE FIRST ENTRY CONTAINS THE NAME OF THE PRIMARY       03200002
*                JOB ENTRY SUBSYSTEM.  THE SECOND ENTRY CONTAINS THE    03250002
*                NAME 'MSTR' FOR THE MASTER SUBSYSTEM.  THE FOLLOWING   03260002
*                ENTRIES CONTAIN THE  JOB ENTRY SUBSYSTEM NAMES.        03300002
*                                                                       03310002
*                SUBSYSTEM CVT'S WILL BE GENERATED AND CHAINED          03320002
*                IN THE ORDER OF THE ENTRIES IN THE NAMES TABLE.        03330002
*                                                                       03350002
*  NOTES                                                                03400002
*                                                                       03450002
*    DEPENDENCIES = SYSGEN SPECIFICATION OF JOB ENTRY SUBSYSTEM         03500002
*                   NAMES                                               03550002
*      CHARACTER-CODE DEPENDENCIES = NONE                               03600002
*    RESTRICTIONS = NONE                                                03650002
*    REGISTER CONVENTIONS = N/A                                         03700002
*    PATCH LABEL = SSPATCH - RESERVED FOR FUTURE USE.                   03750002
*                  NAMES MAY BE ADDED TO TABLE AFTER SYSGEN;            03800002
*                  HOWEVER, THE LAST ENTRY OF TABLE MUST ALWAYS BE      03850002
*                  ZERO TO TERMINATE TABLE SCAN AT IPL TIME.            03900002
*                                                                       03950002
*  MODULE TYPE = TABLE                                                  04000002
*                                                                       04050002
*    MODULE SIZE = 4-CHARACTER ENTRY FOR EACH SUBSYSTEM NAME SPECIFIED  04100002
*                  AT SYSGEN TIME, 4-CHARACTER ENTRY FOR MASTER         04110002
*                  SUBSYSTEM, PLUS PATCH AREA SIZE                      04120002
*                                                                       04150002
*    ATTRIBUTES = NOT EXECUTABLE                                        04200002
*                                                                       04250002
*  ENTRY POINT = IEFJTBNM                                               04300002
*    PURPOSE = TO ACCESS NAME TABLE AT IPL TIME TO INITIALIZE           04350002
*              THE SUBSYSTEM CVT'S                                      04400002
*    LINKAGE = NO LINKAGE TO THIS ROUTINE-                              04450002
*              TABLE IS ACCESSED BY IEFJSINT - SUBSYSTEM INTERFACES     04500002
*              INITIALIZATION ROUTINE                                   04550002
*    INPUT = NONE                                                       04600002
*                                                                       04650002
*  EXIT-NORMAL = NONE                                                   04700002
*    CONDITIONS = NONE                                                  04750002
*    OUTPUT = NONE                                                      04800002
*    RETURN CODES = NONE                                                04850002
*                                                                       04900002
*  EXIT-ERROR = NONE                                                    04950002
*    CONDITIONS = NONE                                                  05000002
*    OUTPUT = NONE                                                      05050002
*    RETURN CODES = NONE                                                05100002
*                                                                       05150002
*  EXTERNAL REFERENCES = NONE                                           05200002
*    ROUTINES = NONE                                                    05250002
*    DATA AREAS = NONE                                                  05300002
*    CONTROL BLOCKS = NONE                                              05350002
*                                                                       05400002
*  TABLES = NONE                                                        05450002
*                                                                       05500002
*  MACROS = NONE                                                        05550002
*    SERIALIZATION = NONE                                               05600002
*                                                                       05650002
*  CHANGE ACTIVITY = Y02668                                             05700002
*                                                                       05750002
*  MESSAGES = NONE                                                      05800002
*                                                                       05850002
*  ABEND CODES = NONE                                                   05900002
*                                                                       05950002
*********************************************************************** 06000002
         EJECT                                                          06050002
         DS    0F                                                       06100002
IEFJTBNM EQU   *                                                        06110002
         DC    CL4'&PRMNAME'      PRIMARY JOB ENTRY SUBSYSTEM NAME      06150002
.*                                                                      06160002
.*  GENERATE SUBSYSTEM NAME ENTRY FOR THE MASTER SUBSYSTEM (MSTR)       06170002
.*                                                                      06180002
         DC    CL4'MSTR'          MASTER SUBSYSTEM NAME                 06190002
.*                                                                      06192002
.*  CHECK IF ANY JOB ENTRY SUBSYSTEM NAMES OTHER THAN THE PRIMARY       06194002
.*  WERE SPECIFIED.  IF NOT, BYPASS GENERATING ENTRIES -                06196002
.*                                                                      06198002
         AIF   (&I EQ 0).FINI                                           06198402
*                                                                       06200002
*  ENTRIES FOR SYSGENED JOB ENTRY SUBSYSTEM NAMES                       06250002
*                                                                       06300002
.LOOP    ANOP                                                           06350002
         DC    CL4'&JESNAME(&K)'                                        06400002
         AIF   (&K EQ &I).FINI                                          06450002
&K       SETA  &K+1                                                     06500002
         AGO   .LOOP                                                    06550002
.FINI    ANOP                                                           06600002
*                                                                       06650002
*  PATCH AREA - FIRST ENTRY OF ZEROS THAT IS FOUND DURING TABLE         06700002
*  SCAN INDICATES END OF TABLE.  THERE MUST ALWAYS BE AN ENTRY          06750002
*  OF ZEROS TO TERMINATE THE TABLE SCAN.                                06760002
*                                                                       06800002
SSPATCH  EQU   *                                                        06850002
         DC    F'0'                                                     06900002
         DC    F'0'                                                     06950002
         DC    F'0'                                                     07000002
         MEND                                                           07050002
