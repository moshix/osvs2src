         MACRO                                                          00900001
&TBLNAME SGIFF0BT &BFRSIZE,&EXPSZE,&NUMDEV,&NUMSEC1,&NUMSEC2,          X01800001
               &NUMSEC3,&NUMSEC4                                        02700001
         LCLA  &TBLNGTH,&TOTAVAL,&NETZONE,&DVCOUNT,&EXPBFR,&TABDISP     03600001
         LCLA  &ZONESZ,&TOTGUAR,&NUMSEC(4),&X1,&SAVE                    04500001
         LCLA  &ZSTBL(4),&GUARTBL(4),&EXTRA,&CTR                        05400001
.*  MACRO REWRITTEN FOR APAR 26262 FOR RELEASE 19                       06300001
&TBLNGTH SETA  &BFRSIZE/256        SET TABLE LENGTH                     07200001
&TOTAVAL SETA  &TBLNGTH            SET TOTAL NO OF BUF SECTIONS AVAIL.  08100001
         AIF   (T'&EXPSZE EQ 'O').BT00       CHECK FOR 2250-1           09000001
&NETZONE SETA  &TOTAVAL/&NUMDEV    SET INITIAL ZONE SIZE                09900001
&DVCOUNT SETA  &NUMDEV             SET NUMBER OF DEVICES                10800001
&EXPBFR  SETA  &EXPSZE             SET AMOUNT OF BUFF FOR EXPRESS       11700001
         AGO   .BT00G              BRANCH TO GENERATE 1ST 2 WORDS       12600001
.BT00    ANOP                                                           13500001
.*    SET FIELDS FOR 2250-1                                             14400001
&DVCOUNT SETA  1                   SET NO OF DEV EQ TO 1                15300001
&EXPBFR  SETA  &BFRSIZE            SET AMT OF BUFF FOR EXPRESS          16200001
&TABDISP SETA  16                  SET DEVICE TABLE DISPLACEMENT        17100001
&ZONESZ  SETA  &TOTAVAL            SET ZONE SIZE EQ FULL BUFFER         18000001
&TOTGUAR SETA  &TOTAVAL            SET TOTAL GUAR EQ FULL BUFFER        18900001
.*    GENERATE 1ST PART OF TABLE                                        19800001
.BT00G   ANOP                                                           20700001
         ENTRY &TBLNAME                                                 21600001
&TBLNAME DC    H'&TBLNGTH'         BUFFER TABLE LENGTH IN SECTIONS      22500001
         DC    H'&DVCOUNT'         NUMBER OF DEVICES                    23400001
         DC    H'&EXPBFR'          AMOUNT OF BUFFER FOR EXPRESS         24300001
         DC    H'&TOTAVAL'         TOTAL AVAIL BUFFER IN SECTIONS       25200001
         AIF   (T'&EXPSZE EQ 'O').BTV22     IF 2250-1 GEN WHOLE TABLE   26100001
&NUMSEC(1) SETA &NUMSEC1           INITIALIZE THE                       27000001
&NUMSEC(2) SETA &NUMSEC2                SUBSCRIPTED VARIABLES FOR       27900001
&NUMSEC(3) SETA &NUMSEC3                     THE NUMBER OF GUAR SECTS   28800001
&NUMSEC(4) SETA &NUMSEC4                          FOR EACH 2250         29700001
.BTP     ANOP                                                           30600001
.* FIND THE LARGEST NUMBER OF GUARANTEED SECTIONS NOT ASSIGNED YET      31500001
&X1      SETA  1                   INITIALIZE COUNTERS                  32400001
&SAVE    SETA  1                   THIS WILL BE THE INDEX OF LARGEST    33300001
.BTR     AIF   (&ZSTBL(&SAVE) EQ 0).BTU  START INDICES WITH VALUES      34200001
&SAVE    SETA  &SAVE+1                            THAT HAVE NOT         35100001
&X1      SETA  &X1+1                              BEEN ASSIGNED         36000001
         AGO   .BTR                               YES.                  36900001
.BTU     AIF   (&X1 GT &NUMDEV).BTERR  ALL ZONES HAVE BEEN ASSIGNED     37800001
.BTQ     AIF   (&X1 EQ &NUMDEV).BTS1  FOUND LARGEST OF REMAINING GUAR.  38700001
&X1      SETA  &X1+1               INCREMENT X1                         39600001
         AIF   (&NUMSEC(&X1) GT &NUMSEC(&SAVE)).BTT                     40500001
         AGO   .BTQ                                                     41400001
.BTT     AIF   (&ZSTBL(&X1) NE 0).BTQ                                   42300001
&SAVE    SETA  &X1            &SAVE WILL CONTAIN THE INDEX OF THE       43200001
.*                                 LARGEST REMAINING NUMSEC REQUEST     44100001
         AGO   .BTQ                                                     45000001
.BTS1    AIF   (&NUMSEC(&SAVE) GT &NETZONE).BTS2 GUAR LARGER THAN ZONE  45900001
.* ALL REMAINING ZONE SIZES ARE SET EQUAL TO NETZONE                    46800001
.* ALL REMAINING GUARANTEED SECTIONS ARE ASSIGNED                       47700001
&X1      SETA  0                        INITIALIZE INDEX                48600001
.BTS11   ANOP                                                           49500001
&X1      SETA  &X1+1                    INCREMENT INDEX                 50400001
         AIF   (&X1 GT &NUMDEV).BTV1    ALL DEVICES HAVE ZONES ASSIGNED 51300001
         AIF   (&ZSTBL(&X1) NE 0).BTS11 BRANCH IF ZONE HAS BEEN ASSIGND 52200001
&ZSTBL(&X1) SETA &NETZONE               SET ZONE SIZE TO NETZONE        53100001
&GUARTBL(&X1) SETA &NUMSEC(&X1)         SET NO. OF GUAR. SEC. TO NUMSEC 54000001
         AGO   .BTS11                   PROCESS NEXT DEVICE             54900001
.BTS2    ANOP                                                           55800001
.*  NUMBER OF GUARANTEED SECTIONS REQUESTED IS GREATER THAN NETZONE     56700001
&CTR     SETA  &CTR+1         KEEPS TRACK OF NO. OF DEVICES PROCESSED   57600001
         AIF   (&CTR EQ &NUMDEV).BTERR  BRANCH SHOULD NEVER BE TAKEN    58500001
&ZSTBL(&SAVE) SETA &NUMSEC(&SAVE)   SET ZONE SIZE TO NO. OF GUAR. SEC.  59400001
&GUARTBL(&SAVE) SETA &NUMSEC(&SAVE) SET NO. OF GUARANTEED SECTIONS      60300001
&NETZONE SETA  (&TOTAVAL-&ZSTBL(1)-&ZSTBL(2)-&ZSTBL(3)-&ZSTBL(4))/(&NUM*61200001
               DEV-&CTR)            RECALCULATE NETZONE                 62100001
         AIF   (&NETZONE LE 0).BTV2 BRANCH TAKEN ONLY IF PARAMETERS     63000001
.*                                      TO SGIFF0BT ARE INCORRECT       63900001
         AGO   .BTP      FIND THE LARGEST REMAINING NUMSEC REQUEST      64800001
.BTERR   ANOP                                                           65700001
.*  THIS WILL ONLY BE EXECUTED IF BAD PARAMETERS ARE PASSED REQUESTING  66600001
.*       MORE BUFFER THAN IS AVAILABLE.                                 67500001
&ZSTBL(&SAVE) SETA &NETZONE        ASSIGN ALL REMAINING BUFFER          68400001
&GUARTBL(&SAVE) SETA &NETZONE           SECTIONS TO THIS DEVICE         69300001
         AGO   .BTV2               GENERATE THE REST OF THE TABLE       70200001
.BTV1    ANOP                                                           71100001
&EXTRA   SETA  &TOTAVAL-&ZSTBL(1)-&ZSTBL(2)-&ZSTBL(3)-&ZSTBL(4)         72000001
         AIF   (&EXTRA LE 0).BTV2       BRANCH IF NO EXTRA SECTIONS     72900001
&ZSTBL(&NUMDEV) SETA &ZSTBL(&NUMDEV)+&EXTRA ASSIGN EXTRA TO LAST DEVICE 73800001
.BTV2    ANOP                                                           74700001
.*  GENERATE THE REST OF THE TABLE                                      75600001
&X1      SETA  1                        INITIALIZE INDEX                76500001
&TABDISP SETA  40                       SET INITIAL TABLE DISPLACEMENT  77400001
.BTV21   ANOP                                                           78300001
&ZONESZ  SETA  &ZSTBL(&X1)              SET ZONE SIZE IN CONSTANT       79200001
&TOTGUAR SETA  &GUARTBL(&X1)            SET GUARANTEED SEC. IN CONSTANT 80100001
.BTV22   ANOP                                                           81000001
         DC    H'0'                     TOTAL SECTIONS ASSIGNED         81900001
         DC    H'&TABDISP'              DEVICE TABLE DISPLACEMENT       82800001
         DC    H'&ZONESZ'               DEVICE ZONE SIZE                83700001
         DC    H'&TOTGUAR'              NUMBER OF GUARANTEED SECTIONS   84600001
         AIF   (T'&EXPSZE EQ 'O').BT10      CHECK FOR 2250-1            85500001
         AIF   (&X1 EQ 4).BT10          CHECK FOR LAST TABLE ENTRY      86400001
&TABDISP SETA  &TABDISP+&ZSTBL(&X1)     INCREMENT TABLE DISPLACEMENT    87300001
&X1      SETA  &X1+1                    INCREMENT INDEX                 88200001
         AIF   (&X1 LE &NUMDEV).BTV21   CONTINUE GENERATING TABLE       89100001
&TABDISP SETA  0              SET DISPL TO 0 FOR UNUSED PORTION OF TABL 90000001
         AGO   .BTV21         CONTINUE GENERATING TABLE                 90900001
.BT10    AIF   (&BFRSIZE EQ 0).BT11     CHECK FOR UNBUFFERED MOD 1      91800001
         DC    XL&TBLNGTH'00'           BUFFER SECTIONS TABLE           92700001
.BT11    ANOP                                                           93600001
         MEND                                                           94500001
