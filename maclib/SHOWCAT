         MACRO                                                          00200000
&LABEL   SHOWCAT &NAME=,&CI=,&AREA=,&ACB=,&MF=    VSAM CATALOG MACRO    00250000
         LCLC  &BRPT                    BRANCH POINT                    00300000
         AIF   ('&MF' EQ '').MFS        BRANCH IF STANDARD FORM         00350000
         AIF   ('&MF' EQ 'L').MFL       BRANCH IF LIST FORM             00400000
         AIF   (N'&MF NE 2).ERR1        CHECK EXEC FORM HAS SUBLIST     00450000
         AIF   ('&MF(1)' EQ 'E').MFE    BRANCH IF EXEC FORM             00500000
         AIF   ('&MF(1)' EQ 'B').MFE    BRANCH IF EXEC/BUILD FORM       00550000
         AGO   .ERR1                    FIRST OP OF EXEC FORM BAD       00600000
.MFS     ANOP                           STANDARD FORM OF SHOWCAT        00650000
&BRPT    SETC  'IGG'.'&SYSNDX'          INITLZ BRANCH POINT             00700000
         AIF   ('&AREA' EQ '').ERR2     CHECK AREA PARM SPECIFIED       00750000
         AIF   ('&NAME' EQ '').S1       BRANCH IF NO NAME PARM          00800000
         AIF   ('&CI' NE '').ERR4       BOTH NAME AND CI PARMS, ERROR   00850000
.*                                      NAME PARM IS GIVEN              00900000
         CNOP  0,4                      SHOWCAT MACRO ALIGNMENT         00950000
&LABEL   BAL   1,&BRPT                  BRANCH AROUND PARM LIST         01000000
         DC    X'80'                    SHOW CAT FLAGS                  01050000
         DC    X'000000'                RESERVED                        01100000
         AIF   ('&NAME'(1,1) EQ '(').S5 BRANCH IF REG FORM              01150000
         DC    A(&NAME)                 NAME ADDRESS                    01200000
         AGO   .S4                      CONTINUE                        01250000
.S5      DC    A(0)                     NAME ADDRESS                    01300000
         AGO   .S4                      CONTINUE                        01350000
.S1      AIF   ('&CI' EQ '').ERR3       AND ALSO NO CI PARM, ERROR      01400000
.*                                      CI PARM IS GIVEN                01450000
         CNOP  0,4                      SHOWCAT MACRO ALIGNMENT         01500000
&LABEL   BAL   1,&BRPT                  BRANCH AROUND PARM LIST         01550000
         DC    X'00'                    SHOW CAT FLAGS                  01600000
         DC    X'000000'                RESERVED                        01650000
         AIF   ('&CI'(1,1) EQ '(').S3   BRANCH IF REG FORM              01700000
         DC    A(&CI)                   CI ADDRESS                      01750000
         AGO   .S4                      CONTINUE                        01800000
.S3      DC    A(0)                     CI ADDRESS                      01850000
.S4      AIF   ('&ACB' EQ '').S7        SEE IF NO ACB ADDRESS           01900000
         AIF   ('&ACB'(1,1) EQ '(').S7  BRANCH IF REG FORM              01950000
         DC    A(&ACB)                  ACB ADDRESS                     02000000
         AGO   .S6                      CONTINUE                        02050000
.S7      DC    A(0)                     ACB ADDRESS                     02100000
.S6      AIF   ('&AREA'(1,1) EQ '(').S8 BRANCH IF REG FORM              02150000
         DC    A(&AREA)                 RETURN AREA ADDRESS             02200000
         AGO   .S9                      CONTINUE                        02250000
.S8      DC    A(0)                     RETURN AREA ADDRESS             02300000
.S9      ANOP                           CONTINUE                        02350000
&BRPT    DS    0H                       END OF PARM LIST                02400000
         AIF   ('&NAME' EQ '').S10      BRANCH IF NO NAME GIVEN         02450000
         AIF   ('&NAME'(1,1) NE '(').S11 BRANCH IF NOT REG FORM         02500000
         ST    &NAME,4(0,1)             PUT NAME ADDR IN PARM LIST      02550000
         AGO   .S11                     CONTINUE                        02600000
.S10     AIF   ('&CI'(1,1) NE '(').S11  BRANCH IF NOT REG FORM          02650000
         ST    &CI,4(0,1)               PUT CI ADDR IN PARM LIST        02700000
.S11     AIF   ('&ACB' EQ '').S12       BRANCH IF NO ACB PARM           02710000
         AIF   ('&ACB'(1,1) NE '(').S12  BRANCH IF NOT REG FORM         02750000
         ST    &ACB,8(0,1)              PUT ACB ADDR IN PARM LIST       02800000
.S12     AIF   ('&AREA'(1,1) NE '(').S13 BRANCH IF NOT REG FORM         02850000
         ST    &AREA,12(0,1)            PUT AREA ADDR IN PARM LIST      02900000
.S13     ANOP                           CONTINUE                        02950000
         LINK  EP=IGG0CLA0              PASS CONTROL TO SHOWCAT MODULE  03000000
         MEXIT                          STANDARD FORM PROCESSED         03050000
.MFL     ANOP                           LIST FORM OF SHOWCAT MACRO      03100000
&LABEL   DS    0F                       SHOWCAT MACRO ALIGNMENT         03150000
         AIF   ('&NAME' EQ '').L1       BRANCH IF NO NAME PARM          03200000
         AIF   ('&CI' NE '').ERR4       BOTH NAME AND CI PARMS, ERROR   03250000
.*                                      NAME PARM IS GIVEN              03300000
         DC    X'80'                    SHOWCAT FLAGS                   03350000
         DC    X'000000'                RESERVED                        03400000
         DC    A(&NAME)                 NAME ADDRESS                    03450000
         AGO   .L3                      CONTINUE                        03500000
.L1      AIF   ('&CI' EQ '').L2         BRANCH IF NO CI PARM            03550000
         DC    X'00'                    SHOWCAT FLAGS                   03600000
         DC    X'000000'                RESERVED                        03650000
         DC    A(&CI)                   CI ADDRESS                      03700000
         AGO   .L3                      CONTINUE                        03750000
.L2      DC    X'00'                    SHOWCAT FLAGS                   03800000
         DC    X'000000'                RESERVED                        03850000
         DC    A(0)                     NAME/CI ADDRESS                 03900000
.L3      AIF   ('&ACB' EQ '').L4        BRANCH IF NO ACB ADDRESS        03950000
         DC    A(&ACB)                  ACB ADDRESS                     04000000
         AGO   .L5                      CONTINUE                        04050000
.L4      DC    A(0)                     ACB ADDRESS                     04100000
.L5      AIF   ('&AREA' EQ '').L6       BRANCH IF NO AREA ADDRESS       04150000
         DC    A(&AREA)                 AREA ADDRESS                    04200000
         AGO   .L7                      CONTINUE                        04250000
.L6      DC    A(0)                     AREA ADDRESS                    04300000
.L7      ANOP                           CONTINUE                        04350000
         MEXIT                          LIST FORM PROCESSED             04400000
.MFE     ANOP                           EXECUTE FORM OF SHOWCAT         04450000
&LABEL   DS    0H                       SHOWCAT MACRO                   04500000
         AIF   ('&MF(2)'(1,1) EQ '(').E1 BRANCH IF REG SPECIFIED        04550000
         LA    1,&MF(2)                 GET REMOTE PARM LIST ADDR       04600000
         AGO   .E2                      ADDRESSABILITY ESTABLISHED      04650000
.E1      ANOP                           REG ADDR FOR PARM LIST          04700000
         AIF   ('&MF(2)' EQ '(1)').E2   BRANCH IF REG 1 SPECIFIED       04750000
         LR    1,&MF(2)                 GET REMOTE PARM LIST ADDR       04800000
.E2      ANOP                           ADDRESSABILITY ESTABLISHED      04850000
         AIF   ('&NAME' EQ '').E3       BRANCH IF NO NAME PARM          04900000
         AIF   ('&CI'   NE '').ERR4     BOTH CI AND NAME PARM SPECIFIED 04950000
.*                                      NAME PARM SPECIFIED             05000000
         AIF   ('&NAME'(1,1) EQ '(').E4 BRANCH IF REG FORM SPECIFIED    05050000
         LA    0,&NAME                  GET NAME ADDRESS                05100000
         ST    0,4(0,1)                 PUT NAME ADDR INTO PARM LIST    05150000
         OI    0(1),X'80'               INDICATE NAME PARM SPECIFIED    05200000
         AGO   .E5                      CONTINUE                        05250000
.E4      ST    &NAME,4(0,1)             PUT NAME ADDR INTO PARM LIST    05300000
         OI    0(1),X'80'               INDICATE NAME PARM SPECIFIED    05350000
         AGO   .E5                      CONTINUE                        05400000
.E3      AIF   ('&CI' EQ '').E5         BRANCH IF NO CI PARM            05450000
         AIF   ('&CI'(1,1) EQ '(').E6   BRANCH IF REG FORM SPECIFIED    05500000
         LA    0,&CI                    GET CI ADDRESS                  05550000
         ST    0,4(0,1)                 PUT CI ADDRESS INTO PARM LIST   05600000
         NI    0(1),X'7F'               INDICATE CI PARM SPECIFIED      05650000
         AGO   .E5                      CONTINUE                        05700000
.E6      ST    &CI,4(0,1)               PUT CI ADDR INTO PARM LIST      05750000
         NI    0(1),X'7F'               INDICATE CI PARM SPECIFIED      05800000
.E5      AIF   ('&ACB' EQ '').E7        BRANCH IF NO ACB ADDR           05850000
         AIF   ('&ACB'(1,1) EQ '(').E8  BRANCH IF REG FORM              05900000
         LA    0,&ACB                   GET ACB ADDRESS                 05950000
         ST    0,8(0,1)                 PUT ACB ADDRESS INTO PARM LIST  06000000
         AGO   .E7                      CONTINUE                        06050000
.E8      ST    &ACB,8(0,1)              PUT ACB ADDRESS INTO PARM LIST  06100000
.E7      AIF   ('&AREA' EQ '').E9       BRANCH IF NO AREA ADDR          06150000
         AIF   ('&AREA'(1,1) EQ '(').E10 BRANCH IF REG FORM             06200000
         LA    0,&AREA                  GET AREA ADDRESS                06250000
         ST    0,12(0,1)                PUT AREA ADDR INTO PARM LIST    06300000
         AGO   .E9                      AREA PARM PROCESSED             06350000
.E10     ST    &AREA,12(0,1)            PUT AREA ADDR INTO PARM LIST    06400000
.E9      AIF   ('&NAME' NE '').E11      BRANCH IF NAME SPECIFIED        06450000
         AIF   ('&CI'   NE '').E11      BRANCH IF CI   SPECIFIED        06500000
         AIF   ('&ACB'  NE '').E11      BRANCH IF ACB  SPECIFIED        06550000
         AIF   ('&AREA' NE '').E11      BRANCH IF AREA SPECIFIED        06600000
         AIF   ('&MF(1)' NE 'B').E11    BRANCH IF NOT BUILD/EXEC FORM   06650000
         XC    0(16,1),0(1)             CLEAR SHOWCAT PARM LIST         06700000
.E11     AIF   ('&MF(1)' EQ 'B').E12    BRANCH IF BUILD/EXEC FORM       06750000
         LINK  EP=IGG0CLA0              PASS CONTROL TO SHOWCAT MODULE  06800000
.E12     ANOP                           LINK WAS NOT REQUESTED          06850000
         MEXIT                          STANDARD FORM PROCESSED         06900000
.ERR1    MNOTE 12,'*** SYNTAX ERROR IN MF OPERAND'                      06950000
         MEXIT                          ERROR EXIT                      07000000
.ERR2    MNOTE 12,'*** AREA PARM NOT SPECIFIED'                         07050000
         MEXIT                          ERROR EXIT                      07100000
.ERR3    MNOTE 12,'*** NEITHER NAME NOR CI SPECIFIED'                   07150000
         MEXIT                          ERROR EXIT                      07200000
.ERR4    MNOTE 12,'*** BOTH NAME AND CI SPECIFIED'                      07250000
         MEXIT                          ERROR EXIT                      07300000
         MEND                                                           07350000
