         MACRO                                                          00700010
&NAME    SLOWPOLL &MASK,&CONNECT=OR,&SECONDS=60                         01400010
.*                                                                      02100010
.* VERSION = 00, LEVEL = 00                                             02800010
.*                                                                      03500010
         GBLA  &IEDQZE(64)                                              04200010
         GBLB  &IEDQZA(64)                                              04900010
         LCLA  &ALG                                                     05600010
         LCLB  &NOGO                                                    06300010
.*                                                                      07000010
.*                                                                      07700010
.********************************************************************** 08400010
.*                                                                    * 09100010
.* THE SLOWPOLL MACRO -                                               * 09800010
.*   * DELAYS FURTHER POLLING WHEN A LINE ERROR OCCURS,               * 10500010
.*   * IS OPTIONAL IN AN INMESSAGE SUBGROUP OF AN MH (AND NOT         * 11200010
.*     PERMITTED IN ANY OTHER SUBGROUP),                              * 11900010
.*   * MAY BE USED MORE THAN ONCE IN A SUBGROUP.                      * 12600010
.*                                                                    * 13300010
.* SLOWPOLL SUSPENDS FURTHER POLLING ON A GIVEN LINE WHEN ERRORS      * 14000010
.* SPECIFIED BY THE ERROR MASK HAVE OCCURRED. THE BITS SPECIFIED BY   * 14700010
.* THE ERROR MASK OPERAND ARE COMPARED TO THE SETTING OF THE BITS IN  * 15400010
.* THE MESSAGE ERROR RECORD FOR THIS MESSAGE - IF SPECIFIED BITS IN   * 16100010
.* THE MESSAGE ERROR RECORD ARE ON, THE POLLING WILL BE SUSPENDED. BY * 16800010
.* SPECIFYING AN ALL-ZERO MASK, OR BY OMITTING THE MASK OPERAND, THE  * 17500010
.* ACTION WILL BE UNCONDITIONAL. POLLING WILL RESUME AFTER THE LENGTH * 18200010
.* OF TIME SPECIFIED IN THE SECONDS OPERAND.                          * 18900010
.*                                                                    * 19600010
.* ****************************************************************** * 20300010
.* *        *        *                 .   .             .       .  * * 21000010
.* *(SYMBOL)*SLOWPOLL*(MASK)(,CONNECT=. AND .)(,SECONDS=. INTEGER .)* * 21700010
.* *        *        *                . OR  .           . 60      . * * 22400010
.* *        *        *                 .## .             .##     .  * * 23100010
.* ****************************************************************** * 23800010
.*                                                                    * 24500010
.*   SYMBOL -                                                         * 25200010
.*     FUNCTION - NAME OF THE MACRO.                                  * 25900010
.*     DEFAULT - NONE. SPECIFICATION IS OPTIONAL.                     * 26600010
.*     FORMAT - MUST CONFORM TO THE RULES FOR ASSEMBLER LANGUAGE      * 27300010
.*              SYMBOLS (SEE THE SYMBOL ENTRY IN THE GLOSSARY).       * 28000010
.*                                                                    * 28700010
.*   MASK -                                                           * 29400010
.*     FUNCTION - SPECIFIES THE FIVE-BYTE BIT CONFIGURATION USED TO   * 30100010
.*                TEST THE MESSAGE ERROR RECORD FOR THE MESSAGE (THE  * 30800010
.*                MESSAGE ERROR RECORD IS DESCRIBED IN APPENDIX B).   * 31500010
.*     DEFAULT - NONE. SPECIFICATION OPTIONAL.                        * 32200010
.*     FORMAT - DECIMAL OR HEXADECIMAL. IF HEXADECIMAL FORMAT IS      * 32900010
.*              USED, FRAMING CHARACTERS MUST BE USED. IF X'' IS      * 33600010
.*              USED, LEADING ZEROES MUST BE CODED, IF XL5'' IS USED, * 34300010
.*              LEADING ZEROES MAY BE OMITTED.                        * 35000010
.*     MAXIMUM - 16777215 OR A HEXADECIMAL FIELD FIVE BYTES IN        * 35700010
.*               LENGTH.                                              * 36400010
.*     NOTES - OMITTING THIS OPERAND OR SPECIFYING AN ALL-ZERO MASK   * 37100010
.*             CAUSES UNCONDITIONAL EXECUTION.                        * 37800010
.*                                                                    * 38500010
.*            .   .                                                   * 39200010
.*   CONNECT=. AND . -                                                * 39900010
.*           . OR  .                                                  * 40600010
.*            .## .                                                   * 41300010
.*     FUNCTION - SPECIFIES THE TYPE OF LOGICAL CONNECTION TO BE MADE * 42000010
.*                BETWEEN THE MASK AND THE MESSAGE ERROR RECORD.      * 42700010
.*     DEFAULT - CONNECT=OR.                                          * 43400010
.*     FORMAT - AND OR OR.                                            * 44100010
.*     NOTES - AND SPECIFIES THAT THE MACRO IS TO BE EXECUTED ONLY IF * 44800010
.*             ALL OF THE BITS SPECIFIED BY MASK ARE ON IN THE        * 45500010
.*             MESSAGE ERROR RECORD. OR SPECIFIES THAT THE MACRO IS   * 46200010
.*             TO BE EXECUTED IF ANY BIT SPECIFIED BY MASK IS ON IN   * 46900010
.*             THE MESSAGE ERROR RECORD.                              * 47600010
.*                                                                    * 48300010
.*            .       .                                               * 49000010
.*   SECONDS=. INTEGER . -                                            * 49700010
.*           . 60      .                                              * 50400010
.*            .##     .                                               * 51100010
.*     FUNCTION - SPECIFIES THE LENGTH OF TIME, EXPRESSED IN SECONDS, * 51800010
.*                DURING WHICH POLLING ON THE LINE WILL BE SUSPENDED. * 52500010
.*     DEFAULT - SECONDS=60.                                          * 53200010
.*     FORMAT - DECIMAL.                                              * 53900010
.*     MINIMUM - 1.                                                   * 54600010
.*     MAXIMUM - 65535.                                               * 55300010
.*                                                                    * 56000010
.********************************************************************** 56700010
.*                                                                      57400010
.*                                                                      58100010
         AIF   (NOT &IEDQZA(9)).B20                                     58800010
         MNOTE 12,'***  IHB312  MUST FOLLOW STARTMH MACRO WITH TSOMH=NO*59500010
               '                                                        60200010
         AGO   .MEND                                                    60900010
.*                                                                      61600010
.B20     AIF   (&IEDQZA(5)).B30                                         62300010
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW INMSG OR OUT*63000010
               MSG MACRO'                                               63700010
&NOGO    SETB  1                                                        64400010
.B30     AIF   ('&MASK' EQ '').B50                                      65100010
         IEDQMASK &MASK,5                                               65800010
         AIF   (&IEDQZA(24)).B40                                        66500010
         AIF   ('&MASK'(1,1) NE 'X').B50                                67200010
         AIF   ('&MASK'(2,1) EQ 'L' AND '&MASK'(3,1) NE '5').B40        67900010
         AIF   ('&MASK'(2,1) NE 'L' AND K'&MASK NE 13).B40              68600010
         AGO   .B50                                                     69300010
.*                                                                      70000010
.B40     MNOTE 12,'***  IHB300  MASK OPERAND INVALID AS SPECIFIED'      70700010
&NOGO    SETB  1                                                        71400010
.B50     AIF   (&SECONDS GT 0 AND &SECONDS LT 65536).B60                72100010
         MNOTE 12,'***  IHB300  SECONDS OPERAND INVALID AS SPECIFIED'   72800010
&NOGO    SETB  1                                                        73500010
.B60     AIF   ('&CONNECT' EQ 'AND' OR '&CONNECT' EQ 'OR').B70          74200010
         MNOTE 12,'***  IHB300  CONNECT OPERAND INVALID AS SPECIFIED'   74900010
&NOGO    SETB  1                                                        75600010
.B70     AIF   (&NOGO).MEND                                             76300010
         SPACE 1                                                        77000010
         IEDQVCON 60,IEDQB4                                             77700010
         AIF   ('&MASK' NE '0' AND '&MASK' NE 'X''0000000000''' AND '&M*78400010
               ASK' NE 'XL5''0000000000''' AND '&MASK' NE '').C50       79100010
&NAME    DC    AL1(&IEDQZE(60)+1),AL1(8),AL2(0)                         79800010
         AGO   .C80                                                     80500010
.*                                                                      81200010
.C50     AIF   ('&CONNECT' EQ 'OR').C60                                 81900010
&ALG     SETA  1                                                        82600010
.C60     ANOP                                                           83300010
&NAME    DC    AL1(&IEDQZE(60)),AL1(12+&ALG),AL1(0)                     84000010
         AIF   ('&MASK'(1,1) EQ 'X').C70                                84700010
         DC    FL5'&MASK'          ERROR MASK                           85400010
         AGO   .C80                                                     86100010
.*                                                                      86800010
.C70     DC    &MASK               ERROR MASK                           87500010
.C80     DC    AL2(&SECONDS),AL2(0)                                     88200010
.MEND    SPACE 1                                                        88900010
         MEND                                                           89600010
