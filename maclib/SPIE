*%/*                                                                    00060002
         MACRO                                                          00070002
&NAME    SPIE  &EXIT,&INTS,&MF=I                                        00100002
.*                                                                  S   00150002
.*  DEFINE LOCAL VARIABLES                                          S   00200002
.*                                                                  S   00250002
         LCLA  &DNO,&ENO,&A,&AA,&B                               YM1995 00300002
         LCLB  &BIT(17)                                             S   00350002
         LCLC  &INDEX,&PM,&PMA,&M1,&M1A,&M2,&M2A,&M3,&M3A,&C,&D,&E      00400002
         LCLC  &FIRST,&N1,&N2,&CC,&PICAR,&PICARX,&PICARS         YM1995 00450002
.*                                                                      00500002
.*  SCAN THE INTERRUPT BITS                                             00550002
.*                                                                      00600002
         AIF   ('&INTS' EQ '').SORTMF                                   00650002
&A       SETA  1                  INDEX THE FIRST INTERRUPT SUB-PARM    00700002
.MAIN    AIF   ('&INTS(&A)' EQ '').BUMP  BLANK OPERANDS IGNORED         00750002
&C       SETC  '&INTS(&A)'        A MORE WORKABLE FORM                  00800002
         AIF   ('&C'(1,1) EQ '(').SERIES  IN PARENS MEANS FROM X TO Y   00850002
         AIF   (T'&INTS(&A) NE 'N').ERROR2  MUST BE A NUMBER            00900002
         AIF   (&INTS(&A) LT 1 OR &INTS(&A) GT 17 OR &INTS(&A) EQ 16).EX00950002
               RROR2              RANGE OF POSSIBILITY              S   01000002
&BIT(&C) SETB  1                  SET APPROPRIATE MASK BIT              01050002
.BUMP    AIF   (&A EQ N'&INTS).DONE  ALL CHECKED                        01100002
&A       SETA  &A+1               BUMP INDEX                            01150002
  AGO    .MAIN     GET NEXT                                             01200002
.*                                                                      01250002
.*  SET A SERIES OF BITS                                                01300002
.SERIES  AIF   ('&C'(3,1) EQ ',').SHORT  ONE DIGIT NUMBER TO START      01350002
.* START WITH A TWO DIGIT NUMBER                                        01400002
&D       SETC  '&C'(2,2)          START OF THE SEQUENCE                 01450002
         AIF   ('&C'(4,1) NE ',' OR '&C'(7,1) NE ')').ERROR2  EXPECTED  01500002
&E       SETC  '&C'(5,2)          END OF SEQUENCE                       01550002
         AGO   .SETSER            GO TO SET THE BITS                    01600002
.* START WITH A ONE DIGIT NUMBER                                        01650002
.SHORT   ANOP                                                           01700002
&D       SETC  '&C'(2,1)          START OF THE SEQUENCE                 01750002
         AIF   ('&C'(5,1) EQ ')').SHORT2   CHECK 2ND LENGTH OF 1        01800002
         AIF   ('&C'(6,1) NE ')').ERROR2 TOO MANY OR TOO LONG           01850002
&E       SETC  '&C'(4,2)          END OF THE SEQUENCE                   01900002
         AGO   .SETSER                                                  01950002
.SHORT2  ANOP                                                           02000002
&E       SETC  '&C'(4,1)          END OF THE SEQUENCE                   02050002
.* SET ON THE SERIES                                                    02100002
.SETSER  ANOP                                                           02150002
&DNO     SETA  &D                 GET THE ALGEBRAIC EQUIVALENT          02200002
&ENO     SETA  &E                 GET THE ALGEBRAIC EQUIVALENT          02250002
         AIF   (&DNO GT &ENO OR &DNO LT 1 OR &ENO GT 17).ERROR2     S   02300002
         AIF   (&ENO EQ 17 AND &DNO NE 17).ERROR2                    S  02350002
.INNER   ANOP                                                           02400002
&BIT(&DNO)  SETB  1               INDEX AND SET ON THE BIT              02450002
         AIF   (&DNO EQ &ENO).BUMP DONE, GET NEXT SUB OPERAND           02500002
&DNO     SETA  &DNO+1             GET NEXT IN SERIES                    02550002
         AGO   .INNER             SET IT ON                             02600002
.DONE    AIF    (NOT &BIT(17)).SORTMF     IF 17, EXTENDED PICA          02650002
&BIT(16) SETB  1                                                        02700002
.*                                                                      02750002
.*  SORT THE FORM OF MACRO REQUESTED                                    02800002
.*                                                                      02850002
.SORTMF  AIF   ('&MF'(1,1) EQ '(').EXECUTE  EXECUTE FORM            S   02900002
         CNOP  0,4                ALIGN PICA TO FULLWORD BOUNDARY   S   02950002
&FIRST   SETC  '&NAME'            TO SET THE LABEL ON THE FIRST LINES   03000002
         AIF   ('&MF' EQ 'I').INLINE        EXPAND IN LINE          S   03050002
         AIF   ('&MF' NE 'L').ERROR3        ONLY CORRECT THING IS LIST  03100002
.*                                                                      03150002
.*  CREATE THE PICA WITHOUT ISSUING THE SVC CALL                        03200002
.*                                                                      03250002
  AIF    (T'&EXIT EQ 'O').DEF                                           03300002
         AIF   ('&EXIT'(1,1) EQ '(').ERROR1  REG VALUES NOT ALLOWED S   03350002
         AGO   .DEF               DEFINE THE PICA                   S   03400002
.*                                                                      03450002
.*  CREATE AND ADDRESS THE PICA AND ISSUE THE SVC                   S   03500002
.*                                                                      03550002
.INLINE  AIF  (T'&EXIT EQ 'O').BAL                                      03600002
         AIF   ('&EXIT'(1,1) NE '(').BAL  EXIT DEFINED BY 'DC'      S   03650002
&FIRST   STCM  &EXIT(1),7,*+9     SET THE EXIT ADDRESS              S   03700002
&FIRST   SETC  ''                 NAME SET, NOW BLANK IT            S   03750002
.BAL     AIF   (&BIT(16)).BALEXT  EXTENDED PICA                     S   03800002
&FIRST   BAL   1,*+10             ADDRESS AND BYPASS THE PICA       S   03850002
         AGO   .BLNKNAM           BLANK THE NAME                    S   03900002
.BALEXT  ANOP                                                           03950002
&FIRST   BAL   1,*+12             BRANCH AROUND ADDRESS THE PICA    S   04000002
.BLNKNAM ANOP                                                           04050002
&FIRST   SETC  ''                 NAME SET, BLANK IT                S   04100002
.*                                                                      04150002
.*  DEFINE THE PICA                                                     04200002
.*                                                                      04250002
.DEF     ANOP                                                           04300002
&FIRST   DC    BL1'0000&BIT(8)&BIT(10)&BIT(13)&BIT(14)' PROGRAM MASKS   04350002
         AIF   ('&EXIT' EQ '').EXAD0                                    04400002
         AIF   ('&EXIT'(1,1) EQ '(').EXAD0                              04450002
         DC    AL3(&EXIT)         EXIT ROUTINE ADDRESS              S   04500002
         AGO   .DEFINT            TO DEFINE THE INTERRUPT TYPES     S   04550002
.EXAD0   DC    AL3(0)             EXIT ROUTINE ADDRESS LOC              04600002
.DEFINT  DC    BL2'&BIT(16)&BIT(1)&BIT(2)&BIT(3)&BIT(4)&BIT(5)&BIT(6)&BX04650002
               IT(7)&BIT(8)&BIT(9)&BIT(10)&BIT(11)&BIT(12)&BIT(13)&BIT(X04700002
               14)&BIT(15)'       THE INTERRUPT MASK BYTES 1 AND 2      04750002
         AIF   (NOT &BIT(16)).TESTX  DONE IF NO EXTENTION           S   04800002
         DC    BL2'0&BIT(17).00000000000000' HANDLE OWN PAGE FAULTS S   04850002
.*                                                                      04900002
.TESTX   AIF   ('&MF' EQ 'I').SVC   FOR INLINE, GO TO ISSUE THE SVC  S  04950002
         MEXIT                      FOR LIST, ALL DONE                  05000002
.*                                                                      05050002
.*                                                                      05100002
.* EXECUTE FORM:  THIS ONE GETS CONFUSING.  LET'S PRAY IT ISN'T MUCH    05150002
.*                                USED.                                 05200002
.*                                                                      05250002
.EXECUTE AIF   ('&MF(1)' NE 'E').ERROR1  CHECK THE DISTINCTIVE 'E'  S   05300002
         AIF   ('&MF(2)'(1,1) EQ '(').MFR  PICA AD IN A  REG     YM1995 05350002
&INDEX   SETC  '+'                DISPLACEMENT 'PLUS A LABEL'           05400002
&PICARX  SETC  '&MF(2)'                                          YM1995 05405002
&PICARS  SETC  '&MF(2)'                                          YM1995 05410002
         AGO   .CKINTS                                           YM1995 05415002
.MFR     ANOP                     DISPLACEMENT IS NULL           YM1995 05420002
&B       SETA  K'&MF-6            TO GET REGISTER                YM1995 05425002
&PICAR   SETC  '&MF'(5,&B)                                       YM1995 05430002
&PICARX  SETC  '(&PICAR,0)'                                      YM1995 05435002
&PICARS  SETC  '(&PICAR)'                                        YM1995 05440002
.CKINTS  AIF   ('&INTS' EQ '').SAVEINT  SAVE THE OLD INTERRUPT MASK S   05450002
.*                                                                      05500002
.*  DEFINE THE MASKS FOR BINARY MVI'S                                   05550002
&PM      SETC  'B''0000'                             PROGRAM MASK       05600002
&PMA     SETC  '&BIT(8)&BIT(10)&BIT(13)&BIT(14)'''                      05650002
&M1      SETC  'B''&BIT(16)&BIT(1)&BIT(2)&BIT(3)'    BYTE ONE OF        05700002
&M1A     SETC  '&BIT(4)&BIT(5)&BIT(6)&BIT(7)'''      INTERRUPT MASK     05750002
&M2      SETC  'B''&BIT(8)&BIT(9)&BIT(10)&BIT(11)'   BYTE TWO           05800002
&M2A     SETC  '&BIT(12)&BIT(13)&BIT(14)&BIT(15)'''                     05850002
&M3      SETC  'B''0&BIT(17)00'                                         05900002
&M3A     SETC  '0000'''                                                 05950002
.*                                                                      06000002
.* SET THE EXIT ADDRESS                                                 06050002
         AIF   ('&EXIT' EQ '').SETMASK      OLD EXIT ADDR OK        S   06100002
         AIF   ('&EXIT'(1,1) EQ '(').XREGAD EXIT ADDR IN A REG      S   06150002
&NAME    LA    15,&EXIT                     ADDRESS THE EXIT        S   06200002
         ST    15,0&INDEX&PICARX            SET ADDR IN THE PICA YM1995 06250002
         AGO   .SETMASK                     SET THE MASKS           S   06300002
.XREGAD  ANOP                                                           06350002
&NAME    ST    &EXIT(1),0&INDEX&PICARX      SET EXIT ADDR IN PICAYM1995 06400002
.*                                                                      06450002
.*  MOVE THE PROGRAM AND INTERRUPT MASKS TO THE EXECUTED PICA           06500002
.SETMASK MVI   0&INDEX&PICARS,&PM&PMA  STORE THE PROGRAM MASK    YM1995 06550002
         MVI   4&INDEX&PICARS,&M1&M1A FIRST BYTE IN INTERRUPT MSKYM1995 06600002
         MVI   5&INDEX&PICARS,&M2&M2A  SECOND BYTE               YM1995 06650002
         AIF   (NOT &BIT(16)).SETR1         NOT AN EXTENDED PICA        06700002
         MVI   6&INDEX&PICARS,&M3&M3A  THIRD BYTE                YM1995 06750002
         AGO   .SETR1                        ADDRESS PICA               06800002
.*                                                                      06850002
.*  DO NOT DISTURB THE PRESENT MASKS; SET EXIT ADDRESS IF NECESSARY     06900002
.*  AND EXECUTE  THE SVC                                                06950002
.*                                                                      07000002
.SAVEINT AIF   ('&EXIT' EQ '').SVCONLY      JUST ADDRESS IT AND LEAVE   07050002
         AIF   ('&EXIT'(1,1) EQ '(').STCMR  REG SPECIFIED,JUST STORE IT 07100002
&NAME    LA    15,&EXIT(1)        ADDR THE EXIT ROUTINE                 07150002
         STCM  15,7,1&INDEX&PICARS  SET ONLY THE ADDRESS BYTES   YM1995 07200002
         AGO   .SETR1             GO TO ADDRESS THE PICA                07250002
.STCMR   ANOP                     REGISTER SPECIFIED                    07300002
&NAME    STCM  &EXIT(1),7,1&INDEX&PICARS SET ONLY THE ADDR BYTES YM1995 07350002
         AGO   .SETR1             GO TO ADDRESS THE PICA                07400002
.*                                                                      07450002
.* IF NO INTERRUPT MASK OR NEW EXIT ADDRESS WERE SPECIFIED THE NAME     07500002
.*  MUST YET BE DEFINED                                                 07550002
.*                                                                      07600002
.SVCONLY ANOP                                                           07650002
&N1      SETC  '&NAME'           SET NAME ON LOAD R1 OR SVC INSTR.      07700002
.*                                                                      07750002
.*  SET THE PICA ADDRESS IN REGISTER 1                                  07800002
.SETR1   AIF   ('&MF(2)'(1,1) EQ '(').AREG                              07850002
&N1      LA    1,&MF(2)           ADDRESS THE PICA                      07900002
         AGO   .SVC                                                     07950002
.AREG    AIF   ('&MF(2)' EQ '(1)').INSURE  NO NEED TO SET IF REG 1      08000002
&N1      IHBINNRA &MF(2)           LOAD REG 1 WITH PICA ADDRESS         08050002
         AGO   .SVC                                                     08100002
.INSURE  ANOP                                                           08150002
&N2      SETC  '&N1'  IF NO PARAM AND REG1 SPEC'D, THERE IS YET NO NAME 08200002
.*                                                                      08250002
.*  THE SVC CALL                                                        08300002
.*                                                                      08350002
.SVC     ANOP                                                           08400002
&N2      SVC   14                 ISSUE THE SPIE SVC                    08450002
         MEXIT                                                          08500002
.*                                                                      08550002
.*  ERROR EXITS                                                         08600002
.*                                                                      08650002
.ERROR1  IHBERMAC  69                                                   08700002
         MEXIT                                                          08750002
.ERROR2  IHBERMAC  37,,&INTS    INVALID SECOND OPERAND  (X,X,X,)        08800002
         MEXIT                                                          08850002
.ERROR3  IHBERMAC 35,,&MF       INVALID MF OPERAND  MF=XXX              08900002
         MEND                                                           08950002
**/                                                                     09000002
* SPIE:  MACRO KEYS(MF);                                                09050002
*        ANS('?'||MACLABEL||'SPIEP '||MACLIST||MACKEYS||';');           09100002
*%END;                                                                  09150002
