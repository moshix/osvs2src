         MACRO                                                          00050021
&SYMBOL  STARTIO    &SRB=,&TCB=                                         00100021
         LCLB  &TCBOSW,&SRBNEG                                          00150021
         AIF   (T'&SRB EQ 'O').E1          REQ. PARAM. MISSING          00200002
         AIF   (T'&SYMBOL EQ 'O').NOSYM    ANY NAME FIELD?              00250002
&SYMBOL  DS    0H                                                       00300021
.NOSYM   AIF   (T'&TCB NE 'O').TCBOK       IS TCB SPECIFIED?            00350002
&TCBOSW  SETB  1                           NO, SET INDICATOR            00400002
         AGO   .SRBTST                     GO TEST SRB                  00450002
.TCBOK   AIF   ('&TCB' NE 'SRB').SRBTST    YES, IS IT SRB?              00500002
&SRBNEG  SETB  1                           YES, SET INDICATOR           00550002
.SRBTST  AIF   ('&SRB'(1,1) EQ '(').SRBREG IS REGISTER SPECIFIED?       00600021
         LA    1,&SRB                      SRB PARAMETER REG            00650002
         AIF   ('&SRBNEG' NE '1').TCBTST   TCB=SRB?                     00700002
         LNR   1,1                         TCB PARAMETER REG            00750002
         AGO   .CVT                                                     00800021
.SRBREG  AIF   ('&SRBNEG' NE '1').POSREG                                00850021
         LNR   1,&SRB(1)                   SRB PARAMETER REG            00900002
         AGO   .CVT                                                     00950021
.POSREG  AIF   ('&SRB' EQ '(1)').TCBTST    ALREADY IN REG 1?            01000002
         LR    1,&SRB(1)                   SRB PARAMETER REG            01050002
.TCBTST  AIF   ('&TCBOSW' NE '1').ZEROTST  TCB OMITTED?                 01100002
         SR    0,0                         TCB PARAMETER REG            01150002
         AGO   .CVT                                                     01200021
.ZEROTST AIF   ('&TCB' NE '0').REGTST      TCB=0?                       01250002
         LNR   0,1                         TCB PARAMETER REG            01300002
         AGO   .CVT                                                     01350021
.REGTST  AIF   ('&TCB'(1,1) NE '(').NOREG  TCB IN REGISTER?             01400002
         AIF   ('&TCB' EQ '(0)').CVT       ALREADY IN REG 0?            01450002
         LR    0,&TCB(1)                   TCB PARAMETER REG            01500002
         AGO   .CVT                                                     01550021
.NOREG   LA    0,&TCB                      TCB PARAMETER REG            01600002
.CVT     L     3,CVTPTR(0,0)               CVT ADDRESS          YM2296P 01650002
         L     15,CVTIXAVL-CVTMAP(,3)      IOCOM ADDRESS        YM2296P 01700002
         L     15,IOCSTIO-IOCOM(,15)       CHAN. SCHED. ADDRESS YM2296P 01750002
         BALR  14,15                       INITIATE THE I/O             01800002
         MEXIT                                                          01850021
.E1      IHBERMAC   01,039                 SRB MISSING                  01900002
         MEND                                                           01950021
