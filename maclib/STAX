          MACRO                                                         00500020
&NAME    STAX  &EXADDR,&OBUF=,&IBUF=,&USADDR=,&REPLACE=,&DEFER=,&MF=I   01000002
*        MACDATE                  Y1   73253                            01050002
.*       MACCOMP                  Y1   SC1C5/PZD81                      01100002
.*       MACSTAT                  Y1   72244/PTMAS24019                 01150002
.*       MACSTAT                  Y1   73134/PTMVS1829                  01200002
.*       MACSTAT                  Y1   73247/PTMVS2296                  01300002
         LCLA  &A                                                       01500020
         LCLC  &GNAME,&H                                                02000020
         LCLA  &REPLSW,&DEFRSW                                          02500002
         LCLA  &REPLYES,&REPLNO,&CLEAR                           YM4019 02550002
&REPLYES SETA  X'30'                                             YM4019 02600002
&REPLNO  SETA  X'40'                                             YM4019 02650002
&CLEAR   SETA  X'40'                                             YM4019 02700002
&GNAME   SETC  'IHB'.'&SYSNDX'                                          04000020
         AIF   ('&MF' EQ 'L' OR '&MF(1)' EQ 'E').DEFER4          YM4019 04050002
         AIF   ('&EXADDR' NE '').DEFER4                          YM4019 04060002
         AIF   ('&OBUF' NE '' OR '&IBUF' NE '').ERROR1           YM4019 04300002
         AIF   ('&REPLACE' NE '' OR '&USADDR' NE '').ERROR1      YM4019 04400002
         AIF   ('&MF' NE 'I').ERROR2                             YM4019 04450002
         AIF   ('&EXADDR' EQ '' AND '&DEFER' EQ '').CANCEL       YM4019 04460002
.DEFER4  AIF   ('&REPLACE' EQ '').DEFER5                                04500020
         AIF   ('&REPLACE' EQ 'YES').DEFER5                             04600020
         AIF   ('&REPLACE' NE 'NO').ERROR1                       YM4019 04650002
&REPLSW  SETA  X'40'                                                    04700020
.DEFER5  ANOP                                                           04800020
         AIF   ('&NAME' EQ '' OR '&MF' EQ 'L').CONTA                    05000020
&NAME    DS    0H                                                       05500020
         AGO   .CONTA                                                   06000020
.ERROR   MNOTE 8,'INVALID REGISTER SPECIFICATION'                YM4019 06500002
         MEXIT                                                          07000020
.ERROR1  MNOTE 8,'INVALID OPTION SPECIFIED'                      YM4019 07500002
         MEXIT                                                          08000020
.CONTA   ANOP                                                           08500020
         AIF   ('&DEFER' EQ '').DEFER1       IS DEFER REQUESTED         08540020
         AIF   ('&DEFER' EQ 'YES').DEFER2    SHOULD DEFER ATTN          08580020
         AIF   ('&DEFER' EQ 'NO').DEFER3     SHOULD CANCEL DEFER ATTN   08620020
         AGO   .ERROR1        INVALID OPERAND HAS BEEN SPECIFIED        08660020
.DEFER2  ANOP                 SET DEFER SWITCH EQUAL TO                 08700020
&DEFRSW  SETA  X'20'          DEFER/YES OR X'20'                        08740020
         AGO   .DEFER1                                                  08780020
.DEFER3  ANOP                 SET DEFER SWITCH EQUAL TO                 08820020
&DEFRSW  SETA  X'10'          DEFER/NO OR X'10'                         08860020
.DEFER1  ANOP                                                           08900020
         AIF   ('&MF' EQ 'L').LROUT      IS THIS A LIST REQUEST         09000020
         AIF   ('&MF' EQ 'I').IROUT      IS THIS A STANDARD REQUEST     09500002
         AIF   ('&MF(1)' EQ 'E').EROUT     IS THIS A EXECUTE REQUEST    10000002
.ERROR2  MNOTE 8,'INVALID MF SPECIFICATION'                      YM4019 10500002
         MEXIT                                                          11000020
.EROUT   ANOP                                                           11500020
         AIF   ('&MF'(4,1) EQ '(').ISAREG                               12000020
         LA    1,&MF(2)      ADDRESS OF PARAMETER LIST INTO REG 1       12500020
         AGO   .CONTB                                                   13000020
.ISAREG  AIF   ('&MF(2)' EQ '(1)').CONTB                                13500020
&A       SETA  K'&MF-6                                                  14000020
&H       SETC  '&MF'(5,&A)                                              14500020
         LR    1,&H          ADDRESS OF PARAMETER LIST INTO REG 1       15000020
.CONTB   ANOP                                                           15500020
         AIF   ('&EXADDR' EQ '').CONTC                                  15700020
         AIF   ('&EXADDR'(1,1) EQ '(').ISAREG1                          16000020
         LA    14,&EXADDR    ADDRESS OF EXIT ROUTINE                    16500020
         ST    14,0(1,0)     INTO PARAMETER LIST                        17000020
         AGO   .CONTC                                                   17500020
.ISAREG1 ST    &EXADDR(1),0(1,0)  ADDRESS OF EXIT ROUTINE INTO LIST     18000020
.CONTC   AIF   ('&USADDR' EQ '').CONTD                                  18500020
         AIF   ('&USADDR'(1,1) EQ '(').ISAREG2                          19000020
         IC    15,16(1,0)    SAVE OPTION BYTE                   @YM2296 19050002
         LA    14,&USADDR    ADDRESS OF USER PARAMETERS                 19500020
         ST    14,16(1,0)    INTO LIST                                  20000020
         STC   15,16(1,0)    RESTORE OPTION BYTE                @YM2296 20050002
         AGO   .CONTD                                                   20500020
.ISAREG2 IC    15,16(1,0)         SAVE OPTION BYTE              @YM2296 20550002
         ST    &USADDR(1),16(1,0) ADDRESS OF USER PARAMETERS INTO       21000002
*                                 LIST                                  21500020
         STC   15,16(1,0)         RESTORE OPTION BYTE           @YM2296 21550002
.CONTD   AIF   ('&OBUF(1)' EQ '').CNTSUB1                               22000020
         AIF   ('&OBUF(1)'(1,1) EQ '(').IRGSUB1                         22500020
         LA    14,&OBUF(1)   ADDRESS OF OUTPUT BUFFERS                  23000020
         ST    14,8(1,0)     INTO LIST                                  23500020
         AGO   .CNTSUB1                                                 24000020
.IRGSUB1 ST   &OBUF(1),8(1,0)  ADDRESS OF OUTPUT BUFFERS INTO LIST      24500020
.CNTSUB1 AIF  ('&OBUF(2)' EQ '').CONTE                                  25000020
         AIF   ('&OBUF(2)'(1,1) EQ '(').IRGSUB2                         25500020
         LA    14,&OBUF(2)(0,0) LENGTH OF OUTPUT BUFFERS        @YM2296 26000002
         STH   14,6(1,0)     INTO LIST                          @YM2296 26500002
         AGO   .CONTE                                                   27000020
.IRGSUB2 STH  &OBUF(2),6(1,0) LENGTH OF OUTPUT BUFFERS IN LIST  @YM2296 27500002
.CONTE   AIF   ('&IBUF(1)' EQ '').CNTSUB2                               28000020
         AIF   ('&IBUF(1)'(1,1) EQ '(').IRGSUB3                         28500020
         LA    14,&IBUF(1)    ADDRESS OF INPUT BUFFERS                  29000020
         ST    14,12(1,0)    INTO LIST                                  29500020
         AGO   .CNTSUB2                                                 30000020
.IRGSUB3 ST   &IBUF(1),12(1,0)    ADDRESS OF INPUT BUFFERS INTO LIST    30500020
.CNTSUB2 AIF  ('&IBUF(2)' EQ '').CONTG                                  31000020
         AIF   ('&IBUF(2)'(1,1) EQ '(').IRGSUB4                         31500020
         LA    14,&IBUF(2)(0,0) LENGTH OF INPUT BUFFERS         @YM2296 32000002
         STH   14,4(1,0)     INTO LIST                          @YM2296 32500002
         AGO   .CONTG                                                   33000020
.IRGSUB4 STH  &IBUF(2),4(1,0) LENGTH OF INPUT BUFFERS IN LIST   @YM2296 33500002
.CONTG   AIF   ('&REPLACE' EQ '').CNTSUB5                               34000002
         AIF   ('&REPLACE' EQ 'YES').CNTSUB6                    @YM1829 34500002
         OI    16(1),&REPLNO      SET REPLACE NO                 YM4019 34550002
         AGO   .CNTSUB5                                          YM4019 34600002
.CNTSUB6 NI    16(1),&REPLYES     INDICATE REPLACE PREVIOUS STAX YM4019 34650002
.CNTSUB5 AIF   ('&DEFER' EQ '').CNTSUB7                          YM4019 34700002
         NI    16(1),&CLEAR       CLEAR DEFER SWITCHES           YM4019 34750002
         OI    16(1),&DEFRSW     SET DEFER SWITCHES              YM4019 34800002
.CNTSUB7 AGO   .SVC                                                     37000002
.LROUT   ANOP                                                           37500020
&NAME    DS    0F                                                       38000020
.CONTL4  AIF   ('&EXADDR' EQ '').DEFER6                                 38200020
         AIF   ('&EXADDR'(1,1) EQ '(').ERROR                            38400020
         DC    AL4(&EXADDR)  ADDRESS OF EXIT ROUTINE                    38450020
         AGO   .DEFER7                                                  38600020
.DEFER6  DC    AL4(0)         ADDRESS OF EXIT ROUTINE                   38800020
.DEFER7  ANOP                                                           39000020
.CNTL6   AIF   ('&IBUF(2)' EQ '').CONTL1                                39500020
         AIF   ('&IBUF(2)'(1,1) EQ '(').ERROR                           40000020
         DC    AL2(&IBUF(2)) LENGTH OF INPUT BUFFERS                    40500020
         AGO   .CONTL2                                                  41000020
.CONTL1  DC    AL2(0)        LENGTH OF INPUT BUFFERS                    41500020
.CONTL2  AIF   ('&OBUF(2)' EQ '').CONTL3                                42000020
         AIF   ('&OBUF(2)'(1,1) EQ '(').ERROR                           42500020
         DC    AL2(&OBUF(2)) LENGTH OF OUTPUT BUFFERS                   43000020
         AGO   .CONTLA                                                  43500020
.CONTL3  DC    AL2(0)        LENGTH OF OUTPUT BUFFERS                   44000020
.CONTLA  AIF   ('&OBUF(1)' EQ '').CONTLB                                44500020
         AIF   ('&OBUF(1)'(1,1) EQ '(').ERROR                           45000020
         DC    AL4(&OBUF(1))  ADDRESS OF OUTPUT BUFFERS                 45500020
         AGO   .CONTL8                                                  46000020
.CONTLB  DC    A(0)          ADDRESS OF OUTPUT BUFFERS                  46500020
.CONTL8  AIF   ('&IBUF(1)' EQ '').CONTL9                                47000020
         AIF   ('&IBUF(1)'(1,1) EQ '(').ERROR                           47500020
         DC    AL4(&IBUF(1))    ADDRESS OF INPUT BUFFERS                48000020
         AGO   .CONTL0                                                  48500020
.CONTL9  DC    A(0)          ADDRESS OF INPUT BUFFERS                   49000020
.CONTL0  DC    AL1(&REPLSW+&DEFRSW)    REPLACE/NO REPLACE, DEFERRAL IND 50000020
.CONTL6  AIF   ('&USADDR' EQ '').CONTL7                                 52000020
         AIF   ('&USADDR'(1,1) EQ '(').ERROR                            52500020
         DC    AL3(&USADDR)   ADDRESS OF USER PARAMETERS                53000020
         AGO   .ENDLIST                                                 53500020
.CONTL7  DC    AL3(0)        ADDRESS OF USER PARAMETERS                 54000020
.ENDLIST  ANOP                                                          54500020
         MEXIT                                                          55000020
.IROUT   ANOP                                                           55500020
         LA    1,&GNAME      ADDRESS OF PARAMETER LIST                  56000020
         AIF   ('&IBUF(2)' EQ '').IROUT1                                56500020
         AIF   ('&IBUF(2)'(1,1) NE '(').IROUT1                          57000020
         STH   &IBUF(2),4(1,0) LENGTH OF INPUT BUFFERS          @YM2296 57500002
.IROUT1  AIF   ('&OBUF(2)' EQ '').IROUT2                                58000020
         AIF   ('&OBUF(2)'(1,1) NE '(').IROUT2                          58500020
         STH   &OBUF(2),6(1,0) LENGTH OF OUTPUT BUFFERS         @YM2296 59000002
.IROUT2  AIF   ('&EXADDR' EQ '').IROUT3                                 59300020
         AIF   ('&EXADDR'(1,1) NE '(').IROUT3                           59600020
         ST    &EXADDR(1),0(1,0)  ADDRESS OF EXIT ROUTINE INTO LIST     60000020
.IROUT3  AIF   ('&USADDR' EQ '').IROUT4                                 60500020
         AIF   ('&USADDR'(1,1) NE '(').IROUT4                           61000020
         IC    15,16(1,0)         GET OPTIONS BYTE              @YM2296 61200002
         ST    &USADDR(1),16(1,0) ADDRESS OF USER PARAMETERS IN LIST    61500020
         STC   15,16(1,0)         PRESERVE OPTIONS BYTE         @YM2296 61700002
.IROUT4  AIF   ('&IBUF' EQ '').IROUT5                                   62000020
         AIF   ('&IBUF(1)'(1,1) NE '(').IROUT5                          62500020
         ST    &IBUF(1),12(1,0)   ADDRESS OF INPUT BUFFERS INTO LIST    63000020
.IROUT5  AIF   ('&OBUF' EQ '').IROUT6                                   63500020
         AIF   ('&OBUF(1)'(1,1) NE '(').IROUT6                          64000020
         ST    &OBUF(1),8(1,0)    ADDRESS OF OUTPUT BUFFERS INTO LIST   64500020
.IROUT6  ANOP                                                           65000020
         B     20(0,1)                                                  65500020
&GNAME   DS    0F                                                       66000020
.CNTD    AIF   ('&EXADDR' EQ '').CNTE                                   66300020
         AIF   ('&EXADDR'(1,1) EQ '(').CNTE                             66600020
         DC    AL4(&EXADDR)  ADDRESS OF EXIT ROUTINE                    67000020
         AGO  .CONTF2                                                   67500020
.CNTE    DC    AL4(0)   ADDRESS OF EXIT ROUTINE                         68000020
.CONTF2  AIF   ('&IBUF(2)' EQ '').CNTA                                  68500020
         AIF   ('&IBUF(2)'(1,1) EQ '(').CNTA                            69000020
         DC    AL2(&IBUF(2)) LENGTH OF INPUT BUFFERS                    69500020
         AGO   .CNTB                                                    70000020
.CNTA    DC    AL2(0)        LENGTH OF INPUT BUFFERS                    70500020
.CNTB    AIF   ('&OBUF(2)' EQ '').CNTC                                  71000020
         AIF   ('&OBUF(2)'(1,1) EQ '(').CNTC                            71500020
         DC    AL2(&OBUF(2))  LENGTH OF OUTPUT BUFFERS                  72000020
         AGO   .CNTK                                                    72500020
.CNTC    DC    AL2(0)        LENGTH OF OUTPUT BUFFERS                   73000020
.CNTK    AIF   ('&OBUF(1)' EQ '').CNTL                                  73500020
         AIF   ('&OBUF(1)'(1,1) EQ '(').CNTL                            74000020
         DC    AL4(&OBUF(1)) ADDRESS OF OUTPUT BUFFERS                  74500020
         AGO   .CNTI                                                    75000020
.CNTL    DC    A(0)          ADDRESS OF OUPTUT BUFFERS                  75500020
.CNTI    AIF   ('&IBUF(1)' EQ '').CNTJ                                  76000020
         AIF   ('&IBUF(1)'(1,1) EQ '(').CNTJ                            76500020
         DC    AL4(&IBUF(1)) ADDRESS OF INPUT BUFFERS                   77000020
         AGO   .CNTM                                                    77500020
.CNTJ    DC    A(0)          ADDRESS OF INPUT BUFFERS                   78000020
.CNTM    DC    AL1(&REPLSW+&DEFRSW)    REPLACE/NO REPLACE, DEFERRAL IND 79000020
.CNTF    AIF   ('&USADDR' EQ '').CNTH                                   80500020
         AIF   ('&USADDR'(1,1) EQ '(').CNTH                             81000020
         DC    AL3(&USADDR)  ADDRESS OF USER PARAMETERS                 81500020
         AGO   .SVC                                                     82000020
.CNTH    DC    AL3(0)        ADDRESS OF USER PARAMETERS                 82500020
         AGO   .SVC                                                     83000020
.CANCEL  ANOP                                                           83500020
&NAME    DS    0H                                                       84000020
         SR    1,1           CANCEL PREVIOUS STAX                       84500020
.SVC     SVC   96                                                       85000020
         MEND                                                           85500020
