         MACRO                                                          00050000
&NAME    STFSMODE &A,&INITIAL=NO,&RSHWKEY=64 PROTOTYPE                  00100000
.********************************************************************** 00150000
.*                                                                    * 00200000
.* STATUS - OS/VS2 TSO/VTAM DYNAMIC RESHOW LEVEL 2.0                  * 00250000
.*                                                                    * 00300000
.* NAME - STFSMODE  (SET FULL SCREEN MODE)                            * 00350000
.*                                                                    * 00400000
.* FUNCTION - THE STFSMODE MACRO IS USED TO SPECIFY WHETHER OR NOT    * 00450000
.*            AN APPLICATION IS PRESENTING DISPLAYS TO A TSO/VTAM     * 00500000
.*            DISPLAY TERMINAL IN FULL SCREEN MODE.                   * 00550000
.*            WHEN INITIALLY SETTING THE MODE ON, INITIAL=YES WILL    * 00560000
.*            PREVENT PAGING FOR THE FIRST FULL SCREEN TPUT ISSUED BY * 00570000
.*            THE APPLICATION.                                        * 00580000
.*            RSHWKEY IS USED TO SPECIFY WHICH PFKEY IS TO BE USED    * 00582000
.*            AS THE RESHOW KEY.  E.G., RSHWKEY=3 MEANS THAT PFKEY 3  * 00584000
.*            IS TO BE THE RESHOW KEY.  WHEN STFSMODE IS OFF,         * 00586000
.*            RSHWKEY IS NOT TO BE SPECIFIED.  WHEN STFSMODE IS ON    * 00588000
.*            AND NO RSHWKEY HAS BEEN SPECIFIED, RSHWKEY WILL DEFAULT * 00590000
.*            TO ZERO.                                                * 00592000
.*                                                                    * 00600000
.*       THE MACRO PROTOTYPE IS ABOVE                                 * 00650000
.*                                                                    * 00700000
.*       THE STANDARD FORM OF THE EXPANSION IS BELOW                  * 00750000
.*&NAME  LA    1,128+64                 FLAGS FOR MODE/INITIAL CALL   * 00800000
.*       SLL   1,24                     SHIFT TO HIGH ORDER BYTE      * 00850000
.*       SR    0,0                      CLEAR REGISTER                * 00860000
.*       LA    0,10                     LOAD RSHWKEY                  * 00870000
.*       OR    1,0                      RSHWKEY IN RIGHTMOST BYTE     * 00880000
.*       LA    0,18                     LOAD ENTRY CODE               * 00900000
.*       SLL   0,24                     SHIFT TO HIGH ORDER BYTE      * 00950000
.*       SVC   94                       TERMINAL CONTROL MACRO SVC    * 01000000
.*                                                                    * 01050000
.********************************************************************** 01100000
         LCLA  &SYSFS                                                   01105000
         LCLA  &SYSINIT                                                 01110000
&SYSFS   SETA  128                                                      01115000
&SYSINIT SETA  0                                                        01120000
         AIF   ('&INITIAL' NE 'YES' AND '&INITIAL' NE 'NO').ERROR1      01125000
         AIF   (&RSHWKEY GT 0 AND &RSHWKEY LT 13).TESTFS                01125700
         AIF   (&RSHWKEY NE 64).ERROR2                                  01126100
         AGO   .NEXT                                                    01127100
.TESTFS  ANOP                                                           01127800
         AIF   ('&A' EQ 'OFF').ERROR3                                   01128500
.NEXT    ANOP                                                           01129200
         AIF   ('&INITIAL' EQ 'NO').TESTA                               01130000
&SYSINIT SETA  64                                                       01135000
.TESTA   ANOP                                                           01140000
         AIF   ('&A' EQ 'ON' OR '&A' EQ '').FSON                        01150000
         AIF   ('&A' EQ 'OFF').FSOFF                                    01200000
.ERROR   IHBERMAC 36,,&A                                                01250000
         MEXIT                                                          01300000
.ERROR1  IHBERMAC 54,,,                                                 01310000
         MEXIT                                                          01320000
.ERROR2  IHBERMAC 54,,RSHWKEY                                           01326000
         MEXIT                                                          01332000
.ERROR3  IHBERMAC 1020,RSHWKEY,&A                                       01338000
         MEXIT                                                          01344000
.FSON    ANOP                                                           01350000
&NAME    LA    1,&SYSFS+&SYSINIT        FLAGS FOR MODE/INITIAL CALL     01400000
         SLL   1,24                     SHIFT TO HIGH ORDER BYTE        01450000
         AGO   .PFCD                                                    01500000
.FSOFF   ANOP                                                           01550000
&NAME    SR    1,1                      CLEAR PARAMETER REGISTER        01600000
.PFCD    ANOP                                                           01608000
         AIF   ('&A' EQ 'OFF').ENTRYCD                                  01616000
         AIF   (&RSHWKEY EQ 64).ENTRYCD                                 01620000
         SR    0,0                      CLEAR REGISTER                  01624000
         LA    0,&RSHWKEY               LOAD RSHWKEY                    01632000
         OR    1,0                      RSHWKEY IN RIGHTMOST BYTE       01638000
.ENTRYCD ANOP                                                           01650000
         LA    0,18                     LOAD ENTRY CODE                 01700000
         SLL   0,24                     SHIFT TO HIGH ORDER BYTE        01750000
         SVC   94                       TERMINAL CONTROL MACRO SVC      01800000
         MEND                                                           01850000
