         MACRO                                                          00050000
&NAME    TPG   &BFF,&SIZE,&EDIT,&WAIT,&HOLD,&BRKI,&PRTY,&MF=            00100000
.* A000000-999999                                              @G76XRYD 00150000
.*D107500-110500                                               @OZ65329 00175036
.*                                                                      00200000
         LCLA  &FLAGON,&FLAGOFF,&FLAGS2,&OPT,&RET                       00250000
         LCLB  &E,&W,&H,&B,&P,&NOEDIT                                   00300000
         LCLC  &PARM,&NDX                                               00350000
&NDX     SETC  '&SYSNDX'                                                00400000
         AIF   (N'&SYSLIST LE 7).POSOPOK .IF TOO MANY OPERANDS          00450000
         MNOTE 12,'IHB300 EXCESSIVE POSITIONAL OPERANDS SPECIFIED'      00500000
         MEXIT                                                          00550000
.POSOPOK ANOP                         ..POSITIONAL OPERANDS OK          00600000
         AIF   ('&MF' EQ '').FORMOK     NON-EXTENDED FORM OK            00650000
         AIF   ('&MF' EQ 'I' OR '&MF' EQ 'L' OR ('&MF(1)' EQ 'E' AND N'*00700000
               &MF EQ 2)).FORMOK     ...FORMAT OPERANDS OK...           00750000
         MNOTE 12,'IHB303 INVALID OPERAND MF=&MF '                      00800000
         MEXIT                                                          00850000
.FORMOK  ANOP                                                           00900000
         AIF   (('&BFF' EQ '' OR '&SIZE' EQ '') AND '&MF' EQ '').ERROR1 00950000
         AIF   ('&SIZE' EQ '' AND '&MF' EQ 'I').SIZERR                  01000000
         AIF   ('&BFF' NE '' OR '&MF' NE 'I').BFFEXOK                   01050000
         MNOTE 12,'IHB300 BUFFER ADDRESS NOT SPECIFIED'                 01100000
         MEXIT                                                          01150000
.BFFEXOK ANOP              ...BUFFER ADDR GIVEN W/ MF=I,E,L             01200000
&FLAGOFF SETA  127                   ...SET FOR AND OPERATION           01250000
&FLAGOFF SETA  &FLAGOFF-64            ..INSURE USER ID FLAG             01300000
.*                                      TURNED OFF FOR EX FORM          01350000
&PARM    SETC  '&EDIT'                                                  01400000
.CKPARM  ANOP                                                           01450000
&RET     SETA  &RET+1                                                   01500000
         AIF   ('&PARM' EQ '').RET           NULL, TRY NEXT             01550000
         AIF   ('&PARM' EQ 'EDIT').EDI       SET EDIT                   01600000
         AIF   ('&PARM' EQ 'ASIS').ASI       SET ASIS                   01650000
         AIF   ('&PARM' EQ 'CONTROL').CON    SET CONTROL                01700000
         AIF   ('&PARM' EQ 'FULLSCR').FULLS  SET FULL SCREEN            01750000
         AIF   ('&PARM' EQ 'NOEDIT').NOED    SET NO EDIT MODE           01800000
         AIF   ('&PARM' EQ 'WAIT').WAI       SET WAIT                   01850000
         AIF   ('&PARM' EQ 'NOWAIT').NOW     SET NOWAIT                 01900000
         AIF   ('&PARM' EQ 'HOLD').HOL       SET HOLD                   01950000
         AIF   ('&PARM' EQ 'NOHOLD').NOH     SET NOHOLD                 02000000
         AIF   ('&PARM' EQ 'NOBREAK').NOB    SET NOBREAK                02050000
         AIF   ('&PARM' EQ 'BREAKIN').BRE    SET BREAKIN                02100000
         AIF   ('&PARM' EQ 'HIGHP').HPR      SET HIGHP                  02150000
         AIF   ('&PARM' EQ 'LOWP').LPR       SET LOWP                   02200000
         AGO   .ERROR3                                                  02250000
.EDI     ANOP                                                           02300000
         AIF   (&E).ERROR2              DUP OPTION                      02350000
&E       SETB  1                        EDIT OPTION SPECFIED            02400000
&FLAGOFF SETA  &FLAGOFF-3               EDIT FOR EXECUTE FORM           02450000
         AGO   .EDDONE                                                  02500000
.ASI     ANOP                                                           02550000
         AIF   (&E).ERROR2              DUP OPTION                      02600000
&E       SETB  1                        EDIT OPTION SPECFIED            02650000
&FLAGOFF SETA  &FLAGOFF-2               1ST BIT OF ASIS FOR EX          02700000
&FLAGON  SETA  &FLAGON+1                2ND BIT OF ASIS FOR EX          02750000
&OPT     SETA  &OPT+1                   SET EDIT=ASIS                   02800000
         AGO   .EDDONE                                                  02850000
.CON     ANOP                                                           02900000
         AIF   (&E).ERROR2              DUP OPTION                      02950000
&E       SETB  1                        EDIT OPTION SPECFIED            03000000
&OPT     SETA  &OPT+2                   SET EDIT=CONTROL                03050000
&FLAGOFF SETA  &FLAGOFF-1               1ST BIT OF CNTR FOR EX          03100000
&FLAGON  SETA  &FLAGON+2                2ND BIT OF CNTR FOR EX          03150000
         AGO   .EDDONE                                                  03200000
.FULLS   ANOP                                                           03250000
         AIF   (&E).ERROR2              DUP OPTION                      03300000
&E       SETB  1                        EDIT OPTION SPECIFIED           03350000
&OPT     SETA  &OPT+3                   CNTL+ASIS FOR FULLSCR           03400000
&FLAGON  SETA  &FLAGON+3                SET FULLSCREEN FOR EX           03450000
         AGO   .EDDONE                                                  03500000
.NOED    ANOP                                                           03550000
         AIF   (&E).ERROR2              DUP OPTION                      03600000
&E       SETB  1                                                        03650000
&NOEDIT  SETB  1                                                        03700000
&OPT     SETA  &OPT+3                   FLAGS1 TO FULLSCR STAN          03750000
&FLAGS2  SETA  3                        SET FLAGS2 TO NOEDIT AND CD     03800000
&FLAGON  SETA  &FLAGON+3                FLAGS1 TO FULLSCR EXEC          03850000
.EDDONE  ANOP                      EDIT OPERAND IN RIGHT PLACE          03900000
         AIF   (&RET NE 1).BADPOS  ..IF NOT IN 3RD POS-WARNING          03950000
         AGO   .RET                ..ELSE OK                            04000000
.WAI     ANOP                                                           04050000
         AIF   (&W).ERROR2              DUP OPTION                      04100000
&W       SETB  1                        WAIT OPTION SPECIFIED           04150000
&FLAGOFF SETA  &FLAGOFF-16              SET WAIT OPTION IN EX           04200000
         AGO   .WAIDONE                 CHECK POSITION                  04250000
.NOW     ANOP                                                           04300000
         AIF   (&W).ERROR2              DUP OPTION                      04350000
&W       SETB  1                        WAIT OPTION SPECIFIED           04400000
&OPT     SETA  &OPT+X'10'               SET WAIT=NOWAIT                 04450000
&FLAGON  SETA  &FLAGON+16               SET FOR NOWAIT IN EXEC          04500000
.WAIDONE ANOP                       ..WAIT IN RIGHT POSITION            04550000
         AIF   (&RET NE 2).BADPOS   ..IF NOT 4TH, WARNING               04600000
         AGO   .RET                 ..ELSE OK                           04650000
.HOL     ANOP                                                           04700000
         AIF   (&H).ERROR2              DUP OPTION                      04750000
&H       SETB  1                        HOLD OPTION SPECIFIED           04800000
&OPT     SETA  &OPT+X'08'               SET HOLD=HOLD                   04850000
&FLAGON  SETA  &FLAGON+8                SET FOR HOLD IN EXEC            04900000
         AGO   .HOLDONE                 CHECK POSITION                  04950000
.NOH     ANOP                                                           05000000
         AIF   (&H).ERROR2              DUP OPTION                      05050000
&H       SETB  1                        NOHOLD OPTION SPECIFIED         05100000
&FLAGOFF SETA  &FLAGOFF-8               SET FOR NOHOLD IN EXEC          05150000
.HOLDONE ANOP                        ..CHECK HOLD POSITION              05200000
         AIF   (&RET NE 3).BADPOS    ..IF ^ 5TH OPER, WARNING           05250000
         AGO   .RET                  ..ELSE OK                          05300000
.BRE     ANOP                                                           05350000
         AIF   (&B).ERROR2              DUP OPTION                      05400000
&B       SETB  1                        BREAK OPTION SPECIFIED          05450000
&OPT     SETA  &OPT+X'04'               SET BREAKIN                     05500000
&FLAGON  SETA  &FLAGON+4                SET BREAKIN IN EXEC             05550000
         AGO   .BRDONE                  CHECK BREAK POSITION            05600000
.NOB     ANOP                                                           05650000
         AIF   (&B).ERROR2              DUP OPTION                      05700000
&B       SETB  1                        BREAK OPTION SPECIFIED          05750000
&FLAGOFF SETA  &FLAGOFF-4               SET NOBREAK OPTION EX           05800000
.BRDONE  ANOP                           CHECK BREAK POSITION            05850000
         AIF   (&RET NE 4).BADPOS    ..IF NOT THE 6TH OPERAND           05900000
         AGO   .RET                  ..ELSE OK                          05950000
.HPR     ANOP                                                           06000000
         AIF   (&P).ERROR2              DUP OPTION                      06050000
&P       SETB  1                        PRIORITY OPTION SPECIFIED       06100000
&FLAGOFF SETA  &FLAGOFF-32              SET HI PRIO OPTION EX           06150000
         AGO   .PRDONE                  CHECK POSITION                  06200000
.LPR     ANOP                                                           06250000
         AIF   (&P).ERROR2              DUP OPTION                      06300000
&P       SETB  1                        PRIORITY OPTION SPECIFIED       06350000
&OPT     SETA  &OPT+X'20'               SET  LOW PRIORITY OPTION        06400000
&FLAGON  SETA  &FLAGON+32               SET LOW PRI OPTION EX           06450000
.PRDONE  ANOP                     ..CHECK PRIORITY OPERAND POS          06500000
         AIF   (&RET NE 5).BADPOS   ..IF ^7TH OPERAND,WARNING           06550000
         AGO   .RET                 ..ELSE OK                           06600000
.BADPOS  ANOP             ..ISSUE WARNING IF OPERAND MISPLACED          06650000
         MNOTE 4,'IHB300 WARNING: &PARM POSITIONAL PARAMETER MISPLACED' 06700000
.RET     ANOP                                                           06750000
&PARM    SETC  '&WAIT'                                                  06800000
         AIF   ('&RET' EQ '1').CKPARM   CHECK WAIT                      06850000
&PARM    SETC  '&HOLD'                                                  06900000
         AIF   ('&RET' EQ '2').CKPARM   CHECK HOLD                      06950000
&PARM    SETC  '&BRKI'                                                  07000000
         AIF   ('&RET' EQ '3').CKPARM   CHECK BRKI                      07050000
&PARM    SETC  '&PRTY'                                                  07100000
         AIF   ('&RET' EQ '4').CKPARM   CHECK PRTY                      07150000
.*  PARAMETERS PROCESSED                                                07200000
         AIF   (&E).EDITSPE             EDIT OPERAND SPECIFIED?         07250000
&OPT     SETA  &OPT+3                   FLAGS1 TO FULLSCR STAN          07300000
&FLAGS2  SETA  3                        SET FLAGS2 TO NOEDIT AND CD     07350000
&FLAGON  SETA  &FLAGON+3                FLAGS1 TO FULLSCR EXEC          07400000
         AGO   .NEDTSPE                 NOEDIT DEFAULTS                 07450000
.EDITSPE AIF   (&NOEDIT).NEDTSPE        NOEDIT SPECIFIED?               07500000
         MNOTE 12,'IHB300 EDIT OPERAND MUST BE ''NOEDIT'' ON TPG MACRO' 07550000
         MEXIT                                                          07600000
.NEDTSPE ANOP                           NOEDIT SPECIFIED OR DEFAULTS    07650000
         AIF   ('&MF' EQ 'L').LFORM       ...LIST EXPANSION...          07700000
         AIF   ('&MF' EQ 'I' OR '&MF' EQ '').STFORM .ST. FORM.          07750000
.******************                                                     07800000
.*  EXECUTE FORM  *                                                     07850000
.******************                                                     07900000
&NAME    CNOP  0,4                          TPUT EXECUTE FORM           07950000
         AIF   ('&MF(2)'(1,1) EQ '(').MFREG  IF IN RX FORM  ..          08000000
         LA    1,&MF(2)                     R1=>USER PARM LIST          08050000
         AGO   .GOTPARM                                                 08100000
.MFREG   ANOP                             .. ELSE, IN A REG ..          08150000
         AIF   ('&MF(2)' EQ '(1)').GOTPARM ..IF NOT REG 1..             08200000
         LR    1,&MF(2)                     POINT R1 AT PARMS           08250000
.GOTPARM ANOP                                                           08300000
         AIF   (&FLAGOFF EQ 127).SKIPAND ..TURN OFF ANY FLAGS?          08350000
         NI    13(1),&FLAGOFF               RESET FLAGS 1               08400000
.SKIPAND ANOP                      ..CHECK FOR FLAGS TURNED ON          08450000
         AIF   (&FLAGON EQ 0).NOSET  ..TURN ON ANY FLAGS?               08500000
         OI    13(1),&FLAGON                IN EXEC PARM LIST           08550000
.NOSET   ANOP                                                           08600000
         MVI   12(1),&FLAGS2                RESET FLAGS2                08650000
         AGO   .PARMCHK                 ..END UNIQUE E CODE..           08700000
.****************************                                           08750000
.*  STANDARD EXTENDED FORM  *                                           08800000
.****************************                                           08850000
.STFORM  ANOP                   ..STANDARD EXTENDED FORM CODE.          08900000
&NAME    CNOP  0,4                          STANDARD EXTENDED           08950000
         BAL   1,*+20                       BRANCH AROUND PARM          09000000
TPG&NDX  DS    0H                           TPUT EXTENDED PARM          09050000
         DC    H'0'                         RESERVED                    09100000
         DC    H'0'                         OUTPUT BUFFER SIZE          09150000
         DC    A(0)                         OUTPUT BUFFER ADDR          09200000
         DC    A(0)                         RESERVED                    09250000
         DC    AL1(&FLAGS2)                 FLAGS 2                     09300000
         DC    AL1(&OPT)                    FLAGS 1                     09350000
         DC    H'0'                         RESERVED BYTES              09400000
.************************************************                       09450000
.*  COMMON CODE FOR STANDARD AND EXECUTE FORMS  *                       09500000
.************************************************                       09550000
.PARMCHK ANOP                           ..GET PARMS FOR E & I           09600000
         AIF   ('&SIZE' EQ '').CKID     ...IF BUFF SIZE GIVEN.          09650000
         AIF   ('&SIZE'(1,1) EQ '(').SIZEREG  ...IF AN INTEGER          09700000
         AIF   (T'&SIZE NE 'N').SIZEQU  BUFSIZE IS EQUATE               09750000
         AIF   (&SIZE GT 32767).SIZERR          .NOT > 32767..          09800000
.SIZEQU  ANOP                                                           09850000
         AIF   ('&MF' NE 'I' AND '&MF' NE '').SIZEEX .STANDARD          09900000
         ORG   TPG&NDX+2                    PUT SIZE VALUE              09950000
         DC    AL2(&SIZE)                   IN PARM LIST                10000000
         ORG                                                            10050000
         AGO   .CKID                                                    10100000
.SIZEEX  ANOP                         ..IF EXECUTE                      10150000
         B     *+6                          BR PAST BUFSIZE             10200000
         DC    AL2(&SIZE)                   BUFSIZE                     10250000
         LH    0,*-2                        GET THE SIZE VALUE          10300000
         STH   0,2(1)                       INTO THE PARM LIST          10350000
         AGO   .CKID                                                    10400000
.SIZEREG ANOP                                 ...ELSE A REG ..          10450000
         AIF   ('&SIZE(1)' EQ '0' OR '&SIZE(1)' EQ '1').REGBAD          10500000
         STH   &SIZE(1),2(1)                PUT SIZE IN PARM            10550000
.CKID    ANOP                           ... CHECK IDS ...               10600000
         AIF   ('&BFF' EQ '').FINISH    ..IF BUFFER ADDR GIVEN          10650000
         AIF   ('&BFF'(1,1) EQ '(').BFFREG  ..IF AN RX ADDR...          10700000
         LA    0,&BFF                       GET ADDR OF BUFFER          11100000
         ST    0,4(1)                       PUT IN PARM LIST            11150000
         AGO   .FINISH                                                  11200000
.BFFREG  ANOP                               ..ELSE IN A REG ..          11250000
         AIF   ('&BFF(1)' EQ '0' OR '&BFF(1)' EQ '1').REGBAD            11300000
         ST    &BFF(1),4(1)                 STORE BUFFER ADDR           11350000
.FINISH  ANOP                                                           11400000
         OI    12(1),128                    SET HI BIT OF PARM          11450000
         ICM   0,B'1000',*-3                SET HI BIT OF R0            11500000
.*                                        FROM 128 IN OI INSTR          11550000
.SVCNEW  ANOP                           .. ISSUE SVC HERE ...           11600000
         SVC   93                           SVC TO MSG ROUTER           11650000
         MEXIT                                                          11700000
.**********************                                                 11750000
.*  LIST FORM CODE    *                                                 11800000
.**********************                                                 11850000
.LFORM   ANOP                                                           11900000
&NAME    DS    0F                           L-FORM PARM LIST            11950000
         DC    H'0'                         RESERVED                    12000000
         AIF   ('&SIZE' NE '').LSTSIZE  .. IF NO BUFFER SIZE .          12050000
         DC    H'0'                         BUFF SIZE HALFWORD          12100000
         AGO   .LBFADDR                                                 12150000
.LSTSIZE ANOP                           .. ELSE A BUFFER SIZE.          12200000
         AIF   ('&SIZE'(1,1) EQ '(').REGERR   .IF NOT IN A REG          12250000
         AIF   (T'&SIZE NE 'N').SIZEQU0 IF BUFSIZE IS EQUATE            12300000
         AIF   (&SIZE GT 32767).SIZERR          .NOT > 32767..          12350000
.SIZEQU0 ANOP                                                           12400000
         DC    AL2(&SIZE)                   BUFFER SIZE VALUE           12450000
.LBFADDR ANOP                           .. CHECK BUFFER ADDR..          12500000
         AIF   ('&BFF' NE '').LBFFLAB   ..IF NO BUFFER ADDR..           12550000
         DC    A(0)                         BUFFER ADDRESS              12600000
         AGO   .LSTUSER                                                 12650000
.LBFFLAB ANOP                           .. ELSE BUFFER ADDRESS          12700000
         AIF   ('&BFF'(1,1) EQ '(').REGERR  ..IF NOT IN A REG           12750000
         DC    A(&BFF)                      ADDR OUTPUT BUFFER          12800000
.LSTUSER ANOP                                                           12850000
         DC    A(0)                         RESERVED                    12900000
         DC    AL1(&FLAGS2)                 FLAGS 2                     12950000
         DC    AL1(&OPT)                    FLAGS 1                     13000000
         DC    H'0'                         RESERVED                    13050000
         MEXIT                                                          13100000
.************                                                           13150000
.*  MNOTES  *                                                           13200000
.************                                                           13250000
.REGERR  ANOP                              ..REGISTER ERROR..           13300000
         MNOTE 12,'IHB300 INCOMPATIBLE OPERANDS: MF=L, REGISTER OPERANDX13350000
                SPECIFIED'                                              13400000
         MEXIT                                                          13450000
.REGBAD  MNOTE 12,'IHB300 BUFADDR AND BUFSIZE REGS CANNOT BE 0 OR 1'    13500000
         MEXIT                                                          13550000
.SIZERR  MNOTE 12,'IHB300 SIZE NOT IN RANGE 0-32767'                    13600000
         MEXIT                                                          13650000
.ERROR1  IHBERMAC 24                                                    13700000
         MEXIT                                                          13750000
.ERROR2  IHBERMAC 54,,,                                                 13800000
         MEXIT                                                          13850000
.ERROR3  IHBERMAC 49,,&PARM                                             13900000
         MEND                                                           13950000
