         MACRO                                                          00400003
&NAME    TPUT  &BFF,&SIZE,&EDIT,&WAIT,&HOLD,&BRKI,&PRTY,&TJID=,        *00450003
               &TJIDLOC=,&ASID=,&ASIDLOC=,&USERIDL=,&MF=       @G76XRYU 00500003
.* A000000-999999                                              @G76XR00 00550003
.* A770501,896051,896076,896158,896167,960000                  @OZ43223 00600003
.* C896050,896054,896063-896064,896075,896089,896157,896159,   @OZ43223 00650003
.* C896166,896168                                              @OZ43223 00700003
.* D896058-896062,896083-896087                                @OZ43223 00750003
.* C204500,206000-207000,216500,218000-218500,D219000          @OZ44359 00770000
.*A167500                                                      @OZ49617 00770136
.*C204500,206000,207500,216500,218000,218300                   @OZ48113 00770236
.*C008500,167600-167900                                        @OZ56262 00780136
.*A175500                                                      @OZ56262 00790036
.*C237500-239500,241500                                        @OZ65329 00793336
.*D140500                                                      @OZ65329 00796636
.*A037500,044500,196500                                        @OZ67665 00798336
.*                                                                      00800003
         LCLA  &FLAGON,&FLAGOFF,&FLAGS2,&OPT,&NTJ,&RET,&BFFPOS @OZ56262 00850036
         LCLB  &E,&W,&H,&B,&P,&OLDBUFF                         @G76XRYU 00900003
         LCLC  &TSID,&TSIDLOC,&PARM,&NDX,&ID                   @G76XRYU 00950003
&NDX     SETC  '&SYSNDX'                                       @G76XRYU 01000003
         AIF   (N'&SYSLIST LE 7).POSOPOK .IF TOO MANY OPERANDS @G76XRYU 01050003
         MNOTE 12,'IHB300 EXCESSIVE POSITIONAL OPERANDS SPECIFIED'      01100003
         MEXIT                                                 @G76XRYU 01150003
.POSOPOK ANOP                         ..POSITIONAL OPERANDS OK @G76XRYU 01200003
         AIF   ('&MF' EQ '').FORMOK     NON-EXTENDED FORM OK   @G76XRYU 01250003
         AIF   ('&MF' EQ 'I' OR '&MF' EQ 'L' OR ('&MF(1)' EQ 'E' AND N'*01300003
               &MF EQ 2)).FORMOK     ...FORMAT OPERANDS OK...  @G76XRYU 01350003
         MNOTE 12,'IHB303 INVALID OPERAND MF=&MF '             @G76XRYU 01400003
         MEXIT                                                 @G76XRYU 01450003
.FORMOK  ANOP                                                  @G76XRYU 01500003
         AIF   (('&BFF' EQ '' OR '&SIZE' EQ '') AND '&MF' EQ '').ERROR1 01550003
         AIF   ('&SIZE' EQ '' AND '&MF' EQ 'I').SIZERR         @G76XRYU 01600003
         AIF   ('&BFF' NE '' OR '&MF' NE 'I').BFFEXOK          @G76XRYU 01650003
         MNOTE 12,'IHB300 BUFFER ADDRESS NOT SPECIFIED'        @G76XRYU 01700003
         MEXIT                                                 @G76XRYU 01750003
.BFFEXOK ANOP              ...BUFFER ADDR GIVEN W/ MF=I,E,L    @G76XRYU 01800003
&ID      SETC  '0'                                             @G76XRYU 01850003
&FLAGOFF SETA  127                   ...SET FOR AND OPERATION  @G76XRYU 01900003
&NTJ     SETA  N'&TJID+N'&TJIDLOC+N'&ASID+N'&ASIDLOC+N'&USERIDL         01950003
         AIF   (&NTJ GT 1).ERROR2                                       02000003
         AIF   (&NTJ EQ 0).KEEPID    ..IF ANY ID SPECIFIED ..  @G76XRYU 02050003
&FLAGOFF SETA  &FLAGOFF-64            ..INSURE USER ID FLAG    @G76XRYU 02100003
.*                                      TURNED OFF FOR EX FORM @G76XRYU 02150003
.KEEPID  ANOP                        ..ELSE RETAIN PRESENT SET @G76XRYU 02200003
&TSID    SETC  '&ASID&TJID'                                             02250003
&TSIDLOC SETC  '&ASIDLOC&TJIDLOC'                                       02300003
         AIF   ('&EDIT' EQ 'R').RF      CHECK FOR R FORM                02350003
&PARM    SETC  '&EDIT'                                                  02400003
         AIF   ('&USERIDL' EQ '').CKPARM                                02450003
&OPT     SETA  X'40'                    DENOTE USERID SPECIFIED         02500003
&FLAGON  SETA  &FLAGON+64               TO OR USER ID FLAG     @G76XRYU 02550003
.CKPARM  ANOP                                                           02600003
&RET     SETA  &RET+1                                                   02650003
         AIF   ('&PARM' EQ '').RET           NULL, TRY NEXT             02700003
         AIF   ('&PARM' EQ 'EDIT').EDI       SET EDIT                   02750003
         AIF   ('&PARM' EQ 'ASIS').ASI       SET ASIS                   02800003
         AIF   ('&PARM' EQ 'CONTROL').CON    SET CONTROL                02850003
         AIF   ('&PARM' EQ 'FULLSCR').FULLS  SET FULL SCREEN    SA60002 02900003
         AIF   ('&PARM' EQ 'NOEDIT').NOED    SET NO EDIT MODE  @G76XRYU 02950003
         AIF   ('&PARM' EQ 'WAIT').WAI       SET WAIT                   03000003
         AIF   ('&PARM' EQ 'NOWAIT').NOW     SET NOWAIT                 03050003
         AIF   ('&PARM' EQ 'HOLD').HOL       SET HOLD                   03100003
         AIF   ('&PARM' EQ 'NOHOLD').NOH     SET NOHOLD                 03150003
         AIF   ('&PARM' EQ 'NOBREAK').NOB    SET NOBREAK                03200003
         AIF   ('&PARM' EQ 'BREAKIN').BRE    SET BREAKIN                03250003
         AIF   ('&PARM' EQ 'HIGHP').HPR      SET HIGHP                  03300003
         AIF   ('&PARM' EQ 'LOWP').LPR       SET LOWP                   03350003
         AGO   .ERROR3                                                  03400003
.EDI     ANOP                                                           03450003
         AIF   (&E).ERROR2              DUP OPTION                      03500003
&E       SETB  1                        EDIT OPTION SPECFIED            03550003
&FLAGOFF SETA  &FLAGOFF-3               EDIT FOR EXECUTE FORM  @G76XRYU 03600003
         AGO   .EDDONE                                         @G76XRYU 03650003
.ASI     ANOP                                                           03700003
         AIF   (&E).ERROR2              DUP OPTION                      03750003
         AIF   (&NTJ EQ 1).ASIDER3      CANT CODE ASID+ USERID @OZ67665 03775036
&E       SETB  1                        EDIT OPTION SPECFIED            03800003
&FLAGOFF SETA  &FLAGOFF-2               1ST BIT OF ASIS FOR EX @G76XRYU 03850003
&FLAGON  SETA  &FLAGON+1                2ND BIT OF ASIS FOR EX @G76XRYU 03900003
&OPT     SETA  &OPT+1                   SET EDIT=ASIS                   03950003
         AGO   .EDDONE                                         @G76XRYU 04000003
.CON     ANOP                                                           04050003
         AIF   (&E).ERROR2              DUP OPTION                      04100003
&E       SETB  1                        EDIT OPTION SPECFIED            04150003
&OPT     SETA  &OPT+2                   SET EDIT=CONTROL                04200003
&FLAGOFF SETA  &FLAGOFF-1               1ST BIT OF CNTR FOR EX @G76XRYU 04250003
&FLAGON  SETA  &FLAGON+2                2ND BIT OF CNTR FOR EX @G76XRYU 04300003
         AGO   .EDDONE                                         @G76XRYU 04350003
.FULLS   ANOP                                                   SA60002 04400003
         AIF   (&E).ERROR2              DUP OPTION              SA60002 04450003
         AIF   (&NTJ EQ 1).ASIDER2      CANT CODE ASID+ USERID @OZ67665 04475036
&E       SETB  1                        EDIT OPTION SPECIFIED   SA60002 04500003
&OPT     SETA  &OPT+3                   CNTL+ASIS FOR FULLSCR   SA60002 04550003
&FLAGON  SETA  &FLAGON+3                SET FULLSCREEN FOR EX  @G76XRYU 04600003
         AGO   .EDDONE                                         @G76XRYU 04650003
.NOED    ANOP                                                  @G76XRYU 04700003
         AIF   (&E).ERROR2              DUP OPTION             @G76XRYU 04750003
         AIF   (&NTJ EQ 1).ASIDERR      CAN'T SAY NOEDIT & ASID@OZ43223 04800003
&E       SETB  1                                               @G76XRYU 04850003
&OPT     SETA  &OPT+3                   FLAGS1 TO FULLSCR STAN @G76XRYU 04900003
&FLAGS2  SETA  1                        SET FLAGS2 TO NOEDIT   @G76XRYU 04950003
&FLAGON  SETA  &FLAGON+3                FLAGS1 TO FULLSCR EXEC @G76XRYU 05000003
.EDDONE  ANOP                      EDIT OPERAND IN RIGHT PLACE @G76XRYU 05050003
         AIF   (&RET NE 1).BADPOS  ..IF NOT IN 3RD POS-WARNING @G76XRYU 05100003
         AGO   .RET                ..ELSE OK                   @G76XRYU 05150003
.WAI     ANOP                                                           05200003
         AIF   (&W).ERROR2              DUP OPTION                      05250003
&W       SETB  1                        WAIT OPTION SPECIFIED           05300003
&FLAGOFF SETA  &FLAGOFF-16              SET WAIT OPTION IN EX  @G76XRYU 05350003
         AGO   .WAIDONE                 CHECK POSITION         @G76XRYU 05400003
.NOW     ANOP                                                           05450003
         AIF   (&W).ERROR2              DUP OPTION                      05500003
&W       SETB  1                        WAIT OPTION SPECIFIED           05550003
&OPT     SETA  &OPT+X'10'               SET WAIT=NOWAIT                 05600003
&FLAGON  SETA  &FLAGON+16               SET FOR NOWAIT IN EXEC @G76XRYU 05650003
.WAIDONE ANOP                       ..WAIT IN RIGHT POSITION   @G76XRYU 05700003
         AIF   (&RET NE 2).BADPOS   ..IF NOT 4TH, WARNING      @G76XRYU 05750003
         AGO   .RET                 ..ELSE OK                  @G76XRYU 05800003
.HOL     ANOP                                                           05850003
         AIF   (&H).ERROR2              DUP OPTION                      05900003
&H       SETB  1                        HOLD OPTION SPECIFIED           05950003
&OPT     SETA  &OPT+X'08'               SET HOLD=HOLD                   06000003
&FLAGON  SETA  &FLAGON+8                SET FOR HOLD IN EXEC   @G76XRYU 06050003
         AGO   .HOLDONE                 CHECK POSITION         @G76XRYU 06100003
.NOH     ANOP                                                           06150003
         AIF   (&H).ERROR2              DUP OPTION                      06200003
&H       SETB  1                        NOHOLD OPTION SPECIFIED         06250003
&FLAGOFF SETA  &FLAGOFF-8               SET FOR NOHOLD IN EXEC @G76XRYU 06300003
.HOLDONE ANOP                        ..CHECK HOLD POSITION     @G76XRYU 06350003
         AIF   (&RET NE 3).BADPOS    ..IF ^ 5TH OPER, WARNING  @G76XRYU 06400003
         AGO   .RET                  ..ELSE OK                 @G76XRYU 06450003
.BRE     ANOP                                                           06500003
         AIF   (&B).ERROR2              DUP OPTION                      06550003
&B       SETB  1                        BREAK OPTION SPECIFIED          06600003
&OPT     SETA  &OPT+X'04'               SET BREAKIN                     06650003
&FLAGON  SETA  &FLAGON+4                SET BREAKIN IN EXEC    @G76XRYU 06700003
         AGO   .BRDONE                  CHECK BREAK POSITION   @G76XRYU 06750003
.NOB     ANOP                                                           06800003
         AIF   (&B).ERROR2              DUP OPTION                      06850003
&B       SETB  1                        BREAK OPTION SPECIFIED          06900003
&FLAGOFF SETA  &FLAGOFF-4               SET NOBREAK OPTION EX  @G76XRYU 06950003
.BRDONE  ANOP                           CHECK BREAK POSITION   @G76XRYU 07000003
         AIF   (&RET NE 4).BADPOS    ..IF NOT THE 6TH OPERAND  @G76XRYU 07050003
         AGO   .RET                  ..ELSE OK                 @G76XRYU 07100003
.HPR     ANOP                                                           07150003
         AIF   (&P).ERROR2              DUP OPTION                      07200003
&P       SETB  1                        PRIORITY OPTION SPECIFIED       07250003
&FLAGOFF SETA  &FLAGOFF-32              SET HI PRIO OPTION EX  @G76XRYU 07300003
         AGO   .PRDONE                  CHECK POSITION         @G76XRYU 07350003
.LPR     ANOP                                                           07400003
         AIF   (&P).ERROR2              DUP OPTION                      07450003
&P       SETB  1                        PRIORITY OPTION SPECIFIED       07500003
&OPT     SETA  &OPT+X'20'               SET  LOW PRIORITY OPTION        07550003
&FLAGON  SETA  &FLAGON+32               SET LOW PRI OPTION EX  @G76XRYU 07600003
.PRDONE  ANOP                     ..CHECK PRIORITY OPERAND POS @G76XRYU 07650003
         AIF   (&RET NE 5).BADPOS   ..IF ^7TH OPERAND,WARNING  @G76XRYU 07700003
         AGO   .RET                 ..ELSE OK                  @G76XRYU 07750003
.BADPOS  ANOP             ..ISSUE WARNING IF OPERAND MISPLACED @G76XRYU 07800003
         MNOTE 4,'IHB300 WARNING: &PARM POSITIONAL PARAMETER MISPLACED' 07850003
.RET     ANOP                                                           07900003
&PARM    SETC  '&WAIT'                                                  07950003
         AIF   ('&RET' EQ '1').CKPARM   CHECK WAIT                      08000003
&PARM    SETC  '&HOLD'                                                  08050003
         AIF   ('&RET' EQ '2').CKPARM   CHECK HOLD                      08100003
&PARM    SETC  '&BRKI'                                                  08150003
         AIF   ('&RET' EQ '3').CKPARM   CHECK BRKI                      08200003
&PARM    SETC  '&PRTY'                                                  08250003
         AIF   ('&RET' EQ '4').CKPARM   CHECK PRTY                      08300003
.*  PARAMETERS PROCESSED                                       @G76XRYU 08350003
         AIF   ('&MF' EQ '' AND &FLAGS2 EQ 0).OLD NON-EXTENDED @G76XRYU 08400003
         AIF   ('&MF' EQ 'L').LFORM       ...LIST EXPANSION... @G76XRYU 08450003
         AIF   ('&MF' EQ 'I' OR '&MF' EQ '').STFORM .ST. FORM. @G76XRYU 08500003
.******************                                            @G76XRYU 08550003
.*  EXECUTE FORM  *                                            @G76XRYU 08600003
.******************                                            @G76XRYU 08650003
&NAME    CNOP  0,4                          TPUT EXECUTE FORM  @G76XRYU 08700003
         AIF   ('&MF(2)'(1,1) EQ '(').MFREG  IF IN RX FORM  .. @G76XRYU 08750003
         LA    1,&MF(2)                     R1=>USER PARM LIST @G76XRYU 08800003
         AGO   .GOTPARM                                        @G76XRYU 08850003
.MFREG   ANOP                             .. ELSE, IN A REG .. @G76XRYU 08900003
         AIF   ('&MF(2)' EQ '(1)').GOTPARM ..IF NOT REG 1..    @G76XRYU 08950003
         LR    1,&MF(2)                     POINT R1 AT PARMS  @G76XRYU 09000003
.GOTPARM ANOP                                                  @G76XRYU 09050003
         AIF   (&FLAGOFF EQ 127).SKIPAND ..TURN OFF ANY FLAGS? @G76XRYU 09100003
         NI    4(1),&FLAGOFF                RESET FLAGS 1      @G76XRYU 09150003
.SKIPAND ANOP                      ..CHECK FOR FLAGS TURNED ON @G76XRYU 09200003
         AIF   (&FLAGON EQ 0).NOSET  ..TURN ON ANY FLAGS?      @G76XRYU 09250003
         OI    4(1),&FLAGON                 IN EXEC PARM LIST  @G76XRYU 09300003
.NOSET   ANOP                                                  @G76XRYU 09350003
         AIF   (&FLAGS2 EQ 0 AND NOT &E).NOFLAG2 ..IF NEW EDIT @G76XRYU 09400003
         MVI   12(1),&FLAGS2                RESET FLAGS2       @G76XRYU 09450003
.NOFLAG2 ANOP                                                  @G76XRYU 09500003
         AGO   .PARMCHK                 ..END UNIQUE E CODE..  @G76XRYU 09550003
.****************************                                  @G76XRYU 09600003
.*  STANDARD EXTENDED FORM  *                                  @G76XRYU 09650003
.****************************                                  @G76XRYU 09700003
.STFORM  ANOP                   ..STANDARD EXTENDED FORM CODE. @G76XRYU 09750003
&NAME    CNOP  0,4                          STANDARD EXTENDED  @G76XRYU 09800003
         AIF   ('&BFF' NE '(1)').NOTOLD  .IF OLD STANDARD FORM @G76XRYU 09850003
         STCM  1,B'0111',*+13               OLD SUPPORT ONLY   @G76XRYU 09900003
&OLDBUFF SETB  1                         .FLAG OLD BUFF ADDR.. @G76XRYU 09950003
.NOTOLD  ANOP                                                  @G76XRYU 10000003
         BAL   1,*+20                       BRANCH AROUND PARM @G76XRYU 10050003
TPUT&NDX DS    0H                           TPUT EXTENDED PARM @G76XRYU 10100003
         DC    H'0'                         ADDR SPACE ID      @G76XRYU 10150003
         DC    H'0'                         OUTPUT BUFFER SIZE @G76XRYU 10200003
         DC    AL1(&OPT)                    FLAGS 1            @G76XRYU 10250003
         DC    AL3(0)                       OUTPUT BUFFER ADDR @G76XRYU 10300003
         DC    A(0)                         ADDRESS OF USER ID @G76XRYU 10350003
         DC    AL1(&FLAGS2)                 FLAGS 2            @G76XRYU 10400003
         DC    AL3(0)                       RESERVED BYTES     @G76XRYU 10450003
.************************************************              @G76XRYU 10500003
.*  COMMON CODE FOR STANDARD AND EXECUTE FORMS  *              @G76XRYU 10550003
.************************************************              @G76XRYU 10600003
.PARMCHK ANOP                           ..GET PARMS FOR E & I  @G76XRYU 10650003
         AIF   ('&SIZE' EQ '').CKID     ...IF BUFF SIZE GIVEN. @G76XRYU 10700003
         AIF   ('&SIZE'(1,1) EQ '(').SIZEREG  ...IF AN INTEGER @G76XRYU 10750003
         AIF   (T'&SIZE NE 'N').SIZEQU  IF AN EQUATE           @OZ43223 10800003
         AIF   (&SIZE GT 32767).SIZERR          .NOT > 32767.. @G76XRYU 10850003
.SIZEQU  ANOP                                                  @OZ43223 10900003
         AIF   ('&MF' NE 'I' AND '&MF' NE '').SIZEEX .STANDARD @G76XRYU 10950003
         ORG   TPUT&NDX+2                   PUT SIZE VALUE     @G76XRYU 11000003
         DC    AL2(&SIZE)                   IN PARM LIST       @OZ43223 11050003
         ORG                                                            11100003
         AGO   .CKID                                           @G76XRYU 11150003
.SIZEEX  ANOP                         ..IF EXECUTE             @G76XRYU 11200003
         B     *+6                          BR PAST SIZE VALUE @OZ43223 11250003
         DC    AL2(&SIZE)                   BUFSIZE            @OZ43223 11300003
         LH    0,*-2                        GET THE SIZE VALUE @G76XRYU 11350003
         STH   0,2(1)                       INTO THE PARM LIST @G76XRYU 11400003
         AGO   .CKID                                           @G76XRYU 11450003
.SIZEREG ANOP                                 ...ELSE A REG .. @G76XRYU 11500003
         STH   &SIZE(1),2(1)                PUT SIZE IN PARM   @G76XRYU 11550003
.CKID    ANOP                           ... CHECK IDS ...      @G76XRYU 11600003
         AIF   (&NTJ EQ 0).CKADDR       ..IF THERE IS AN ID..  @G76XRYU 11650003
         AIF   ('&TSID' EQ '').CKIDLOC  ..IF AN ASID # GIVEN.. @G76XRYU 11700003
         AIF   ('&TSID'(1,1) EQ '(').ASIDREG  ..IF AN INTEGER. @G76XRYU 11750003
         AIF   ((T'&ASID NE 'N' AND T'&TJID EQ 'O') OR (T'&TJID NE 'N' X11800003
               AND T'&ASID EQ 'O')).IDEQU                      @OZ43223 11850003
         AIF   (&TSID GT 32767).IDERROR         .NOT > 32767.. @G76XRYU 11900003
.IDEQU   ANOP                                                  @OZ43223 11950003
         AIF   ('&MF' NE 'I' AND '&MF' NE '').IDEX ..STANDARD  @G76XRYU 12000003
         ORG   TPUT&NDX                     PUT ASID VALUE     @G76XRYU 12050003
         DC    AL2(&TSID)                   IN PARM LIST       @G76XRYU 12100003
         ORG                                                            12150003
         AGO   .CKADDR                                         @G76XRYU 12200003
.IDEX    ANOP                               IF EXECUTE         @G76XRYU 12250003
         B     *+6                          GET AROUND ID #    @G76XRYU 12300003
         DC    AL2(&TSID)                   PUT ID # HERE      @OZ43223 12350003
         LH    0,*-2                        STORE ASID # IN    @G76XRYU 12400003
         STH   0,0(1)                       PARAMETER LIST     @G76XRYU 12450003
         AGO   .CKADDR                                         @G76XRYU 12500003
.ASIDREG ANOP                           .. ELSE ASID IN A REG. @G76XRYU 12550003
         STH   &TSID,0(1)                   STORE REG IN PARM  @G76XRYU 12600003
         AGO   .CKADDR                                         @G76XRYU 12650003
.CKIDLOC ANOP                           .. ELSE ID AT AN ADDR  @G76XRYU 12700003
.*                                            (ASIDLOC)        @G76XRYU 12750003
         AIF   ('&TSIDLOC' EQ '').CKUSER ..IF AN ADDRESS GIVEN @G76XRYU 12800003
         AIF   ('&TSIDLOC'(1,1) EQ '(').LOCIDRG  ..IF RX ADDR. @G76XRYU 12850003
         MVC   0(2,1),&TSIDLOC              MOVE ID INTO PARMS @G76XRYU 12900003
         AGO   .CKADDR                                         @G76XRYU 12950003
.LOCIDRG ANOP                           .. ELSE ID ADDR IN REG @G76XRYU 13000003
         MVC   0(2,1),0&TSIDLOC             MOVE ID INTO PARMS @G76XRYU 13050003
         AGO   .CKADDR                                         @G76XRYU 13100003
.CKUSER  ANOP                           ..ELSE A USER ID GIVEN @G76XRYU 13150003
         AIF   ('&USERIDL'(1,1) EQ '(').USEREG ..IF RX ADDR..  @G76XRYU 13200003
         AIF   ('&USERIDL'(K'&USERIDL,1) EQ ')' OR ('&MF' NE 'I' AND '&*13250003
               MF' NE '')).USERX         ..IF STANDARD FORM    @G76XRYU 13300003
         ORG   TPUT&NDX+8                   PUT USER ID ADDR   @G76XRYU 13350003
         DC    A(&USERIDL)                  IN PARM LIST       @G76XRYU 13400003
         ORG                                                            13450003
         AGO   .CKADDR                                         @G76XRYU 13500003
.USERX   ANOP                         ..IF EXECUTE OR LA ADDR  @G76XRYU 13550003
         LA    0,&USERIDL                   GET USER ID ADDR   @G76XRYU 13600003
         ST    0,8(1)                       INTO THE PARM LIST @G76XRYU 13650003
         AGO   .CKADDR                                         @G76XRYU 13700003
.USEREG  ANOP                           ...      ELSE A REG .. @G76XRYU 13750003
         ST    &USERIDL(1),8(1)             PUT ID IN PARM     @G76XRYU 13800003
.CKADDR  ANOP                           ..GET OUT BUFFER ADDR  @G76XRYU 13850003
         AIF   (&OLDBUFF).FINISH        ..IF OLD FORM,COMPLETE @G76XRYU 13900003
         AIF   ('&BFF' EQ '').FINISH    ..IF BUFFER ADDR GIVEN @G76XRYU 13950003
         AIF   ('&BFF'(1,1) EQ '(').BFFREG  ..IF AN RX ADDR... @G76XRYU 14000003
         LA    0,&BFF                       GET ADDR OF BUFFER @G76XRYU 14400003
         STCM  0,B'0111',5(1)               PUT IN PARM LIST   @G76XRYU 14450003
         AGO   .FINISH                                         @G76XRYU 14500003
.BFFREG  ANOP                               ..ELSE IN A REG .. @G76XRYU 14550003
         STCM  &BFF(1),B'0111',5(1)         STORE BUFFER ADDR  @G76XRYU 14600003
.FINISH  ANOP                                                  @G76XRYU 14650003
         OI    12(1),128                    SET HI BIT OF PARM @G76XRYU 14700003
         ICM   0,B'1000',*-3                SET HI BIT OF R0   @G76XRYU 14750003
.*                                        FROM 128 IN OI INSTR @G76XRYU 14800003
.SVCNEW  ANOP                           .. ISSUE SVC HERE ...  @G76XRYU 14850003
         SVC   93                           SVC TO MSG ROUTER  @G76XRYU 14900003
         MEXIT                                                 @G76XRYU 14950003
.**********************                                        @G76XRYU 15000003
.*  LIST FORM CODE    *                                        @G76XRYU 15050003
.**********************                                        @G76XRYU 15100003
.LFORM   ANOP                                                  @G76XRYU 15150003
         AIF   ('&TSIDLOC' EQ '').IDLOCOK ..IF L FORM INVALID. @G76XRYU 15200003
         MNOTE 12,'IHB300 INCOMPATIBLE OPERANDS: MF=L, ASIDLOC'         15250003
         MEXIT                                                 @G76XRYU 15300003
.IDLOCOK ANOP                           ..ASIDLOC OPERAND NULL @G76XRYU 15350003
&NAME    DS    0F                           L-FORM PARM LIST   @G76XRYU 15400003
         AIF   ('&TSID' NE '').LSTID    ..IF NO TSID GIVEN..   @G76XRYU 15450003
         DC    H'0'                         ASID HALFWORD      @G76XRYU 15500003
         AGO   .LBFFSIZ                                        @G76XRYU 15550003
.LSTID   ANOP                           ..ELSE TSID SPECIFIED. @G76XRYU 15600003
         AIF   ('&TSID'(1,1) EQ '(').REGERR   ..NOT A REGISTER @G76XRYU 15650003
         AIF   ((T'&ASID NE 'N' AND T'&TJID EQ 'O') OR (T'&TJID NE 'N' X15700003
               AND T'&ASID EQ 'O')).IDEQU0                     @OZ43223 15750003
         AIF   (&TSID GT 32767).IDERROR         ..NOT > 32767. @G76XRYU 15800003
.IDEQU0  ANOP                                                  @OZ43223 15850003
         DC    AL2(&TSID)                   ADDR SPACE VALUE   @OZ43223 15900003
.LBFFSIZ ANOP                           .. CHECK BUFFER SIZE . @G76XRYU 15950003
         AIF   ('&SIZE' NE '').LSTSIZE  .. IF NO BUFFER SIZE . @G76XRYU 16000003
         DC    H'0'                         BUFF SIZE HALFWORD @G76XRYU 16050003
         AGO   .LBFADDR                                        @G76XRYU 16100003
.LSTSIZE ANOP                           .. ELSE A BUFFER SIZE. @G76XRYU 16150003
         AIF   ('&SIZE'(1,1) EQ '(').REGERR   .IF NOT IN A REG @G76XRYU 16200003
         AIF   (T'&SIZE NE 'N').SIZEQU0 IF AN EQUATE           @OZ43223 16250003
         AIF   (&SIZE GT 32767).SIZERR          .NOT > 32767.. @G76XRYU 16300003
.SIZEQU0 ANOP                                                  @OZ43223 16350003
         DC    AL2(&SIZE)                   BUFFER SIZE VALUE  @OZ43223 16400003
.LBFADDR ANOP                           .. CHECK BUFFER ADDR.. @G76XRYU 16450003
         DC    AL1(&OPT)                    FLAGS 1            @G76XRYU 16500003
         AIF   ('&BFF' NE '').LBFFLAB   ..IF NO BUFFER ADDR..  @G76XRYU 16550003
         DC    AL3(0)                       BUFFER ADDRESS     @G76XRYU 16600003
         AGO   .LSTUSER                                        @G76XRYU 16650003
.LBFFLAB ANOP                           .. ELSE BUFFER ADDRESS @G76XRYU 16700003
         AIF   ('&BFF'(1,1) EQ '(').REGERR  ..IF NOT IN A REG  @G76XRYU 16750003
&BFFPOS  SETA  2                                               @OZ56262 16756236
.LRXCK   ANOP                                                  @OZ56262 16762436
         AIF   (&BFFPOS GT K'&BFF).LBFFOK                      @OZ56262 16768636
         AIF   ('&BFF'(&BFFPOS,1) EQ '(').RXERR                @OZ56262 16774836
&BFFPOS  SETA  &BFFPOS+1                                       @OZ56262 16781036
         AGO  .LRXCK                                           @OZ56262 16787236
.LBFFOK  ANOP                                                  @OZ56262 16793436
         DC    AL3(&BFF)                    ADDR OUTPUT BUFFER @G76XRYU 16800003
.LSTUSER ANOP                                                  @G76XRYU 16850003
         AIF   ('&USERIDL' NE '').LUSADDR  ..IF NO USER ID ..  @G76XRYU 16900003
         DC    A(0)                         USER ID            @G76XRYU 16950003
         AGO   .LISTEND                                        @G76XRYU 17000003
.LUSADDR ANOP                              ..ELSE USER ID ...  @G76XRYU 17050003
         AIF   ('&USERIDL'(1,1) EQ '(').REGERR  ..IF NOT REG . @G76XRYU 17100003
         DC    A(&USERIDL)                  USER ID            @G76XRYU 17150003
.LISTEND ANOP                               .. GEN FLAGS2 ..   @G76XRYU 17200003
         DC    AL1(&FLAGS2)                 FLAGS 2            @G76XRYU 17250003
         DC    AL3(0)                       RESERVED           @G76XRYU 17300003
         MEXIT                                                 @G76XRYU 17350003
.REGERR  ANOP                              ..REGISTER ERROR..  @G76XRYU 17400003
         MNOTE 12,'IHB300 INCOMPATIBLE OPERANDS: MF=L, REGISTER OPERANDX17450003
                SPECIFIED'                                     @G76XRYU 17500003
         MEXIT                                                 @G76XRYU 17550003
.RXERR   ANOP                                                  @OZ56262 17560036
         MNOTE 12,'IHB300 INCOMPATIBLE OPERANDS: MF=L, RX FORM OPERAND X17570036
                SPECIFIED'                                     @OZ56262 17580036
         MEXIT                                                 @OZ56262 17590036
.*********************                                         @G76XRYU 17600003
.*   REGISTER FORM   *                                         @G76XRYU 17650003
.*********************                                         @G76XRYU 17700003
.RF      ANOP                                                           17750003
         AIF   ('&MF' EQ '').RFNOMF   ..IF MF OPERAND GIVEN... @G76XRYU 17800003
         MNOTE 12,'IHB300 INCOMPATIBLE OPERANDS: R, MF'        @G76XRYU 17850003
         MEXIT                                                 @G76XRYU 17900003
.RFNOMF  ANOP                         ..IF NOEDIT SPECIFIED... @G76XRYU 17950003
         AIF   ('&WAIT' NE 'NOEDIT' AND '&HOLD' NE 'NOEDIT' AND '&BRKI'*18000003
               NE 'NOEDIT' AND '&PRTY' NE 'NOEDIT').RFNOED     @G76XRYU 18050003
         MNOTE 12,'IHB300, INCOMPATIBLE OPERANDS: R, NOEDIT'   @G76XRYU 18100003
         MEXIT                                                 @G76XRYU 18150003
.RFNOED  ANOP                         CHECK REMAINING OPERANDS @G76XRYU 18200003
         AIF   ('&WAIT' NE '' OR '&HOLD' NE '' OR '&BRKI' NE '' OR '&PRX18250003
               TY' NE '' OR &NTJ GT 0).RERR                             18300003
         AIF   ('&BFF'(1,1) NE '(' OR '&SIZE'(1,1) NE '(').RERR         18350003
&NAME    DS    0H                                                       18400003
         AIF   ('&BFF' EQ '(1)').SIZE                                   18450003
         LR    1,&BFF(1)                LOAD OPTIONS & BUFFER ADDR      18500003
.SIZE    AIF   ('&SIZE' EQ '(0)').SVC    ..IF REG 0-ISSUE SVC..         18550003
         LR    0,&SIZE(1)               LOAD TJID & BUFFER SIZE         18600003
         AGO   .SVC                     ... ISSUE SVC CALL ...          18650003
.************                                                  @G76XRYU 18700003
.*  MNOTES  *                                                  @G76XRYU 18750003
.************                                                  @G76XRYU 18800003
.SIZERR  ANOP                      ..SIZE INTEGER NOT 0-32767. @G76XRYU 18850003
&PARM    SETC  'SIZE'                ..INDICATE BAD SIZE PARM. @G76XRYU 18900003
         AGO   .INTERR                                         @G76XRYU 18950003
.IDERROR ANOP                      ..ASID INTEGER NOT 0-32767. @G76XRYU 19000003
&PARM    SETC  'ASID'                ..INDICATE BAD ASID PARM. @G76XRYU 19050003
.INTERR  MNOTE 12,'IHB300 &PARM NOT IN RANGE 0-32767'          @G76XRYU 19100003
         MEXIT                                                 @G76XRYU 19150003
.RERR    IHBERMAC 192                                                   19200003
         MEXIT                                                          19250003
.ERROR1  IHBERMAC 24                                                    19300003
         MEXIT                                                          19350003
.ERROR2  IHBERMAC 54,,,                                                 19400003
         MEXIT                                                          19450003
.ERROR3  IHBERMAC 49,,&PARM                                             19500003
         MEXIT                                                 @OZ43223 19550003
.ASIDERR MNOTE 12,'IHB300 ASID/USERID INVALID WITH NOEDIT'     @OZ43223 19600003
         MEXIT                                                 @OZ43223 19650003
.ASIDER2 MNOTE 12,'IHB300 ASID/USERID INVALID WITH FULLSCR'    @OZ67665 19660036
         MEXIT                                                 @OZ67665 19670036
.ASIDER3 MNOTE 12,'IHB300 ASID/USERID INVALID WITH ASIS'       @OZ67665 19680036
         MEXIT                                                 @OZ67665 19690036
.***********************                                       @G76XRYU 19700003
.*  NON-EXTENDED FORM  *                                       @G76XRYU 19750003
.***********************                                       @G76XRYU 19800003
.OLD     ANOP            ....NON EXTENDED TPUT EXPANSION       @G76XRYU 19850003
         AIF   ('&BFF'(1,1) EQ '(' OR '&SIZE'(1,1) EQ '(').REGFM  REG.  19900003
.*                                      FORM MACRO                      19950003
         AIF   ('&TSID' EQ '').CHKLOC   GOTO CHECK TSIDLOC              20000003
         AIF   ('&TSID'(1,1) EQ '(').REGFM   REG. FORM MACRO            20050003
&ID      SETC  '&TSID'                  SET TSID                        20100003
         AGO   .EXPAND                  GOTO EXPANSION                  20150003
.CHKLOC  AIF   ('&TSIDLOC' EQ '').EXPAND  GO EXPAND IF NO 'TSIDLOC'     20200003
         AIF   ('&TSIDLOC'(1,1) EQ '(').REGFM  REG. FORM MACRO          20250003
.*   REGULAR FORMAT OF MACRO                                            20300003
.EXPAND  CNOP  0,4                                                      20350003
         AIF   ('&TSIDLOC' NE '').LOC   TJLOC SPECIFIED                 20400003
&NAME    B     *+10                    BRANCH AROUND CONSTANTS @OZ48113 20450036
         DC    AL2(&ID)                 TSID                            20500003
         DC    AL2(&SIZE)               BUFFER SIZE                     20550003
         DC    AL1(&OPT,0)             OPTION BYTE AND FILLER  @OZ48113 20600036
         L     0,*-6                    LOAD SIZE              @OZ48113 20650036
         LA    1,&BFF                   LOAD BUFFER ADDRESS    @OZ48113 20700036
         ICM   1,8,*-10                 LOAD OPTION BYTE       @OZ48113 20750036
.SVC     ANOP                                                           20775036
         AIF   ('&USERIDL' EQ '').GENSVC                                20800003
         AIF   ('&USERIDL'(1,1) EQ '(').LR                              20850003
         LA    15,&USERIDL                                              20900003
         AGO   .GENSVC                                                  20950003
.LR      ANOP                                                           21000003
         AIF   ('&USERIDL' EQ '(15)').GENSVC                            21050003
         LR    15,&USERIDL(1)                                           21100003
.GENSVC  ANOP                                                           21150003
         SVC   93                       ISSUE TGET/TPUT SVC             21200003
         SPACE 1                                                        21250003
         MEXIT                                                          21300003
.*   TSIDLOC IS SPECIFIED                                               21350003
.LOC     ANOP                                                           21400003
&NAME    ICM   0,B'0011',&TSIDLOC       TSID IN HIGH 2 BYTES   @G76XRYU 21450003
         SLL   0,16                     SHIFT TJID TO HI-ORDER BYTES    21500003
         AIF   (&OPT EQ 0).ZERO         BRANCH IF OPTIONS ARE ZERO      21550003
         O     0,*+8                    LOAD BUFFER SIZE                21600003
         B     *+10                    BRANCH AROUND CONSTANTS @OZ48113 21650036
         DC    AL2(0)                   TJID                            21700003
         DC    AL2(&SIZE)               BUFFER SIZE                     21750003
         DC    AL1(&OPT,0)             OPTION BYTE AND FILLER  @OZ48113 21800036
         LA    1,&BFF                   LOAD BUFFER ADDRESS    @OZ48113 21850036
         ICM   1,8,*-6                  LOAD OPTION BYTE       @OZ48113 21900036
         AGO   .SVC                     GOTO ISSUE SVC                  21950003
.ZERO    LA    1,&SIZE                  LOAD BUFFER SIZE                22000003
         OR    0,1                      OR IT INTO REGISTER 0           22050003
         LA    1,&BFF                   Z HIGH ORDR BYTE & LD BFR ADDR  22100003
         AGO   .SVC                     GOTO ISSUE SVC                  22150003
.*   REGISTER FORMAT                                                    22200003
.REGFM   AIF   ('&TSIDLOC' EQ '').NOLOC   SKIP IF TJIDLOC NOT SPECIFIED 22250003
         AIF   ('&TSIDLOC'(1,1) EQ '(').REGLOC    LOC IN REG  FORM      22300003
&NAME    ICM   15,B'0011',&TSIDLOC      TSID IN HIGH 2 BYTES   @G76XRYU 22350003
         SLL   15,16                    MOVE TJID TO HI=ORDER 2 BYTES   22400003
         AGO   .CHKSIZE                 GOTO CHECK SIZE                 22450003
.REGLOC  ANOP                                                           22500003
&NAME    ICM   15,B'0011',0&TSIDLOC     TSID IN HIGH 2 BYTES   @G76XRYU 22550003
         SLL   15,16                    MOVE TJID TO HI-ORDER 2 BYTES   22600003
         AGO   .CHKSIZE                 GOTO CHECK SIZE                 22650003
.*   WHEN TJIDLOC IS NOT SPECIFIED                                      22700003
.NOLOC   AIF   ('&TSID' NE '').TJIDYES                                  22750003
&NAME    DS    0H                       TJID IS '0'                     22800003
         AGO   .CHKSIZE                 GOTO CHECK SIZE                 22850003
.TJIDYES AIF   ('&TSID'(1,1) EQ '(').IDREG   TJID IN REG FORM           22900003
&NAME    LA    15,&TSID                 LOAD TJID                       22950003
         AGO   .SHIFT                   GOTO SHIFT REG.0                23000003
.IDREG   ANOP                                                           23050003
&NAME    LR    15,&TSID                 LOAD TJID IN REG 15             23100003
.SHIFT   SLL   15,16                    SHIFT TJID TO HI-ORDER BYTE     23150003
.*  CHECK SIZE OF BUFFER (BY NOW TJID IS IN HI-ORDER 2 BYTES OF REG 15) 23200003
.CHKSIZE AIF   ('&SIZE'(1,1) EQ '(').SZREG   SKIP IF SIZE IN REG        23250003
         LA    0,&SIZE                  LOAD SIZE IN REG.0              23300003
         AGO   .ORSIZE                  GOTO OR SIZE                    23350003
.SZREG   AIF   ('&SIZE' EQ '(0)').ORSIZE  GOTO SET SIZE IN REG. 0       23400003
         LR    0,&SIZE(1)               LOAD BUFFER SIZE                23450003
.ORSIZE  AIF   ('&TSID' EQ '' AND '&TSIDLOC' EQ '').PAST                23500003
         OR    0,15                     LOAD TJID + BUFFER SIZE         23550003
.PAST    AIF   (&OPT EQ 0 AND '&BFF'(1,1) EQ '(').ORONE                 23600003
         AIF   (&OPT EQ 0 AND '&BFF'(1,1) NE '(').ZOPT ZERO OPTN        23650003
         CNOP  0,4                                                      23700003
         AIF   ('&BFF'(1,1) EQ '(').REGGO   BFFR IN REG.       @OZ65329 23742836
         CNOP  0,4                                             @OZ65329 23785636
         B     *+6                                             @OZ65329 23828436
         DC    AL1(&OPT,0)              OPTION BITS AND PAD    @OZ65329 23871236
         LA    1,&BFF                   LOAD POINTER TO BUFFER @OZ65329 23914036
         ICM   1,8,*-6                  GET OPTION BYTE        @OZ65329 23956836
         AGO   .SVC                     GOTO ISSUE SVC                  24000003
.ZOPT    LA    1,&BFF                   LOAD BUFFER ADDR IN REG 1       24050003
         AGO   .SVC                     ISSUE SVC                       24100003
.REGGO   CNOP  0,4                                             @OZ65329 24120036
         B     *+8                                             @OZ65329 24140036
         DC    AL1(&OPT)                OPTION BITS            @OZ65329 24160036
         DC    AL3(0)                   BUFFER ADDR            @OZ65329 24180036
         AIF   ('&BFF' EQ '(1)').ORONE  GOTO OR OPTIONS                 24200003
         L     1,*-4                    LOAD OPTIONS                    24250003
         OR    1,&BFF(1)                SET OPTIONS AND BUFFER ADDR     24300003
         AGO   .SVC                     GOTO ISSUE SVC                  24350003
.ORONE   LA    1,0(,&BFF(1))            CLR HIGH ORDR BYTE + LD BFR ADR 24400003
         AIF   (&OPT EQ 0).SVC          GOTO ISSUE SVC IF OPT EQ 0      24450003
         O     1,*-8                    SET OPTIONS                     24500003
         AGO   .SVC                                                     24550003
         MEND                                                           24600003
