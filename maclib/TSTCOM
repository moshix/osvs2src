         MACRO                                                          00050003
&ANYNM   TSTCOM  &TDCBA,&TDEVT,&TMF                                     00100003
         GBLB  &QUITSW                                                  00150003
.*  FUNCTION - TEST DCB,DEVT,AND MF    PARAMETERS FOR VALID SPEC        00250003
.*           - CAUSE THE PARAMETER LIST TO BEGIN ON WORD BOUNDARY       00300003
.*             FOR THE STANDARD AND LIST FORMS.                         00350003
.*       TEST MF  PARAMETER                                             00400003
         AIF   ('&TMF' EQ '' OR '&TMF' EQ 'L' OR '&TMF(1)' EQ 'E').NXTTX00450003
               ST1     TEST FOR STD LIST OR EXECUTE FORMS               00500003
         IHBERMAC 035,,&TMF,,,0    INVALID MACRO FORM MNOTE             00550003
&QUITSW  SETB  1                   SET MACRO TERMINATION FLAG           00600003
         AGO   .NXTS1A             GO TEST DCB OPERAND                  00620003
.NXTTST1 AIF  ('&TMF' EQ '' OR '&TMF' EQ 'L').NXTS1A    STD/LIST FORM   00650003
         AIF   ('&TMF(2)' NE '' AND '&TMF(2)' NE '(0)').NXTS1A          00700003
         IHBERMAC 035,,&TMF,,,0    INVALID MACRO FORM MNOTE             00800003
&QUITSW  SETB  1                   SET MACRO TERMINATION FLAG           00850003
.*       TEST DCB ADDRESS                                               00900003
.NXTS1A  ANOP                                                           00950003
         AIF   ('&TDCBA' NE '' OR '&TMF' EQ 'L' OR '&TMF(1)' EQ 'E').NXX01000003
               TTST2         TEST IF DCB SPECIFIED FOR STANDARD FM      01050003
         IHBERMAC  006     ISSUE MNOTE DCB REQUIRED NOT SPECIFIED       01100003
&QUITSW  SETB  1                   SET MACRO TERMINATION FLAG           01150003
         AGO   .NXTTST3            GO TEST DEVT OPERAND                 01170003
.NXTTST2 AIF   ('&TDCBA' EQ '').NXTTST3 DCB PARAMETER PRESENT           01200003
         AIF   ('&TMF' EQ 'L').NXTS1B  TEST FOR MF=L                    01210003
         AIF   ('&TDCBA' NE '(1)' AND '&TDCBA' NE '(0)').NXTTST3        01220003
.*                           TEST IF DCB SPEC IN REG 0 OR 1,            01250003
         IHBERMAC  252,,&TDCBA     INVALID DCB OPERAND                  01300003
&QUITSW  SETB  1                   SET MACRO TERMINATION FLAG           01350003
         AGO   .NXTTST3                GO TEST DEVT PARAMETER           01360003
.NXTS1B  AIF   ('&TDCBA'(1,1) NE '(').NXTTST3 VALID FORM MF=L           01370003
         IHBERMAC  069                 INVALID REG NOTATION WITH MF=L   01380003
&QUITSW  SETB  1                   SET MACRO TERMINATION FLAG           01390003
.*       TEST DEVT PARAMETER                                            01400003
.NXTTST3 AIF   ('&TDEVT' NE '').NXTTST4  TEST IF DEVT SPEC REQ PARM     01450003
         IHBERMAC  260                                                  01500003
         AGO   .SETQUIT                  GO SET QUIT SWITCH ON          01600003
.*       AT THIS TIME 3886 AND SENECA ARE ONLY DEVICES SUPPORTED        01650003
.NXTTST4 AIF   ('&TDEVT' EQ '3886' OR '&TDEVT' EQ '3890').NXTTST5       01700003
         IHBERMAC  253,,&TDEVT     INVALID DEVICE TYPE                  01750003
         AGO   .SETQUIT                  GO SET QUIT SWITCH ON          01900003
.NXTTST5 AIF   (&QUITSW).FINI      TEST QUIT SWITCH                     01950003
.*       FORCE WORD BOUNDARY ALIGNMENT FOR LIST AND STANDARD FORMS      02000003
.ALIGN   AIF   ('&TMF' EQ 'L').LISTALN    TEST FOR LIST FM ALIGN DS     02150003
         CNOP  0,4                                                      02200003
         MEXIT                                                          02250003
.LISTALN ANOP                                                           02300003
         DS    0F                                                       02350003
         MEXIT                                                          02400003
.SETQUIT ANOP                                                           02450003
&QUITSW  SETB  1                                                        02500003
.FINI    MEND                                                           02550003
