         PRINT OFF                                                      00050007
         MACRO                                                          00050407
&NAME    BNGHJN &LABEL,&HJN                                             00050807
         LCLC  &HJA,&HJB                                                00051207
&HJA     SETC  '&SYSPARM'(1,4)                                          00051607
&HJB     SETC  '&SYSPARM'(5,4)                                          00052007
         AIF   ('&LABEL' EQ '').NOBRNCH                                 00052407
         B     &LABEL .                 BR AROUND CONSTANTS             00052807
         SPACE 2                                                        00053207
.NOBRNCH ANOP                                                           00053607
         AIF   ('&NAME' EQ '').NOLABEL                                  00054007
         DC    C'&NAME' .              MODULE IDENTIFIER                00054407
.NOLABEL ANOP                                                           00054807
         DC    X'&HJA' .                DATE OF MODIFICATION            00055207
         AIF   ('&HJN' NE 'HJN').DATE                                   00055607
         DC    X'&HJB' .                HJN OF MODIFICATION             00056007
.DATE    ANOP                                                           00056407
         AIF   ('&LABEL' EQ '').NOLBL                                   00056807
         SPACE 2                                                        00057207
&LABEL   DS    0H                                                       00057607
.NOLBL   ANOP                                                           00058007
         MEND                                                           00058407
         PRINT ON                                                       00058807
*********************************************************************** 00059207
*                                                                     * 00100000
* MODULE NAME = BNGIEXIT                                              * 00150000
*                                                                     * 00200000
* DESCRIPTIVE NAME = IMS DEMF USER EXIT SYMBOLIC NAME CONVERSION      * 00250000
*                    MODULE                                           * 00300000
*                                                                     * 00350000
* COPYRIGHT = NONE                                                    * 00400000
*                                                                     * 00450000
* STATUS = RELEASE 1, LEVEL 1                                         * 00500000
*                                                                     * 00550000
* FUNCTION = CONVERTS SYMBOLIC NAME TO EXPLICIT HARDWARE ADDRESSES    * 00600000
*            IN THE FORM OF CONTROL/LINE/TERMINAL FOR REMOTE TERMINAL * 00650000
*            SYMBOLIC NAME OR IN THE FORM OF TERMINAL ADDRESS ONLY    * 00700000
*            FOR LOCAL TERMINAL SYMBOLIC NAME.                        * 00750000
*                                                                     * 00800000
* NOTES = NONE                                                        * 00850000
*                                                                     * 00900000
*    DEPENDENCIES =                                                   * 00950000
*                                                                     * 01000000
*    THE FOLLOWING ARE IMS CONTROL BLOCKS AND THEIR LABELS AS USED BY * 01050000
*    DEMF IMS BNGIEXIT MODULE:                                        * 01100000
*                                                                     * 01150000
*    IMS                                                              * 01200000
*    CONTROL BLOCKS                  LABELS USED                      * 01250000
*    ----------------------------    -----------                      * 01300000
*                                                                     * 01350000
*    COMMUNICATION LINE BLOCK        CLBFLAG3                         * 01400000
*    (CLB)                           CLB3DECB                         * 01450000
*                                    CLB3CL40                         * 01500000
*                                    CLBDCB                           * 01550000
*                                                                     * 01600000
*    COMMUNICATION NAME TABLE        CNTNAME1                         * 01650000
*    (CNT)                           CNTCTBPT                         * 01700000
*                                                                     * 01750000
*    COMMUNICATION TERMINAL BLOCK    CTBCLB                           * 01800000
*    (CTB)                           CTBTERM                          * 01850000
*                                                                     * 01900000
*    SYSTEM CONTENTS DIRECTORY       SCDCNT                           * 01950000
*    (SCD)                           SCDCNTN                          * 02000000
*                                    SCDCNTL                          * 02050000
*                                                                     * 02100000
*    RESTRICTIONS = NONE                                              * 02150000
*                                                                     * 02200000
*    REGISTER CONVENTIONS =  R7  ADDRESS OF COMM TERM BLOCK (CTB)     * 02250000
*                            R9  ADDRESS OF COMM LINK BLOCK (CLB)     * 02300000
*                            R10 ADDRESS OF COMM NAME TABLE (CNT)     * 02350000
*                            R11 ADDRESS OF SYSTEM CONTENTS DIR (SCD) * 02400000
*                                                                     * 02450000
*    PATCH LABEL = BNGIPTCH                                           * 02500000
*                                                                     * 02550000
* MODULE TYPE =  IMS USER EXIT                                        * 02600000
*                                                                     * 02650000
*    PROCESSOR = ASSEMBLER H                                          * 02700000
*                                                                     * 02750000
*    MODULE SIZE = 1524 DECIMAL BYTES                                 * 02800000
*                                                                     * 02850000
*    ATTRIBUTES =  SERIALLY REUSABLE                                  * 02900000
*                                                                     * 02950000
* ENTRY POINT = BNGIEXIT                                              * 03000000
*                                                                     * 03050000
*    PURPOSE = SEE FUNCTION                                           * 03100000
*                                                                     * 03150000
*    LINKAGE = R1  LOADED WITH THE PARAM LIST POINTER                 * 03200000
*              R14 LOADED WITH THE RETURN ADDR (USED BY SUBROUTINE)   * 03250000
*              R15 LOADED WITH ENTRY POINT                            * 03300000
*                                                                     * 03350000
* INPUT = PARAM LIST ITEMS USED ARE :                                 * 03400000
*                                                                     * 03450000
* OUTPUT = PARAM LIST ITEMS USED ARE :                                * 03500000
*                                                                     * 03550000
* EXIT-NORMAL = REGISTER 15 RETURN CODE RETURNED TO CALLER :          * 03600000
*               00 - OPERATION PERFORMED AS REQUESTED                 * 03650000
*                                                                     * 03700000
* EXIT-ERROR  = NONE                                                  * 03750000
*                                                                     * 03800000
* EXTERNAL REFERENCE = NONE                                           * 03850000
*                                                                     * 03900000
*    ROUTINES =                                                       * 03950000
*                                                                     * 04000000
*    DATA AREAS =                                                     * 04050000
*                                                                     * 04100000
*    CONTROL BLOCK = DCB                                              * 04150000
*                                                                     * 04200000
* TABLES = SEE DEPENDENCIES ABOVE                                     * 04250000
*                                                                     * 04300000
* MACROS = SAVE, RETURN                                               * 04350000
*                                                                     * 04400000
* CHANGE ACTIVITIES AS FOLLOWS:                                       * 04450000
* CHANGE 01  11/15/77  PROLOGUE ADDED                                 * 04450107
* CHANGE 02  02/22/78  BNGHJN MACRO ADDED                             * 04450207
*D100750                                                       @OZ29911 04450307
*A078000                                                       @OZ29911 04450407
*A060000,061000,063000                                         @OZ30477 04450507
*C103500,134500                                                @OZ30477 04450607
*                                                                     * 04550000
*********************************************************************** 04600000
BNGIEXIT  TITLE  'DEMF TRANSACTION INPUT EDIT USER EXIT FOR IMS 1.1.4'  04650000
*********************************************************************** 04700000
*                                                                     * 04750000
* SINCE AN IMS MESSAGE PROCESSING PROGRAM (BMP OR MPP) HAS NO ACCESS  * 04800000
* TO IMS CONTROL BLOCKS AND ONE DESIRABLE FEATURE OF DEMF (DISPLAY    * 04850000
* EXCEPTION MONITORING FACILITY) IS THE ABILITY TO DISPLAY A TERMINAL * 04900000
* ERROR DATA WHEN ONLY ITS SYMBOLIC NAME IS KNOWN, OR NOTHING IS KNOWN* 04950000
* EXCEPT THAT IT IS CONNECTED TO AN DEMF-SUPPORTING SOFTWARE PACKAGE, * 05000000
* IMS BEING SUCH PACKAGE, SOME WAY NEEDED TO BE FOUND TO OBTAIN LINE, * 05050000
* CONTROL UNIT, AND TERMINAL ADDRESSES, AVAILABLE ONLY IN THE IMS     * 05100000
* CONTROL REGION, AND PASS THIS DATA TO THE DEMF TRANSACTION PROCESSOR, 05150000
* EXECUTING IN ITS OWN SEPARATE REGION. DEMF DATA IS ACCESSED BY LINE,* 05200000
* CONTROL UNIT AND TERMINAL ADDRESSES, NOT BY SYMBOLIC. THE EASIEST WAY 05250000
* TO IMPLEMENT THIS FUNCTION IS BY IMS TRANSACTION INPUT EDIT USER EXIT 05300000
* THIS PROGRAM BEING SUCH EXIT FOR DEMF TRANSACTIONS. IF OMITTED FROM * 05350000
* THE IMS CONTROL REGION,THE ONLY EFFECT TO DEMF IS THAT EXCEPTION DATA 05400000
* WILL BE AVAILABLE ONLY BY LINE, CONTROL UNIT AND TERMINAL ADDRESSES * 05450000
* ENTERED BY THE OPERATOR.                                            * 05500000
*                                                                     * 05550000
* A SPECIAL OPERAND WILL BE ADDED TO DEMF TRANSACTION MESSAGES BY THIS* 05600000
* PROGRAM TO INDICATE THE LINE, CONTROL UNIT AND TERMINAL ADDRESS OF  * 05650000
* THE TERMINAL MAKING THE REQUEST & OF ANOTHER TERMINAL WHOSE SYMBOLIC* 05700000
* NAME IS KNOW. TRANSACTIONS MODIFIED BY THIS PROGRAM AND THE RESULTS * 05750000
* PASSED TO THE DEMF TRANSACTION PROCESSOR ARE AS FOLLOW:             * 05800000
*                                                                     * 05850000
*    TRANSACTION ENTERED:        TRANSACTION PASSED TO PROCESSOR:     * 05900000
*        DEMF 3270                   DEMF 3270 X'FF'LLLCCTT - REMOTE  * 05950000
*        DEMF 3270                   DEMF 3270 X'FF'TTT     - LOCAL   * 06000000
*        DEMF 3270 (VTAM TERMINAL)   DEMF 3270 X'FE'VTAMNAME - @OZ30477 06020007
*        DEMF 3270 SYMBNAME          DEMF 3270 SYMBNAME X'FF'LLLCCTT  * 06050000
*        DEMF 3270 SYMBNAME          DEMF 3270 SYMBNAME X'FF'TTT      * 06100000
* DEMF 3270 SYMBNAME(VTAM)    DEMF 3270 SYMBNAME X'FE'VTAMNAME @OZ30477 06120007
*        DEMF 3270 N1 N2 ...         INPUT PASSED TO DISP UNCHANGED   * 06150000
*        DEMF (29-BYTES) MENU (X'ED 3270 OPTION)                      * 06200000
*                                    DEMF 3270 X'FF'LLLCCTT - REMOTE  * 06250000
*                                    DEMF 3270 X'FF'TTT     - LOCAL   * 06300000
*                                    DEMF 3270 X'FE'VTAMNAME - @OZ30477 06304007
*                                                              @OZ30477 06308007
* THE X'FE' IS A SPECIAL INDICATOR MEANING THAT THE FOLLOWING  @OZ30477 06312007
* DATA IS:                                                     @OZ30477 06316007
*                                                              @OZ30477 06320007
*     VTAMNAME - VTAM NODE NAME                                @OZ30477 06324007
*                                                              @OZ30477 06328007
*     SINCE THERE IS NO WAY TO CONVERT AN IMS SYMBOLIC         @OZ30477 06332007
*     TERMINAL NAME TO A HARDWARE ADDRESS UNDER VTAM, THE      @OZ30477 06336007
*     VTAM NODE NAME ASSOCIATED WITH THE IMS SYMBOLIC NAME IS  @OZ30477 06340007
*     PASSED INSTEAD OF THE HARDWARE ADDRESS.                  @OZ30477 06344007
*                                                                     * 06350000
* THE X'FF' IS A SPECIAL INDICATOR MEANING THAT THE FOLLOWING DATA IS:* 06400000
*                                                                     * 06450000
*     LLL     - THE CHANNEL AND UNIT LINE ADDRESS FROM THE O/S UCB    * 06500000
*     CC & TT - THE CONTROL UNIT AND TERMINAL TP-LINE FOR REMOTE      * 06550000
*               ADDRESSING CHARACTERS FROM THE IMS CTB.               * 06600000
*                                                                     * 06650000
*     TTT     - THE TERMINAL TP-LINE FOR LOCAL USING ADDRESSING       * 06700000
*               CHARACTERS FROM THE O/S UCB.                          * 06750000
*                                                                     * 06800000
* THE SYMBOLIC DEVICE NAME MUST BE 1-8 CHARACTERS BUT IF THE NAME IS  * 06850000
*    3 CHARACTERS OR LESS, AT LEAST ONE MUST NOT BE HEX-EQUAL(0-9,A-F)* 06900000
*    OR THE USER MAY GET A SYMBOLIC CONVERSION WHEN HE MEANT TO       * 06950000
*    ENTER A HARDWARE ADDRESS.                                        * 07000000
*                                                                     * 07050000
*********************************************************************** 07100000
         EJECT                                                          07150000
BNGIEXIT CSECT                                                          07200000
*        PRINT NOGEN                                                    07250000
         USING *,BASE1             PROGRAM BASE                         07300000
         USING IECTDECB,CLBREG     IMS COMMUNICATION LINE BLOCK         07350000
         USING CNT,CNTREG          IMS COMMUNICATION NAME TABLE         07400000
         USING CTB,CTBREG          IMS COMMUNICATION TERMINAL BLOCK     07450000
         USING IHADCB,DCBREG       O/S DATA CONTROL BLOCK               07500000
         USING DEB,DEBREG          O/S DATA EXTENT BLOCK                07550000
         USING MSG,MSGREG          TRANSACTION MESSAGE SEGMENT BUFFER   07600000
         USING SCD,SCDREG          IMS SYSTEM CONTENTS DIRECTORY        07650000
         USING UCB,UCBREG          O/S UNIT CONTROL BLOCK               07700000
         SAVE  (14,12),,BNGIEXIT.DEMF.TRANSACTION.INPUT.EDIT.USER.EXIT  07750000
         LR    BASE1,R15           SET PROGRAM BASE                     07800000
BNGIEXIT BNGHJN SKIPID                                         @OZ29911 07820007
*        L     SCDREG,=V(XXXISCD)   SET IMS SCDREG                      07850000
*        L     SCDREG,0(SCDREG)                                         07900000
         L     MSGREG,DECAREA      SET MESSAGE SEGMENT BASE             07950000
         LA    MSGREG,0(MSGREG)    INSURE TOP BYTE IS ZERO              08000000
         XC    PARM1(40),PARM1     INSURE ALL PARMS ZEROS               08050000
         CLI   MSGZ2,0             MSG EDITED BY MFS OR BASIC? 0=BASIC  08100000
         BNE   MFSEDIT             BYPASS EXTENDING 10 BYTES IF MFS     08150000
*********************************************************************** 08200000
*                                                                     * 08250000
*        BASIC MSG EDIT PROCESSING                                    * 08300000
*                                                                     * 08350000
*********************************************************************** 08400000
         LA    DATAREG,MSGDATA     DATAREG = POINT TO INPUT DATA        08450000
         LH    DATALOOP,MSGLL      LOAD MSG LENGTH                      08500000
         SH    DATALOOP,=H'5'      SUBTRACT (LLZZ) + (1 FOR TR)         08550000
         EX    DATALOOP,TRNSLATE   CHANGE LOWER CASE TO UPPER CASE      08600000
         LA    DATALOOP,5(DATALOOP)   RESTORE LENGTH OF MSG             08650000
         AR    DATALOOP,MSGREG     POINT TO END OF MSG                  08700000
         BAL   LINKREG,EDITRTN     CALL EDIT ROUTINE                    08750000
         CLC   PARM2(5),=X'F3F2F7F000' IS 3270 REQUEST ?                08800000
         BNE   RETURN              NO.                                  08850000
         CLI   PARM3,X'00'         YES. IS SYMBOLIC NAME ENTERED ?      08900000
         BE    G3270               NO. FIND HDWR ADDR FOR RQSD TERM     08950000
         CLI   PARM4,X'00'         YES. ANY MORE PARMS ?                09000000
         BNE   RETURN              YES. MUST BE HDWR ADDRESSES          09050000
         L     CNTREG,SCDCNT       SET COMMUNICATION NAME TABLE         09100000
         LH    R0,SCDCNTN            POOL ADDRESS AND CNT COUNT         09150000
         OC    PARM3,=CL8' '       MASK TO BLANK ON BINARY ZEROS        09200000
CKNXTCNT CLC   PARM3,CNTNAME1       IS CNTNAME1 = REQUEST ?             09250000
         BE    CNTFOUND            SET CLB/CTB POINTERS IF EQUAL        09300000
         AH    CNTREG,SCDCNTL      MOVE TO NEXT COMM. NAME TABLE ENTRY  09350000
         BCT   R0,CKNXTCNT         KEEP CHECKING UNTILL ALL ARE CHECKED 09400000
*                                                                       09450000
RETURN   RETURN (14,12),T,RC=0          RETURN TO IMS WITH GOOD CODE    09500000
*                                                                       09550000
TRNSLATE TR    0(1,DATAREG),LOTOUP      TRANSLATE LOWER CASE TO UPPER   09600000
*                                                                       09650000
*                                                                       09700000
*********************************************************************** 09750000
*                                                                     * 09800000
*        BASIC EDIT PROCESSING FOR 3270 SYMBOLIC TERMINAL             * 09850000
*                                                                     * 09900000
*********************************************************************** 09950000
CNTFOUND L     CTBREG,CNTCTBPT     SET CTB ADDRESS FOR SYMBOLIC TERMNAL 10000000
         L     CLBREG,CTBCLB       SET CLB ADDRESS FOR SYMBOLIC TERMNAL 10050000
*********************************************************************** 10100000
*                                                                     * 10150000
*        BASIC EDIT PROCESSING FOR 3270 DEFAULT TERMINAL              * 10200000
*                                                                     * 10250000
*********************************************************************** 10300000
G3270    OC    CLBDCB,CLBDCB       IS CLBDCB = ZEROS, THEN THIS@OZ30477 10350007
*                                      IS VTAM TERMINAL REQUEST@OZ30477 10353007
         BNZ   G3270AA             NO. A BTAM TERMINAL REQUEST.@OZ30477 10356007
         LH    R1,MSGLL            LOAD MSG LENGTH             @OZ30477 10359007
         AR    R1,MSGREG           R1=POINT TO END OF MSG      @OZ30477 10362007
         MVI   0(R1),X'FE'        SET VTAM CONVERSION INDICATOR@OZ30477 10365007
         MVC   1(8,R1),CLBNAME    MOVE VTAM NODE NAME TO OUTPUT@OZ30477 10368007
         LH    R1,MSGLL            LOAD MSG LENGTH             @OZ30477 10371007
         LA    R1,9(R1)            BUMP R1=NEW BASIC LENGTH    @OZ30477 10374007
         STH   R1,MSGLL            STORE BACK NEW BASIC LENGTH @OZ30477 10377007
         B     RETURN                                          @OZ30477 10380007
G3270AA  BAL   LINKREG,CONVSYMN    CALL CONVERT SYMBOLIC RTN   @OZ30477 10383007
         CLI   PARM4,X'00'         IS SYMBOLIC NAME CONVERTED?          10400000
         BE    RETURN              NO                                   10450000
         LH    R1,MSGLL            LOAD MSG LENGTH                      10500000
         AR    R1,MSGREG           R1=POINT TO END OF MSG               10550000
         MVI   0(R1),X'FF'         SET SYMBOLIC CONVERSION INDICATOR    10600000
         MVC   1(3,R1),PARM4       MOVE HWR LINE/TERM ADDR TO OUTPUT    10650000
         UNPK  DBLWD,CTBTERM(3)    UNPACK 3 CNTRL & TERM ADDRESSES      10700000
         MVC   4(4,R1),DBLWD+3     MOVE CNTRL & TERM ADDR TO OUTPUT     10750000
         TR    4(4,R1),HXTOEB      CONVERT CNTRL & TERM ADDR TO EBCDIC  10800000
         LH    R1,MSGLL            LOAD MSG LENGTH                      10850000
         LA    R1,8(R1)            BUMP R1=NEW BASIC LENGTH             10900000
         STH   R1,MSGLL            STORE BACK NEW BASIC LENGTH          10950000
         B     RETURN                                                   11000000
*********************************************************************** 11050000
*                                                                     * 11100000
*        MFS MSG EDIT PROCESSING                                      * 11150000
*                                                                     * 11200000
*********************************************************************** 11250000
MFSEDIT  LA    DATAREG,MSGDATA        POINT TO KEY-IN AREA              11300000
         LA    DATALOOP,33            TRANSLATE FROM 'DEMF' ON TO END   11350000
*                                       OF 29-BYTE KEY-IN AREA          11400000
         EX    DATALOOP,TRNSLATE      CHANGE LOWER CASE TO UPPER CASE   11450000
         CLC   MSGKEYED,=CL29' '   WAS ANYTHING KEYED IN ?              11500000
         BNE   MFS0002             YES. GO EDIT INPUT                   11550000
         CLC   MSGCMDCD,=C'MENU'   IS INPUT FROM MENU SCREEN ?          11600000
         BNE   RETURN              NO. RETURN TO IMS                    11650000
         CLC   MSGDPRSS,=C'DEPRESS' IS THIS FROM MENU PAGE 1 ?          11700000
         BNE   RETURN              NO. RETURN TO IMS                    11750000
         CLI   MSG3270,C' '        IS THIS 3270 REQUEST FROM MENU ?     11800000
         BE    RETURN              NO. RETURN TO IMS                    11850000
         B     MFS3270             YES. GO FIND HARDWARE ADDRESS        11900000
MFS0002  LA    DATAREG,MSGKEYED    POINT TO KEYED-IN DATA               11950000
         LR    DATALOOP,DATAREG    SETUP DATA LOOP                      12000000
         LA    DATALOOP,29(DATALOOP) SET DATALOOP=END OF 29 BYTE AREA   12050000
         BAL   LINKREG,EDITRTN     CALL EDIT ROUTINE                    12100000
         CLC   PARM1(5),=X'F3F2F7F000' IS 3270 REQUEST ?                12150000
         BNE   RETURN              NO. RETURN TO IMS                    12200000
         CLI   PARM2,X'00'         YES. IS SYMBOLIC NAME ENTERED ?      12250000
         BE    MFS3270             NO. FIND HDWR ADDR FOR RQSD TERM     12300000
         CLI   PARM3,X'00'         YES. ANY MORE PARMS ?                12350000
         BNE   RETURN              YES. MUST BE HDWR ADDRESSES          12400000
         L     CNTREG,SCDCNT       SET COMMUNICATION NAME TABLE         12450000
         LH    R0,SCDCNTN            POOL ADDRESS AND CNT COUNT         12500000
         OC    PARM2,=CL8' '       MASK TO BLANK ON BINARY ZEROS        12550000
MFS0004  CLC   PARM2,CNTNAME1      IS CNTNAME1= REQUEST ?               12600000
         BE    MFS0006             SET CLB/CTB POINTERS IF EQUAL        12650000
         AH    CNTREG,SCDCNTL      MOVE TO NEXT COMM. NAME TABLE ENTRY  12700000
         BCT   R0,MFS0004          KEEP CHECKING UNTILL ALL ARE CHECKED 12750000
         B     RETURN              NO MATCH TO IMS SYMBOLICS, SO RETURN 12800000
*********************************************************************** 12850000
*                                                                     * 12900000
*        MFS EDIT PROCESSING FOR 3270 SYMBOLIC TERMINAL               * 12950000
*                                                                     * 13000000
*********************************************************************** 13050000
MFS0006  L     CTBREG,CNTCTBPT     SET CTB ADDRESS FOR SYMBOLIC TERMNAL 13100000
         L     CLBREG,CTBCLB       SET CLB ADDRESS FOR SYMBOLIC TERMNAL 13150000
*********************************************************************** 13200000
*                                                                     * 13250000
*        MFS EDIT PROCESSING FOR 3270 DEFAULT TERMINAL                * 13300000
*                                                                     * 13350000
*********************************************************************** 13400000
MFS3270  OC    CLBDCB,CLBDCB     IS CLBDCB = ZEROS, THEN THIS  @OZ30477 13450007
*                                   IS A VTAM TERMINAL REQUEST.@OZ30477 13453007
         BNZ   MFS0008             NO. A BTAM TERMINAL REQUSET.@OZ30477 13456007
         LA    R1,MSGKEYED   SET R1=ADDR OF 29 BYTE KEY-IN AREA@OZ30477 13459007
         MVC   MSGKEYED,=CL29' '   CLEAR KEY-IN AREA           @OZ30477 13462007
         MVC   0(5,R1),=C'3270 '   MOVE '3270 ' TO 29 BYTE AREA@OZ30477 13465007
         LA    R1,5(R1)            PRESET 'R1' PASS '3270 '    @OZ30477 13468007
         CLI   PARM2,X'00'         IS SYMBOLIC NAME IN PARM2 ? @OZ30477 13471007
         BE    MFS0005             NO. JUST ADD VTAM NAME      @OZ30477 13474007
         MVC   5(8,R1),PARM2      YES. ADD SYMBOLIC NAME TO MSG@OZ30477 13477007
         LA    R1,13(R1)        POINT PASS '3270 ' AND SYMBOLIC@OZ30477 13480007
MFS0005  MVI   0(R1),X'FE'        SET VTAM CONVERSION INDICATOR@OZ30477 13483007
         MVC   1(8,R1),CLBNAME    MOVE VTAM NODE NAME TO OUTPUT@OZ30477 13486007
         B     RETURN                                          @OZ30477 13489007
MFS0008  BAL   LINKREG,CONVSYMN   CALL CONVERT SYMBOLIC ROUTINE@OZ30477 13492007
         CLI   PARM4,X'00'         IS SYMBOLIC NAME CONVERTED?          13500000
         BE    RETURN              NO                                   13550000
         LA    R1,MSGKEYED         SET R1=ADDR OF 29 BYTE KEY-IN AREA   13600000
         MVC   MSGKEYED,=CL29' '   CLEAR KEY-IN AREA                    13650000
         MVC   0(5,R1),=C'3270 '    MOVE '3270 ' TO 29 BYTE AREA        13700000
         LA    R1,5(R1)            PRESET 'R1' PASS '3270 '             13750000
         CLI   PARM2,X'00'         IS SYMBOLIC NAME IN PARM2 ?          13800000
         BE    MFS0010             NO. JUST ADD HARDWARE ADDRESSES      13850000
         MVC   5(8,R1),PARM2       YES. ADD SYMBOLIC NAME TO MSG        13900000
         LA    R1,13(R1)           POINT PASS '3270 ' AND SYMBOLICNAME  13950000
MFS0010  MVI   0(R1),X'FF'         SET SYMBOLIC CONVERSION INDICATOR    14000000
         MVC   1(3,R1),PARM4       MOVE HWR LINE/TERM ADDR TO OUTPUT    14050000
         UNPK  DBLWD,CTBTERM(3)    UNPACK 3 CNTRL & TERM ADDRESSES      14100000
         MVC   4(4,R1),DBLWD+3     MOVE CNTRL & TERM ADDR TO OUTPUT     14150000
         TR    4(4,R1),HXTOEB      CONVERT CNTRL & TERM ADDR TO EBCDIC  14200000
         B     RETURN                                                   14250000
*********************************************************************   14300000
*                                                                   *   14350000
*        INPUT EDITING SUBROUTINE FOR BOTH BASIC AND MFS MSG        *   14400000
*                                                                   *   14450000
*********************************************************************   14500000
EDITRTN  LA    PARMREG,PARM1       POINT TO PARMS.                      14550000
         LR    R1,PARMREG                                               14600000
         MVI   NEWPARM,C'1'        TURN ON 'NEW PARM' SWITCH.           14650000
EDIT001  CLI   0(DATAREG),C'A'     IS IT AN 'A' OR HIGHER?              14700000
         BL    EDIT004             NO. CHECK NEW PARM SWITCH.           14750000
         MVI   NEWPARM,C'0'        YES. SET NEW PARM SWITCH OFF.        14800000
         MVC   0(1,R1),0(DATAREG)  PUT THIS BYTE IN THIS PARM           14850000
         CLI   PARM5,X'00'         ARE ALL PARMS USED UP?               14900000
         BNER  LINKREG             YES. RETURN TO CALLER                14950000
         LA    R1,1(R1)            BUMP PARM POINTER WORKREG.           15000000
EDIT003  LA    DATAREG,1(DATAREG)  BUMP TIOA POINTER.                   15050000
         CR    DATAREG,DATALOOP    IS THERE MORE DATA IN TIOA?          15100000
         BL    EDIT001             YES. GO PROCESS MORE DATA.           15150000
         BR    LINKREG             NO. RETURN TO CALLER.                15200000
EDIT004  CLI   NEWPARM,C'1'        IS NEW-PARM SWITCH ON?               15250000
         BE    EDIT003             YES. CHECK FOR MORE DATA.            15300000
         CLI   PARM5,X'00'         NO. ARE ALL PARMS USED UP?           15350000
         BNER  LINKREG             YES. RETURN TO CALLER                15400000
         LA    PARMREG,8(PARMREG)  NO. BUMP TO NEXT NEW PARM.           15450000
         LR    R1,PARMREG          POINT WORK REG TO NEXT PARM.         15500000
         MVI   NEWPARM,C'1'        TURN ON NEW-PARM SWICH.              15550000
         B     EDIT003             GO CHECK FOR MORE DATA.              15600000
*********************************************************************** 15650000
*        AT THIS POINT UP TO 5 PARMS HAVE BEEN EDITED AND             * 15700000
*        ARE LEFT JUSTIFIED WITH TRAILING X'00'S                      * 15750000
*********************************************************************** 15800000
*********************************************************************** 15850000
*                                                                     * 15900000
*        SUBROUTINE TO CONVERT SYMBOLIC NAME TO HARDWARE DEVICE       * 15950000
*                                                                     * 16000000
********************************************************************* * 16050000
CONVSYMN TM    CLBFLAG3,CLB3DECB     DID IMS USE THIS CLB FOR HIS OWN?  16100000
         BO    D2                  YES. GO GET DCB FROM SAVE AREA       16150000
         TM    CLBFLAG3,CLB3CL40     DID IMS USE THIS CLB FOR HIS OWN?  16200000
         BO    D2                  YES. GO GET DCB FROM SAVE AREA       16250000
         L     DCBREG,DECDCBAD     GET LINE'S DCB ADDRESS               16300000
D1       TM    DCBOFLGS,DCBOFOPN   IS DCB OPEN?                         16350000
         BZ    RETURN              LEAVE IF 'NOT AVAIL'                 16400000
         L     DEBREG,DCBDEBAD     GET LINE'S DEB ADDRESS IF OPEN       16450000
         XR    R2,R2               PREPARE FOR CALCULATIONS:            16500000
         IC    R2,DECRLN           GET ACCESS METHOD'S DD-CARD NUMBER   16550000
         BCTR  R2,0                CHANGE TO RELATIVE DD # FROM DD 1    16600000
         SLL   R2,2                MULTIPLY RELATIVE DD # BY FOUR       16650000
         L     UCBREG,DEBUCBAD(R2) GET LINE'S UCB ADDRESS               16700000
         LA    UCBREG,0(UCBREG)    DROP TOP BYTE OF UCBREG              16750000
         CLI   UCBID,UCBSTND       MAKE ONE UCB ADDRESS VALIDITY CHECK  16800000
         BNER  LINKREG             LEAVE 'NOT AVAIL', SOMETHING'S WRONG 16850000
         MVC   PARM4(3),UCBNAME    ADD LINE'S ADDRESS TO SPECIAL OPRAND 16900000
         BR    LINKREG             RETURN TO EDIT MAIN-LINE             16950000
D2       L     DCBREG,CLBDCB       LOAD DCBREG FROM CLB SAVE AREA       17000000
         B     D1                                                       17050000
*                                                                       17100000
         EJECT                                                          17150000
*********************************************************************** 17200000
BNGIPTCH DC    C'PATCH AREA S-CONS:',128S(*)                            17250000
*********************************************************************** 17300000
*                                                                       17350000
COMMAREA DS    0D       MAP OF COMMUNICATION AREA                       17400000
PARM1    DS    D          1ST INPUT PARM - DEMF                         17450000
PARM2    DS    D          2ND INPUT PARM - 3270                         17500000
PARM3    DS    D          3RD INPUT PARM - LLL OR TTT                   17550000
PARM4    DS    D          4TH INPUT PARM - CC  OR NULLS                 17600000
PARM5    DS    D          5TH INPUT PARM - TT  OR NULLS                 17650000
DBLWD    DS    D          DOUBLEWORD WORK AREA                          17700000
NEWPARM  DS    C          SWITCH TO DETERMINE START OF NEW PARM         17750000
HXTOEB   DS    0D                                                       17800000
         DC    4XL16'00',C' ',XL15'00',7XL16'00'  THIS TABLE CONVERTS   17850000
         DC    X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'  HEX CHARS           17900000
         DC    2XL16'00'                CHARS FA-THRU-FF BACK           17950000
         DC    X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'  A THRU F            18000000
*                                                                       18050000
LOTOUP   DS    0F       LOWER CASE TO UPPER CASE TR TABLE               18100000
         DC    X'000102030405060708090A0B0C0D0E0F'                      18150000
         DC    X'101112131415161718191A1B1C1D1E1F'                      18200000
         DC    X'202122232425262728292A2B2C2D2E2F'                      18250000
         DC    X'303132333435363738393A3B3C3D3E3F'                      18300000
         DC    X'404142434445464748494A4B4C4D4E4F'                      18350000
         DC    X'505152535455565758595A5B5C5D5E5F'                      18400000
         DC    X'606162636465666768696A6B6C6D6E6F'                      18450000
         DC    X'707172737475767778797A7B7C7D7E7F'                      18500000
         DC    X'80C1C2C3C4C5C6C7C8C98A8B8C8D8E8F'                      18550000
         DC    X'90D1D2D3D4D5D6D7D8D99A9B9C9D9E9F'                      18600000
         DC    X'A0A1E2E3E4E5E6E7E8E9AAABACADAEAF'                      18650000
         DC    X'F0F1F2F3F4F5F6F7F8F9BABBBCBDBEBF'                      18700000
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'                      18750000
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'                      18800000
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'                      18850000
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'                      18900000
         LTORG                                                          18950000
*********************************************************************** 19000000
*                                                                       19050000
DEB      DSECT ,      O/S DATA EXTENT BLOCK MAP:                        19100000
         DS    XL32     BASIC SECTION - UN-REFERENCED                   19150000
DEBUCBAD DS    F        UCB ADDRESS OF FIRST LINE FOR THE CLB/DCB       19200000
*                                                                       19250000
MSGIN    DSECT ,      MAP OF TRANSACTION MESSAGE SEGMENT:               19300000
MSG      DS    0F       START OF TRANSACTION MESSAGE SEGMENT            19350000
MSGLL    DS    H        LENGTH OF MESSAGE (INCLUDING LL,Z1,Z2)          19400000
MSGZ1    DS    X          UN-REFERENCED - SHOULD ALWAYS BE X'00'        19450000
MSGZ2    DS    X        IMS EDIT INDICATOR - =0 BASIC EDIT              19500000
*                                            >0 MFS EDIT                19550000
TIOADBA  DS    0F       START OF MESSAGE DATA (INPUT AREA)              19600000
MSGDATA  DS    0C       FIRST BYTE OF MESSAGE DATA POINTER              19650000
MSGTXCD  DS    CL5      'DEMF ' AREA                                    19700000
MSGKEYED DS    CL29     29 BYTES KEY-IN AREA                            19750000
MSGCMDCD DS    CL4      4 BYTES CMND CODE AREA                          19800000
MSGMENU  DS    0C       START OF MENU PAGE1 INPUT AREA FOR (X)          19850000
MSGALL   DS    C          MENU PAGE1 1ST (X) INPUT AREA                 19900000
MSG3270  DS    C          MENU PAGE1 2ND (X) INPUT AREA                 19950000
MSGRMOT  DS    C          MENU PAGE1 3RD (X) INPUT AREA                 20000000
MSGLOCL  DS    C          MENU PAGE1 4TH (X) INPUT AREA                 20050000
MSGDPRSS DS    CL7        MENU PAGE1 5TH INPUT AREA (FOR ENTER KEY)     20100000
*                                                                       20150000
UCB      DSECT ,      O/S UNIT CONTROL BLOCK MAP:                       20200000
         DS    XL2        UN-REFERENCED                                 20250000
UCBID    DS    X        UCB IDENTIFIER                                  20300000
UCBSTND  EQU   X'FF'    UCB IDENTIFIER VALUE                            20350000
         ORG   UCB+13     UN-REFERENCED                                 20400000
UCBNAME  DS    CL3      PRINTABLE LINE ADDRESS (CUU)                    20450000
         DCBD  DSORG=XE,DEVD=BS                                         20500000
         ICLI  CLBBASE=0,CNTBASE=0,CTBBASE=0                            20550000
         ISCD  SECTYPE=DSECT,SCDBASE=0                                  20600000
*                                                                       20650000
R0       EQU   0                                                        20700000
R1       EQU   1                                                        20750000
R2       EQU   2                                                        20800000
DATAREG  EQU   2              POINTER TO DATA TO BE EDIT                20850000
DCBREG   EQU   3                                                        20900000
DEBREG   EQU   4              O/S DATA EXTENTED BLOCK                   20950000
UCBREG   EQU   5              O/S UNIT CONTROL BLOCK                    21000000
MSGREG   EQU   6              POINTER TO INPUT MESSAGE                  21050000
CTBREG   EQU   7              COMMUNICATIONS TERMINAL BLOCK             21100000
DATALOOP EQU   8              LENGTH OF DATA TO BE EDIT                 21150000
CLBREG   EQU   9              COMMUNICATIONS LINE BLOCK                 21200000
CNTREG   EQU   10             COMMUNICATIONS NAME TABLE                 21250000
SCDREG   EQU   11             SYSTEMS CONTENTS DIRECTORY                21300000
BASE1    EQU   12             BASE REGISTER                             21350000
*UNUSED  EQU   13             POINTER TO IMS REGISTER SAVE AREA         21400000
LINKREG  EQU   14             LINKAGE REGISTER                          21450000
PARMREG  EQU   15             POINTER TO PARAMETERS 1,2,3,4             21500000
R15      EQU   15             ENTRY POINT TO THIS PROGRAM FROM IMS      21550000
         END   BNGIEXIT                                                 21600000
