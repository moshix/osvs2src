         MACRO                                                          00050041
&NAME    CONTIME &TIMER=,&LOCAL=YES,&STCLOCK=YES                        00100041
* MACCOMP-SC1CM                                                         00150041
* MACSTAT-@G17EP5W                                                      00200041
* MACMEAN-CONVERT TIMER TO BINARY NUMBER REPRESENTING HH.MM.SS.T        00250041
         LCLB   &B        FLAG TO DETERMINE CODE GENERATION             00300041
&NAME    DS     0H                                                      00350041
         AIF    ('&LOCAL' EQ 'YES' OR '&LOCAL' EQ 'NO').NEXT1           00400041
&B       SETB   (1)       SET FLAG TO TERMINATE                         00450041
         IHBERMAC 54,,&LOCAL                                            00500041
.NEXT1   AIF    ('&STCLOCK' EQ 'YES' OR '&STCLOCK' EQ 'NO').NEXT2       00550041
&B       SETB   (1)       SET FLAG TO TERMINATE                         00600041
         IHBERMAC 54,,&STCLOCK                                          00650041
.NEXT2   AIF   ('&TIMER' NE '').CONTNUE                                 00700041
&B       SETB  (1)        SET FLAG TO TERMINATE                         00750041
         MNOTE 12,'THE NAME OF AN EIGHT CHARACTER FIELD WHICH CONTAINS C00800041
                THE TIME VALUE TO BE CONVERTED MUST BE SPECIFIED.'      00850041
.CONTNUE AIF   (&B).EXIT  ANY ERRORS FOUND--EXIT                        00900041
         AIF   ('&LOCAL' NE 'YES').NOLOC   LOCAL TIME REQUIRED          00950041
         L     1,CVTPTR                GET ADDRESS OF CVT TO ACCESS     01000041
         L     0,CVTTZ(1)              LOCAL TIME DIFFERENCE            01050041
.NOLOC   IHBINNRA &TIMER               LOAD REG 1 WITH ADDRESS          01100041
         AIF   ('&STCLOCK' EQ 'NO').NEXT3 NEED TO GET TOD VALUE?        01150041
         STCK  0(1)                    GET TOD CLOCK VALUE              01200041
         BC    8,IHBA&SYSNDX           IF SUCCESSFUL,CONVERT VALUE      01250041
         LA    0,4                     UNSUCCESFUL, SET BAD RETURN CODE 01300041
         SR    1,1                     UNSUCCESSFUL,SET ANSWER TO ZERO  01350041
         B     IHBB&SYSNDX             AND EXIT                         01400041
.NEXT3   ANOP                                                           01450041
IHBA&SYSNDX  L  1,0(1)                 GET HIGH ORDER WORD FROM TOD     01500041
         AIF   ('&LOCAL' NE 'YES').GMEANTM LOCAL TIME REQUIRED          01550041
         AR    1,0                     CHANGE TO LOCAL TIME             01600041
.GMEANTM SR    0,0                     ZERO EVEN REGISTER               01650041
         SLDL  0,7                     MULTIPLY BY 2**7                 01700041
         CNOP  0,4                     BRANCH AROUND CONSTANTS          01750041
         B     *+8                     BRANCH AROUND CONSTANTS          01800041
         DC    F'10546875'             CONSTANT VALUE K                 01850041
         D     0,*-4                   EVEN REG HAS 2**7 * NBR 2**20    01900041
*                                      U-SEC SINCE MIDNIGHT             01950041
         SRL   0,7                     FACTOR OUT 2**7 VALUE            02000041
         IHBINNRA &TIMER               LOAD ADDRESS IN REG 1            02050041
         L     1,4(1)                  GET LOW ORDER TOD VALUE          02100041
         SRDL  0,12                    REMOVE LOW ORDER BITS TO GET     02150041
*                                      RESOLUTION TO 1 MICRO-SEC        02200041
         D     0,=F'100000'            GET RESOLUTION TO .1 SEC IN      02250041
*                                      REG 1                            02300041
         LTR   1,1                     IF RESULT ^= EXACTLY MIDNIGHT    02350041
         BC    7,IHBC&SYSNDX           USE THAT VALUE, OTHERWISE        02400041
         LA    1,1                     SET TO 1 INCREMENT PAST MIDNITE  02450041
IHBC&SYSNDX SR    0,0                  SET RETURN CODE TO GOOD          02500041
IHBB&SYSNDX DS   0H                                                     02550041
.EXIT    MEXIT                                                          02600041
         MEND                                                           02650041
