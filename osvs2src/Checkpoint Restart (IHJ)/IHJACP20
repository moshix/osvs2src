*                                                                       00050020
         COPY  LCGASMSW                                                 00159902
*                                                                       00250020
         TITLE 'IGC0A06C (IHJACP20) -PRESERVE 1 (CHR)'                  00300018
IGC0A06C CSECT                                                          00700017
*                                                                YM2776 00750001
*FUNCTION/OPERATION  THIS ROUTINE WRITES OUT THE CHECKPOINT HEADER      01400017
*                    RECORD (CHR)                                       02100017
*                                                                       02800017
*ENTRY POINTS  IGC0A06C                                                 03500017
*                                                                       04200017
*INPUT  ADDRESS OF THE CHECKPOINT WORK AREA IN REGISTER 1               04900017
*                                                                       05600017
*OUTPUT  ADDRESS OF THE CHECKPOINT WORK AREA IN REGISTER 1              06300017
*                                                                       07000017
*EXITS-NORMAL                                                           07700017
*         1.) XCTL TO IGC0D06C TO WRITE OUT DSDRS                       08400017
*         2.) XCTL TO IGC0206C IF EOV OCCURS FOR THE FIRST TIME WITH    09100017
*                       CHKPT DATA SET ON TAPE                          09800017
*                                                                       10500017
*     -ERROR XCTL TO I/O RESUME AFTER INITIALIZING CKMSGCOD AND         11200017
*             CKRETCOD AS FOLLOWS                                       11900017
*                 CKRETCOD    CKMSGCOD     ERROR CONDITION              12600017
*                 X'000C'      X'0317'       I/O ERROR CREATING CHKPT   13300017
*                                                        DATA SET       14000017
*                  X'0008'     X'031B'       EOV ON DA OR FOR A SECOND  14700017
*                                              TIME ON TAPE             15400017
*                                                                       16100017
*TABLES/WORK AREAS  CHKWA,TCB,DCB,DEB                                   16800017
*                                                                       17500017
*ATTRIBUTES  REENTRANT, SUPERVISOR STATE, TYPE 4 SVC ROUTINE            18200017
*                                                                       18900017
*RESTRICTIONS  MAY NOT BE USED IN OS/MFT                                19600017
*                                                                       20300017
* CHANGE ADDITIONS DELETIONS FOR VS2 037                                20350037
*A26660018-26794000                                            @ZA27702 20400037
*                                                                       20450037
*NOTES NONE                                                             21000017
         EJECT                                                          21700017
         USING CHKWA,RC                                                 22400017
         BALR  RB,0                BASE ADDR.                           23100018
         USING *,RB                                                     23800017
         B     BEGIN               BRANCH AROUND EYE CATCHER            23850000
         CNOP  0,8                 ALIGN TO A DOUBLE WORD BOUNDARY      23900000
         DC    C'IGC0A06C'         CSECT NAME                           23950000
BEGIN    EQU   *                                                        24000000
         LR    RC,R1               GET ADDRESS OF CHKPT WORK AREA       24500017
         LA    RD,CKREGSAV                                              25200017
         L     R2,CKDCBAD          GET DCB ADDRESS                      25900017
         USING IHADCB,R2                                                26600017
*                                                                     * 26650000
*********************************************************************** 26650400
*        TO PROVIDE SYSTEM INTEGRITY, THE CHECKPOINT DEB MUST BE      * 26652000
*        VERIFIED TO BE ON THE TCB DEB CHAIN, AND IN THE JOBSTEP      * 26654000
*        DEB TABLE.                                                   * 26656000
*********************************************************************** 26656400
*                                                                     * 26658000
         DEBCHK  (R2),TYPE=VERIFY,AM=SAM                                26658400
*                                                                     * 26660018
*                                                            @ZA27702   26720037
         TM    CKFLAG1,CKOPEN      CHECKPOINT OPEN DS ?      @ZA27702   26722037
         BO    BYPASS              YES BYPASS                @ZA27702   26724037
         TM    DCBCIND2,DCBCNCHS   CHAIN SCHEDULING IN USE ? @ZA27702   26726037
         BNO   BYPASS              NO BYPASS                 @ZA27702   26728037
         L     RF,DCBIOBAD         GET THE C. S. IOB         @ZA27702   26730037
         L     R1,8(RF)            GET OLDEST ICB            @ZA27702   26732037
         SR    R9,R9               ZERO REG 9                @ZA27702   26734037
         ICM   R9,1,3(RF)          GET NOP OFFSET            @ZA27702   26736037
         AR    R1,R9               SET UP ADDRESS OF NOP     @ZA27702   26738037
         ST    R1,12(RF)           SET UP NOP OFFSET IN IOB  @ZA27702   26740037
         OI    12(RF),X'80'        TURN ON HIGH ORDER BIT    @ZA27702   26742037
OTHER    EQU   *                                             @ZA27702   26744037
         CLI   0(R1),X'88'         TICNOP OPCODE ?           @ZA27702   26746037
         BE    NEXT                YES BRANCH TO TEST NEXT   @ZA27702   26748037
         CLI   0(R1),X'03'         NOP ( FOR TAPE )          @ZA27702   26750037
         BE    NEXT                YES BRANCH TO TEST NEXT   @ZA27702   26752037
         TM    CKFLAG1,CKTAPE      DATASET ON TAPE ???       @ZA27702   26754037
         BNO   DISK                NO MUST BE DA BRANCH      @ZA27702   26756037
         MVI   0(R1),X'03'         SET UP FOR NOP            @ZA27702   26758037
         B     NEXT                PROCESS NEXT ICB          @ZA27702   26760037
DISK     EQU   *                                             @ZA27702   26762037
         LA    R9,8(R1)            SET REG 9 TO NEXT CCW     @ZA27702   26764037
         ST    R9,0(R1)            SET UP NEXT CCW           @ZA27702   26766037
         MVI   0(R1),X'88'         SET UP TICNOP             @ZA27702   26768037
NEXT     EQU   *                                             @ZA27702   26770037
         TM    CKFLAG1,X'80'       SECOND PASS ?             @ZA27702   26772037
         BO    BYPASS              YES YOU ARE FINISHED      @ZA27702   26774037
         OI    CKFLAG1,X'80'       SET FLAG UP               @ZA27702   26776037
         L     R1,8(RF)            GET FIRST ICB AGAIN       @ZA27702   26778037
         L     R1,0(R1)            GET THE OTHER ICB         @ZA27702   26780037
         SR    R9,R9               ZERO REG 9 AGAIN          @ZA27702   26782037
         ICM   R9,1,3(RF)          GET NOP OFFSET            @ZA27702   26784037
         AR    R1,R9               SET UP TO POINT TO NOP    @ZA27702   26786037
         B     OTHER               PROCESS OTHER ICB         @ZA27702   26788037
BYPASS   EQU   *                                             @ZA27702   26790037
         NI    CKFLAG1,X'7F'       TURN OFF BIT              @ZA27702   26792037
*                                                            @ZA27702   26794037
*********************************************************************** 26822037
*        FOR DA - DEB ADDR IS SAVED AND USED FOR COMPARISON TO TEST   * 26840018
*          FOR EOV CONDITION BECAUSE EOV GETS A NEW DEB               * 26900018
*        FOR TAPE - VOLUME SEQUENCE NUMBER IN DEB IS SAVED AND USED   * 26960018
*          FOR TESTING AN EOV CONDITION BECAUSE EOV INCREMENTS VOL    * 27020018
*          SEQ NO                                                     * 27080018
*********************************************************************** 27140018
*                                                                     * 27200018
         L     RF,DCBDEBAD         GET DEB ADDRESS                      27300017
         TM    CKFLAG1,CKTAPE      IS CHKPT DATA SET ON TAPE            28000017
         BO    PRETAPE             BRANCH IF YES                        28700017
         MVC   CKTSTEOV(BYTE),DEBNMEXT(RF)   SAVE NUMBER OF EXTENTS FOR 29400018
*                                          END OF VOLUME TESTS          30100017
         B     PREBUFF             SKIP SAVING EOV INFO FOR TAPE        30800018
PRETAPE  EQU   *                                                        31500017
         USING DEBSECT,RF          MAPPING OF DEB                       31900018
         LA    RF,VOLSEQNO         GET ADDR OF VOLUME SEQ. NO. IN DEB   32300018
         ST    RF,CKTSTEOV         SAVE INFO FOR EOV TEST               32900017
         MVC   CKEOVCMR(LNWORD),ZERO(RF)                                33000018
*                                                                       33100018
*********************************************************************** 33200018
*                                                                     * 33300018
*        INITIALIZE CONTROL BLOCKS AND THEN WRITE CHR TO CHKPT D.S.   * 33400018
*                                                                     * 33500018
*********************************************************************** 33600018
*                                                                     * 33700018
PREBUFF  EQU   *                                                        34300017
         LR    R9,RC                                                    35000017
         A     R9,CKBFOFST         GET ADDRESS OF BUFFER                35700017
         ST    R9,PRENXB           INITIALIZE OUTPUT BUFFER HANDLER'S   37100017
         MVC   PRENOB(LNWORD),DSDRLN         PARAMETERS                 37800018
*                                                                YM6541 37805002
*        BEFORE WRITING THE CHR GET THE REGION END ADDRESS       YM6541 37810002
*        AND CALCULATE THE CURRENT SIZE OF REGION TO BE SAVED    YM6541 37815002
*                                                                YM6541 37820002
         USING CHRSECT,R9          ADDRESS CHR                   YM6541 37825002
         L     RF,CVTPTR           GET CVT ADDRESS               YM6541 37830002
         USING CVTMAP,RF           ADDRESS CVT                   YM6541 37835002
         L     RF,CVTTCBP          GET ADDR OF TCB/ASCB ADDR'S   YM6541 37840002
         L     RF,CURASOFS(,RF)    GET CURRENT ASCB ADDR         YM6541 37845002
         USING ASCB,RF             ADDRESS ASCB                  YM6541 37846002
         L     RF,ASCBLDA          GET LDA ADDR                  YM6541 37847002
         USING LDA,RF              ADDRESS LDA                   YM6541 37848002
         L     RF,CURRGNTP         GET CURRENT REGION END ADDR   YM6541 37849002
         DROP  RF                  LDA                           YM6541 37849202
         ST    RF,CKPPE            SAVE REGION END ADDR          YM6541 37849402
         S     RF,CKPPM            GET REGION SIZE               YM6541 37849602
         ST    RF,CHRPPE           PUT REGION SIZE IN CHR        YM6541 37849802
         DROP  R9                  CHR                           YM6541 37849902
         TM    CKFLAG1,CKVEQR      IS THIS V=R?                  YM2776 37850001
CKVEQR   EQU   8                        V=R BIT IN CKFLAG1      YA00258 37860002
         BNO   CKNOVEQR            NO, GO AROUND REAL PROCESSING YM2776 37900001
         LRA   R9,0(,R9)           GET REAL ADDRESS OF CHR       YM2776 37950001
CKNOVEQR EQU   *                                                 YM2776 38000001
         ST    R9,CKAREA1                                               38500017
         LA    RF,CHRLN                                                 39200017
         STH   RF,CKLNGTH1         INITIALIZE DECB1                     39900017
         LA    R9,PRESYNAD         MODIFY DCBSYNAD SO THAT PRESERVE     40600017
         ST    R9,PREAD            SYNAD WILL GET CONTROL IF I/O ERROR  41300017
         MVC   DCBSYN1(LNADDR),PREAD2            OCCURS                 42000018
         LA    R9,CKDECB1          GET DECB ADDRESS                     42700017
         WRITE (R9),SF,,,,,,MF=E   WRITE OUT CHR                        43400017
         CHECK (R9)                                                     44100017
         OI    CKFLAG2,CKCHR       SET FLAG - CHR WRITTEN               44800017
*                                                                     * 44808018
*********************************************************************** 44816018
*                                                                     * 44824018
*        TEST FOR SUCCESSFUL WRITE - I/O ERROR OR EOV CONDITION       * 44832018
*        IF ERROR OCCURS STORE ERROR CODES IN CHKWA AND XCTL TO       * 44840018
*        IGC0N06C - CHKPT D.S. NOT COMPLETED                          * 44848018
*                                                                     * 44856018
*********************************************************************** 44864018
*                                                                     * 44872018
         TM    CKFLAG1,CKTAPE      CHKPT DATA SET ON TAPE               44880017
         BO    PRETAEOV            BRANCH IF YES                        44960017
         L     RF,DCBDEBAD         GET DEB ADDRESS                      45040017
         CLC   CKTSTEOV(BYTE),DEBNMEXT(RF)   NUMBER OF EXTENTS CHANGED  45120018
         BNE   PREOVER             BRANCH IF YES--EOV ON D.A. ERROR     45200017
         B     PREIOCHK            BRANCH TO CHECK FOR I/O ERROR        45280017
PRETAEOV EQU   *                                                        45360017
         L     RF,CKTSTEOV                                              45500017
         CLC   ZERO(LNWORD,RF),CKEOVCMR      DID EOV OCCUR              46200018
         BNE   PREEOV              BRANCH IF YES                        46900017
PREIOCHK EQU   *                                                        47200017
         CLI   CKRETCD1,CKIOERR    DID I/O ERROR OCCUR                  47700018
         BE    PREERXIT            BRANCH IF YES                        48300017
         TM    CKFLAG1,CKBPAM      IS CHKPT DATA SET OPENED FOR BPAM    49000017
*                                                                     * 49070018
*********************************************************************** 49140018
*                                                                     * 49210018
*        IF CHKPT D.S BPAM - NOTE CHR SO THAT D.S CAN BE STOWED UNDER * 49280018
*        CHECKID IN CHKPT EXIT RTN                                    * 49350018
*                                                                     * 49420018
*********************************************************************** 49490018
*                                                                     * 49560018
         BZ    PREXIT              BRANCH IF NO--SKIP NOTE              49700017
         NOTE  (R2)                                                     50400017
         ST    R1,CKTTR            SAVE NOTED TTRZ                      51100017
*                                                                     * 51180018
*********************************************************************** 51260018
*                                                                     * 51340018
*        TRANSFER CONTROL TO NEXT MODULE                              * 51420018
*                                                                     * 51500018
*********************************************************************** 51580018
*                                                                     * 51660018
PREXIT   EQU   *                                                        51800017
         MVC   PRESV(LXCTLST),PREDSDR   TRANSFER CONTROL TO IGC0D06C    52500018
PREOUT   EQU   *                                                        53200017
         LR    R1,RC               PUT W. A. ADDRESS IN PARM REG        53900017
         LA    RF,PRESV            SET UP PARAMETER LIST FOR XCTL       54600017
         ST    RF,PRESV8                                                55300018
         LA    RF,PRESV8                                                56000018
         XCTL  SF=(E,(15))         TRANSFER CONTROL TO NEXT MODULE      56700017
*                                                                     * 56750018
*********************************************************************** 56800018
*        EOV ERROR RTN - IF CHKPT D.S. ON DISC, CHKPT NOT CONTINUED   * 56850018
*        AND CONTROL IS TRANSFERED TO IGC0N06C TO RESTORE I/O AND EXIT* 56900018
*        IF CHKPT D.S. ON TAPE, CONTROL IS TRANSFERED TO IGC0206C TO  * 56950018
*        ATTEMPT TO WRITE A COMPLETE ENTRY ON ONE VOLUME. IF EOV      * 57000018
*        OCCURS FOR A 2ND TIME ON TAPE(INDICATING ONE ENTRY CANNOT FIT* 57050018
*        ON ONE VOLUME) CHKPT IS NOT CONTINUED AND THE SAME PATH IS   * 57100018
*        FOLLOWED AS EOV ON DISC.                                     * 57150018
*                                                                     * 57200018
*********************************************************************** 57250018
*                                                                     * 57300018
PREEOV   EQU   *                                                        57400017
         TM    CKFLAG2,CKEOV       HAS EOV OCCURRED BEFORE              58100017
         BO    PREOVER             BRANCH YES - ERROR                   58800017
         OI    CKFLAG2,CKEOV       SET EOV FLAG                         60900017
         MVC   PRESV(LXCTLST),PREDSDR   SET UP XCTL PARM LIST           61600018
         MVI   PRESV4,CHAR2                                             62300018
         B     PREOUT              GO TO ISSUE XCTL                     63000018
PREOVER  EQU   *                                                        63700017
         MVI   CKRETCD1,CKPGMERR   INDICATE PROGRAMMER ERROR            64400018
         LA    RA,CKERIO7                                               65100017
         STH   RA,CKMSGCOD         PUT ERROR CODE IN WORK AREA          65800017
PREMVER  EQU   *                                                        66500017
         MVC   PRESV(LXCTLST),PREIORE   MOVE IORESUME'S NAME TO XCTLIST 66600018
*                                                                     * 66700018
*********************************************************************** 66800018
*                                                                     * 66900018
*        I/O ERROR AND SYNAD RTN. WHEN AN I/O ERROR OCCURS PRESERVE'S * 67000018
*        SYNAD RTN GETS CONTROL FROM DCB SYNAD EXIT AND CKRETCOD FIELD* 67100018
*        IN CHKWA IS SET TO INDICATE I/O ERROR CCCURED. CONTROL IS RE-* 67200018
*        TURNED FROM SYNAD RTN. AFTER WRITE IS CHECK'D CKRETCOD IS    * 67300018
*        TESTED FOR I/O ERROR,IN WHICH CASE CONTROL IS TRANSFERED TO  * 67400018
*        IGC0N06C TO RESTORE I/O AND EXIT                             * 67500018
*                                                                     * 67600018
*********************************************************************** 67700018
*                                                                     * 67800018
         B     PREOUT              GO TO XCTL                           67900017
PREERXIT EQU   *                                                        68600017
         LA    RA,CKERIO3          I/O ERROR WRITING CHKPT DATA SET     69300017
         STH   RA,CKMSGCOD         PUT IN WORK AREA                     70000017
         B     PREMVER             BRANCH TO SET UP FOR EXIT            70700017
PRESYNAD EQU   *                                                        71400017
         MVI   CKRETCD1,CKIOERR    MOVE RETURN CODE TO WORK AREA        72100018
         BR    RE        RETURN                                         72800018
CKERIO3  EQU   X'0317'             WRITE UNSUCCESSFUL                   73500017
CKERIO7  EQU   X'031B'             INVALID EOV                          74200017
DEBNMEXT EQU   16                                                       74500017
CHRLN    EQU   400       LENGTH OF CHR                                  74600018
LNWORD   EQU   4         WORD LENGTH                                    74700018
LNADDR   EQU   3         ADDRESS LENGTH                                 74800018
LXCTLST  EQU   8         LENGTH OF XCTL PARM LST                        74900018
BYTE     EQU   1         LENGTH OF BYTE (NO. EXTENTS IN DEB)            75000018
ZERO     EQU   0         USED FOR ADDRESSING                            75100018
CHAR2    EQU   C'2'      CHARACTER NEEDED TO ALTER MODULE NAME TO 206C  75200018
CURASOFS EQU   12                  OFFSET TO CURRENT ASCB        YM6541 75250002
DSDRLN   DC    F'400'              LENGTH OF DSDRS AND CHR              75600017
*        MODULE NAMES USED IN XCTL PARMLST                            * 75900018
PREDSDR  DC    C'IGC0D06C'         PRESERVE 2(NORMAL FLOW)              76400018
PREIORE  DC    C'IGC0N06C'         RESUME I/O (I/O ERROR)               77000018
CKCHR    EQU   128                                                      77700017
*                                                                     * 77750000
*        MAINTENANCE SPACE                                            * 77760000
*                                                                     * 77770000
PATCH    DC    XL((*-IGC0A06C)/20)'00'   FIVE PERCENT MAINTENANCE SPACE 77780000
         EJECT                                                          77780402
*                                                                       77782002
*        MAPPING OF CHECKPOINT HEADER RECORD(CHR)                     * 77784002
*                                                                       77786002
CHRSECT  DSECT                                                          77788002
         DS    0D                                                       77788402
CHRCOUNT DS    H    NUMBER OF CHKPTS TAKEN                              77788802
CHRCKIDL DS    H    LENGTH OF CHECKID                                   77789202
CHRCHKID DS    4F   CHECKID                                             77789602
CHRDDNM  DS    2F   DDNAME OF CHECKPOINT DATA SET.                      77789702
CHRPPM   DS    CL4       LOW ORDER ADDR P/P CORE                        77789802
CHRPPE   DS    CL4       HIGH ORDER ADDRESS P/P CORE                    77789902
CHRBLKSI DS    CL2       CHKPT D.S.  BLKSIZE                            77793202
CHRTIOTL DS    CL2       LENGTH OF TIOT                                 77795202
CHRFLAGS DS    CL1       CHKPT FLAG 1                                   77795602
CHRWACL  DS    CL3       CHKPT W.A. LENGTH                              77796002
CHRCKWAD DS    0F                                                       77796402
CHRFLAG2 DS    CL1       CHKPT FLAG 2                                   77796502
CHRWAAD  DS    CL3       CHKPT W.A. ADDR                                77796602
CHRSVRB  DS    CL4       ADDR OF CHKPT SVRB                             77822402
CHRPPML  DS    CL4       LOW ORDER ADDR CHKPT SVRB                      77832402
CHRPPEL  DS    CL4       HIGH ORDER ADDR CHKPT SVRB                     77842402
CHRCKID2 EQU   CHRCHKID+1                                               77844402
CHRPADH  EQU   *    PADDING BEGINS HERE.                                77846402
*                                                                     * 77848402
*        MAPPING OF DEB TO GET VOLUME SEQUENCE NO                       77874202
DEBSECT  DSECT                                                          77900018
INITIAL  DS    9F        INITIAL SECTION OF DEB                         78000018
VOLSEQNO DS    1F        VOLUME SEQUENCE NO  FOR TAPE                   78100018
         IEEVCHWA   VER=3                                               78400000
CKTSTEOV EQU   CKUNIT                                                   79100017
CKEOVCMR EQU   CKUNIT+4                                                 79800017
PRESV    DS    1F        XCTL PARM LIST - MODULE NAME                   82200018
PRESV4   DS    1C                                                       82500018
PRESV5   DS    CL3                                                      82800018
PRESV8   DS    1F        POINTER TO PARMLST                             83100018
PRENXB   DS    1F        ADDR OF BUFFER                                 83400018
PRENOB   DS    1F        LENGTH OF BUFFER                               83700018
*        OUTPUT BUFFER HANDLER'S PARAMETERS                             84000018
PREAD    DS    0F        WORK AREA FOR ADDRESSES                        84300018
         DS    1C                                                       84600018
PREAD2   DS    3C                                                       84900018
*        INFO FOR TEST OF EOV CONDITION                               * 85200018
PREADEOV DS    1F        ADDRESS OF TEST FOR EOV CONDITION              85500018
PRNEOV   DS    1F        VALUE USED IN TEST FOR EOV CONDITION           85800018
PRESAVE  DS    12F       SAVE AREA                                      86100018
PRELNGTH EQU   *                                                        87500017
         DCBD  DSORG=(BS,PO),DEVD=(DA,TA)                               88900017
*        ADDRESSING BYTES WITHIN WORD                                   89000018
DCBSYN1  EQU   DCBSYNAD+1     DCB SYNAD ADDRESS                         89100018
CKRETCD1 EQU   CKRETCOD+1     RETURN CODE FIELD IN CHKWA                89200018
         EJECT                                                          89250002
         CVT                                                            89300002
         EJECT                                                          89350002
         IHAASCB                                                        89400002
         EJECT                                                          89450002
         IHALDA                                                         89500002
         END                                                            89600017
