*                                                                       00050000
         COPY  LCGASMSW                                                 00100000
  TITLE 'IGC0205B (IHJARS01) RESTART HOUSEKEEPING ROUTINE-2ND LOAD'     00102002
*                                                                       00110002
**********************************************************************  00112002
*STATUS - CHANGE LEVEL 000                                              00120002
*                                                                       00150000
*TITLE 'IGC0205B (IHJARS01) RESTART HOUSEKEEPING ROUTINE-2ND LOAD'      00400002
*                                                                       00450002
*FUNCTION/OPERATION  IGC0205B WILL DO THE FOLLOWING               MFTCR 01200018
*                   1.)  MOVE THE IOB, ICBS, AND CHANNEL PROGRAMS       01600017
*                      ASSOCIATED WITH THE CHKPT DATA SET TO THE        02000017
*                      RESTART WORK AREA AND UPDATE ALL POINTERS TO     02400017
*                      THE IOB, ICBS, AND CHANNEL PROGRAMS.             02800017
*                   2.)  FOR PCP, MOVE THE DEB ASSOCIATED WITH THE      03200017
*                      CHKPT DATA SET TO THE RESTART WORK AREA.         03600017
*                   3.)  ISSUE A POINT MACRO SO THAT THE NEXT           04200017
*                      READ MACRO WILL READ IN THE FIRST CIR.           04800017
*                   4.)  CALCULAGE OFFSET TO BUFFER AND LENGTH OF       05200017
*                      REPDCB'S WORK AREA AND SAVE THESE VALUES IN      05600017
*                      THE RESTART WORK AREA.                           06000017
*ENTRY POINTS - FROM IGC0005B(IHJARS00)                                 06400002
*                                                                       06450002
*INPUT - ADDRESS OF THE RESTART WORK AREA IN REGISTER 1.                06800018
*                                                                       06850002
*OUTPUT- ADDRESS OF THE RESTART WORK AREA IN REGISTER 1.                07200018
*                                                                       07250002
*EXTERNAL ROUTINES AND SVC ROUTINES - BSAM POINT                        07600018
*                                     XCTL                              08000017
*                                                                       08050002
*EXITS-NORMAL  XCTL TO REPMAIN, IGC0505B                                08400017
*                                                                       08450002
*EXITS-ERROR- NONE.                                                     08800018
*                                                                       08850002
*WORK AREAS - RSTWA IS THE MAPPING OF THE RESTART WORK AREA.            09200018
*             TAPESECT IS THE MAPPING OF AN IOB, TWO ICBS AND THEIR     09600017
*           RESPECIVE CHANNEL PROGRAMS USED FOR READING IN THE CHKPT    10000017
*           DATA SET FROM TAPE USING CHAINED SCHEDULING                 10400017
*             DASECT IS THE MAPPING OF AN IOB, TWO ICBS AND THEIR       10800017
*           RESPECTIVE CHANNEL PROGRAMS USED FOR READING IN THE CHKPT   11200017
*           DATA SET FROM D.A. USING CHAINED SCHEDULING.                11600017
*                                                                       11650002
*TABLES REFERENCED - TCB, DCB, DEB, IOB, ICB.                           12000018
*ATTRIBUTES - REENTRANT, SUPERVISOR STATE, TYPE 4 SVC ROUTINE.          12400018
*                                                                       12450002
*NOTES - NONE.                                                          13200018
*                                                                       13250002
* RELEASE 037 CHANGES ADDS DELETIONS                                    13260037
*A585340-585390                                                @ZA17064 13270037
**********************************************************************  13300002
*                                                                       13350002
IGC0205B CSECT                                                          13600018
         BALR  RB,ZERO             SET UP MODULE ADDRESSABILITY         14000018
         USING *,RB                                                     14400018
         B     BEGIN               BRANCH AROUND EYE CATCHER            14450000
         CNOP  0,8                 ALIGN TO A DOUBLE WORD BOUNDARY      14500000
         DC    C'IGC0205B'         CSECT NAME                           14550000
         DC    C'JAN 1977'         LAST UPDATE DATE                     14560037
         DC    C'@ZA17064'         LAST APAR                            14570037
BEGIN    EQU   *                                                        14600000
         USING IHADCB,R6           ADDRESS CHKPT DCB.                   15200017
         USING R2SECT,RA           ADDR IGC0205BS W.A.            MFTCR 15600018
         USING RSTWA,RC            ADDRESS RESTART WORK AREA.           16000017
         LR    RC,R1               GET ADDR OF RESTART WORK AREA.       16400017
         L     R4,RSTCBAD          GET TCB ADDR.                        16800017
         LA    R6,RSBDCB           GET ADDRESS OF RESTART DCB.          17200017
*                                                                       17250002
*        TO PREVENT CLOSE FROM ABENDING IF ERRORS OCCUR DURING   YM7073 17300002
*        RESTART, THE CHECKPOINT DATA SET DEB WILL BE REMOVED    YM7073 17350002
*        FROM THE DEB TABLE AND THE TCB.                         YM7073 17400002
*                                                                       17450002
*        LA    R1,0(R6)           LOAD ADDRESS INTO PARM REG     YM7073 17500037
*        DEBCHK (R1),TYPE=DELETE  REMOVE DEB FROM DEB TABLE      YM7073 17520037
*        SR    R1,R1              ZERO OUT REGISTER              YM7073 17540037
*        ST    R1,TCBDEB(R4)      ZERO OUT TCBDEB FIELD          YM7073 17560037
         LR    RA,RC                                                    17600017
         A     RA,RSMWOFST         GET ADDRESS OF IGC0205BS W.A.  MFTCR 18000018
         LA    R1,RSIOBCP          GET ADDR OF RELOCATE AREA     Y02076 18400002
         L     R2,DCBIOBA          GET ADDR OF IOB.                     18800017
         TM    RSFLAG1,RSTAPE      IS DATA SET ON A D.A. DEVICE         19200017
         BZ    RSTDA               YES-GO TO MOVE D.A. IOB, ICBS        19600017
*                                  AND CPS TO W.A.                      20000017
*                                                                     * 20500018
*********************************************************************** 20600018
*                                                                     * 20700018
*        THIS SECTION WILL MOVE THE TAPE IOB,ICBS, AND CHANNEL        * 20800017
*     PROGRAMS CONSTRUCTED BY OPEN FOR THE RESTART DATA SET TO THE    * 21200017
*     RESTART WORK AREA.                                              * 21600017
*                                                                     * 21700018
*********************************************************************** 21800018
*                                                                     * 21900018
         USING TAPESECT,R1         ADDR AREA WHERE IOB, ICBS AND CPS    22000017
*                                  WILL BE MOVED TO.                    22400017
         MVC   TIOB(TIOBL),ZERO(R2)  MOVE IOB TO W.A.                   22800018
         LA    R2,TIOB             GET ADDRESS OF IOB.                  23200017
         ST    R2,DCBIOBA          UPDATE IOB ADDR FIELD IN DCB.        23600017
         ST    R2,RSTAD            UPDATE POINTER TO NEXT IOB.          24000017
         MVC   TNIOB1(LIOB),RSTAD1                                      24400018
         LA    R2,TIOBECB          GET ECB ADDR.                        24800017
         ST    R2,TECBPT           UPDATE POINTER TO ECB.               25200017
         LA    R2,TREAD            GET ADDR OF CHANNEL PROGRAM.         25600017
         ST    R2,TCPSTART         UPDATE POINTER TO CHANNEL PROGRAM.   26000017
RSTDRT   EQU   *                                                        26400017
*                                                                     * 36640018
*********************************************************************** 36660018
*                                                                     * 36680018
*        CALCULATE OFFSETS WITHIN RESTART WORKAREA FOR BUFFER AND     * 36700018
*        REPDCB'S WORKAREA                                            * 36720018
*                                                                     * 36740018
*********************************************************************** 36760018
*                                                                     * 36780018
RSTSKP2  EQU   *                                                        36800017
         L     R1,RSWACL           GET LENGTH OF WA              Y02076 36850002
         SH    R1,SSCRBUFL         LENGTH OF SSCR/SUR BUFFER     Y02076 37800002
         ST    R1,RSBFOFST         SAVE OFFSET IN W.A.                  38400017
RSTREPL  EQU   *                                                        38700017
         L     R2,RSWACL           COMPUTE LENGTH OF REPDCB'S W.A.      38800017
         S     R2,RSWAOFST                                              39200017
         ST    R2,RSDCBL           SAVE LENGTH IN W.A.                  39600017
*                                                                     * 39630018
*********************************************************************** 39660018
*                                                                     * 39690018
*        FOR CHKPT D.S. ON D/A, ISSUE POINT MACRO TO POSITION D.S. TO * 39720018
*        FIRST CIR IN CHKPT ENTRY SO THAT REPMAIN CAN RESTORE P/P     * 39750018
*        CORE (TTR OF CIR PASSED TO RESTART FROM IEFDSDRP WHO ISSUED  * 39780018
*        A CORRESPONDING NOTE MACRO)                                  * 39810018
*                                                                     * 39840018
*********************************************************************** 39870018
*                                                                     * 39900018
         TM    RSFLAG1,RSTAPE      IS DATA SET ON TAPE                  40000017
         BO    RSTTAPE             YES-GO TO POSITION TAPE TO 1ST CIR   40400017
         LA    R0,RSCIRTTR         LOAD PARM REG WITH POINTER TO TTRN.  40800017
         LA    R1,RSBDCB           LOAD PARM REG. WITH DCB ADDR.        41200017
         POINT (R1),(R0)           INSURE THAT THE NEXT RECORD TO       41600017
*                                  BE READ WILL BE THE FIRST CIR.       42000017
*                                                                     * 42050018
*   THE FOLLOWING CODE HAS BEEN ADDED TO ENHANCE SYSTEM INTEGRITY       42060002
*   BY PERFORMING LENGTH-CHECKING ON CIR*S AND SUR'S AS THEY ARE        42070002
*   READ IN BY REPMAIN.                                                 42080002
*                                                                       42090002
*   IF THE CHECKPOINT BEING READ IS INTACT AND VALID, ALL RECORDS WILL  42092002
*   BE OF THE EXACT LENGTH COMPUTED BY REPMAIN AND PLACED IN THE        42094002
*   DECB BEFORE EACH READ.  THUS DETECTION OF A WRONG LENGTH RECORD     42096002
*   ALLOWS US TO PREVENT RESTORING OF SYSTEM CONTROL BLOCKS FROM        42098002
*   INVALID RECORDS.                                                    42098402
*                                                                       42098802
*   WRONG LENGTH RECORD DETECTION IS ACCOMPLISHED BY:                   42099202
*                                                                       42099602
*          (1)   TURNING OFF THE SILI BIT IN THE READ DATA CCW BUILT    42099702
*                BY BSAM.  BSAM BUILT THE CCW WITH THE SILI ON          42099802
*                BECAUSE IT USES THIS METHOD TO AVOIID LENGTH           42099902
*                CHECKING ON FORMAT U RECORDS.                          42116602
*                                                                       42126602
*          (2)   TESTING, IN ALL REPMAIN SYNAD ROUTINES, TO SEE IF      42128602
*                THE I/0 ERROR THAT GAVE SYNAD CONTROL WAS A WRONG      42130602
*                LENGTH RECORD.  IF SO, A UNIQUE ERROR CODE IS SET.     42132602
*                                                                       42133002
*   THE FOLLOWING CODE LOCATES THE SILI BIT AND TURNS IT OFF.  THIS     42133102
*   CODE IS DEPENDANT UPON THE FORMAT OF BSAM CHANNEL PROGRAMS.         42133202
*                                                                       42138802
RSTSKP3  EQU   *                                                YA00153 42140802
         LA    R6,RSBDCB                GENERATE DCB POINTER    YA00153 42142802
         USING IHADCB,R6                ADDRESS CHKPT DCB       YA00153 42143202
         L     R1,DCBIOBA               POINT TO IOB            YA00153 42143602
         USING DASECT,R1                ADDRESS IT              YA00153 42144002
         L     R1,DCPAD                 GET CP START ADDRESS    YA00153 42144102
         DROP  R1                                               YA00153 42144202
         CLI   0(R1),TAPECC             TEST FOR TAPE CHAN PROG YA00153 42144302
         BNE   RSTSILI2                 NO-TRY NEXT TYPE        YA00153 42158202
         NI    TAPESILI(R1),X'FF'-SILIBIT  TURN OFF SILI BIT    YA00153 42168202
         B     RSTSKP4                                          YA00153 42170202
RSTSILI2 EQU   *                                                YA00153 42170602
         CLI   0(R1),RPSCC              TEST FOR DISK -- RPS    YA00153 42171002
         BNE   RSTSILI3                 NO- ASSUME DISP CP      YA00153 42171402
         NI    RPSSILI(R1),X'FF'-SILIBIT  TURN OFF SILI BIT     YA00153 42171802
         B     RSTSKP4                                          YA00153 42171902
RSTSILI3 EQU   *                                                YA00153 42172002
         NI    DISKSILI(R1),X'FF'-SILIBIT  TURN OFF DISK SILI   YA00153 42172102
*********************************************************************** 42172202
*                                                                     * 42186102
*        XCTL TO REPMAIN TO RESTORE P/P CORE                          * 42200018
*                                                                     * 42250018
*********************************************************************** 42300018
*                                                                     * 42350018
RSTSKP4  EQU   *                                                YA00153 42400002
         LR    R1,RC               GET ADDR OF RSTWA                    42800002
         MVC   RHRXL(LMODNAME),RHRXRPM                                  43000018
*                                  MOVE NAME OF NEXT MODULE TO RECEIVE  43200018
         LA    RF,RHRXL            CONTROL TO XCTL PARM LIST IN W.A.    43600017
         ST    RF,RHRXL8                                                44000018
         LA    RF,RHRXL8                                                44400018
         XCTL  SF=(E,(RF))         TRANSFER CONTROL TO REPMAIN.         44800017
         USING DASECT,R1           ADDRESS AREA WHERE IOB, ICBS AND     45200017
*                                  CPS WILL BE MOVED TO.                45600017
*                                                                     * 45700018
*********************************************************************** 45800018
*                                                                     * 45900018
*        THIS SECTION WILL MOVE THE DIRECT ACCESS IOB, ICBS, AND      * 46000017
*     CHANNEL PROGRAMS CONSTRUCTED BY OPEN FOR THE RESTART DATA SET   * 46400017
*     TO THE RESTART WORK AREA.                                       * 46800017
*                                                                     * 46900018
*********************************************************************** 47000018
*                                                                     * 47100018
RSTDA    EQU   *                                                        47200017
         LA    RE,IOBL             GET NON-RPS IOB LENGTH        M5907  47300020
         CLI   DCPS-DIOB(R2),RPSOPCD Q - RPS CHANNEL PROGRAM     M5907  47400020
         BNE   RSTDA0              IF NOT, BR                    M5907  47500020
         LA    RE,RPSIOBL          GET RPS IOB LENGTH            M5907  47600020
RSTDA0   EQU   *                                                 M5907  47700020
         BCTR  RE,0                ADJUST LNGTH FOR MOVE INS     M5907  47900020
         EX    RE,RSTMVIOB         MOVE IOB/CP TO WORKAREA       M5907  48000020
         LA    R2,DIOB             GET ADDR OF IOB.                     48400017
         ST    R2,DCBIOBA          UPDATE JOB ADDR FIELD IN DCB.        48800017
         ST    R2,RSTAD            UPDATE POINTER TO NEXT IOB.          49200017
         MVC   DNIOB1(LIOB),RSTAD1                                      49600018
         LA    R2,DECB             GET ECB ADDR.                        50000017
         ST    R2,DECBAD           UPDATE POINTER TO ECB.               50400017
         LA    R2,DCPS             GET ADDR OF CHANNEL PROGRAM.         50800017
         ST    R2,RSTAD                                                 51200017
         MVC   DCPAD1(LCPPOINT),RSTAD1  UPDATE POINTER TO CP.           51600018
         CLI   DCPS,RPSOPCD        Q - RPS CHANNEL PROGRAM       M5907  51700020
         BE    RSTDA2              IF YES, BR                    M5907  51800020
         MVC   DCPT1(LCPPOINT),RSTAD1   UPDATE POINTER TO CP IN TIC CCW 52000018
         LA    R2,DSEEK3                GET ADDR OF SEEK ADDR           52400018
         ST    R2,RSTAD            UPDATE POINTER TO SEEK ADDR          52800017
         MVC   DCPS1(LCCW),RSTAD1       IN SEARCH CCW.                  53000018
         LA    R2,DCPRCNT          GET ADDR OF READ COUNT CCW.          53600017
         ST    R2,RSTAD            UPDATE POINTER TO READ COUNT         54000017
         MVC   DCPTI1(LCCW),RSTAD1      CCW IN TIC CCW.                 54300018
RSTDA1   EQU   *                                                        54600017
         LA    R2,DCBFDAD3         GET ADDR OF FDAD ADDR IN DCB.        54900018
         ST    R2,RSTAD            UPDATE POINTER TO FDAD IN READ       55200017
         MVC   DCPRCNT1(LCCW),RSTAD1    COUNT CCW.                      55300018
         B     RSTDRT                   GO BACK.                        55400018
*                                                                M5907  55407020
* RPS CHANNEL PROGRAMS HAVE TWO ADDITIONAL CCW'S, A COUNT FIELD  M5907  55414020
* READ-IN AREA, AND AN EXTENSION TO HANDLE SECTOR THETA'S        M5907  55421020
*                                                                M5907  55428020
RSTDA2   EQU   *                                                 M5907  55435020
         LA    R2,RPSCPS           GET ADDR OF SEARCH ID         M5907  55442020
         ST    R2,RSTAD              EQ CCW AND UPDATE           M5907  55449020
         MVC   RPST+1(LCCW),RSTAD1     TIC CCW                   M5907  55456020
         LA    R2,DSEEK3           GET ADDR OF SEARCH ARG        M5907  55463020
         ST    R2,RSTAD              AND UPDATE SEARCH           M5907  55470020
         MVC   RPSCPS+1(LCCW),RSTAD1   ID EQ CCW                 M5907  55477020
         LA    R2,RSECTOR1         GET ADDR OF THETA-1           M5907  55484020
         ST    R2,RSTAD              AND UPDATE                  M5907  55491020
         MVC   RPSCPSS+1(LCCW),RSTAD1  SET SECTOR CCW            M5907  55498020
         LA    R2,RPSRCNT          GET ADDR OF CCW5              M5907  55505020
         ST    R2,RSTAD              AND UPDATE                  M5907  55512020
         MVC   RPSTI+1(LCCW),RSTAD1    TIC CCW                   M5907  55519020
         LA    R2,RPSCOUNT         GET ADDR OF COUNT FIELD       M5907  55526020
         ST    R2,RSTAD              READ-IN AREA AND UPDATE     M5907  55533020
         MVC   RPSRCNT+1(LCCW),RSTAD1  READ COUNT CCW            M5907  55540020
         LA    R2,RSECTOR2         GET ADDR OF THETA-2           M5907  55547020
         ST    R2,RSTAD              AND UPDATE                  M5907  55554020
         MVC   RPSRS+1(LCCW),RSTAD1    READ SECTOR CCW           M5907  55561020
         B     RSTDRT              GO HANDLE DEB NEXT            M5907  55568020
*                                                                     * 55575020
*********************************************************************** 55600018
*                                                                     * 55700018
*        SUBROUTINE USED TO MOVE REMAINDER  OF DEB TO RESTART WORKAREA* 55800018
*                                                                     * 55900018
*********************************************************************** 56000018
*                                                                     * 56100018
RSTMOV2  EQU   *                                                        56400017
         MVC   ZERO(DEC256,R8),ZERO(R9)                                 56800018
         AH    R8,=H'256'                                               57200017
         SH    R7,=H'256'                                               57600017
         AH    R9,=H'256'                                               58000017
         BR    RE                  RETURN TO CALLER                     58400017
*                                                                     * 58400518
*********************************************************************** 58401018
*                                                                     * 58401518
*        SUBROUTINE TO POSITION TAPE CHKPT D.S. TO FIRST CIR BY       * 58402018
*        READING EACH RCD ON D.S. CHECKING FIRST FOR CORRECT CHKPT    * 58402518
*        ENTRY(COMPARING ON CHECKIDS,PASSED BY IEFDSDRP) AND, WITHIN  * 58403018
*        CORRECT ENTRY SEARCH FOR LAST DSDS(SEARCHING ON CODE INDICA- * 58403518
*        TING END OF DSDRS)                                           * 58404018
*                                                                     * 58404518
*********************************************************************** 58405018
*                                                                     * 58405518
RSTTAPE  EQU   *                                                        58407017
         LA    RD,RSREGSAV         ADDR REG SAVEAREA FOR READ MACRO     58414018
         LA    R7,RSBDCB                                                58421017
         ST    R7,RSDCBAD1                                              58428017
         LR    R7,RC               GET ADDR OF IOB IN W.A.              58435017
         A     R7,RSWACL                                                58442017
         SH    R7,=H'48'                                                58449017
         ST    R7,RSTAD                                                 58456017
         MVC   RSIOBP11(LIOBADDR),RSTAD1  MOVE IOB ADDR TO DECB IN W.A. 58461018
         LA    R5,LREC                    LENGTH OF RECORD              58466018
         STH   R5,RSLNGTH1         SAVE LENGTH OF RECORD IN DECB        58472017
         LA    R5,RSDECB1          GET ADDR OF DECB FOR READ            58474017
         L     R7,RSBFOFST         BUFFER ADDR                          58477017
         SH    R7,=H'200'                                               58484017
         AR    R7,RC                                                    58491017
         ST    R7,RSTAD                                                 58498017
         MVC   RSAREA11(LRIAADDR),RSTAD1  MOVE READ-IN AREA ADR TO DECB 58502018
*        POSITION TAPE TO CORRECT CHKPT ENTRY                           58506018
RSTRGN   EQU   *                                                        58512017
         BAL   R2,RSTREAD          GO TO READ RECORD FROM CHKPT D.S.    58519017
         CLC   CHRPOS(LCHR,R7),RSTCHKID   IS THIS THE CORRECT CHR       58526018
         BNE   RSTRGN              NO-GO TO READ NEXT RECORD            58533017
         LA    R6,RSBDCB           GET RESTART DCB ADDRESS     @ZA17064 58534037
         LA    R1,0(R6)            LOAD ADDRESS IN REG 1       @ZA17064 58535037
         DEBCHK (R1),TYPE=DELETE   DELETE THE DEB              @ZA17064 58536037
         SR    R1,R1               ZERO REG 1                  @ZA17064 58537037
         ST    R1,TCBDEB(4)        ZERO TCB DEB FIELD          @ZA17064 58538037
*                                                              @ZA17064 58539037
*        TAPE POSITIONED TO FIRST DSDR                                  58540018
RSTCOMP  EQU   *                                                        58547017
         XR    R9,R9               INITIALIZE SWITCH FOR NO.RCD.IN BUFR 58557018
RSTTPK   EQU   *                                                        58568017
         LR    R8,R7               INITIALIZE REG WITH BUFFER ADDR      58575017
RSTTPK1  EQU   *                                                        58582017
         BAL   R2,RSTREAD          READ ANOTHER RECORD                  58589017
RSTTP1   EQU   *                                                        58596017
         CLI   DSDRPOS(R8),LASTDSDR     END OF DSDR'S                   58603018
         BE    RSTSKP3             YES                                  58610017
         LA    R4,L23DSDR               LENGTH OF TYPE 2,3 DSDR'S       58617018
         CLI   DSDRPOS(R8),HEXZERO      TYPE 1 DSDR                     58624018
         BNE   RSTTP2              NO                                   58631017
         LA    R4,L1DSDR(R4)       GET LENGTH OF TYPE 1 DSDR            58638018
RSTTP2   EQU   *                                                        58645017
         AR    R8,R4               INCREMENT BUFFER POINTER TO NEXT RCD 58652018
RSTTP3   EQU   *                                                        58659017
         LTR   R9,R9               SWITCH=0(MUST CHECK 2ND RCD IN BUFFR 58666018
         BNE   RSTSW3              NO-CHECKED SECOND                    58673017
         LA    R9,ONE              CHECK FOR SECOND RCD                 58680018
         B     RSTTP1              GO TO CHECK IF END OF DSDR'S         58687018
RSTSW3   EQU   *                                                        58694017
         CLI   ONE(R8),LASTDSDR    END-OF-DSDR'S                        58701018
         BE    RSTSKP3             YES-RETURN TO XCTL TO REPMAIN        58708018
         B     RSTCOMP             READ NEXT RECORD                     58715018
RSTREAD  EQU   *                                                        58722017
         READ  (R5),SF,,,,,,MF=E                                        58729017
         CHECK (R5)                                                     58736017
         BR    R2                  RETURN TO CALLER                     58743017
RSTMVC1  MVC   ZERO(ONE,R8),ZERO(R9)                                    59143018
RSTMVIOB MVC   DIOB(ONE),ZERO(R2)  MOVE IOB/CP TO WORKAREA       M5907  59343020
RHRXRPM  DC    C'IGC0505B'         MODULE NAME FOR REPMAIN              59600017
SSCRBUFL DC    H'4096'             MAX LENGTH OF SSCR RECORD     Y02076 59650002
*                                                                     * 60250000
*        MAINTENANCE SPACE                                            * 60300000
*                                                                     * 60350000
PATCH    DC    XL((*-IGC0205B)/20)'00'   FIVE PERCENT MAINTENANCE SPACE 60360000
*                                                                     * 60370000
TAPESECT DSECT                                                          60400017
*        IOB                                                            60800017
TIOB     DS    0D                                                       61200017
TNIOB    DS    F         POINTER TO NEXT IOB.                           61600017
TIOBECB  DS    F    ECB                                                 62000017
TFLAGS   DS    F    FLAGS                                               62400017
TECBPT   DS    F    ADDR OF ECB                                         62800017
TIOBCSW  DS    2F   LOW ORDER BYTES OF LAST CSW                         63200017
TCPSTART DS    F    ADDR OF CHANNEL PROGRAM                             63600017
TDCBPT   DS    F    ADDR OF DCB                                         64000017
TEND     DS    2F                                                       64400017
TREAD    DS    2F        READ CCW                                       64800017
DASECT   DSECT                                                          65200017
*        IOB                                                            65600017
DIOB     DS    0D                                                       66000017
DNIOB    DS    F         POINTER TO NEXT IOB.                           66400017
DECB     DS    F    ECB                                                 66800017
DSENSE   DS    F    FLAGS                                               67200017
DECBAD   DS    F    ADDR OF ECB                                         67600017
DIOBCSW  DS    2F   LOW ORDER BYTES OF CSW                              68000017
DCPAD    DS    F         ADDR OF CHANNEL PROGRAM.                       68400017
DDCB     DS    F    ADDR OF DCB                                         68800017
DEND     DS    2F                                                       69200017
DSEEK    DS    2F                                                       69600017
*                                                                M5907  69690020
* NON-RPS CHANNEL PROGRAM                                        M5907  69780020
*                                                                M5907  69870020
DCPS     DS    2F   SEARCH CCW                                   M5907  69960020
DCPT     DS    2F   TIC CCW                                      M5907  70050020
DCPTI    DS    2F   TIC CCW                                      M5907  70140020
DCPRCNT  DS    2F   READ COUNT CCW                               M5907  70230020
DCPRDAT  DS    2F   READ DATA CCW                                M5907  70320020
*                                                                M5907  70410020
* RPS CHANNEL PROGRAM AND THETA EXTENSION                        M5907  70500020
*                                                                M5907  70590020
         ORG   DCPS                                              M5907  70680020
RPSCPSS  DS    2F   SET SECTOR CCW                               M5907  70770020
RPSCPS   DS    2F   SEARCH CCW                                   M5907  70860020
RPST     DS    2F   TIC CCW                                      M5907  70950020
RPSTI    DS    2F   TIC CCW                                      M5907  71040020
RPSRCNT  DS    2F   READ COUNT CCW                               M5907  71130020
RPSRDAT  DS    2F   READ DATA CCW                                M5907  71220020
RPSRS    DS    2F   READ SECTOR CCW                              M5907  71310020
RPSCOUNT DS    2F   COUNT READ-IN AREA                           M5907  71400020
RSECTOR1 DS    XL1  SECTOR THETA-1                               M5907  71490020
RSECTOR2 DS    XL1  SECTOR THETA-2                               M5907  71580020
         DS    XL6  RESERVED                                     M5907  71670020
R2SECT   DSECT                     IGC0205BS W.A.                 MFTCR 71900018
RSTCHKID DS    CL16      CHECKID                                        72200017
RSTAD    DS    F                                                        72400017
RHRXL    DS    3F   XCTL PARM LIST                                      72800017
         DCBD  DSORG=(BS,PO),DEVD=(DA,TA)                               73600017
         IEEVRSWA   VER=3                                               74400000
RST256   EQU   256                                                      74800017
DEBAPPEN EQU   29   OFFSET TO APP.VEC.TBL. POINTER IN DEB.              75000017
DDL      EQU   8    LENGTH OF DDNAME FIELD IN DCB.                      75200017
LENL     EQU   32             OFFSET DEBLNGTH FROM BEG OF APPEND. TABLE 75600017
IOBL     EQU   88                  NON-RPS IOB/CP LENGTH         M5907  75700020
RPSIOBL  EQU   120                 RPS IOB/CP LENGTH             M5907  75800020
RPSOPCD  EQU   X'23'               RPS OP CODE (SET SECTOR)      M5907  75900020
DCBDECBL EQU   128  LENGTH OF RESTART'S DCB AND TWO DECB.               76000017
DIOBL    EQU   88        LENGTH OF D.A  IOB AND CP                      76400017
TIOBL    EQU   48        LENGTH OF TAPE IOB/CP.                         76800017
RSDIRACC EQU   2    INDICATES DIRECT ACCESS DEVICE                      77200017
KCVTMVT  EQU   X'10'                                                    77600017
TIOLEN   EQU   28                                                       78000017
RSL      EQU   256  LENGTH OF RSTWA TO BE ZEROED OUT.                   78400017
SVRBL    EQU   144  LENGTH OF RESTART SVRB                              78800017
DCBL     EQU   88   LENGTH OF BSAM/BPAM DCB.                            79200017
RSTWAL   EQU   RSTIOT-RSTWA   LENGTH OF RESTART W.A.                    79600017
RSTRKOV  EQU   64                 INDICATES TRACK OVERFLOW              79800017
R0       EQU   0                                                        80000017
R1       EQU   1                                                        80400017
R2       EQU   2                                                        80800017
R3       EQU   3                                                        81200017
R4       EQU   4                                                        81600017
R5       EQU   5                                                        82000017
R6       EQU   6                                                        82400017
R7       EQU   7                                                        82800017
R8       EQU   8                                                        83200017
R9       EQU   9                                                        83600017
RA       EQU   10                                                       84000017
RB       EQU   11                                                       84400017
RC       EQU   12                                                       84800017
RD       EQU   13                                                       85200017
RE       EQU   14                                                       85600017
RF       EQU   15                                                       86000017
*                                                                       86009018
*        DECIMAL CONSTANTS, POSITIONS, LENGTHATTRIBUTES                 86018018
*                                                                       86027018
ZERO     EQU   0              DECIMAL - 0                               86036018
ONE      EQU   1              DECIMAL - 1                               86045018
DEC256   EQU   256            DECIMAL - 256                             86054018
TCBDEB   EQU   8                  OFFSET IN TCB TO TCBDEB        YM7073 86056002
*                                                                       86063018
TNIOB1   EQU   TNIOB+1        POSITIONS IN RELATION TO PREVIOUSLY       86072018
RSTAD1   EQU   RSTAD+1        DEFINED AREAS                             86081018
DCBDEBA1 EQU   DCBDEBAD+1     *                                         86090018
RHRXL8   EQU   RHRXL+8        *                                         86099018
DNIOB1   EQU   DNIOB+1        *                                         86108018
DCPAD1   EQU   DCPAD+1        *                                         86117018
DCPT1    EQU   DCPT+1         *                                         86126018
DCPS1    EQU   DCPS+1         *                                         86135018
DCPTI1   EQU   DCPTI+1        *                                         86144018
DCBFDAD3 EQU   DCBFDAD+3      *                                         86153018
DCPRCNT1 EQU   DCPRCNT+1      *                                         86162018
RSIOBP11 EQU   RSIOBPT1+1     *                                         86171018
RSAREA11 EQU   RSAREA1+1      READ-IN AREA ADDRESS                      86180018
DSEEK3   EQU   DSEEK+3        SEEK ADDRESS                              86189018
*                                                                       86198018
LIOB     EQU   3    LENGTH OF IOB                                       86207018
LDCB     EQU   3              DCB                                       86216018
LPTRAVT  EQU   3              POINTER TO APP. VEC. TBL.                 86225018
LMODNAME EQU   8              MODULE NAME                               86234018
LREC     EQU   400            RECORD                                    86243018
LRIAADDR EQU   3              READ-IN AREA ADDRESS                      86252018
LIOBADDR EQU   3              IOB ADDRESS                               86261018
LCHR     EQU   16             CHR                                       86270018
L23DSDR  EQU   178            TYPE 2,3 DSDR                             86279018
LCPPOINT EQU   3              POINTER TO CP                             86288018
LCCW     EQU   3              CCW                                       86297018
L1DSDR   EQU   17   PLUS 178  TYPE 1 DSDR                        YM5302 86306003
*                                                                       86315018
CHRPOS   EQU   4              CHR                                       86324018
DSDRPOS  EQU   1              DSDR                                      86333018
*                                                                       86342018
*      - MASKS -                                                        86351018
*                                                                       86360018
HEXZERO  EQU   X'00'          HEXADECIMAL ZERO                          86369018
LASTDSDR EQU   X'10'          END OF DSDRS                              86378018
TAPECC   EQU   X'02'                    1ST CC IN TAPE CH PGM   YA00153 86388002
TAPESILI EQU   X'04'                    TAPE READ CCW+4         YA00153 86398002
SILIBIT  EQU   X'20'                    SILI BIT IN CCW FLAGS   YA00153 86398402
RPSCC    EQU   X'23'                    1ST CC IN RPS CH PGM    YA00153 86398802
RPSSILI  EQU   X'2C'                    RPS READ CCW+4          YA00153 86399202
DISKSILI EQU   X'24'                    DISK READ CCW+4         YA00153 86399602
 END                                                                    86400017
