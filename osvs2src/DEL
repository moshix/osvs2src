         MACRO                                                          00900002
&TAG     DEL   &TREE,&LV=,&T=,&TYPE=,&S=,&SP=,                         X01800002
               &FREE=,&RECL=                                            02700002
.*********************** SEPTEMBER 27, 1973. ************************** 03600002
.* THE DEL MACRO-INSTRUCTION IS USED TO DELETE A KEY-ADDRESS          * 04500002
.* ASSOCIATION PAIR FROM THE RADIX PARTITION TREE. THE MEANINGS OF THE* 05400002
.* PARAMETERS ARE AS FOLLOWS:                                         * 06300002
.********************************************************************** 07200002
.* TREE: THE TREE PARAMETER SPECIFIES THE ADDRESS OF THE RADIX        * 08100002
.*       PARTITION TREE, AS IT IS OBTAINED FROM USING THE STREE       * 09000002
.*       MACRO-INSTRUCTION. THE ADDRESS MAY EITHER BE IN MAIN STORAGE * 09900002
.*       OR IN A REGISTER. IF "LABEL" IS CODED FOR THE TREE PARAMETER,* 10800002
.*       THE ADDRESS IS IN A WORD IN MAIN STORAGE. IF "(TREE)" IS     * 11700002
.*       CODED, THE ADDRESS IS IN THE CORRESPONDING REGISTER.         * 12600002
.* LV:   LV MEANS "LENGTH VALUE", AND IS USED TO SPECIFY THE LENGTH OF* 13500002
.*       THE RECORD IF THE RECORD AREA IS TO BE RELEASED TO THE SYSTEM* 14400002
.*       VIA THE FSPACE MACRO-INSTRUCTION. IF THE LV PARAMETER IS NOT * 15300002
.*       CODED, THEN NO FSPACE OPERATION TAKES PLACE, BUT ONLY THE    * 16200002
.*       SPACE FOR THE RADIX PARTITION TREE ENTRY IS RELEASED.        * 17100002
.*       THE LV PARAMETER IS CODED EXACTLY THE SAME WAY AS IN THE     * 18000002
.*       FSPACE MACRO-INSTRUCTION. IF THE RECORD IS A VARIABLE LENGTH * 18900002
.*       RECORD WITH EITHER A ONE-BYTE OR HALFWORD COUNT AT THE FRONT * 19800002
.*       OF THE RECORD, THE LENGTH VALUE MAY BE CODED AS:             * 20700002
.*       LV=((15),1) FOR A ONE BYTE LENGTH;                           * 21600002
.*       LV=((15),2) FOR A HALFWORD LENGTH FIELD.                     * 22500002
.*       IF THE RECORD IS A FIXED LENGTH RECORD, THEN "LV=LENGTH"     * 23400002
.*       SHOULD BE CODED, WHERE "LENGTH" IS THE RECORD LENGTH.        * 24300002
.*                                                                    * 25200002
.* S,SP: THESE PARAMETERS ARE CODED EXACTLY THE SAME WAY AS IN THE    * 26100002
.*       FSPACE MACRO-INSTRUCTION; SEE THE DESCRIPTION OF FSPACE FOR  * 27000002
.*       DETAILS.                                                     * 27900002
.* FREE: THIS PARAMETER IS PRESENT ONLY FOR COMPATIBILITY WITH EARLIER* 28800002
.*       VERSIONS, AND SHOULD NOT BE USED.                            * 29700002
.* RECL: THIS PARAMETER IS PRESENT ONLY FOR COMPATIBILITY WITH EARLIER* 30600002
.*       VERSIONS, AND SHOULD NOT BE USED.                            * 31500002
.*       THE RETURN CODE IN REGISTER 15 IS THE ADDRESS THAT WAS       * 32400002
.*       ASSOCIATED WITH THE KEY BEFORE THE DELETE IF THE LENGTH VALUE* 33300002
.*       IS NOT CODED. THE CONDITION CODE SETTING DOES NOT ALWAYS     * 34200002
.*       AGREE WITH THE RETURN CODE IN THIS EVENT, SINCE THE CONDITION* 35100002
.*       CODE IS SET TO 01 (MINUS) WHEN THE DEL MACRO-INSTRUCTION     * 36000002
.*       DELETES THE LAST KEY-ADDRESS PAIR IN THE RPT. WHEN THE LV    * 36900002
.*       PARAMETER IS NOT CODED A BM INSTRUCTION WILL BRANCH WHEN THE * 37800002
.*       ADDRESS RETURNED IN REGISTER 15 IS THE LAST ADDRESS THAT WAS * 38700002
.*       IN THE RPT. A BNM INSTRUCTION WILL BRANCH WHEN THE ADDRESS   * 39600002
.*       RETURNED IN REGISTER 15 WAS NOT THE LAST ADDRESS IN THE RPT. * 40500002
.*       WHEN THE LENGTH VALUE IS CODED, THE CONDITION CODE SETTING IS* 41400002
.*       THE SAME AS ABOVE, BUT THE RETURN CODE IN REGISTER 15 IS SET * 42300002
.*       TO ZERO, SINCE THE FSPACE MACRO-INSTRUCTION SETS IT TO ZERO. * 43200002
.*       THE CONDITION CODE IS PRESERVED THROUGH THE FSPACE           * 44100002
.*       MACRO-INSTRUCTION, SO THAT IT IS THE SAME AS IT IS WHEN      * 45000002
.*       RETURNING FROM THE DEL SUBROUTINE IN THE MODULE IGARPT01.    * 45900002
.* AFTER THE DEL OPERATION, THE CURSOR IS SET BETWEEN THE TWO KEYS ON * 46800002
.* EITHER SIDE OF THE DELETED KEY, SO THAT IS A SCANL OR SCANR        * 47700002
.* MACRO-INSTRUCTION IS SUBSEQUENTLY EXECUTED THE CURSOR IS POSITIONED* 48600002
.* TO THE KEY LOWER OR HIGHER THAN THE DELETED KEY.                   * 49500002
.* THUS IT IS POSSIBLE TO PROCESS ALL THE ADDRESS IN THE RPT IN       * 50400002
.* ASCENDING ORDER, AND DELETE EACH PAIR AFTER IT HAS BEEN PROCESSED, * 51300002
.* BY USING ISCAN TO INITIALIZE THE CURSOR, AND THEN FOLLOWING EACH   * 52200002
.* SCANR BY A DEL, WHICH DELETES EACH ENTRY AFTER IT HAS BEEN         * 53100002
.* PROCESSED.                                                         * 54000002
.* A SAMPLE PROGRAM TO DO THIS IS AS FOLLOWS:                         * 54900002
.*       ISCAN TREE  SET THE CURSOR TO THE INITIAL STATE.             * 55800002
.* MORE: SCANR TREE,DONE=END  SET THE CURSOR TO THE NEXT.             * 56700002
.*       PROCESS THE RECORD AT THE ADDRESS IN REGISTER 15.            * 57600002
.*       DEL TREE,LV=80  DELETE THE CURSOR-SELECTED KEY-ADDRESS PAIR  * 58500002
.*                        FROM THE RADIX PARTITION TREE, AND RELEASE  * 59400002
.*                        THE 80-BYTE RECORD AREA TO THE SYSTEM VIA   * 60300002
.*                        THE FSPACE MACRO-INSTRUCTION.               * 61200002
.*       BNM MORE  BRANCH BACK TO SCAN MORE IF THERE IS STILL AT LEAST* 62100002
.*                        ONE ENTRY.                                  * 63000002
.********************************************************************** 63900002
         GBLC  &IGABLST  THE OFFSET INTO THE TREE HEADER FOR THE LIST  *64800002
                         OF RPT ENTRY POINTS.                           65700002
         GBLC  &IGADEL   INDEX OF THE ADDRESS OF THE DELETE ROUTINE    *66600002
                         ADDRESS IN THE LIST OF ENTRY POINTS IN THE    *67500002
                         TREE HEADER.                                   68400002
         GBLC  &IGADDR   THE ADDRESS OF THE MODULE IGARPTXX.            69300002
         GBLC  &IGARPT#  THE TYPE OF THE RADIX PARTITION TREE FROM ONE *70200002
               OF THE PARAMETERS T OR TYPE.                             71100002
         LCLC  &R,&Q                                                    72000002
.*A000000                                                        Y02147 72900002
.********************************************************************** 73800002
         AIF   (K'&RECL EQ 0).NEW  SEE IF THE OLD PARAMETER RECL IS    *74700002
                         CODED INSTEAD OF LV.                           75600002
&TAG     DEL   &TREE,T=&T,TYPE=&TYPE,LV=&RECL,S=&S,SP=&SP               76500002
         AGO   .FIN                                                     77400002
.NEW     ANOP                                                           78300002
      RPTDSECT T=5,DS=NO                                                79200002
      RPTDSECT T=8,DS=NO                                                80100002
.********************************************************************** 81000002
&R       SETC  'R'                                                      81900002
         AIF   (K'&TREE EQ K'&TREE(1)+2 AND N'&TREE EQ 1).LW2           82800002
&R       SETC  ' '                                                      83700002
.LW2     AIF   ('&TREE' NE '(1)').LW3  SEE IF THE TREE ADDRESS IS      *84600002
                         ALREADY IN R1.                                 85500002
         AIF   (K'&TAG EQ 0).LW4  SEE IF THERE IS NO TAG.               86400002
&TAG     EQU   *                                                        87300002
         AGO   .LW4      SKIP THE LR INSTRUCTION.                       88200002
.LW3     ANOP                                                           89100002
&TAG     L&R   1,&TREE(1)                                               90000002
.LW4     ANOP                                                           90900002
         L     15,(&IGABLST+&IGADEL)(,1)  ADDRESS OF THE DELETE ROUTINE 91800002
         BALR  14,15           LINK TO THE DELETE ROUTINE.              92700002
         AIF   (K'&LV EQ 0).FIN  SEE IF A LENGTH VALUE IS GIVEN.        93600002
         AIF   ('&LV' EQ '0').FIN  SEE IF THE LENGTH IS ZERO.           94500002
        FSPACE R,A=(15),S=&S,SP=&SP,LV=&LV  FREE THE RECORD AREA.       95400002
         SPM   14        RESTORE CONDITION CODE.                        96300002
FIN&SYSNDX EQU *                                                        97200002
.FIN     ANOP                                                           98100002
         MEND                                                           99000002
