*********************************************************************** 00007017
*                                                                     * 00014017
*                   *****       I E B B S C A N        *****          * 00021017
*                                                                     * 00028017
* STATUS. CHANGE LEVEL 000.                                           * 00035017
*                                                                     * 00042017
* FUNCTION/OPERATION.                                                 * 00049017
*        THIS CSECT SCANS CONTROL CARD STATEMENTS,SETS FLAGS IN       * 00056017
*        THE COMMUNICATION REGION AT SWITCHRD AND RETURNS POINTERS    * 00063017
*        TO IEBASCAN FOR ANALYSIS.                                    * 00070017
*                                                                     * 00077017
* ENTRY POINTS.                                                       * 00084017
*        ENTERED FROM IEBASCAN AT IEBBSCAN.                           * 00091017
*                                                                     * 00098017
* INPUT.                                                              * 00105017
*        CONTROL CARD PARAMETERS TO BE ANALYZED AT SWITCHRD.          * 00112017
*                                                                     * 00119017
* OUTPUT.                                                             * 00126017
*        DIAGNOSTIC MESSAGES FROM IEBUPLOG.                           * 00133017
*                                                                     * 00140017
* EXITS-NORMAL.                                                       * 00147017
*        RETURN TO IEBBSCAN AFTER A PARAMETER HAS BEEN ANALYZED.      * 00154017
*        IF A CONTINUATION CARD MUST BE READ,CONTROL IS PASSED TO     * 00161017
*        IEBUPDT2 WHICH WILL READ A CARD FROM SYSIN AND PASS          * 00168017
*        CONTROL TO IEBBSCAN VIA IEBASCAN.                            * 00175017
*                                                                     * 00182017
* EXITS-ERROR.                                                        * 00189017
*        FLAGS ARE SET AND CONTROL IS RETURNED TO IEBASCAN.           * 00196017
*                                                                     * 00203017
* EXTERNAL ROUTINES.                                                  * 00210017
*        IEBASCAN,IEBUPLOG AND IEBUPDT2.                              * 00217017
*                                                                     * 00224017
* TABLES/WORK AREAS.                                                  * 00231017
*        SWITCHRD WHICH IS A BUFFER IN IEBUPCON CONTAINING THE        * 00238017
*        CONTROL CARD UNDERGOING ANALYSIS AND SWITCHES.               * 00245017
*                                                                     * 00252017
* ATTRIBUTES.                                                         * 00259017
*        SERIALLY REUSABLE, BOLCK LOADS.                              * 00266017
*                                                                     * 00273017
*********************************************************************** 00280017
         EJECT                                                          00300017
*********************************************************************** 00600017
*                      CONTROL STATEMENT ANALYSIS ROUTINES            * 00900017
*********************************************************************** 01200017
         SPACE 1                                                        01500017
GR0      EQU   0                                                        01800017
GR1      EQU   1                                                        02100017
GR2      EQU   2                                                        02400017
GR3      EQU   3                                                        02700017
GR4      EQU   4                                                        03000017
GR5      EQU   5                                                        03300017
GR6      EQU   6                                                        03600017
GR7      EQU   7                                                        03900017
GR8      EQU   8                                                        04200017
GR9      EQU   9                                                        04500017
GR10     EQU   10                                                       04800017
GR11     EQU   11                                                       05100017
GR12     EQU   12                                                       05400017
GR13     EQU   13                                                       05700017
GR14     EQU   14                                                       06000017
GR15     EQU   15                                                       06300017
D0       EQU   0                        DISPLACEMENT OF 0       YM07522 06300402
D1       EQU   1                        DISPLACEMENT OF 1       YM07522 06302002
LENGTH   EQU   0                       LENGTH OF PARAM REGISTER 0       06600017
SCANADR  EQU   1                       ADDRESS OF PARAMETER REGISTER 1  06900017
BIT80    EQU   X'80'                   ERROR SWITCH                     07200017
BIT40    EQU   X'40'                   BYPASS SWITCH                    07500017
BIT20    EQU   X'20'                   EOF, PARAMETER FOLLOWS SWITCH    07800017
BIT10    EQU   X'10'                   FIRST ENTRY, PARTIAL PARAMETER   08100017
BIT08    EQU   X'08'                   COMMAND, COLUMN 72 NON-BLANK SW  08400017
BIT04    EQU   X'04'                   KEYWORD, COLUMN 72 NON-BLANK SW  08700017
BIT02    EQU   X'02'                   PARAMETER SWITCH                 09000017
BIT01    EQU   X'01'                   LENGTH EQUALS ZERO SWITCH        09300017
RESET0   EQU   X'00'                   RESET BITS                       09600017
EQUAL    EQU   C'='                    HEX 'EQUAL'                      09900017
COMMA    EQU   C','                    HEX 'COMMA'                      10200017
BLANKCOL EQU   C' '                    HEX 'BLANK'                      10500017
SWF0     EQU   X'F0'                    DOUBLE PARM FLAGS       YM07522 10502002
SWF8     EQU   X'F8'                                            YM07522 10504002
SWFC     EQU   X'FC'                                            YM07522 10506002
SWFE     EQU   X'FE'                                            YM07522 10508002
         SPACE 1                                                        10800017
IEBBSCAN CSECT                                                          11100017
*                                                                A27507 11200019
*A684500-686700,699000,699500                                    A48723 11250022
*C750000                                                         A48723 11300022
*A63004-63020,105020-105080,329420-329440,345420-345440         YM07522 11302002
*A346020-346080,347020-347080.671020-671080                     YM07522 11304002
*C346000,346200,346400,347000,347200,671000,672000              YM07522 11308002
*A240100,240500,270100,270200,492500,493000,495500,660500,      YA01700 11318002
*A661000,699100,699200,705500,706000,711500,771500,772000       YA01700 11328002
*C264000,663000,780000                                          YA01700 11338002
*D243000,702000                                                 YA01700 11348002
*A 534500-535500                        @XA16051=(ORG)@YA14019=@ZA16462 11398099
RDCARD   SAVE  (14,12)                                                  11400017
         BALR  GR2,0                    ESTABLISH ADDRESSABILITY        11700017
         USING *,GR2                    USE REGISTER 2                  12000017
         USING IEBUPCON,GR12            COMMUNICATION REGION            12300017
         ST    GR13,RRSAVE+4            SAVE OLD SAVE AREA ADDRESS      12600017
         LA    GR13,RRSAVE              ESTABLISH NEW SAVE ADDRESS      12900017
         L     GR3,STOREG+8            LOAD ADDRESS OF LAST PASS        13200017
         CLI   CCSCANSW,BIT01          IS IT RETURN FROM CONTINUATION   13500017
         BE    RESETCON                YES-SCAN CONTINUATUION           13800017
         NI    SWITCHRD,X'7F'          RESET ERROR BIT                  14100017
         TM    SWITCHRD,BIT40          TEST BYPASS SWITCH               14400017
         BC    1,KBYPO                 IF ON GO TO BYPASS ROUTINE       14700017
         TM    PARAMSW,BIT08           TEST READ CARD SWITCH            15000017
         BC    1,KGTCD                 IF ON, READ A CARD               15300017
         TM    PARAMSW,BIT20           DOES A PARAMETER FOLLOW          15600017
         BC    1,SCANR                 YES- SCAN THE CARD               15900017
         TM    PARAMSW,BIT40           CONTINUATION SWITCH SET          16200017
         BC    1,KCONT                 YES- PROCESS CONTINUED PART      16500017
         LA    GR3,SWITCHRD+1          LOAD ADDRESS OF FIRST COLUMN     16800017
         CLC   0(2,GR3),DOTSLASH       IS CARD A CONTROL CARD  (./)     17100017
         BNE   KTER                    NO- ERROR CONDITION              17400017
         LA    GR3,2(GR3)              SKIP TO NAME COLUMN              17700017
KNAME    CLI   0(GR3),BLANKCOL         IS COLUMN BLANK                  18000017
         LA    GR5,LASTCOLM            LOAD 5 WITH COLUMN 71 OF CARD    18300017
         BE    KOMMD                   YES- GO TO COMMAND WORD ROUTINE  18600017
         CR    GR3,GR5                 TEST IF SCANNED TO COLUMN 71     18900017
         BE    KNOP                    YES- END OF CARD REACHED         19200017
         LA    GR3,1(GR3)              GO TO NEXT COLUMN                19500017
         B     KNAME                   CHECK FOR BLANK                  19800017
KOMMD    LA    GR3,1(GR3)              SPACE TO NEXT COLUMN AFTER COMD  20100017
         OI    PARAMSW,BIT04           SET COMMAND SWITCH PARAM FOLLOW  20400017
         OI    SWITCHRD,BIT08          SET COMMAND BIT '08'             20700017
         NI    DELIMIN,RESET0          RESET DELIMITER INDICATORS       21000017
KABC     CLI   0(GR3),BLANKCOL         IS COLUMN BLANK                  21300017
         BNE   SCAN                    NO- CHECK PARAMETER              21600017
         CR    GR3,GR5                 IS THIS THE END COLUMN           21900017
         BE    KNOP                    YES- END OF CARD REACHED         22200017
         LA    GR3,1(GR3)              ADJUST POINTER TO NEXT COLUMN    22500017
         B     KABC                    CONTINUE SCAN                    22800017
KCONT    LA    GR3,SWITCHRD+1          FIRST COLUMN OF CONTINUED CARD   23100017
         CLC   0(2,GR3),DOTSLASH       IS CARD A CONTROL CARD           23400017
         BNE   KTER                    NO- ERROR CONDITION              23700017
         LA    GR3,SWITCHRD+16         SET POINTER TO COLUMN 16         24000017
         TM    PARAMSW,BIT10      PARTIAL PARAMETER SW SET?    @YA01700 24100002
         BO    CONTINUE           CONTINUE SCANNING             YA01700 24200002
SCANR    NI    SWITCHRD,X'F7'          RESET COMMAND BIT                24600017
SCAN     NI    SWITCHRD,X'98'          RESET ALL BUT ERROR, 1ST ENTRY   24900017
         NI    PARAMSW,X'14'           RESET  BUT COMD/PARA FOLL A48723 25200000
         MVC   DELIMIN2(1),DELIMIN     SAVE DELIMITER SWITCH SETTINGS   25500017
         NI    COL72LG,X'0C'           RESET EXIT CONTROL SWITCH        25800017
         NI    DELIMIN,RESET0          CLEAR DELIMITER SWITCH           26100017
CONTINUE SR    GR7,GR7                 CLEAR LENGTH REGISTER    YA01700 26400002
         LR    GR1,GR3                 SAVE POINTER TO PRESENT COLUMN   26700017
         LA    GR5,LASTCOLM            LOAD ADDRESS OF END COLUMN       27000017
         CR    GR3,GR5            ALREADY IN COL. 72?           YA01700 27010002
         BE    KPART              YES,GO TO PART PARAMETER SAVE YA01700 27020002
KOMPAR   CLI   0(GR3),EQUAL            IS CHARACTER A DELIMITER  A48723 27050022
         BNH   DELIMIT                 YES-'EQUAL' CHARAC OR LOW A48723 27100022
KOMPAR2  LA    GR7,1(GR7)              CHECK ONE FURTHER         A48723 27150022
KOMPAR3  CR    GR3,GR5                ALREADY AT COL 72          A48723 27900022
         BE    KPART                   YES-GO TO PARTIAL PARAMETER SAVE 28500017
         LA    GR3,1(GR3)              UP POINTER TO NEXT COLUMN A48723 28800022
         B     KOMPAR                  CONTINUE SCANNING FOR DELIMITER  29100017
KNOP     OI    SWITCHRD,BIT80          FLAG AS NO COMD OR PARAM ON CARD 29400017
         SR    GR1,GR1                 CLEAR SCAN REGISTER              29700017
         SR    GR7,GR7                 CLEAR LENGTH REGISTER            30000017
KNOPAR   OI    SWITCHRD,BIT40          NO KEYWORD OR PARAMETERS FOLLOW  30300017
KPASS    LA    GR3,1(GR3)              UP POINTER TO NEXT COLUMN        30600017
         NI    PARAMSW,X'EB'           CLEAR ALL BUT BYPASS-PART PARAM. 30900017
         ST    GR7,STOREG              STORE LENGTH REGISTER            31200017
         ST    GR1,STOREG+4            STORE SCANADR REGISTER           31500017
         ST    GR3,STOREG+8            STORE ADDRESS REGISTER           31800017
         L     GR13,RRSAVE+4                                            32100017
         LM    GR14,GR12,12(GR13)                                       32400017
         BR    GR14                    RETURN TO MAINSTREAM             32700017
DELIMIT  CLI   0(GR3),C'$'              CHARACTER A DOLLAR SIGN         32740017
         BE    KOMPAR2                  IF SO, OK                       32780017
         CLI   0(GR3),X'7C'             CHARACTER AN AT SIGN            32820017
         BE    KOMPAR2                  IF SO, OK                       32860017
         CLI   0(GR3),C'#'              CHARACTER A POUND SIGN          32900017
         BE    KOMPAR2                  IF SO, OK                       32940017
         CLI   D0(GR3),C'*'             CHARACTER AN ASTERISK   YM07522 32942002
         BE    KOMPAR2                  IF SO, OK               YM07522 32944002
         CLI   0(GR3),C'('              CHARACTER A LEFT PARENTHESIS    32980017
         BE    LFTPAREN                 IF SO,CHECK FURTHER             33020017
         CLI   0(GR3),C''''             CHARACTER A QUOTE               33060017
         BE    QUOTE                    IF SO,CHECK FURTHER             33100017
         CLI   0(GR3),C')'              CHARCTER A RIGHT PARENTHESIS    33140017
         BE    RTPAREN                  IF SO,CHECK FURTHER             33180017
DELIMB   CR    GR3,GR5                  END COLUMN                      33220017
         BE    SETCC2                  YES- SET INDICATOR               33300017
         OI    COL72LG,BIT40           SET DELIMITER BEFORE LAST SWITCH 33600017
         TM    PARAMSW,BIT10           IS PARTIAL PARAMETER SWITCH ON   33900017
         BC    1,KPPAR                 YES-COMPLETE PARAMETER           34200017
         B     TESTPAR                 CHECK PARAMETER                  34500017
QUOTE    CLI   TOTALSW,X'F0'            TOTALING REQUESTED              34520017
         BE    LFTPAREN                 IF SO,CHECK FURTHER             34540017
         CLI   SQFSW,SWF0               SEQFLD BEING PROCESSED  YM07522 34542002
         BE    LFTPAREN                 YES - TREAT AS PAREN    YM07522 34544002
         B     RTPAREN                  IF NOT,TREAT AS RGTH PAREN      34560017
LFTPAREN CLI   TOTALSW,X'F0'            IS LEFT PAREN EXPECTED          34580017
         BE    TOTLP                    YES - FOR TOTAL         YM07522 34600002
         CLI   SQFSW,SWF0               FOR SEQFLD              YM07522 34602002
         BNE   DELIMB                   NO - ERROR              YM07522 34604002
         MVI   SQFSW,SWF8               SET FOR COMMA NEXT      YM07522 34606002
         B     LPCONT                   CONTINUE                YM07522 34608002
TOTLP    MVI   TOTALSW,SWF8             SET FOR COMMA NEXT      YM07522 34620002
LPCONT   LA    GR1,D1(GR1)              UP POINTER TO NEXT CHAR YM07522 34640002
         B     KOMPAR3                  ANALYZE NEXT             A48723 34660000
RTPAREN  CLI   TOTALSW,X'FC'            IS RIGHT PAREN EXPECTED         34680017
         BE    TOTRP                    YES - FOR TOTAL         YM07522 34700002
         CLI   SQFSW,SWFC               FOR SEQFLD              YM07522 34702002
         BNE   KPASS                    NO -ERROR               YM07522 34704002
         MVI   SQFSW,SWFE               RIGHT PAREN FOUND       YM07522 34706002
         B     KOMPAR3                  CONTINUE                YM07522 34708002
TOTRP    MVI   TOTALSW,SWFE             RIGHT PAREN FOUND       YM07522 34720002
         B     KOMPAR3                  PREPARE TO RETURN        A48723 34740000
LENGTH0  OI    COL72LG,BIT01           SET LENGTH EQUAL TO ZERO         34800017
         B     DELIM                   CHECK DELIMITERS                 35100017
SETCC2   OI    COL72LG,BIT20           SET DELIMITER IN LAST SWITCH     35400017
         TM    PARAMSW,BIT10           IS PARTIAL PARAMETER SWITCH ON   35700017
         BC    1,KTERBY                YES- LENGTH IS TOO LONG          36000017
TESTPAR  LTR   GR7,GR7                 CLEAR LENGTH REGISTER            36300017
         BC    8,LENGTH0               LENGTH EQUALS ZERO, SET SWITCH   36600017
         OI    COL72LG,BIT02           SET LENGTH GREATER THAN ZERO     36900017
DELIM    CLI   0(GR3),EQUAL            TEST IF DELIMITER IS AN EQUAL    37200017
         BE    KEY                     YES- GO TO KEY WORD ROUTINE      37500017
         CLI   0(GR3),COMMA            TEST FOR COMMA                   37800017
         BE    PARAMC                  YES- GO TO PARAMETER ROUTINE     38100017
         CLI   0(GR3),BLANKCOL         TEST FOR BLANK                   38400017
         BE    PARBLANK                YES- GO TO HANDLE BLANK PARAM    38700017
         TM    COL72LG,BIT20           IS DELIMITER IN END COLUMN       39300017
         BC    1,KSETP                 YES- SET PARAMETER FOLLOWS SW.   39600017
KINVAL   OI    SWITCHRD,BIT80          SET INVALID DELIMITER ERROR SW   39900017
         LA    GR7,1(GR7)              ADJUST LENGTH REGISTER           40200017
         LA    GR3,1(GR3)              ADJUST SCAN POINTER              40500017
         CLI   0(GR3),EQUAL            IS CHARACTER A DELIMITER         40800017
         BL    KINV                    YES- TWO DELIMITERS IN A ROW     41100017
         CR    GR3,GR5                 END COLUMN                       41400017
         BL    KINVAL                  NO- SCAN SOME MORE               41700017
         TM    COL72LG,BIT08           IS COLUMN 72 BLANK               42000017
         BC    1,KTERBY                YES                              42300017
         LA    GR7,1(GR7)              ADJUST LENGTH REGISTER           42600017
         B     KPTERR                  ESTABLISH PARAMETER              42900017
KINV     TM    PARAMSW,BIT04           IS COMMAND CONTROL SWITCH ON     43200017
         BC    1,KPCMD                 YES- ADJUST POINTER TO NEXT ONE  43500017
PRFOL    OI    PARAMSW,BIT20           SET PARAMETER FOLLOWS SWITCH     43800017
         B     KPASS                   GO TO RETURN                     44100017
KBYPO    CLI   SWITCHRD+72,BLANKCOL    IS CONTINUATION COLUMN 72 BLANK  44400017
         BNE   KGTCD                   NO- GET ANOTHER CARD             44700017
         NI    SWITCHRD,BIT10          RESET PARTIAL PARAMETER SWITCH   45000017
         NI    PARAMSW,RESET0          RESET ENTRY CONTROL SWITCH       45300017
KGTCD    CLI   FIRSENTR,BIT10          VERY FIRST CARD                  45600017
         BE    GOAHEAD                 YES- PAGE ALREADY SKIPPED        45900017
         CLI   COMDCD,X'0E'            IS THIS A COMMAND CARD           46200017
         BNE   GOAHEAD                 NO- JUST PRINT CARD              46500017
         CLI   FIRSTCON,X'0F'          FIRST PART OF CONTINUATION CARD  46800017
         BE    GOAHEAD                 NO- DO NOT SKIP PAGE             47100017
         MVI   FIRSTCON,X'0F'          SET AS FIRST CONTINUATION CARD   47400017
         LA    GR14,ONTOCC             RETURN ADDRESS FROM 'PRINTY'     47700017
         MVI   PRIMESW,X'0E'           FORCE PRINTING OF HEADER         48000017
         B     PRINTY                  PRINT FIRST CONTINUATION CARD    48300017
GOAHEAD  BAL   GR14,CARDPRNT           PRINT REST OF CONTINUATION CARD  48600017
ONTOCC   MVI   CCSCANSW,BIT01          SET SCAN ROUTINE READ SWITCH     48900017
         L     GR15,PROCESS            ADDRESS OF CSECT WITH READCC     49200017
         TM    PARAMSW,BIT10      IS PARTIAL PARAMETER SW ON?   YA01700 49300002
         BC    1,RETURN           YES,SET UP FOR RETURN         YA01700 49400002
         BR    GR15                    ENTER THE CSECT                  49500017
RETURN   BALR  GR7,GR15           ENTER UPDT2 TO GET CONT. CARD YA01700 49600002
RESETCON MVI   CCSCANSW,RESET0         CLEAR CONTINUATION SWITCH        49800017
         B     KCONT                   ANALYSE CONTINUATION CARD        50100017
PARBLANK TM    PARAMSW,BIT04           IS COMMAND SWITCH ON             50400017
         BC    1,KPCMD                 YES-GO TO ADJUST POINTER         50700017
         OI    SWITCHRD,BIT02          SET PARAMETER PRESENT BIT        51000017
         OI    DELIMIN,BIT08           SET BLANK DELIMITER SWITCH       51300017
         TM    DELIMIN2,X'80'           WAS LAST A COMMAND WORD         51340017
         BC    1,DECID                  IF SO,CHECK SOME MORE           51380017
         CLI   SWITCHRD+72,BLANKCOL     IS COL 72 BLANK                 51420017
         BE    SCANERR                  IF SO,ERROR                     51460017
         TM    DELIMIN2,BIT40          WAS LAST DELIMITER BLANK    UL0H 51500017
         BC    14,SCANERR               IF NOT,ERROR                    51540017
DECID    TM    COL72LG,BIT20           IS DELIMITER IN END COLUMN       51600017
         BC    1,KWENT                 YES- SET TO READ A CARD          51900017
         TM    COL72LG,BIT01           IS LENGTH EQUAL TO ZERO          52200017
         BC    1,KFORZRO               YES-SET UP FOR ZERO LENGTH       52500017
         OI    PARAMSW,BIT20           SET PARAMETER FOLLOWS SWITCH     52800017
         TM    DELIMIN,BIT08           IS DELIMITER A BLANK             53100017
         BC    8,KPASS                 NO- RETURN WITH POINTERS SET     53400017
         CLI   SWITCHRD+72,BLANKCOL    COL72 BLANK?            @ZA16462 53450099
         BE    KETBYP                  YES, BRANCH             @ZA16462 53500099
         OI    COL72LG,BIT80           SET COL72 NOTBLNK SW    @ZA16462 53550099
KETBYP   OI    SWITCHRD,BIT40          SET BYPASS SWITCH                53700017
         B     KPASS                   GET SET TO RETURN                54000017
KFORZRO  SR    GR1,GR1                 CLEAR SCAN REGISTER              54300017
         SR    GR7,GR7                 CLEAR LENGTH REGISTER            54600017
         TM    DELIMIN,BIT08           IS DELIMITER A BLANK             54900017
         BC    1,KETRDCD               GO TO SET READ CARD AND CONTINUE 55200017
         OI    PARAMSW,BIT20           SET PARAMETER FOLLOWS SWITCH     55500017
         TM    DELIMIN,BIT80           IS DELIMITER AN EQUAL SIGN       55800017
         BC    1,KPPAR                 YES- SET UP ERROR CONDITION      56100022
         B     KPASS                   RETURN                           56400017
KETRDCD  OI    PARAMSW,X'48'           SET TO READ CARD AND CONTINUE    56700017
         TM    DELIMIN2,BIT40          IS LAST DELIMITER A COMMA        57000017
         BC    1,KGTCD                 YES- READ A CARD                 57300017
         TM    COL72LG,BIT08           IS COLUMN 72 BLANK               57600017
         BC    8,KETBYP                NO- SET BYPASS SWITCH AND RETURN 57900017
         LA    GR7,SWITCHRD+16         CARD COLUMN 16                   58200017
         CR    GR3,GR7                 IS SCAN ON COLUMN 16             58500017
         BE    KETBYP                  YES- SET BYPASS SWITCH           58800017
         TM    DELIMIN2,EQUAL          WAS LAST DELIMITER AN EQUAL      59100017
         BC    1,KETBYP                YES- SET BYPASS SWITCH           59400017
KTERBY   OI    SWITCHRD,BIT40          SET BYPASS SWITCH                59700017
KTER     OI    SWITCHRD,BIT80          SET ERROR SWITCH                 60000017
         B     KPASS                   RETURN                           60300017
KWENT    OI    PARAMSW,X'48'           SET TO READ CARD AND CONTINUE    60600017
         TM    COL72LG,BIT08           IS COLUMN 72 BLANK               60900017
         BC    1,KETCTBL               YES- SET BYPASS SWITCH AND CHECK 61200017
         TM    COL72LG,BIT01           IS LENGTH EQUAL TO ZERO          61500017
         BC    1,KZERO                 NO- RETURN                       61800017
         TM    DELIMIN,BIT08           IS DELIMITER A BLANK             62100017
         BC    1,KETBYP                YES- SET BYPASS SWITCH, RETURN   62400017
         B     KPASS                   RETURN                           62700017
KZERO    SR    GR7,GR7                 CLEAR LENGTH REGISTER            63000017
         TM    DELIMIN,BIT80           IS DELIMITER AN EQUAL            63300017
         BC    1,KTER                  YES- SET ERROR SWITCH            63600017
         TM    DELIMIN,BIT40           IS DELIMITER A COMMA             63900017
         BC    8,KGTCD                 NO- READ A CARD                  64200017
         CLC   DELIMIN(1),DELIMIN2     IS DELIMITER SAME AS LAST        64500017
         BE    KPASS                   YES- RETURN                      64800017
KETCTBL  OI    SWITCHRD,BIT40          SET BYPASS SWITCH                65100017
         TM    DELIMIN,BIT40           IS DELIMITER A COMMA             65400017
         BC    1,KTER                  YES- SET ERROR SWITCH            65700017
         B     KPASS                   RETURN                           66000017
KEY      TM    DELIMIN2,BIT80     LAST DELIMITER ALSO AN EQUAL? YA01700 66100002
         BC    1,SCANERR          YES,BRANCH TO GIVE ERROR MSG  YA01700 66200002
         OI    SWITCHRD,BIT04     SET KEY WORD SWITCH           YA01700 66300002
         OI    DELIMIN,BIT80           SET DELIMITER SWITCH             66600017
         CLI   1(GR3),BLANKCOL    IS  A BLANK AFTER THE EQUAL    A27507 66700022
         BE    SCANERR    ERROR, NO KEYWORDS BEGIN WITH A BLANK  A27507 66800019
         B     DECID                   GO TO CHECK DELIMITER            66900017
PARAMC   CLI   TOTALSW,X'F8'            WAS A COMMA FOUND               67000017
         BE    PTOTC                    YES - FOR TOTAL         YM07522 67100002
         CLI   SQFSW,SWF8               WAS A COMMA FOUND       YM07522 67102002
         BNE   PARAMD                   NO - CONTINUE           YM07522 67104002
         MVI   SQFSW,SWFC               FLAG FOR SEQFLD         YM07522 67106002
         B     PARAMD                   CONTINUE                YM07522 67108002
PTOTC    MVI   TOTALSW,SWFC             IF SO,FLAG AS SUCH      YM07522 67200002
PARAMD   OI    SWITCHRD,BIT02           FLAG PARAMETER FOUND            67300017
         NI    PARAMSW,X'DF'           SET PARAM SW FOLLOW OFF   A48723 67350022
         OI    DELIMIN,BIT40           SET DELIMITER COMMA SWITCH       67500017
         TM    DELIMIN2,X'80'           LAST A COMMAND                  67600017
         BC    14,SCANERR              IF NOT, CHECK SOME MORE     UL0H 67700017
         B     DECID                   GO TO CHECK DELIMITER            67800017
KPART    TM    PARAMSW,BIT10           IS PARTIAL PARAMETER SWITCH ON   68100017
         BC    1,KTERBY                YES-SET BYPASS AND ERROR SWITCH  68400017
         CLI   0(GR5),BLANKCOL         NO CONTINUATION CHAR      A48723 68450022
         BE    KPTERR                  NO CONTINUATION EXPECTED  A48723 68500022
CONTINU  TM    PARAMSW,BIT04           COMMAND SCANNING          A48723 68550022
         BC    1,SET                   YES SET BIT               A48723 68600022
         TM    SWITCHRD,BIT04          KEYWORD SCANNED ALREADY   A48723 68610000
         BC    1,KPTERR                YES,DO NOT SET KEYW SW    A48723 68620022
         OI    COL72LG,X'04'           SET BIT KEYWORD SCANNING  A48723 68650022
         B     KPTERR                  CONTINUE SCANNING         A48723 68660022
SET      OI    COL72LG,X'08'           SET BIT COMMAND SCANNING  A48723 68670022
KPTERR   OI    PARAMSW,BIT10           SET PARTIAL PARAMETER SWITCH     68700017
         TM    COL72LG,BIT08           IS COLUMN 72 BLANK               69000017
         BC    1,KLAST                 YES- GO TO SET PARAMETER BIT     69300017
         MVI   SAVEPAPR,BIT40          FLAG PARTIAL PARAMETER STORED    69600017
         MVC   SAVEPAPR+1(19),SAVEPAPR STORE PARTIAL PARAMETER   A48723 69900022
         LTR   GR7,GR7            IS LENGTH ZERO?               YA01700 69910002
         BZ    SKIPMOVE           YES,SKIP MOVING PART PARAM    YA01700 69920002
         BCTR  GR7,0                   SUBTRACT CONT. CHAR       A48723 69950022
         LR    GR10,GR7                                                 70500017
         LTR   GR10,GR10          IS LENGTH ZERO?               YA01700 70600002
         BZ    SKIPMOVE           YES,SKIP MOVING PART PARAM    YA01700 70700002
         BCTR  GR10,0                  DECREMENT FOR EXECUTE            70800017
         EX    GR10,MOVEP              MOVE PARTIAL PARAMETER TO BUFFER 71100017
SKIPMOVE STC   GR7,SWITCHRD+82    SAVE LENGTH OF PART PARAM     YA01700 71200002
         OI    PARAMSW,BIT40           SET CONTINUE BIT                 71400017
         TM    COL72LG,BIT04           IS COLUMN 72 BLANK               71700017
         BC    1,KGTCD                 NO- GET A CARD                   72000017
         OI    SWITCHRD,BIT80          YES- FLAG AS ERROR               72300017
         B     KETBYP                  SET BYPASS SWITCH                72600017
KLAST    OI    SWITCHRD,BIT02          SET PARAMETER BIT                72900017
         B     KETBYP                  SET BYPASS BIT                   73200017
KPPAR    SR    GR8,GR8                 CLEAR WORK REGISTER              73500017
         IC    GR8,SWITCHRD+82         LENGTH OF FIRST PART PARAMETER   73800017
         LTR   GR7,GR7                 IS LENGTH OF SECOND PART ZERO    74100017
         BC    8,KOMPL                 YES- THE PARAMETER IS COMPLETE   74400017
         LA    GR6,SAVEPAPR            NO- ADDRESS OF FIRST PART        74700017
         AR    GR6,GR8                 GET SAVED LENGTH          A48723 75000022
         LR    GR10,GR7                                                 75300017
         BCTR  GR10,0                  DECREMENT FOR EXECUTE            75600017
         EX    GR10,MOVEPP             MOVE SECOND PART TO BUFFER       75900017
KOMPL    AR    GR7,GR8                 TOTAL LENGTH OF PARAMETER        76200017
         LA    GR1,SAVEPAPR            LOAD PARAMETER BUFFER ADDRESS    76500017
         NI    COL72LG,X'0C'           CLEAR LENGTH BITS, LEAVE'72' BIT 76800017
         OI    COL72LG,X'42'           SET LENGTH GREATER THAN ZERO     77100017
         TM    PARAMSW,BIT10      IS PARTIAL PARAMETER SW ON?   YA01700 77200002
         BC    1,AROUND           YES,SKIP DELIMITER SAVE       YA01700 77300002
         MVC   DELIMIN2(1),DELIMIN     SAVE DELIMITER SWITCH SETTINGS   77400017
         NI    DELIMIN,RESET0          CLEAR DELIMITER BITS             77700017
AROUND   NI    PARAMSW,X'EF'      RESET PART PARAMETER SWITCH   YA01700 78000002
         B     DELIM                   CHECK DELIMITERS                 78300017
KPCMD    LA    GR3,1(GR3)              ADJUST POINTER TO NEXT COLUMN    78600017
         CLI   0(GR3),BLANKCOL         IS COLUMN BLANK                  78900017
         BNE   KPCMA                   NO- CHECK FOR COMMA              79200017
         CR    GR5,GR3                 IS THIS THE LAST COLUMN          79500017
         BE    KNOPAR                  YES- NO PARAMETER FOLLOWS        79800017
         B     KPCMD                   CONTINUE SCAN                    80100017
KPCMA    CLI   0(GR3),COMMA            IS COMMA THE FIRST CHARACTER     80400017
         BE    KNOPAR                  YES- NO PARAMETER FOLLOWS        80700017
         BCTR  GR3,0                   BACK UP REGISTER 3 TO PARAMETER  81000017
         B     PRFOL                   GO TO SET UP FOR PARAMETER       81300017
KSETP    OI    PARAMSW,BIT10           SET PARAMETER FOLLOWS SWITCH     81600017
         B     CONTINU                 GO TO SET PARTIAL PARAM   A48817 82200022
CARDPRNT MVI   ASISMSW,X'0F'           FORCE PRINTING OF CARD           82500017
PRINTY   MVC   LOGOUTAR+7(80),SWITCHRD+1  MOVE CARD TO BUFFER           82800017
         L     GR15,PRIMEWRT           ADDRESS OF MESSAGE ROUTINE       83100017
         BR    GR15                     PRINT IT                        83140017
SCANERR  LA    GR11,SCANMSG             SCAN ERROR MSG                  83180017
         CLI   PRINTED,X'0F'              WAS   RECORD PRINTED          83220017
         BE    SKIPPRNT                 IF SO DO NOT PRINT IT           83260017
         BAL   GR14,CARDPRNT            PRINT RECORD                    83300017
SKIPPRNT L     GR15,PRIMEWRT            ADDRESS OF LOG ROUTINE          83340017
         MVI   PRINTED,X'00'            FLAG AS NOT PRINTED             83380017
         BALR  GR14,GR15                PRINT RECORD                    83420017
         MVI   FLUSHSW,X'0F'            SET FLUSH SWITCH                83460017
         MVI   RIDIT2SW,X'00'           RESET SECOND PARM SWITCH        83500017
         L     GR15,PROCESS             ADDRESS OF IEBUPDT2             83540017
         BR    GR15                     PREPARE TO FLUSH                83580017
         EJECT                                                          83700017
         SPACE 1                                                        84000017
MOVEP    MVC   SAVEPAPR(1),0(GR1)      STORE PARTIAL PARAMETER          84300017
MOVEPP   MVC   0(1,GR6),0(GR1)         RECREATES PARAMETER              84600017
PROCESS  DC    V(IEBUPDT2)                                              84900017
PRIMEWRT DC    V(IEBUPLOG)                                              85000017
RRSAVE   DS    18F                                                      85200017
DOTSLASH DC    C'./'                   CONTROL CARD INDICATION          85500017
PODSET   EQU   2                                                        86100017
PSDSET   EQU   X'40'                                                    86400017
         IEBUPCON                                                       86700017
         END   IEBBSCAN                                                 87000017
