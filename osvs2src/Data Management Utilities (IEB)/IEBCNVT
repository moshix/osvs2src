CNVT     TITLE 'IEBCOPY TTR / MBBCCHHR CONVERT ROUTINE - IEBCNVT'       00350000
IEBCNVT  CSECT                                                          00400000
         SPACE 2                                                        00600000
****************************************************************        00900000
*                                                              *        01200000
* TITLE - IEBCOPY LOAD TTR / MBBCCHHR CONVERSION ROUTINE.      *        01500000
*                                                              *        01800000
* STATUS - CHANGE LEVEL 003.                                   *        02100000
*                                                              *        02400000
* FUNCTION - CONVERT RELATIVE DISK ADDRESSES OF THE FORM TTR0  *        02700000
*            TO FULL DISK ADDRESS MBBCCHHR OR VISE VERSA FOR   *        03000000
*            PDS'S THAT ARE BEING LOADED ONLY.                 *        03300000
*                                                              *        03600000
* ENTRY POINTS - THIS MODULE HAS TWO ENTRY POINTS WHICH ARE    *        03900000
*                IEBCNVT AND IEBCNVT1.                         *        04200000
*                                                              *        04500000
* **** ENTRY POINT IEBCNVT - TTR0 TO MBBCCHHR CONVERSION ***** *        04550000
*                                                              *        04600000
* INPUT - REGISTER 0 CONTAINS TTR0 TO BE CONVERTED.            *        04800000
*         REGISTER 1 CONTAINS ADDRESS OF ORIGINAL UNLOADED     *        05100000
*                    DEB.                                      *        05400000
*         REGISTER 2 CONTAINS ADDRESS OF AN 8 BYTE AREA FOR    *        05700000
*                    THE RESULTANT MBBCCHHR.                   *        06000000
*                                                              *        06300000
* OUTPUT - MBBCCHHR IN AREA POINTED TO BY REGISTER 2 ON INPUT. *        06600000
*                                                              *        06900000
* ************************************************************ *        06950000
*                                                              *        07000000
* **** ENTRY POINT IEBCNVT1 - MBBCCHHR TO TTR0 CONVERSION **** *        07050000
*                                                              *        07100000
* INPUT - REGISTER 1 CONTAINS ADDRESS OF ORIGINAL UNLOADED     *        07150000
*                    DEB.                                      *        07160000
*         REGISTER 2 CONTAINS ADDRESS OF AN 8 BYTE AREA        *        07170000
*                    CONTAINING THE MBBCCHHR TO BE CONVERTED.  *        07180000
*                                                              *        07190000
* OUTPUT - REGISTER 0 CONTAINS THE RESULTANT TTR0.             *        07192000
*                                                              *        07194000
* ************************************************************ *        07196000
*                                                              *        07198000
* EXTERNAL REFERENCES - NONE.                                  *        07200000
*                                                              *        07500000
* EXITS NORMAL - RETURN TO CALLER WITH A RETURN CODE OF 0 IN   *        07800000
*                REGISTER 15.                                  *        08100000
*                                                              *        08400000
* EXITS ERROR - RETURN TO CALLER WITH A RETURN CODE OF 4 IN    *        08700000
*               REGISTER 15.                                   *        09000000
*                                                              *        09300000
* TABLES/WORK AREAS - 'DEVTAB' DSECT (DEVTYPE MAPPING).        *        09900000
*                   - 'MBBCCHHR' DSECT (MBBCCHHR MAPPING).     *        10200000
*                   - 'IECDSECT' DSECT (DEB MAPPING).          *        10500000
*                                                              *        10800000
* ATTRIBUTES - SERIAL REUSABLE.                                *        11100000
*                                                              *        11400000
* CHARACTER CODE DEPENDENCY - NONE.                            *        11700000
*                                                              *        12000000
* NOTES - STANDARD LINKAGE IS DONE, BUT NO NEW SAVE AREA       *        12300000
*          IS PROVIDED SINCE NO EXTERNAL CALL'S ARE MADE.      *        12350000
*                                                              *        12600000
****************************************************************        12900000
         EJECT                                                          13200000
* THE FOLLOWING ARE REGISTER EQUATES                                    13500000
         SPACE 1                                                        13800000
RGTTR    EQU   0                  TTR PARAMETER REGISTER                14100000
RGDEB    EQU   1                  INPUT DEB POINTER REGISTER            14400000
RGMBB    EQU   2                  POINTER TO MBBCCHHR AREA              14700000
RGBASE   EQU   8                  BASE REGISTER                         15000000
RGV      EQU   9                  WORK REGISTER                         15300000
RGW      EQU   10                 WORK REGISTER                         15600000
RGX      EQU   11                 WORK REGISTER                         15900000
RGY      EQU   12                 WORK REGISTER                         16200000
RGZ      EQU   13                 SAVE AREA POINTER REGISTER            16500000
RGRCD    EQU   15                 RETURN CODE REGISTER                  16800000
         SPACE 1                                                        17100000
****************************************************************        17400000
         SPACE 1                                                        17700000
* THE FOLLOWING ARE EQUATES FOR SYMBOLIC CONSTANT REFERENCE             18000000
         SPACE 1                                                        18300000
C1       EQU   1                  GENERAL CONSTANT OF 1                 18900000
         SPACE 1                                                        19800000
SAV20    EQU   20                 SAVE AREA DISPLACEMENT TO             20450000
*                                  SAVED CONTENTS OF REGISTER 0         20500000
         SPACE 1                                                        20700000
ONEBYTE  EQU   8                  LENGTH IN BITS OF ONE BYTE            21000000
         SPACE 1                                                        21300000
LENBCH   EQU   6                  LENGTH OF BBCCHH                      21600000
LENXTN   EQU   16                 LENGTH OF DA DEB EXTENT               22200000
         SPACE 1                                                        22500000
RC4      EQU   4                  A RETURN CODE OF 4                    22800000
         SPACE 1                                                        23100000
DEVBUMP  EQU   16                 OFFSET FOR DEVTYPE POINTER            23400000
*                                  ADJUSTMENT                           23700000
****************************************************************        24000000
         EJECT                                                          24300000
         SAVE  (14,12),,*         SAVE REGISTERS                        24600000
         SPACE 1                                                        24900000
CNVT0000 EQU   *                                                        24950000
         BALR  RGBASE,0           ESTABLISH ADDRESSABILITY              25200000
         USING *,RGBASE                                                 25500000
         SPACE 1                                                        25800000
         USING DXDEB,RGDEB        DEB DSECT ADDRESSABILITY              27900000
         USING MBBCCHHR,RGMBB     MBBCCHHR DSECT ADDRESSABILITY         28200000
         SPACE 1                                                        28500000
         SR    RGRCD,RGRCD        ZERO RETURN CODE REGISTER             28550000
         SR    RGV,RGV            CLEAR WORK REGISTER                   28800000
         IC    RGV,DXDEBUSR       LOAD NUMBER OF EXTENTS                29100000
         LR    RGW,RGV            LOAD LIMIT FOR LOOP CONTROL           29400000
         SRL   RGTTR,ONEBYTE      SHIFT OUT N OF TTRN                   29700000
         STC   RGTTR,R            STORE R OF MBBCCHHR                   30000000
         SRL   RGTTR,ONEBYTE      SHIFT OUT R OF TTRN - TT IS           30300000
*                                  NOW IN LOW ORDER 2 BYTES             30600000
*                                  OF REGISTER                          30900000
CNVT0020 EQU   *                                                        31200000
         SH    RGTTR,DXDEBNTR     DECREASE TT BY NUMBER OF              31500000
*                                  TRACKS IN CURRENT EXTENT             31800000
*                                  DESCRIPTION                          32100000
         BM    CNVT0040           IF THE RESULT OF THE ABOVE            32400000
*                                  SUBTRACTION IS NEGATIVE, THEN        32700000
*                                  THE PROPER EXTENT FOR FURTHER        33000000
*                                  CONVERSION HAS BEEN FOUND            33300000
         LA    RGDEB,LENXTN(0,RGDEB) BUMP DEB ADDRESSABILITY            33600000
*                                     REGISTER TO ACCESS NEXT           33900000
*                                     EXTENT DESCRIPTION                34200000
         BCT   RGW,CNVT0020       CONTINUE SEARCHING FOR PROPER         34500000
*                                  EXTENT DESCRIPTION UNLESS ALL        34800000
*                                  HAVE ALREADY BEEN USED               35100000
         LA    RGRCD,RC4          ALL EXTENT DESCRIPTIONS HAVE          35400000
*                                  BEEN INVESTIGATED, THEREFORE         35700000
*                                  THE INPUT TTR WAS INVALID SO         36000000
*                                  SET THE RETURN CODE TO 4             36300000
         B     CNVT0100           BRANCH TO EXIT LINKAGE                36600000
CNVT0040 EQU   *                                                        36900000
         SR    RGV,RGW            CALCULATE M OF MBBCCHHR               37200000
         STC   RGV,M              STORE M OF MBBCCHHR                   37500000
         MVC   BB(LENBCH),DXDEBBIN  INITIALIZE BBCCHH OF                37800000
*                                    MBBCCHHR                           38100000
         AH    RGTTR,DXDEBNTR     ADD BACK THE NUMBER OF TRACKS         38400000
*                                  WHICH WHEN SUBTRACTED MADE           38700000
*                                  TT OF TTRN NEGATIVE                  39000000
         SPACE 1                                                        39300000
****************************************************************        39600000
*                                                              *        39900000
*   THE VALUE TT IN REGISTER RGTTR NOW CONTAINS THE TRACK      *        40200000
* ADDRESS RELATIVE TO THE CURRENT EXTENT DESCRIPTION WHICH IS  *        40500000
* TO BE PLACED IN THE CCHH PORTION OF THE OUTPUT MBBCCHHR.     *        40800000
*                                                              *        41100000
****************************************************************        41400000
         SPACE 1                                                        41700000
         BZ    CNVT0100           IF THE RESULT OF THE ABOVE            42000000
*                                  ADDITION IS 0, THEN THE INIT-        42300000
*                                  IALIZED VALUE OF THE CCHH            42600000
*                                  PORTION OF MBBCCHHR IS VALID,        42900000
*                                  SO RETURN TO CALLER                  43200000
         L     RGY,DXDEBUCB       LOAD POINTER TO UNLOADED DEV-         43500000
*                                  TYPE INFORMATION MINUS 16            43800000
*                                  BYTES                                44100000
         LA    RGY,DEVBUMP(0,RGY) ADJUST POINTER TO ADDRESS OF          44400000
*                                  UNLOADED DEVTYPE INFORMATION         44700000
         SPACE 1                                                        45000000
         USING DEVTAB,RGY         DEVTYPE DSECT ADDRESSABILITY          45300000
         SPACE 1                                                        45600000
         LR    RGX,RGTTR          INITIALIZE FOR LATER DIVISION         45650000
         LH    RGV,DEVTRK         LOAD NUMBER OF TRACKS PER             45700000
*                                  CYLINDER                             45750000
         AH    RGX,DXDEBSHH       ADD CURRENT DEB EXTENT START          45800000
*                                  HH TO CURRENT VALUE OF TT            45850000
         SR    RGW,RGW            CLEAR WORK REGISTER FOR               45900000
*                                  DIVISION                             45950000
         DR    RGW,RGV            DIVIDE UPDATED TT BY NUMBER OF        46000000
*                                  TRACKS PER CYLINDER                  46050000
         SPACE 1                                                        46100000
****************************************************************        46150000
*                                                              *        46200000
*   THE RESULT OF THE ABOVE DIVISION IS AS FOLLOWS -           *        46250000
*        RGW - THE REMAINDER REGISTER CONTAINS THE FINAL       *        46300000
*               HH VALUE OF THE OUTPUT MBBCCHHR.               *        46350000
*        RGX - THE QUOTIENT REGISTER CONTAINS THE VALUE        *        46400000
*               TO BE ADDED TO THE CURRENT DEB EXTENT          *        46450000
*               START CC WHICH WILL YIELD THE FINAL CC         *        46500000
*               VALUE OF THE OUTPUT MBBCCHHR.                  *        46550000
*                                                              *        46600000
****************************************************************        46650000
         SPACE 1                                                        46700000
         STC   RGW,HH+C1          STORE FINAL HH VALUE OF OUTPUT        46750000
*                                  MBBCCHHR (HIGH ORDER HH BYTE         46800000
*                                  IS ALWAYS 0 FOR ALL SUPPORTED        46850000
*                                  DEVICES AND IS NOT STORED)           46900000
         AH    RGX,DXDEBSCC       ADD CURRENT DEB EXTENT START          46950000
*                                  CC TO ABOVE CALCULATED               47000000
*                                  QUOTIENT                             47050000
         STC   RGX,CC+C1          STORE LOW ORDER C OF OUTPUT           47100000
*                                  MBBCCHHR                             47150000
         SRL   RGX,ONEBYTE        SHIFT HIGH ORDER C BYTE TO            47200000
*                                  LOW ORDER BYTE OF REGISTER           47250000
         STC   RGX,CC             STORE HIGH ORDER C OF OUTPUT          47300000
*                                  MBBCCHHR                             47350000
         SPACE 1                                                        47400000
CNVT0100 EQU   *                                                        54600000
         RETURN (14,12),T,RC=(15) RETURN TO CALLER                      56100000
         EJECT                                                          56150000
         DS    0F                 FULL WORD ALIGNMENT                   56200000
         ENTRY IEBCNVT1                                                 56250000
IEBCNVT1 EQU   *                  MBBCCHHR TO TTR0 ENTRY POINT          56300000
         SAVE  (14,12),,IEBCNVT1  SAVE REGISTERS                        56350000
CNVT2000 EQU   *                                                        56400000
         BALR  RGBASE,0           ESTABLISH ADDRESSABILITY              56450000
         LA    RGY,CNVT2000-CNVT0000 RELATIVE TO FIRST                  56500000
         SR    RGBASE,RGY             ENTRY POINT                       56550000
         SPACE 1                                                        56600000
         SR    RGTTR,RGTTR        CLEAR TTR0 OUTPUT REGISTER            56650000
         SR    RGRCD,RGRCD        ZERO RETURN CODE REGISTER             56700000
         SR    RGY,RGY            CLEAR WORK REGISTER                   56750000
         SPACE 1                                                        56800000
         IC    RGY,M              PICK UP M OF INPUT MBBCCHHR           56850000
         LTR   RGY,RGY            IS M LARGER THAN 0                    56900000
         BZ    CNVT2040           NO, DO CONVERSION USING               56950000
*                                  FIRST DEB EXTENT                     57000000
         SPACE 1                                                        57050000
CNVT2020 EQU   *                                                        57100000
         AH    RGTTR,DXDEBNTR     ADD NUMBER OF TRACKS IN               57150000
*                                  CURRENT EXTENT DESCRIPTION           57200000
*                                  TO CURRENT VALUE OF TT               57250000
         LA    RGDEB,LENXTN(0,RGDEB) BUMP DEB ADDRESSABILITY            57300000
*                                     REGISTER TO ACCESS NEXT           57350000
*                                     EXTENT DESCRIPTION                57400000
         BCT   RGY,CNVT2020       CONTINUE LOOPING UNTIL PROPER         57450000
*                                  DEB EXTENT IS FOUND                  57500000
         SPACE 1                                                        57550000
CNVT2040 EQU   *                                                        57600000
         L     RGY,DXDEBUCB       LOAD POINTER TO UNLOADED              57650000
*                                  DEVTYPE INFORMATION MINUS 16         57700000
*                                  BYTES                                57750000
         LA    RGY,DEVBUMP(0,RGY) ADJUST POINTER TO ADDRESS OF          57800000
*                                  UNLOADED DEVTYPE INFORMATION         57850000
         SPACE 1                                                        57900000
         SR    RGW,RGW            CLEAR WORK REGISTER                   57950000
         IC    RGW,CC             LOAD HIGH ORDER C OF INPUT            58000000
*                                  MBBCCHHR                             58050000
         SLL   RGW,ONEBYTE        SHIFT TO MAKE ROOM FOR LOW            58100000
*                                  ORDER C BYTE                         58150000
         IC    RGW,CC+C1          LOAD LOW ORDER C OF INPUT             58200000
*                                  MBBCCHHR                             58250000
         SH    RGW,DXDEBSCC       SUBTRACT CURRENT DEB EXTENT           58300000
*                                  START CC FROM CC OF INPUT            58350000
*                                  MBBCCHHR                             58400000
         MH    RGW,DEVTRK         MULTIPLY RESIDUAL NUMBER OF           58410000
*                                  CYLINDERS BY THE NUMBER OF           58420000
*                                  TRACKS PER CYLINDER                  58430000
         SR    RGX,RGX            CLEAR WORK REGISTER                   58440000
         IC    RGX,HH+C1          LOAD LOW ORDER H OF INPUT             58442000
*                                  MBBCCHHR (HIGH ORDER H BYTE          58444000
*                                  FOR ALL SUPPORTED DEVICES            58446000
*                                  IS ALWAYS 0)                         58448000
         SH    RGX,DXDEBSHH       SUBTRACT CURRENT DEB EXTENT           58448400
*                                  START HH FROM HH OF INPUT            58448800
*                                  MBBCCHHR                             58449200
         AR    RGW,RGX            CALCULATE TOTAL NUMBER OF             58450000
*                                  RESIDUAL TRACKS                      58500000
         AR    RGTTR,RGW          ADD RESIDUAL TRACKS TO NUMBER         58550000
*                                  OF TRACKS IN PREVIOUS EXTENTS        58600000
*                                  (IF ANY)                             58650000
         SLL   RGTTR,ONEBYTE      MAKE ROOM FOR R OF TTR0               58700000
         IC    RGTTR,R            LOAD R FROM MBBCCHHR                  58750000
         SLL   RGTTR,ONEBYTE      MAKE FINAL RESULT TTR0                58800000
         ST    RGTTR,SAV20(0,RGZ) STORE TTR0 IN CALLERS SAVE            58850000
*                                  SO THAT IT WILL BE PLACED            58900000
*                                  IN REGISTER 0 BY RETURN              58950000
*                                  MACRO EXPANSION                      59000000
         B     CNVT0100           RETURN TO CALLER                      59050000
         EJECT                                                          66000000
****************************************************************        66300000
*                                                              *        66600000
*   THE FOLLOWING IS IEBCNVT'S WORK AREA                       *        66900000
*                                                              *        67200000
****************************************************************        67500000
         SPACE 1                                                        68400000
PATCHLEN EQU   (*-IEBCNVT)/20     LENGTH OF 5 PERCENT PATCH AREA        68700000
CNVT9000 DC    XL(PATCHLEN)'00'   PATCH AREA                            69000000
         EJECT                                                          69300000
****************************************************************        69600000
*                                                              *        69900000
*   DEVTYPE MAPPING DSECT                                      *        70200000
*                                                              *        70500000
****************************************************************        70800000
         SPACE 1                                                        71100000
DEVTAB   DSECT                                                          71400000
         DS    XL10               UNREFERENCED FIELDS                   71700000
DEVTRK   DS    XL2                TRACKS PER CYLINDER                   72900000
         SPACE 2                                                        73200000
****************************************************************        73500000
*                                                              *        73800000
*   MBBCCHHR MAPPING DSECT                                     *        74100000
*                                                              *        74400000
****************************************************************        74700000
         SPACE 1                                                        75000000
MBBCCHHR DSECT                                                          75300000
M        DS    XL1                EXTENT NUMBER                         75600000
BB       DS    XL2                BIN NUMBER                            75900000
CC       DS    XL2                CYLINDER NUMBER                       76200000
HH       DS    XL2                HEAD NUMBER                           76500000
R        DS    XL1                RECORD NUMBER                         76800000
         EJECT                                                          77100000
IECDSECT DSECT                                                          77400000
         IECDSECT                                                       77700000
         END                                                            78000000
