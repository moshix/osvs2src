 SPACE 2                                                                00200017
*********************************************************************** 00400017
*                                                                     * 00600017
* NAME   IEBCULET                                                     * 00800017
*                                                                     * 01000017
*********************************************************************** 01200017
*                                                                     * 01400017
* FUNCTION/OPERATION                                                  * 01600017
*                                                                     * 01800017
*     THIS ROUNTINE WILL ALWAYS BE ENTERRED VIA DCBEXLST ENTRY FOR    * 02000017
*  USER INPUT HEADER/TRAILER LABEL EXITS. ITS MAIN FUNCTIONS ARE AS   * 02200017
*  FOLLOWS                                                            * 02400017
*                                                                     * 02600017
*   1. TO SET ITS OWN ADDRESSABILITY                                  * 02800017
*   2. TO SET COMMUNICATION REGION'S ADDRESSABILITY                   * 03000017
*   3. THEN BRANCH TO PROPER ADDRESS FOR LABEL SAVING OR EXIT TO USER * 03200017
*      INHDR/INTLR                                                    * 03400017
*        'HDRLBEX1' ENTRY FOR SYSUT1 USER INPUT HEADER                * 03600017
*        'HDRLBEX2' ENTRY FOR SYSUT2 USER INPUT HEADER                * 03800017
*        'TLRLBEX1' ENTRY FOR SYSUT1 USER INPUT TRAILER               * 04000017
*        'TLRLBEX2' ENTRY FOR SYSUT2 USER INPUT TRAILER               * 04200017
*   4. TO SAVE THE LABELS FOR LATER COMPARISON IF REQUESTED BY USER   * 04400017
*   5. GIVE CONTROL TO USER INHDR/INTLR IF THERE IS ONE               * 04600017
*   6. INTERPRET USER RETURN CODE                                     * 04800017
*   7. PRINT USER WISH TO STOP RECEIVING ANY MORE LABEL MESSAGE       * 05000017
*   8. EXIT TO OPEN/CLOSE/EOV AFTER EACH LABEL BEING HANDLED PROPERLY * 05200017
*                                                                     * 05400017
*********************************************************************** 05600017
*                                                                     * 05800017
* ENTRY POINTS                                                        * 06000017
*                                                                     * 06200017
*   'USERLAB' IS THE ONLY ENTRY POINT TO THIS MODULE VIA USER LABEL   * 06400017
*      ENTRY IN DCBEXLST                                              * 06600017
*                                                                     * 06800017
*********************************************************************** 07000017
*                                                                     * 07200017
* INPUT                                                               * 07400017
*                                                                     * 07600017
*   INPUT TO THIS MODULE ARE AS FOLLOWS                               * 07800017
*                                                                     * 08000017
*   1. SWITCHES AND PARAMETERS WHICH ARE SET UP BY IEBCANAL AND       * 08200017
*      IEBCMAIN IN THE COMMUNICATION REGION OF ROOT SEGMENT           * 08400017
*   2. 16 BYTES PARAMETER LIST WHICH IS SET UP BY OPEN/CLOSE/EOV FOR  * 08600017
*      EACH LABEL TO BE VERIFIED                                      * 08800017
*                                                                     * 09000017
*********************************************************************** 09200017
*                                                                     * 09400017
* OUTPUT                                                              * 09600017
*                                                                     * 09800017
*     THE OUTPUT FROM THIS MODULE IS DIAGNOSTIC AND INFORMATIONAL     * 10000017
*   MESSAGES.                                                         * 10200017
*                                                                     * 10400017
*********************************************************************** 10600017
*                                                                     * 10800017
* EXTERNAL ROUTINES                                                   * 11000017
*                                                                     * 11200017
*     THIS MODULE USES ROOT SEGMENT'S PRINT ROUTINE, ZZPR AND JPRINTRT* 11400017
*   SUBROUTINE TO PRINT MESSAGES. OTHER POSSIBLE EXTERNAL ROUTINES ARE* 11600017
*   THE USER INHDR/INTLR ROUTINES. LINKAGE TO THESE ROUTINES IS MADE  * 11800017
*   BY BRANCH AND LINK INSTRUCTION.                                   * 12000017
*                                                                     * 12200017
*********************************************************************** 12400017
*                                                                     * 12600017
* EXITS                                                               * 12800017
*                                                                     * 13000017
*     THE ONLY EXIT MADE FROM THIS MODULE IS A RETURN BACK TO         * 13200017
*   OPEN/CLOSE/EOV AFTER EACH LABEL BEING HANDLED ACCORDINGLY         * 13400017
*                                                                     * 13600017
*********************************************************************** 13800017
*                                                                     * 14000017
* TABLES/WORK AREA                                                    * 14200017
*     THERE IS NO TABLES/WORK AREA IN THIS MODULE                     * 14400017
*                                                                     * 14600017
*********************************************************************** 14800017
*                                                                     * 15000017
* ATTRIBUTES                                                          * 15200017
*                                                                     * 15400017
*     NON-REUSABLE                                                    * 15600017
*                                                                     * 15800017
*********************************************************************** 16000017
  EJECT                                                                 16200017
COMMX    DSECT                                                          16400017
         USING COMMX,10                                                 16600017
SAVE1315 DS    3F             SAVE AREA FOR REGS 13,14,15               16800017
SAVEREGS DS    18F            MY REGISTER SAVE AREA                     17200017
PARALIST DS    8F             THE EXITS PARAMETER LIST-USER ROUTS       17400017
*                               1. INHDR PARAMETER NAME                 17600017
*                               2. INTLR PARAMETER NAME                 17800017
*                               3. ERROR PARAMETER NAME                 18000017
*                               4. PRECOMP PARAMETER NAME               18200017
DDSYSIN  DS    2F             SYSIN PARAMETER                           18400017
DDSUBNAM DS    4F             DDNAME PARAMETER LIST                     18600017
*                               1. SYSUT1 DDNAME                        18800017
*                               2. SYSUT2 DDNAME                        19000017
HICONCOD DS    2X             RETURN CODE STORAGE AREA                  19200017
CCADSORG DS    1X             SET TO '01' IF PARTITIONED DATA SET       19400017
ABORTSW  DS    1X             ABORT SWTICH-SET TO '01' IF ON            19600017
LABEL    DS    1X             SET TO X'00' IF DATA=YES             UL0H 19800017
*                                    X'01' IF DATA=NO              UL0H 20000017
*                                    X'10' IF DATA=ONLY            UL0H 20200017
*                                    X'20' IF DATA=ALL             UL0H 20400017
UEXIT    DS    1X             SET TO 01 IF OPEN TOOK USER LAB EXIT UL0H 20600017
UHRADR   DS    1F             CONTAIN ADDR. OF INHDR               UL0H 20800017
UTRADR   DS    1F             CONTAIN ADDR. OF INTLR               UL0H 21000017
INHD1    DS    1F             HAS ADDR. OF SYSUT1 HD LAB SAVE AREA UL0H 21200017
INHD2    DS    1F             HAS ADDR. OF SYSUT2 HD LAB SAVE AREA UL0H 21400017
INHDCT1  DS    1F             CONT. NO. OF HEADER LABELS FOR SYSUT1UL0H 21600017
INHDCT2  DS    1F             CONT. NO. OF HEADER LABELS FOR SYSUT2UL0H 21800017
INTL1    DS    1F             HAS ADDR. OF SYSUT1 TL LAB SAVE AREA UL0H 22000017
INTL2    DS    1F             HAS ADDR. OF SYSUT2 TL LAB SAVE AREA UL0H 22200017
INTLCT1  DS    1F             CONT. NO. OF TRAILER LABEL FOR SYSUT1UL0H 22400017
INTLCT2  DS    1F             CONT. NO. OF TRAILER LABEL FOR SYSUT2UL0H 22600017
BUFF1    DS    1F             CONTAIN ADDR. OF READ AREA-DS1       VS0H 22800017
BUFF2    DS    1F             CONTAIN ADDR. OF READ AREA-DS2       VS0H 23000017
LBUFF1   DS    1F             LENGTH OF BUFFER FOR SYSUT1 RCD PROC VS0H 23200017
LBUFF2   DS    1F             LENGTH OF BUFFER FOR SYSUT2 RCD PROC VS0H 23400017
LORECO   DS    1F             TOTAL LOGICAL RECORD COUNTER         VS0H 23600017
RECLTH   DS    1H             LOGICAL RECORD LENGTH-DS1            VS0H 23800017
RECLH2   DS    1H             LOGICAL RECORD LENGTH-DS2            VS0H 24000017
DSET1    DS    1F            ADDRS. OF SYSUT1 DCB IN IEBCQSAM      VS0H 24200017
DSET2    DS    1F            ADDRS. OF SYSUT2 DCB IN IEBCQSAM      VS0H 24400017
INPUTLOC DS    1F            ADDR. OF INPUT BUFFER FOR BLPRT RT    VS0H 24600017
NOEXTSW  DS    1X             NO EXIT TO USER LAB RTN IF SET TO 01 UL0H 24800017
TERMSW   DS    1X             TERM. AFTER LAB. PROC. IF SET TO 01  UL0H 25000017
EXIT1SW  DS    1X             SET TO 01 IF THERE IS AN USER INHDR  UL0H 25200017
EXIT3SW  DS    1X             SET TO 01 IF THERE IS AN USER INTLR  UL0H 25400017
VSSWT    DS    1X             SET TO 01 IF BSAM USED TO PROC VS RCDVS0H 25600017
*                             SET TO 02 IF QSAM USED TO PROC VS RCDVS0H 25800017
NOLB     DS    1X             SET TO 01 IF NO USER LABEL ON SYSUT1 UL0H 26000017
*                             SET TO 02 IF NO USER LABEL ON SYSUT2 UL0H 26200017
LABSW    DS    1X             SET TO 01; SYSUT1 HEADER LABEL PROC  UL0H 26400017
*                             SET TO 02; SYSUT2 HEADER LABEL PROC  UL0H 26600017
*                             SET TO 04; SYSUT1 TRAILER LABEL PROC UL0H 26800017
*                             SET TO 08; SYSUT2 TRAILER LABEL PROC UL0H 27000017
PRINTOUT DS    0F             SYSPRINT DCB                              27200017
*********************************************************************** 27400017
*        USER INPUT HEADER LABEL EXITS ARE TAKEN VIA THIS SECTION OF  * 27600017
*        CODE  USER INPUT HEADER LABELS ARE SAVED FOR LATER COMPARISON* 27800017
*        IF REQUESTED BY USER                                         * 28000017
*********************************************************************** 28200017
USERLAB  CSECT                                                          28400017
*                                                                     * 28450017
*        THIS MODULE IS TOTALLY NEW FOR RELEASE 17                    * 28500017
*                                                                     * 28550017
R0       EQU   0                                                        28552017
R1       EQU   1                                                        28554017
R2       EQU   2                                                        28556017
R3       EQU   3                                                        28558017
R4       EQU   4                                                        28560017
R5       EQU   5                                                        28562017
R6       EQU   6                                                        28564017
R7       EQU   7                                                        28566017
R8       EQU   8                                                        28568017
R9       EQU   9                                                        28570017
R10      EQU   10                                                       28572017
R11      EQU   11                                                       28574017
R12      EQU   12                                                       28576017
R13      EQU   13                                                       28578017
R14      EQU   14                                                       28580017
R15      EQU   15                                                       28582017
         BALR  R2,R0         ESTABLISH ADDRESSABILITY                   28682017
         USING *,R2                                                     28782017
         STM   R1,R14,USAVE                                             28882017
         DROP  R2                                                       28982017
         BALR  R12,R0        RE-ESTABLISH ADDRESSABILITY                29082017
         USING *,R12                                                    29182017
         LA    R13,MYSAVE                                               29282017
         CLI   LABSW,X'01'    IS IN SYSUT1 HEADER LABEL PROC.           30200017
         BE    HDRLBEX1       YES, BRANCH                               30400017
         CLI   LABSW,X'02'    IS IN SYSUT2 HEADER LABEL PROC.           30600017
         BE    HDRLBEX2       YES, BRANCH                               30800017
         CLI   LABSW,X'04'    IS IN SYSUT1 TRAILER LABEL PROC.          31000017
         BE    TLRLBEX1       YES, BRANCH                               31200017
         CLI   LABSW,X'08'    IS IN SYSUT2 TRAILER LABEL PROC.          31400017
         BE    TLRLBEX2       YES, BRANCH                               31600017
         B     RET1           BRANCH                                    31800017
HDRLBEX1 MVC   LABMSG2+9(8),PARALIST *  INITIALIZE MSG: XX EXITS TO  *  31900017
         MVI   LAB2+3,X'F1'          *  (INHDR) IS MADE FOR SYSUT(1) *  32000017
*                                    *  RETURN CODE FROM USER ROUTINE*  32100017
*                                    *  IS XX                        *  32200017
         OI    UEXIT,X'01'     IN SYSUT1 LABEL EXIT                     32300017
         TM    8(R1),X'80'    DID UNCORRECTABLE I/O ERROR OCCUR         32400017
         BO    IOERR1         WHILE READ USER INPUT HD ON SYSUT1        32600017
NTEST1   L     R3,0(R1)       GET ADDR. OF LABEL BUFFER AREA            32800017
         C     R3,ZERO        IS ADDR. OF BUFFER AREA ZERO              33000017
         BE    FSTNOHD1       YES, BRANCH                               33200017
         CLI   3(R3),X'F1'    IS THIS THE 1ST LABEL OF THE GROUP        33400017
         BE    FSTHD1         YES, BRANCH                               33600017
         TM    FSW1,X'02'     IS THIS THE VOL-SWITCH HD ON SYSUT1       33800017
         BO    INHEXT1        YES, BRANCH                               34000017
         TM    LABEL,X'01'    IS DATA=NO                                34200017
         BO    INHEXT1        YES, BRANCH                               34400017
         B     INIT1          NO, GO SAVE THE LABEL                     34600017
FSTHD1   TM    FSW1,X'01'     IS THIS THE 1ST GROUP OF HD ON SYSUT1     34800017
         BZ    INTLAB1        NO, THIS IS VOL-SWITCH TIME HDS           35000017
         MVI   FSW1,X'00'     YES, TURN SW OFF TO IND. 1ST HD OF        35200017
*                             1ST HD GROUP ON SYSUT1 HAS READ           35400017
FSTIME1  TM    LABEL,X'01'    IS DATA=NO                                35600017
         BO    INHEXT1        YES, LABEL SAVING IS NOT NECESSARY        35800017
         LR    R2,R1          NO, GET 640 BYTES TO SAVE LABEL           36000017
         GETMAIN   R,LV=640                                             36200017
         ST    R1,INHD1                                                 36400017
         LR    R1,R2                                                    36600017
         B     INIT1          BRANCH                                    36800017
FSTNOHD1 TM    FSW1,X'01'     IS THIS THE FIRST GROUP OF HD ON DS1      37000017
         BZ    INTLAB1        NO, BRANCH                                37200017
         XI    NOLB,X'01'     YES,TURN NOLB SW TO IND. NO LABEL         37400017
*                            ON SYST1                                   37600017
          MVI  FSW1,X'00'                                               37800017
         B     INHEXT1        BRANCH                                    38000017
INIT1    L     R2,INHD1      GET ADDR. OF SYSUT1 HD LAB SAVE AREA       38200017
         L     R5,INHDCT1                                               38400017
         M     R4,EIGHTY                                                38600017
         AR    R2,R5          REG.2 CONT. ADDR.OF NEXT AVAIL.LABEL      38800017
*                             SAVE AREA FOR SYSUT1                      39000017
         L     R5,INHDCT1                                               39200017
         LA    R5,1(R5)       ADD 1 TO INPUT HEADER LAB CT FOR          39400017
         ST    R5,INHDCT1     SYSUT1                                    39600017
         B     LABSAVE        BRANCH                                    39800017
HDRLBEX2 MVC   LABMSG2+9(8),PARALIST *  INITIALIZE MSG : XX EXITS TO *  39900017
         MVI   LAB2+3,X'F2'          *  (INHDR) IS MADE FOR SYSUT(2) *  40000017
*                                    *  RETURN CODE FROM USER ROUTINE*  40100017
*                                    *  IS XX                        *  40200017
         OI    UEXIT,X'02'     IN SYSUT2 LABEL EXIT                     40300017
         TM    8(R1),X'80'    DID I/O ERROR OCCUR WHILE READ USER       40400017
         BO    IOERR2         INPUT HEADER ON SYSUT2                    40600017
NTEST2   L     R3,0(R1)       GET ADDR. OF LABEL BUFFER AREA            40800017
         C     R3,ZERO        IS ADDR. OF LABEL BUFFER ZERO             41000017
         BE    FSTNOHD2       YES, BRANCH                               41200017
         CLI   3(R3),X'F1'    IS THIS THE 1ST LABEL OF THE GROUP        41400017
         BE    FSTHD2         YES, BRANCH                               41600017
         TM    FSW2,X'02'     IS THIS THE VOL-SWITCH HD ON SYSUT2       41800017
         BO    INHEXT2        YES, BRANCH                               42000017
         TM    LABEL,X'01'    IS DATA=NO                                42200017
         BO    INHEXT2        YES, BRACH                                42400017
         B     INIT2          BRANCH                                    42600017
*                                                                       42800017
*        THIS IS INH1 ON SYSUT2                                         43000017
FSTHD2   TM    FSW2,X'01'     IS THIS THE 1ST GROUP OF HD ON SYSUT1     43200017
         BZ    INTLAB2        NO, THIS IS VOL-SWITCH TIME HDS           43400017
         MVI   FSW2,X'00'     YES,TURN SW OFF TO IND. 1ST HD OF         43600017
*                             1ST HD GROUP ON SYSUT2 HAS READ           43800017
FSTIME2  TM    LABEL,X'01'    IS DATA=NO                                44000017
         BO    INHEXT2        YES, LABEL SAVING IS NOT NECESSARY        44200017
         LR    R2,R1                                                    44400017
         GETMAIN   R,LV=640                                             44600017
         ST    R1,INHD2                                                 44800017
         LR    R1,R2                                                    45000017
         B     INIT2          BRANCH                                    45200017
*                                                                       45400017
*        NO HEADER LABEL ON SYSUT2                                      45600017
FSTNOHD2 TM    FSW2,X'01'     IS THIS THE 1ST GROUP OF HD ON SYSUT2     45800017
         BZ    INTLAB2        NO,BRANCH                                 46000017
         XI    NOLB,X'02'     YES,TURN NOLB SW ON TO IND. NO HEADER     46200017
*                             LABEL ON SYSUT2                           46400017
          MVI  FSW2,X'00'                                               46600017
         B     INHEXT2        BRANCH                                    46800017
INIT2    L     R2,INHD2       GET ADDR. OF SYSUT2 HD LAB SAVE AREA      47000017
         L     R5,INHDCT2                                               47200017
         M     R4,EIGHTY                                                47400017
         AR    R2,R5          REG.2 CONT. ADDR OF NEXT AVAIL. LABEL     47600017
*                             SAVE AREA FOR SYSUT2                      47800017
         L     R5,INHDCT2                                               48000017
         LA    R5,1(R5)       ADD 1 TO INPUT HEADER LAB CT FOR          48200017
         ST    R5,INHDCT2     SYSUT2                                    48400017
*        THIS SECTION OF CODE WILL SAVE INPUT HEADER LABEL FOR LATER  * 48600017
*        COMPARISON.                                                  * 48800017
LABSAVE  L     R3,0(R1)       GET ADDR. OF LABEL BUFFER                 49000017
         MVC   0(80,R2),0(R3) MOVE LABEL INTO LABEL SAVE AREA           49200017
         TM    EXIT1SW,X'01'  IS THERE AN USER INHDR                    49400017
         BZ    RET            NO                                        49600017
         TM    NOEXTSW,X'01'  YES,IS NO EXIT TO INHDR SW ON             49800017
         BO    RET            YES,NO EXIT TO INHDR SW IS ON             50000017
GOUHR    L     R15,UHRADR    NO,GO TO USER INHDR                        50200017
         BALR  R14,R15        BRANCH                                    50400017
         B     CKRCODE        BRANCH                                    50600017
*        INITIALIZATION FOR SYSUT1 VOL-SWITCH HEAER PROCESSING          50800017
INTLAB1  TM    TERMSW,X'01'   WAS TERM SW SET BY VOL-SWITCH TRAILER     51000017
*                             LABEL PROCESSING                          51200017
         BO    RET1           YES, BRANCH                               51400017
         MVI   FSW1,X'02'     SET FSW1 TO IND. IN VOL-SWITCH HEAD       51600017
         MVC   INHDCT1(4),ZERO  SET HEADER LAB, COUNT TO ZERO           51800017
INHEXT1  L     R5,INHDCT1                                               52000017
         LA    R5,1(R5)                                                 52200017
         ST    R5,INHDCT1                                               52400017
         B     INHEXT         BRANCH                                    52600017
*        INITIALIZATION FOR SYSUT2 VOL SWITCH HEADER PROCESSING         52800017
INTLAB2  TM    TERMSW,X'01'   WAS TEMM SW SET BY VOL-SWITCH TRAILER     53000017
*                             LABEL PROCESSING                          53200017
         BO    RET1           YES, BRANCH                               53400017
         MVI   FSW2,X'02'     SET FSW2 TO IND. IN VOL-SWITCH HEAD       53600017
         MVC   INHDCT2(4),ZERO     SET HEADER LAB. COUNT TO ZERO        53800017
INHEXT2  L     R5,INHDCT2                                               54000017
         LA    R5,1(R5)                                                 54200017
         ST    R5,INHDCT2                                               54400017
INHEXT   TM    EXIT1SW,X'01'  IS THERE AN USER INHDR                    54600017
         BZ    RET1           NO, IGNORE THE REST OF LABELS             54800017
         B     GOUHR          BRANCH                                    55000017
*********************************************************************** 55200017
*        USER INPUT TRAILER LABEL EXITS ARE TAKEN VIA THIS SECTION OF * 55400017
*        CODE. USER INPUT TRAILER LABELS ARE SAVED FOR LATER COMPARISO* 55600017
*        IF REQUESTED BY USER                                         * 55800017
*********************************************************************** 56000017
TLRLBEX1 MVC   LABMSG2+9(8),PARALIST+8 * INIT. MSG: X EXITS TO (INTLR)* 56100017
         MVI   LAB2+3,X'F1'            * IS MADE FOR SYSUT(2) RETURN  * 56200017
*                                      * CODE FROM USER ROUTINE IS XX * 56300017
         L     R3,0(R1)       GET ADDR. OF LABEL BUFFER AREA            56600017
         C     R3,ZERO        IS ADDR. OF LABEL BUFFER AREA ZERO        56800017
         BNE   TTESTIO1       NO, BRANCH                                57000017
         XI    NOLB,X'01'     YES,TURN NOLB  SW ON TO IND. NO LAB       57200017
*                             ON SYSUT1                                 57400017
         MVC   INTLCT1(4),ZERO     SET SYSUT1 TRAILER LAB COUNT 0       57600017
         B     TEXT1          BRANCH                                    57800017
TTESTIO1 TM    8(R1),X'80'    DID I/O ERROR OCCUR WHILE READ USER       58000017
         BO    TIOERR1        INPUT TRAILER ON SYSUT1                   58200017
TNTEST1  TM    LABEL,X'01'    DATA=NO                                   58400017
         BO    INTEXT1        YES,DATA=NO                               58600017
         CLI   3(R3),X'F1'    IS THIS THE 1ST LABEL OF THE GROUP        58800017
         BNE   INITT1         NO, BRANCH                                59000017
         MVC   INTLCT1(4),ZERO     SET SYSUT1 TRAILER LAB COUNT 0       59200017
         NI    NOLB,X'20'     TURN NOLB SW OFF                          59400017
         CLC   INTL1(4),ZERO  HAS VOL-SWITCH TIME TRAILER LABELS        59600017
*                             BEEN SAVED                                59800017
         BNE   INITT1         YES, USE THE SAME SAVE AREA TO OVER       60000017
*                             LAY THOSE LABELS                          60200017
         LR    R2,R1                                                    60400017
         GETMAIN   R,LV=640                                             60600017
         ST    R1,INTL1       SAVE GOTTEN CORE ADDR. AT INTL1           60800017
         LR    R1,R2                                                    61000017
INITT1   L     R2,INTL1                                                 61200017
         L     R5,INTLCT1                                               61400017
         M     R4,EIGHTY                                                61600017
         AR    R2,R5          REG2.HAS ADDR. OF NEXT AVAIL LABEL        61800017
*                             SAVE AREA FOR SYSUT1                      62000017
         L     R5,INTLCT1                                               62200017
         LA    R5,1(R5)       ADD 1 TO INPUT TRAILER LABEL CT FOR       62400017
         ST    R5,INTLCT1     SYSUT1                                    62600017
         B     TLABSAVE       BRANCH                                    62800017
*                                                                       63000017
TLRLBEX2 MVC   LABMSG2+9(8),PARALIST+8 * INIT. MSG: X EXITS TO (INTLR)* 63100017
         MVI   LAB2+3,X'F2'            * IS MADE FOR SYSUT(2) RETURN  * 63200017
*                                      * CODE FROM USER ROUTINE IS XX * 63300017
         L     R3,0(R1)       GET ADDR. OF LABEL BUFFER AREA            63600017
         C     R3,ZERO        IS ADDR. OF LABEL BUFFER AREA ZERO        63800017
         BNE   TTESTIO2       NO, BRANCH                                64000017
         XI    NOLB,X'02'     YES, TURN NOLB SW ON TO IND. NO LAB       64200017
         MVC   INTLCT2(4),ZERO     SET SYSUT2 TRAILER LABEL COUNT 0     64400017
         B     TEXT2          BRANCH                                    64600017
TTESTIO2 TM    8(R1),X'80'    DID I/O ERROR OCCUR WHILE READ USER       64800017
         BO    TIOERR2        INPUT TRAILER ON SYSUT2                   65000017
TNTEST2  TM    LABEL,X'01'    DATA=NO                                   65200017
         BO    INTEXT2        YES, DATA=NO                              65400017
         CLI   3(R3),X'F1'    IS THIS THE 1ST LABEL OF THE GROUP        65600017
         BNE   INITT2         NO, BRANCH                                65800017
         MVC   INTLCT2(4),ZERO     SET SYSUT2 TRAILER LABEL COUNT 0     66000017
          NI   NOLB,X'01'    TURN NOLB SW OFF                           66200017
         CLC   INTL2(4),ZERO  HAS VOL-SWITCH TIME TRAILER LABEL         66400017
*                             BEEN SAVED                                66600017
         BNE   INITT2         YES,USE THE SAME SAVE ARER TO OVER        66800017
*                             LAY THOSE LABELS                          67000017
         LR    R2,R1                                                    67200017
         GETMAIN   R,LV=640                                             67400017
         ST    R1,INTL2       SAVE GOTTEN CORE ADDR. AT INTL2           67600017
         LR    R1,R2                                                    67800017
INITT2   L     R2,INTL2                                                 68000017
         L     R5,INTLCT2                                               68200017
         M     R4,EIGHTY                                                68400017
         AR    R2,R5          REG.2 HAS ADDR. OF NEXT AVAIL. LABEL      68600017
*                             SAVE AREA FOR SYSUT2                      68800017
         L     R5,INTLCT2                                               69000017
         LA    R5,1(R5)       ADD 1 TO INPUT TRAILER LABEL CT FOR       69200017
         ST    R5,INTLCT2     SYSUT2                                    69400017
*                                                                       69600017
TLABSAVE L     R3,0(R1)       GET ADDR. OF LABEL BUFFER                 69800017
         MVC   0(80,R2),0(R3) MOVE LABEL INTO LABEL SAVE AREA           70000017
         TM    EXIT3SW,X'01'  IS THERE AN USER INTLR                    70200017
         BZ    RET            NO,THERE IS NO USER INTLR                 70400017
         TM    NOEXTSW,X'01'  YES,IS NO MORE EXIT TO INTLR SW ON        70600017
         BO    RET            YES,NO MORE EXIT TO INTLR                 70800017
GOTLR    L     R15,UTRADR    NO,GO TO USER INTLR                        71000017
         BALR  R14,R15        GO TO USER INTLR                          71200017
CKRCODE  STC   R15,RETCODE                                              71400017
         CLI   RETCODE,X'04'  RET. CODE FROM USER = 4                   71600017
         BNE   CKMORE                                                   71800017
         B     RET                                                      72000017
CKMORE   CLI   RETCODE,X'00'   RET CODE FROM USER = 0                   72200017
         BE    NOEXT          YES, BRANCH                               72400017
         MVI   TERMSW,X'01'   NO,SET TERM. AFTER LABEL PROC. SW ON      72600017
NOEXT    MVI   NOEXTSW,X'01' TURN NO EXIT TO USER LAB. PROC. SW ON      72800017
* INIT. MSG: IEB248I (NO.) EXITS TO (USER EXIT RTN) IS MADE FOR       * 72860017
*                    SYSUT(1/2) RETURN CODE FROM USER ROUTINE IS (NO.)* 72920017
         CVD   R15,CONVTDEC             CONVERT THE USER RET CODE       73000017
         UNPK  PTAREA(3),CONVTDEC+6(2)  TO PRINTABLE CHARACTER          73200017
         OI    PTAREA+2,X'F0'                                           73400017
         MVC   LAB3+8(2),PTAREA+1                                       73600017
         CVD   R5,CONVTDEC              CONVERT THE NO. OF EXITS        74200017
         UNPK  PTAREA(3),CONVTDEC+6(2)                                  74400017
         OI    PTAREA+2,X'F0'           PRINTABLE CHARACTER             74600017
         MVC   LABMSG+8(1),PTAREA+2                                     74700017
         MVC   MYPRTARE(120),BLANKS                                     74800017
         MVC   MYPRTARE(84),LABMSG                                      74900017
         LA    R0,119                                                   75200017
         LA    R1,MYPRTARE                                              75400017
         L     R15,MYPRTCON                                             75600017
         BALR  R14,R15        GO TO PRINT THE MSG                       75800017
         TM    LABEL,X'30'    IS DATA=ALL OR DATA=ONLY                  76000017
         BM    RET            YES, GO GET MORE LABEL                    76200017
RET1     LA    R15,0                                                    76400017
         LM    R1,R14,USAVE                                             76600017
           BR  R14            RET. TO OPEN/CLOSE/EOV                    76800017
RET      LA    R15,4          GO GET MORE LABELS                        77000017
         LM    R1,R14,USAVE                                             77200017
         BR    R14            RET. TO OPEN/CLOSE/EOV                    77400017
INTEXT1  CLI   3(R3),X'F1'    IS THIS THE 1ST LABEL OF THE GROUP        77600017
         BNE   TEXT1          NO, BRANCH                                77800017
         MVC   INTLCT1(4),ZERO     SET SYSUT1 TRAILER LAB COUNT 0       78000017
TEXT1    L     R5,INTLCT1                                               78200017
         LA    R5,1(R5)                                                 78400017
         ST    R5,INTLCT1                                               78600017
         B     INTEXT         BRANCH                                    78800017
INTEXT2  CLI   3(R3),X'F1'    IS THIS THE 1ST LABEL OF THE GROUP        79000017
         BNE   TEXT2          BRANCH                                    79200017
         MVC   INTLCT2(4),ZERO  SET SYSUT2 TRAILER LABEL COUNT 0        79400017
TEXT2    L     R5,INTLCT2                                               79600017
         LA    R5,1(R5)                                                 79800017
         ST    R5,INTLCT2                                               80000017
INTEXT   TM    EXIT3SW,X'01'  IS THERE AN USER INTLR                    80200017
         BZ    RET1           NO, IGNORE THE REST OF THE LABELS         80400017
         B     GOTLR          BRANCH                                    80600017
*                                                                       80800017
IOERR1   ST    R1,SAVE1                                                 81000017
         LA    R1,43          I/O ERROR WHILE READ INPUT HEADER ON DS1  81200017
         L     R15,PRINCONS                                             81400017
         BALR  R14,R15        PRINT MSG                                 81600017
         L     R1,SAVE1                                                 81800017
         B     NTEST1         BRANCH                                    82000017
*                                                                       82200017
IOERR2   ST    R1,SAVE1                                                 82400017
         LA    R1,44          I/O ERROR WHILE READ INPUT HEADER ON DS2  82600017
         L     R15,PRINCONS                                             82800017
         BALR  R14,R15        PRINT MSG                                 83000017
         L     R1,SAVE1                                                 83200017
         B     NTEST2         BRANCH                                    83400017
TIOERR1  ST    R1,SAVE1                                                 83600017
         LA    R1,45          I/O ERROR WHILE READ INPUT TRAILER ON DS1 83800017
         L     R15,PRINCONS                                             84000017
         BALR  R14,R15        PRINT MSG                                 84200017
         L     R1,SAVE1                                                 84400017
         B     TNTEST1        BRANCH                                    84600017
*                                                                       84800017
TIOERR2  ST    R1,SAVE1                                                 85000017
         LA    R1,46          I/O ERROR WHILE READ INPUT TRAILER ON DS2 85200017
         L     R15,PRINCONS                                             85400017
         BALR  R14,R15        PRINT MSG                                 85600017
         L     R1,SAVE1                                                 85800017
         B     TNTEST2        BRANCH                                    86000017
*                                                                       86200017
PRINCONS DC    V(JPRINTRT)                                              86400017
MYPRTCON DC    V(ZZPR)                                                  86600017
SAVE1    DS    1F                                                       86800017
SAVE14   DC    F'0'           REG.14 SAVE AREA                          87000017
EIGHTY   DC    F'80'                                                    87200017
ZERO     DC    F'0'                                                     87400017
BLANKS   DC    CL4'    '                                                87600017
MYPRTARE DS    30F                                                      87800017
USAVE    DS    14F                                                      88000017
MYSAVE   DS    18F                                                      88200017
PTAREA   DS    1F                                                       88400017
CONVTDEC DS    1D                                                       88600017
C16      DC    X'F1F6'                                                  88800017
FSW1     DC    X'01'          FIRST TIME SW FOR SYSUT1 INHDR EXIT       89600017
FSW2     DC    X'01'          FIRST TIME SW FOR SYSUT2 INHDR EXIT       89800017
RETCODE  DC    X'00'          RET. CODE FROM USER LABEL RTN             90000017
TFSW1    DC    X'01'          FIRST TIME SW FOR SYSUT1 INTLR EXIT       90200017
TFSW2    DC    X'01'          FIRST TIME SW FOR SYSUT2 INTLR EXIT       90400017
*********************************************************************** 90420017
*        THE FOLLOWING MESSAGE IS BUILT AT EXECUTION TIME             * 90440017
LABMSG   DC    CL10'IEB248I   '                                         90460017
LABMSG2  DC    CL32'EXITS TO          IS MADE FOR SY'                   90480017
LAB2     DC    CL32'SUT    RETURN CODE FROM USER ROU'                   90500017
LAB3     DC    CL10'TINE IS   '                                         90520017
         END                                                            90600017
