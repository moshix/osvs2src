 TITLE 'IEBISL-IEBISAM UTILITY PROGRAM - LOAD ROUTINE '                 00200016
*STATUS: CHANGE LEVEL 000                                             * 00400016
*FUNCTION/OPERATION: THIS ROUTINE RECONSTRUCTS AN INDEXED SEQUENTIAL  * 00600016
*   DATASET FROM THE INFORMATION CONTAINED IN AN UNLOADED VERSION OF  * 00800016
*   THE DATASET. FOR EACH OF THE FOLLOWING FIELDS,THE CORRESPONDING   * 01000016
*   VALUE OBTAINED FRON THE DATASET WHEN IT WAS UNLOADED WILL BE EM-  * 01200016
*   PLOYED IN RECONSTRUCTING THE NEW DATASET: OPTCD,RECFM,LRECL,BLK-  * 01400016
*   SIZE,RKP,NTM,KEYLEN,AND CYLOFL.                                   * 01600016
*                                                                     * 01800016
*INPUT: LENGTH AND ADDRESS OF THE RECORD TO BE CONSTRUCTED. DATA      * 02000016
*   PASSED BY LOCATION AND A RETURN CODE.                             * 02200016
*                                                                     * 02400016
*   REGISTER 13 MUST CONTAIN THE ADDRESS OF THHE CALLING PROGRAM'S    * 02600016
*   SAVE AREA,REGISTER 15 MUST CONTAIN THE ADDRESS OF THIS MODULE     * 02800016
*   FOR EVERY ENTRY TI THIS MODULE(EXCEPT FROM IEBISSI).                03000016
*                                                                     * 03200016
*OUTPUT: INDEXED SEQUENTIAL DATASET,COMPLETION CODE OR MESSAGE NUMBER * 03400016
*                                                                     * 03600016
*EXTERNAL ROUTINES:                                                   * 03800016
*   IEBISSI-TO GET THE LENGTH AND ADDRESS OF THE RECORD.              * 04000016
*                                                                     * 04200016
*EXITS-NORMAL:                                                        * 04400016
*   IEBISF- AFTER THE DATASET HAS BEEN RECONSTRUCTED SUCCESSFULLY.    * 04600016
*                                                                     * 04800016
*      -ERROR:                                                        * 05000016
*   IEBISF- WHEN EVER ANY ERROR CONDITIONS ARE ENCOUNTERED FOR INPUT. * 05200016
*   AND/OR OUTPUT WITH A MESSAGE NUMBER AND/OR A COMPLETION CODE      * 05400016
*                                                                     * 05600016
*ATTRIBUTES: REENTRANT                                             000G 05800016
*                                                                     * 06000016
*TABLES/WORKAREAS:                                                    * 06200016
*   QISAM- WORK AREA FOR DCB INFORMATION MAINTAINED WHEN THE DATASET  * 06400016
*   WAS UNLOADED.                                                     * 06600016
*                                                                     * 06800016
*********************************************************************** 06850040
*                                                                       07000016
*  REGISTER ASSIGNMENTS                                                 07200016
*                                                                       07400016
*********************************************************************** 07450040
BASE     EQU   10                  BASE REGISTER                        07600016
MADDR    EQU   15                  ADDRESSIBILITY REGISTER              07800016
SAVERG   EQU   13                  SAVE AREA REGISTER                   08000016
PARAM1   EQU   1                   PARAMETER REGISTER                   08200016
PARAM0   EQU   0                   PARAMETER REGISTER-LIMIT             08400016
RETREG   EQU   14                  RETURN REGISTER                      08600016
LINKRG   EQU   15                  LINK REGISTER                        08800016
WORKAREA EQU   11                       WORKAREA REGISTER          000G 09000016
WK3      EQU   3                   MESSAGE NUMBER REGISTER              09200016
WK4      EQU   4                   COMMON AREA ADDRESS REGISTER         09400016
WK2      EQU   2                   DCB ADDRESSING REGISTER              09600016
WK6      EQU   6                   UNLOADED DCB ADDRESSING REGISTER     09800016
WK8      EQU   8                   EVEN REGISTER FOR DIVISION           10000016
WK5      EQU   5                   WORK REGISTER                        10200016
WK9      EQU   9                  ODD REGISTER FRO DIVISION             10400016
RCODE    EQU   15                  RETURN CODE REGISTER                 10600016
*                                                                       10800016
*********************************************************************** 10850040
         EJECT                                                          10900040
IEBISL   CSECT                                                          11000016
*********************************************************************** 11050040
*                  FIXES THIS MODULE                                  * 11100040
*                     LATEST FIRST                                    * 11150040
*********************************************************************** 11200040
*                                                                       11250040
*A 404500-405000                             @XA16077=@YA15383=@ZA18349 11260000
*C 135000                                    @XA16077=@YA15383=@ZA18349 11270000
*D 371200-375200                             @YA13318=@XA14197=@ZA10932 11300040
*C 136000,369600                             @YA13318=@XA14197=@ZA10932 11350040
*A 134500-135000,370900-374400               @YA13318=@XA14197=@ZA10932 11400040
*                                                                       11450040
*A334400-335000                                                 OY01167 12993016
*D334400-335300                                                 OY01167 12994016
*                                                                       12994440
*A221000,222200,520000-520400,521200,535000,536000,538300,      OY01166 12995016
*A538600,541000                                                 OY01166 12996016
*D521200-526000,520000                                          OY01166 12997016
*                                                                       12997440
*0516                                                              000H 12999040
* 342000                                                         A32042 12999940
*A222100-224000                                                  A41745 13049940
*A159000,344000,520000-521000                                    A45167 13059940
*D344000,520000                                                  A45167 13069940
*A212500-212560,326500,327000,340500-341500,500500,500600,502900 A48756 13079940
*A510000-514708                                                  A48756 13089940
*C318000,368000,378000,424000,476000,500800                      A48756 13091940
*D328000,334000,380000-384000,428000,462000,480000-484000        A48756 13093940
*A212600,327500-334400,335300-335440,266000                      A52028 13095940
*D212500-218000,222100-222200,266000,334400,335600,653500        A52028 13097940
*********************************************************************** 13098340
         EJECT                                                          13098740
         SAVE  (14,12),T,ISL-V1-66           SAVE REGISTERS             13099916
         USING IEBISL,BASE                   ESTABLEIH BASE REGISTER    13200016
         LR    BASE,MADDR                     REGISTER                  13400016
         B     APARNO                   BRANCH AROUND APAR NO  @ZA10932 13450040
         DC    C'IEBISL OZ18349'        LAST APAR THIS MODULE  @ZA18349 13500000
APARNO   LR    WORKAREA,PARAM1          PICK UP WORK AREA PTR  @ZA10932 13600040
         USING IEBWORK,WORKAREA                                         13800016
         ST    SAVERG,SAVE1BK           SET BACK CHAIN POINTER     000G 14000016
         LR    SAVERG,WORKAREA          UPDATE TO NEW SAVE AREA    000G 14200016
         LA    WK2,SAVE2                SET UP FORWRD CHAIN POINTER000G 14400016
         ST    WK2,SAVE1FD                 TO NEXT SAVE AREA       000G 14600016
*********************************************************************** 14650040
*                                                                       14800016
*  THIS SECTION PASSES THE ADDRESS OF THE IEHISR COMMON WORK AREA TO  * 15000016
*THE INPUT ROUTINE IEHISSI.                                           * 15200016
*                                                                     * 15400016
*********************************************************************** 15450040
         SR    PARAM0,PARAM0                 INDICATE INITIAL ENTRY     15600016
         LR    PARAM1,WORKAREA          LOAD COMMON AREA ADDRESS   000G 15800016
         MVI   ISSWITCH,X'00'      ZERO SWITCH BEFORE TESTING    A45167 15900021
TOWARD   L     LINKRG,VSSI                   LOAD IEHISSI ADDRESS       16000016
         BALR  RETREG,LINKRG                 BRANCH TO IEHISSI          16200016
*********************************************************************** 16250040
*                                                                     * 16400016
*  THIS SECTION DETERMINES THE RETURN CODE PASSED BY IEHISSI, FOR ANY * 16600016
*RETURN CODE DIFFERENT FROM ZERO, A MESSAGE NUMBER IS SET IN THE IS-  * 16800016
*MESS FIELD OF THE IEBISR COMMON WORK AREA OR A COMPLETION CODE IS    * 17000016
*PLACED IN THE ISCOMP FIELD,THE OUTPUT DATASET IS CLOSED,SAVE AREA IS * 17200016
*POPPED UP AND AN EXIT IS MADE TO ISF.                                * 17400016
*                                                                     * 17600016
*********************************************************************** 17650040
         STC   RCODE,DOUBLE+7           STORE RETURN CODE          000G 17800016
         CLI   DOUBLE+7,X'00'           IS RETURN CODE 0           000G 18000016
         BE    COMPET                        YES,BRANCH                 18200016
         CLI   DOUBLE+7,X'04'           IS RETURN CODE 4           000G 18400016
         BE    SETA                          YES BRANCH                 18600016
         CLI   DOUBLE+7,X'08'           IS RETURN CODE 8           000G 18800016
         BE    SETAS                         YES,BRANCH                 19000016
         CLI   DOUBLE+7,X'12'           IS RETURN CODE 12          000G 19200016
         BE    SETAL                         YES,BRANCH                 19400016
         CLI   DOUBLE+7,X'16'           IS RETURN CODE 16          000G 19600016
         BE    SETAL1                        YES,BRANCH                 19800016
         CLI   DOUBLE+7,X'20'           IS RETURN CODE 20          000G 20000016
         BE    SETSW                         YES,BRANCH                 20200016
         CLI   DOUBLE+7,X'24'           RETURN CODE 24             000G 20400016
         BE    SETAL2                   INVALID DCB PARAMETERS     000G 20600016
         LA    WK3,0                   MESSAGE NO = 0  NO ERRORS   000G 20800016
FINI     STH   WK3,REENTAR+32           STORE MESSAGE NUMBER       000G 21000016
         MVC   ISMESS(2),REENTAR+32     MOVE NUMBER TO ISMESS      000G 21200016
         B     SHORTBR                  TEST FOR ERRORS          A52028 21260021
MITE     MVC   REENTAR(OPENL-CLOSEL),CLOSEL   MOVE L FORM OF CLOSE 000G 22000016
         OI    ISMESS,X'10'       SET FLAG FOR SYSNAD ROUTINE   OY01166 22100016
         CLOSE (DCBAR2),MF=(E,REENTAR) CLOSE OUTPUT DATA SET       000G 22200016
         NI    ISMESS,X'00'       SET OFF FLAG FOR SYSNAD ROUT. OY01166 22220016
         FREEPOOL DCBAR2                FREEPOOL OUTPUT BUFFER   A41745 22250021
BYPASS   SR    WK3,WK3                  SET UP XCTL AREA         A41745 22400021
         ST    WK3,XCTLAREA+4                                      000G 22600016
         MVI   WHICH+6,C' '             MOVE INTO XCTLAREA NAME OF 000G 22800016
         MVI   WHICH+5,C'F'                NEXT MODULE - IEBISF    000G 23000016
         L     SAVERG,4(0,SAVERG)            POP UP SAVE AREA           23200016
         L     RETREG,12(0,SAVERG)           RESTORE REGISTER FOURTEEN  23400016
         LR    PARAM1,WORKAREA          LOAD ADDRESS OF WORKAREA   000G 23600016
         XCTL  (2,12),MF=(E,(1)),SF=(E,XCTLAREA)  BRANCH TO IEHISF 000G 23800016
SETA     LA    WK3,2                   MESSAGE NUMBER = 2, SYN-    000G 24000016
         B     FINI                           CHRONOUS ERROR-INPUT      24200016
SETAS    LA    WK3,0                         MESSAGE NUMBER=20, OPERA-  24400016
         B     FINI                           TION COMPLETED            24600016
SETAL    LA    WK3,4                   MESAGE NUMBER = 4, LIMIT    000G 24800016
         B     FINI                           EXCEEDED                  25000016
SETAL1   LA    WK3,9                    MESSAGE NUMBER=9,INPUT     000H 25200017
         B     FINI                           OUT OF SEQUENCE           25400016
SETAL2   LA    WK3,1                    INVALID DCB PARAMETERS     000G 25600016
         B     FINI                  C                             000G 25800016
SETSW    MVC   ISCOMP(2),H16            COMPLETION CODE = 16       000G 26000016
         LA    WK3,7                    SET MESSAGE FOR MISSING DD 000G 26200016
         STH   WK3,ISMESS                                          000G 26400016
         B     BYPASS                   SYSUT1 NOT OPEN,SO BYP.  A52028 26600021
*********************************************************************** 26650040
*                                                                     * 26800016
*  THIS SECTION, UPON FIRST RETURN FROM IEHISSI,PASS THE LENGTH OF THE* 27000016
*ISAM DCB AS THE LIMIT AND THE ADDRESS OF 'QISAM',FOR THE RETRIEVAL OF* 27200016
*THE ISAM DCB WRITTEN OUT WHEN DATA SET WAS UNLOADED.                 * 27400016
*                                                                     * 27600016
*********************************************************************** 27650040
COMPET   LTR   PARAM0,PARAM0                 FIRST RETURN               27800016
         BNE   TSTALL                        NO,BRANCH                  28000016
         LA    PARAM0,154                                               28200016
         LA    PARAM1,QISAM                  LOAD QISAM ADDRESS         28400016
         MVI   DOUBLE+6,X'FF'           INDICATE OUTPUT NOT OPEN   000G 28600016
         B     TOWARD                        GO GET WRITTEN DCB         28800016
TSTALL   CLI   DOUBLE+6,X'FF'           OUTPUT OPEN                000G 29000016
         BNE   RESUME1                                                  29200016
*********************************************************************** 29250040
*                                                                     * 29400016
*  THIS SECTION NOVES THE DDNAME FROM IEHISR ISUT2 FIELD INTO THE OUT-* 29600016
*PUT DCB,AND OPENS THE DATASET. A CHECK FDR A SUCCESSFUL OPEN IS MADE.* 29800016
*                                                                     * 30000016
*********************************************************************** 30050040
         MVC   DCBAR2(LABEL-DCBISL),DCBISL                         000G 30200016
         USING IHADCB,WK2                                               30400016
         LA    WK2,DCBAR2                                          000G 30600016
         MVC   DCBDDNAM(8),ISUT2        MOVE DDNAME FROM ISUT2     000G 30800016
         MVC   REENTAR(EIGHT-OPENL),OPENL   MOVE L FORM OF OPEN TO 000G 31000016
*                                               WORKAREA           000G 31200016
         OPEN  (DCBAR2,(OUTPUT)),MF=(E,REENTAR) OPEN OUTPUT DATASET000G 31400016
         TM    DCBOFLGS,X'10'                WAS OPEN SUCCESSFUL        31600016
         BZ    SETDOUBL           NO,BRANCH                      A48756 31800000
*********************************************************************** 31850040
*  THIS SECTION CHECKS TO SEE IF THE INCONSISTENCY INDICATOR FOR THE  * 32000016
*SPECIFIED FIELDS IN THE OUTPUT DCB IS ON. IF SO A MESSAGE NUMBER IS  * 32200016
*SET AND A BRANCH TO CLOSE AND EXIT IS TAKEN.                         * 32400016
*                                                                     * 32600016
*********************************************************************** 32610040
         B     RESUME             CONT, SYSUT2 DCB ERROR WILL    A48756 32650021
*                                 BE CHECKED IN IEBISSI          A48756 32700021
SHORTBR  LTR   WK3,WK3            MESSAGE NUMBER 0 ?             A52028 32750021
         BNZ   CHSYSUT2           NO, BRANCH                     A52028 32800021
         TM    DCBOFLGS,X'10'     SYSUT2 DCB OPEN ?             OY01167 33440016
         BO    MITE               YES, CLOSE SYSUT2 DATA SET    OY01167 33470016
         MVI   ISMESS+1,X'09'     SET MSG NBR --OUT OF SEQ--    OY01167 33500016
CHSYSUT2 MVC   ISCOMP(2),EIGHT          COMPLETION CODE = 8      A52028 33532021
         TM    DCBOFLGS,X'10'      SYSUT2 OPENED ?               A52028 33540021
         BZ    BYPASS              NO, DO NOT CLOSE SYSUT2       A52028 33542021
         B     MITE                YES, CLOSE SYSUT2             A52028 33544021
*********************************************************************** 33594040
*  THIS SECTION GETS THE ADDRESS OF THE NEXT AVAILABEL OUTPUT AREA AND* 33600016
*LOADS THE LIMIT FOR IEBISSI.                                         * 33800016
*                                                                     * 34000016
*********************************************************************** 34010040
SETDOUBL MVI   DOUBLE+7,X'20'     SET ERROR BIT, NO OPEN UT2     A48756 34050021
         LA    PARAM0,100         SET REG 0 UNEQ. ZERO           A48756 34100000
         B     TOWARD             BRANCH TO IEBISSI              A48756 34150000
RESUME   MVI   ISSWITCH,X'FF'      MARK SYSUT2 OPEN              A45167 34400021
         LH    PARAM0,DOUBLE+4          LOAD LIMIT ADDRESS IN 1    000G 34600016
         AH    PARAM0,REENTAR+34        ADD KEYLENGTH TO RECORD    000G 34800016
         B     TOWARD                                                   35000016
*********************************************************************** 35050040
*  THIS IS THE OUTPUT DCB EXIT. THE NTM,CYLOFL AND OPTCD DCB FIELDS OF* 35200016
*THE UNLOADED DATASET ARE COPIED IN THE RESPECTIVE FIELDS OF THE NEW  * 35400016
*DATASET. IF THE RECORD FORMAT IN THE UNLOADRD(OLD) AND NEW DATASETS  * 35600016
*IS NOT THE SAME AND THE LOGICAL RECORD LENGTH FIELD OF THE NEW DATA  * 35800016
*SET IS GREATER THAN THAT OF THE OLD, A BRANCH IS MADE TO INDICATE IN-* 36000016
*CONSISTENCY. IF NO BLKSIZE IS SPECIFIED FOR THE NEW DATASET THE      * 36200016
*LOGICAL RECORD LENGTH IS USED AS THE LIMIT.  IF RECFM SPECIFIES      * 36300016
*VARIABLE THEN THE RKP MUST BE EQUAL TO OR GREATER THAN 4             * 36400016
*********************************************************************** 36450040
ISLEXT   LA    WK6,QISAM                     ADDRESS OF UNLOADED DCB    36600016
         XC    DOUBLE+6(2),DOUBLE+6     SET CONSIST FLAGS TO 0   A48756 36800021
         OC    DCBNTM(1),DCBNTM         NTM SPECIFIED              000G 36880040
         BNZ   OPTCD                    YES, NTM SPECIFIED     @ZA10932 36960040
         MVC   DCBNTM(1),54(WK6)        GET OLD NTM FIELD          000G 37040016
OPTCD    OC    DCBOPTCD(1),DCBOPTCD     OPTCD SPECIFIED        @ZA10932 37090040
         BNZ   CYLOF                    YES, OPTCD SPECIFIED   @ZA10932 37140040
         MVC   DCBOPTCD(1),52(WK6)      GET OLD OPTCD          @ZA10932 37190040
CYLOF    TM    DCBOPTCD,X'08'           OTPCD=Y SPECIFIED      @ZA10932 37240040
         BZ    TLRECL                   NO,THEN NO CYLOF       @ZA10932 37290040
         OC    DCBCYLOF(1),DCBCYLOF     CYLOF SPECIFIED        @ZA10932 37340040
         BNZ   TLRECL                   YES, CYLOF SPECIFIED   @ZA10932 37390040
         MVC   DCBCYLOF(1),55(WK6)      GET OLD CYLOF          @ZA10932 37440040
TLRECL   CLC   DCBLRECL(2),ZERO        LRECL FIELD = ZERO?         000G 37600016
         BE    MVERECL                  YES,USE UNLOADED VALUE   A48756 37800000
*********************************************************************** 37850040
*                                                                     * 38600016
*  THIS SECTION DETERMINES THE RECORD FORMAT. IF FIXED, AND THE BLOCK * 38800016
*SIZE IS NOT A MULTIPLE OF THE LOGICAL RECORD LWNGTH,A BRANCH IS MADE * 39000016
*TO INDICATE INCONSISTEMCY. OTHERWISE,THE LOGICAL RECORD LENGTH IS    * 39200016
*USED AS THE LINIT.                                                   * 39400016
*********************************************************************** 39450040
TBLKSI   LH    WK9,DCBBLKSI                  LOAD BLOCKSIZE             39600016
         LTR   WK9,WK9                                                  39800016
         BZ    MVEBLK                        YES,USE UNLOADED VALUE     40000016
TRECFM   CLI   DCBRECFM,X'00'                RECFM FIELD=0?             40200016
         BE    MVEFM                         YES,BRANCH                 40400016
         TM    DCBRECFM,X'08'           NEW RECFM VBS OR FBS   @ZA18349 40450000
         BO    NONCON                   YES, INVALID FOR ISAM  @ZA18349 40500000
CHECKF   TM    DCBRECFM,X'80'           NEW RECFM FIXED            000G 40600016
         BZ    TVARY                         NO,BRANCH                  40800016
         TM    36(WK6),X'80'                 OLD RECFM FIXED?           41000016
         BO    TMULTPLE                      YES,BRANCH                 41200016
         MVI   DOUBLE+6,X'01'           SET INVALID INDICATOR      000G 41400016
TMULTPLE LH    WK5,DCBLRECL                  LOAD LOGICAL RECORD LENT   41600016
         SR    WK8,WK8                                                  41800016
         DR    WK8,WK5                       IS BLOCK SIZE A MULTIPLE   42000016
         LTR   WK8,WK8                        OF LOGICAL RECORD LENGTH  42200016
         BE    TKEYLFLD                                                 42400021
         MVI   DOUBLE+6,X'01'           SET INVALID INDICATOR      000G 42600016
         B     TKEYLFLD                                                 43000016
MVEFM    MVC   DCBRECFM(1),36(WK6)           GET OLD REFM FIELD         43200016
         B     CHECKF                                              000G 43400016
MVERECL  MVC   DCBLRECL(2),82(WK6)           GET OLD LRECL VALUE        43600016
         B     TBLKSI                                                   43800016
MVEBLK   MVC   DCBBLKSI(2),62(WK6)           GET OLD BLKSIZE            44000016
         LH    WK9,DCBBLKSI                  LOAD BLOCKSIZE             44200016
         B     TRECFM                                                   44400016
*********************************************************************** 44450040
*                                                                     * 44600016
*  THIS SECTION USES THE BLKSIZE AS THE LIMIT FOR RECORDS OF VARIABLE * 44800016
*FORMAT,IF THE BLOCK SIZE IS GREATER THAN OR EQUAL TO THE LOGICAL RE- * 45000016
*CORD LENGTH.                                                         * 45200016
*********************************************************************** 45250040
TVARY    TM    36(WK6),X'40'                 OLD RECFM VARIABLE?        45400016
         BZ    NONCON                        NO,BRANCH                  45600016
VARBLE   CH    WK9,DCBLRECL                  BLKSI GREATER THAB OR      45800016
         BL    NONCON                         EQUAL TO LRECL? NO,BRANCH 46000016
*********************************************************************** 46050040
*                                                                     * 46400016
*   THIS SECTION DETERMINES IF THE RKP IS GREATER THAN THE LGICAL RE- * 46600016
*CORD LENGTH MINUS THE KEYLEN. IF SO, THE INCONSISTENCY INDICATOR IS  * 46800016
*SET.AND A RETURN TO THE OPEN ROUTINE IS MADE.                        * 47000016
*                                                                     * 47200016
*********************************************************************** 47250040
TKEYLFLD CLI   DCBKEYLE,X'00'                KEYLEN FIELD ZERO?         47400016
         BNE   TMINUS                   NO, DON'T USE OLD KEYLE  A48756 47600000
         MVC   DCBKEYLE(1),16(WK6)           USE OLD KEYLEN             47800016
TMINUS   LH    WK8,DCBLRECL                  LOAD LOGICAL RECRD LENGTH  48600016
         SR    WK5,WK5                                                  48800016
         IC    WK5,DCBKEYLE                  GET KEY LENGTH             49000016
         STC   WK5,REENTAR+35                                           49200016
         SR    WK8,WK5                       SUBTRACT KEYLE FROM LRECK  49400016
         LH    WK5,DCBRKP                    LOAD ELATIVE KEY POSITION  49600016
         LTR   WK5,WK5                       RKP=0?                     49800016
         BZ    TSTOLD                                                   50000016
TRKP     CLI   DCBRECFM,X'80'     SYSUT2 HAS RECFM F ?           A48756 50050021
         BNE   RKPTEST            NO, CHECK FOR VALID RKP        A48756 50060021
         LTR   WK5,WK5                  IS RKP=0                 A28558 50080021
         BZ    RTOPEN                   YES, DO NOT SET          A28558 50160019
*                                       NONCONSISTENCY INDICATOR A28558 50240019
RKPTEST  EQU   *                                                 A48756 50290021
         CH    WK8,DCBRKP               RKP FIELD GT             A28558 50320019
*                                             LRECL MINUS THE KEYLEN    50400016
         BNL   RTOPEN                        NO, BRANCH                 50600016
NONCON   MVI   DOUBLE+6,X'01'           SET NONCONSISTENCY INDICATR000G 50800016
RTOPEN   CLI   DOUBLE+6,X'00'     DCB ERROR ENCOUNTERED ?        A48756 51000021
         BNE   ERRDCB             YES, RETURN TO OPEN            A48756 51050021
         TM    DCBRECFM,X'C0'     RECFM SYSUT2 U ?               A48756 51160021
         BO    ERRDCB1            YES, PRINT ERRMSG IEB601I      A48756 51170000
         TM    36(WK6),X'40'      RECFM SYSUT1 V ?               A48756 51196021
         BZ    FIXRECFM           NO, RECFM IS F/FB              A48756 51198021
         CLC   DCBLRECL(2),82(WK6) LRECL UT2 < LRECL UT1 ?       A48756 51198421
         BL    ERRDCB1            YES, ERROR                     A48756 51198800
         LH    WK5,DCBLRECL       LOAD UT2 LRECL                 A48756 51199221
         LA    WK5,4(WK5)         ADD 4 TO UT2 LRECL             A48756 51199321
         CH    WK5,DCBBLKSI       LRECL+ 4 > BLKSI ?             A48756 51199421
         BC    12,RETURN2         NO, OKE                        A48756 51199521
         STH   WK5,DCBBLKSI       SET DEFAULT BLKSI              A48756 51199621
         B     ERRDCB1            SET ERRBIT                     A48756 51199700
TSTL1L2  CLC   DCBLRECL(2),82(WK6) UT1 LRECL EQ. UT2 LRECL ?     A48756 51199821
         BNE   ERRDCB1            NO, PRINT ERRMSG IEB601I       A48756 51199900
         B     RETURN2            RETURN TO OPEN SYSUT2          A48756 51200021
FIXRECFM SR    WK5,WK5            ZERO REGISTER 5                A48756 51212322
         IC    WK5,16(WK6)        LOAD KELENGTH IN REGISTER 4    A48756 51222322
         LH    WK8,82(WK6)        LOAD UT2 LRECL IN REGISTER 6   A48756 51224322
         CLC   60(2,WK6),ZERO     UT1 RKP=0 ?                    A48756 51249921
         BNE   TSTRKP2            NO, TEST DCB VALUES            A48756 51259921
         CLC   DCBRKP(2),ZERO     UT2 RKP=0 ?                    A48756 51269921
         BNE   TSTRECF1           NO, TEST UT1 DCB VALUES        A48756 51279921
         CLC   DCBRECFM(1),36(WK6) RECFM UT1 EQ. RECFM UT2       A48756 51280322
         BE    TSTL1L2            YES, TEST FOR EQUAL LRECL      A48756 51280722
         TM    36(WK6),X'90'      UT1 RECFM = F ?                A48756 51293921
         BM    COMPL1L2           YES, COMPARE UT1 & UT2 LRECL'S A48756 51295921
NOEQL1L2 SR    WK8,WK5            SET WK6 : LRECL1 MINUS KEYLEN1 A48756 51297922
         CH    WK8,DCBLRECL       LRECL2 EQ.TO LRECL1 MINUS KEY? A48756 51299121
         BC    8,RETURN2          YES, OKE RETURN TO OPEN UT2    A48756 51299521
         STH   WK8,DCBLRECL       SET LRECL2 = LRECL1 MINUS KEY1 A48756 51299621
         STH   WK8,DCBBLKSI       SET BLKS2 = LRECL1 MINUS KEY1  A48756 51299721
         B     RETURN2            RETURN TO OPEN SYSUT2          A48756 51310821
COMPL1L2 AR    WK8,WK5            SET WK6: LRECL1 PLUS KEYLEN1   A48756 51333200
         CH    WK8,DCBLRECL     UT2(RCFM=FB) EQ. LRC1 PLUS KEY ? A48756 51343221
         BC    8,RETURN2          YES, RETURN TO OPEN SYSUT2     A48756 51353221
         STH   WK8,DCBLRECL       LRECL2=LRECL1 PLUS KEYLEN1     A48756 51363221
         MR    WK8,WK8            MULTIPLY R9 AND R8             A48756 51365221
         STH   WK9,DCBBLKSI       BLKSI2= R9 * (LRECL1 PLUS KEY) A48756 51365621
         B     RETURN2            RETURN TO OPEN SYSUT2          A48756 51365721
TSTRKP2  CLC   DCBRKP(2),ZERO     UT2 RKP=0 ?                    A48756 51366421
         BNE   TSTL1L2            NO, TEST FOR EQUAL LRECL'S     A48756 51366521
         TM    DCBRECFM,X'90'     UT2 RECFM=FB ?                 A48756 51377621
         BO    TSTL1L2            YES, TEST FOR EQUAL LRECL'S    A48756 51387621
         B     NOEQL1L2           NO, CHECK FOR UNEQUAL LRECL'S  A48756 51388021
TSTRECF1 TM    36(WK6),X'90'      UT1 RECFM = FB ?               A48756 51388421
         BO    TSTL1L2            YES, TST FOR EQ. LRECL'S       A48756 51388821
         B     COMPL1L2           NO, CHECK FOR UNEQUAL LRECL'S  A48756 51390822
ERRDCB   MVI   DCBKEYLE,X'10'        SET DEFAULT                 A48756 51404600
         MVI   DCBRECFM,X'80'        VALUES                      A48756 51414600
         MVC   DCBRKP(2),ZERO        TO ASSURE CORRECT           A48756 51424600
         NI    DCBBLKSI,X'00'        OPENING                     A48756 51426600
         MVI   DCBBLKSI+1,X'50'      OF SYSUT2                   A48756 51434600
         MVC   DCBLRECL(2),DCBBLKSI  DATA SET.                   A48756 51436600
ERRDCB1  MVI   DOUBLE+6,X'01'        PRINT ERRMSG IEB601I        A48756 51444600
RETURN2  MVC   DOUBLE+4(2),DCBLRECL  USE LRECL AS LIMIT          A48756 51470821
         RETURN                                                         51497421
         DS    0F                                                       51531621
TESTS    DC    X'85'                                                    51565821
         DC    AL3(ISLEXT)                                              51600016
*********************************************************************** 51650040
*  THIS IS THE OUTPUT DATASET SYNAD ROUTINE                           * 51800016
*********************************************************************** 51850040
MESS     TM    ISMESS+1,X'FF'      ALREADY MESS NBR SET ?       OY01166 52000016
         BCR   4,RETREG           YES, RETURN TO CLOSE          OY01166 52010016
         TM    ISMESS,X'10'       ARE WE IN CLOSE ?             OY01166 52020016
         BC    1,DONTRES          YES, DONT RESTORE REGISTERS   OY01166 52030016
         L     SAVERG,4(SAVERG)   ADDRESS SAVE AREA ISL REGIST. OY01166 52040016
         LM    14,12,12(SAVERG)    GET ISL REGISTERS             A45167 52060021
DONTRES  EQU   *                  DONT RESTORE ISL REGISTERS    OY01166 52120016
ENUFROOM SYNADAF ACSMETH=QISAM,PARM1=DCBAR2,PARM2=(0)              000G 53000016
         MVC   MESSAGE+8(79),49(PARAM1)   MOVE IN MESSAGE          000G 53400016
         MVI   ISMESS+1,X'02'     SET SYNAD MSG INDICATOR       OY01166 53500016
         MVI   ISCOMP+1,X'08'     SET CONDITION CODE            OY01166 53600016
         SYNADRLS                                                  000G 53800016
         TM    ISMESS,X'10'       ARE WE IN SYSUT2 CLOSE ?      OY01166 53830016
         BCR   1,RETREG           YES, RETURN TO CLOSE          OY01166 53860016
         MVC   REENTAR(OPENL-CLOSEL),CLOSEL MOVE L FORM FOR CLSE A24745 53900018
         CLOSE (DCBAR1),MF=(E,REENTAR) CLOSE INPUT DATA SET      A24745 54000018
         LA    WK3,2              LOAD SYNAD MSG NBR IN REG 3   OY01166 54100016
         B     SETA                                                000G 54200016
TSTOLD   CLC   60(2,WK6),ZERO         OLD RKP FIELD = 0 ?          000G 55200016
         BE    CHECKD                   YES, BRANCH                000G 55400016
         MVC   DCBRKP(2),60(WK6)             GET OLD RKP                55600016
CHECKD   TM    DCBRECFM,X'80'           NEW RECFM FIXED            000G 55620016
         BC    1,TRKP                   YES,NEW RECFM FIXED             55640016
         CLI   DCBRKP+1,X'04'           RKP LESS THAN 4            000G 55660016
         BL    NONCON                   YES RKP LESS THAN 4        000G 55680016
         B     TRKP                                                000G 55780016
*********************************************************************** 55830040
*                                                                       56000016
*CONSTANTS                                                              56200016
*                                                                       56400016
*********************************************************************** 56450040
CLOSEL   CLOSE (,DISP),MF=L                                             56600016
OPENL    OPEN  (,OUTPUT),MF=L                                           56800016
EIGHT    DC    H'8'               COMPLETION CODE CONSTANT              57000016
ZERO     DC    H'0'                                                000G 57200016
VSSI     DC    V(IEBISSI)         IEBISSI ADDRESS-INPUT ROUTINE         57400016
H16      DC    H'16'                                                    57600016
*                                                                       57800016
DCBISL   DCB   DSORG=IS,MACRF=PL,DDNAME=SYSUT2,                        *58000016
               SYNAD=MESS,EXLST=TESTS                                   58200016
LABEL    DS    0F                      DUMMY LABEL                 000G 58400016
         DCBD  DSORG=(IS)                                               58600016
IEBWORK  DSECT                                                          58800016
SAVE1    DS    1F                                                       59000016
SAVE1BK  DS    1F                                                       59200016
SAVE1FD  DS    1F                                                       59400016
         DS    15F                                                      59600016
SAVE2    DS    1F                                                       59800016
SAVE2BK  DS    1F                                                       60000016
SAVE2FD  DS    1F                                                       60200016
         DS    15F                                                      60400016
SAVE3    DS    1F                                                       60600016
SAVE3BK  DS    1F                                                       60800016
SAVE3FD  DS    1F                                                       61000016
         DS    15F                                                      61200016
*********************************************************************** 61250040
*IEHISR WORKAREA                                                        61400016
*********************************************************************** 61450040
ISINVOK  DS    F                        ADDRESS OF PARAMETER LIST AT    61600016
*                                          INVOCATION                   61800016
ISPRINT  DS    2F                       DDNAME FOR SYSPRINT             62000016
ISUT1    DS    2F                       DDNAME FOR SYSUT1               62200016
ISUT2    DS    2F                       DDNAME FOR SYSUT2               62400016
ISCOMP   DS    H                        COMPLETION CODE                 62600016
ISMESS   DS    H                        MESSAGE NUMBER                  62800016
ISPAGE   DS    H                        PAGE NUMBER FOR SYSPRINT        63000016
ISCNTRL  DS    CL1                      CONTROL BYTE                    63200016
*********************************************************************** 63250040
* MASKS FOR TESTING CONTROL BYTE - ISCONTRL                             63400016
*********************************************************************** 63450040
ISSWC    EQU   X'80'                    COPY                            63600016
ISSWU    EQU   X'40'                    UNLOAD                          63800016
ISSWL    EQU   X'20'                    LOAD                            64000016
ISSWP    EQU   X'10'                    PRINT                           64200016
ISSWN    EQU   X'08'                    NO CONVERT REQUESTED            64400016
ISSWPI   EQU   X'04'                    PRINT INDEX                     64600016
ISSWPP   EQU   X'02'                    PRINT PRIME                     64800016
ISSWPO   EQU   X'01'                    PRINT OVERFLOW                  65000016
ALLMASK  EQU   X'07'                                                    65200016
RESUME1  EQU   RESUME                                            A32042 65300020
ISSWITCH DS    CL1                      EXTRANEOUS SWITCHES             65400016
FIRSTTM  EQU   X'80'                    FIRST TIME SWITCH               65600016
*                                                                       65800016
XCTLAREA DS    2F                                                       66000016
WHICH    DS    2F                                                       66200016
ALIGN    DS    1F                                                       66400016
DOUBLE   DS    1D                                                       66600016
EXITADDR DS    2F                                                       66800016
REENTAR  DS    9F                                                       67000016
DCBAR1   DS    64F                                                      67200016
DCBAR2   DS    64F                                                      67400016
MESSAGER DS    CL1                                                      67600016
MESSAGE  DS    CL120                                                    67800016
RECADDR  DS    1F                                                  000G 68000016
         DS    0D                                                       68200016
QISAM    DS    CL250                    UNLOADED DCB WORK AREA          68400016
HEADERC  DS    CL1                                                      68600016
HEADERT  DS    CL144                    HEADER AREA                     68800016
TARGET   DS    2F                       DATE HOLD AREA                  69000016
PAGENO   DS    1F                       HOLD PAGE NUMBER FOR A HEADING  69200016
PAGEPTR  DS    1F                       HOLD POINTER TO WHERE PAGE      69400016
*                                       NUMBER IS TO BE PLACED          69600016
         END                                                            69800016
