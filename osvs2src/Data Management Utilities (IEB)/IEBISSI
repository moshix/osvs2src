 TITLE 'IEBISSI-IEBISAM UTILITY PROGRAM-LOAD SUBROUTINE'                00300016
*STATUS: CHANGE LEVEL                                                 * 00600016
*FUNCTION/OPERATION: GIVEN A LIMIT(MAXIMUM NUMBER OF CHARACTERS OF    * 00900016
*   CHARACTERS THAT SHOULD BE TRANSMITTED,AND THE ADDRESS OF THE AREA * 01200016
*   TO WHICH THESE CHARACTERS ARE TO BE TRANSMITTED, THIS SUBROUTINE  * 01500016
*   PROCESSES 80 BYTE CARD IMAGES OF FORMAT DESCRIBED BELOW. A POINTER* 01800016
*   TO THE CURRENT INPUT AREA ADDRESS AND THE NUMBER OF BYTES REMAIN- * 02100016
*   ING TO BE PROCESSED ON THE CARD IMAGE ARE MAINTAINED. A CHECK FOR * 02400016
*   SEQUENTIAL INPUT IS MADE EACH TIME A CARD IMAGE IS OBTAINED.      * 02700016
*ENTRY POINTS: IEBISSI - TO GET A RECORD LENGRH AND ADDRESS OF THE    * 03000016
*   RECORD.   BAL  14,IEHISSI. DATA IS PASSED BY LOCATION.            * 03300016
*INPUT:ALL ZEROS IN REG 0 ON FIRST ENTRY.OTHERWISE,A LIMIT AND ADDRESS* 03600016
*       CARDS OR TAPE,80 BYTE CARD IMAGES WITH FOLLOWING FORMAT       * 03900016
*   SEQUENCE NUMBER: A TWO BYTE NUMBER WHICH IS ZERO FOR THE FIRST    * 04200016
*   CARD IMAGE AND ONE GREATER FOR EACH SUCCEEDING CARD IMAGE.        * 04500016
*   LENGTH: A TWO  BYTE  FIELD SPECIFYING THE NUMBER OF BYTES OF IN-  * 04800016
*   FORMATION BETWEEN THIS LENGTH FIELD AND THE NEXT. THIS FIELD IN-  * 05100016
*   DICATES HOW THE FOLLOWING INFORMATION IS SEGMENTED ON SEQUENTIAL  * 05400016
*   CARD IMAGES(IF IT IS SEGMENTED).                                  * 05700016
*   INFORMATION: THIS FIELD CONTAINS THE INFORMATION FROM A GIVEN RE  * 06000016
*   CORD,IT MAY BE SEGMENTED ON SEQUENTIAL CARD IMAGES.               * 06300016
*   END OF INPUT : A 0,1 , OR  2 BYTE FIELD OF ZERO CONTAINED ONLY ON * 06600016
*   THE LAST CARD IMAGE TO DESIGNATE  END OF  INPUT                   * 06900016
*                                                                     * 07200016
*   REGISTER 13 MUST CONTAIN  THE ADDRESS OF THE CALLING PROGRAM'S    * 07500016
*   SAVE AREA,REGISTER 14 MUST CONTAIN THE RETURN ADDRESS IN THE      * 07800016
*   CALLING PROGRAM,AND REGISTER 15 MUST CONTAIN THE ADDRESS OF THIS  * 08100016
*   MODULE                                                            * 08400016
*OUTPUT: A VARIABLE LENGTH RECORD, THE LENGTH OF WHICH DOES NOT EXCEED* 08700016
*   THE VALUE GIVEN THIS ROUTINE IN REGISTER ZERO UPON ENTRY.         * 09000016
*EXTERNAL ROUTINES: IEBISL - LOAD RTN. GET LIMIT AND ADDRESS TO WHICH * 09300016
*   INFORMATION IS TO BE TRANSMITTED.                                 * 09600016
*EXITS-NORMAL: TO CALLING PROGRAM WITH A RETURN CODE OF 0 IN REGISTER * 09900016
*   15,THE ACTUAL LENGTH OF THE INFORMATION TRANSMITTED, AND THE      * 10200016
*   ADDRESS OF THIS INFORMATION. CONTENTS OF OTHER REGISTERS UNCHANGED* 10500016
*     -ERROR: TO CALLING PROGRAM WITH ONE OF THE FOLLOWING RETURN CODE* 10800016
*   S AND THE CONTENTS OF THE OTHER REGISTERS UNCHANGED,DATASET CLOSED* 11100016
*   RETURN CODES:                                                     * 11400016
*   FOUR(4) = AN EXIT TO THE INPUT DCB SYNAD ROUTINE WAS TAKEN.       * 11700016
*   EIGHT(8)= END OF INPUT DATA.                                      * 12000016
*   TWELVE(12)= NUMBER OF CHARACTERS TO BE TRANSMITTED EXCEEDS LIMIT. * 12300016
*   SIXTEEN(16)= INPUT OUT OF SEQUENCE.                               * 12600016
*   TWENTY(20)=  OPEN UNSUCCESSFUL FOR INPUT DATASET                  * 12900016
*TABLES/WORKAREAS:                                                    * 13200016
*   SPACE = NUMBER OF BYTES TO BE PROCESSED AREA                      * 13500016
*   CSQ = NUMBER USED TO CHECK SEQUENTIAL INPUT WORK AND SAVE AREA    * 13800016
*   HOLD = WORK AREA FOR ACTUAL LENGTH OF INFORMATION TRANSMITTED     * 14100016
*   SAVARA =  SAVE AREA FOR REGISTERS                                 * 14400016
*ATTRIBUTES\ REENTRANT                                             000G 14700016
*NOTES:  BASE REGISTER        BASREG                                  * 15000016
*        SAVE REGISTER        SAVREG = 13                             * 15300016
*        PARAMETER REGISTERS  PARM0 =0                                * 15600016
*                             PARM1 =1                                * 15900016
*        RETURN CODE REGISTER      RCRG=15                            * 16200016
*        RETURN REGISTER           NSIRG                              * 16500016
*        WORK REGISTERS            WK2            WK6                 * 16800016
*                                  WK3            WK7                 * 17100016
*        ADDRESSING REGISTERS      INPT           TRANS               * 17400016
*  REGISTER ASSIGNMENTS                                                 17700016
PARM00   EQU   0        PARAMETER*LIMIT REGISTER                        18000016
PARM1    EQU   1         REGISTERS*ADDRESS OF TRANSMITTED INFORMATION   18300016
WK2      EQU   2                   WORK REGISTER                        18600016
WK3      EQU   3                   BYTES TO BE PROCESSED WORK REGISTER  18900016
PARM0    EQU   4                                                        19200016
NSIRG    EQU   5                   RETURN REGISTER FOR PROC SECTION     19500016
WK6    EQU   6                                                          19800016
WK7      EQU   7                   WORK REGISTER FOR RECORD LENGTH      20100016
INPT     EQU   8                   INPUT ADDRESS REGISTER               20400016
TRANS    EQU   9                   OUTPUT ADDRESS REGISTER              20700016
BASREG   EQU   10                  BASE REGISTER                        21000016
WORKAREA EQU   11                       WORKAREA REGISTER          000G 21300016
SAVREG   EQU   13                 SAVE RGISTER                          21600016
RCRG     EQU   15                  RETURN CODE REGISTER                 21900016
IEBISSI  CSECT                                                          22200016
*0181480000                                                        7060 23000017
*                                                                A31442 23400020
*A762100-765000                                                  A41745 23450021
*A399300-401700,585100-587100,787000                             A45167 23550021
*D387000,579000                                                  A45167 23650021
*C258000                                                         A45167 23750021
*A390500-392000                                                  A48756 23800021
*C399300                                                         A48756 23850021
*A742500                                                                23870021
*A STATEMENTS FOR FIX 01165 ARE REPLACED BY STMNTS FOR FIX 01167OY01165 23877016
*A586600-586660                                                 OY01167 23884016
*D586600                                                        OY01167 23891016
* WITH RELEASE 16 THE ENTIRE MODULE WAS REPLACED                   000G 23900016
*  WITH RELEASE 16 THIS MODULE WAS MADE REENTRANT BY USING A       000G 24100016
*A DSECT CALLED IEBWORK WHICH WAS DESIGNED AS A COMMON WORKAREA.   000G 24300016
*  THIS SECTION PERFORMS THE INITIALIZATION NECESSARY FOR EACH ENTRY  * 24600016
         SAVE  (14,12),T,SSI-66-V1           SAVE REGISTERS             24900016
         USING IEBISSI,BASREG                ESTABLISH BASE REGISTER    25200016
         LR    BASREG,RCRG                   ESTABLISH BASE REGISTER    25500016
         LTR   PARM0,PARM00        SECOND ENTRY ?                A45167 25800021
         BNE   SKIP                    NO, BRANCH                  000G 26100016
         LR    WORKAREA,PARM1           PICK UP WORKAREA POINTER   000G 26400016
         USING IEBWORK,WORKAREA                                         26700016
SKIP     ST    SAVREG,SAVE2BK           SET BACK CHAIN POINTER     000G 27000016
         LA    SAVREG,SAVE2             UPDATE TO NEW SAVE AREA    000G 27300016
         LA    WK2,SAVE3                SET UP FORWARD CHAIN POINTR000G 27600016
         ST    WK2,SAVE2FD                 TO NEXT SAVE AREA       000G 27900016
         LTR   PARM00,PARM00                IS THIS INITIAL ENTRY       28200016
         BNE   COMP                         NO,BRANCH                   28500016
         SPACE                                                          28800016
*                                                                     * 29100016
*  THIS SECTION LOADS THE ADDRESS OF THE IEBISR COMMON AREA, MOVES THE* 29400016
*  DDNAME FROM ISUT1 FIELD OF COMMON AREA TO THE INPUT  DCB,OPENS THE * 29700016
*  INPUT  DATASET, SAVES THE APPROPIATE POINTERS ANS SETS RETURN CODE * 30000016
*                                                                     * 30300016
         MVC   DCBAR1(LABEL-INDCB),INDCB                           000G 30600016
         USING IHADCB,WK2                                               30900016
         LA    WK2,DCBAR1                                          000G 31200016
         MVI   REENTAR+33,X'00'                                         31500016
         MVC   DCBDDNAM(8),ISUT1        MOVE DDNAME FROM ISUT1     000G 31800016
         MVC   REENTAR(CLOSEL-OPENL),OPENL   MOVE L FORM OF OPEN   000G 32100016
*                                               TO WORKAREA        000G 32400016
         OPEN  (DCBAR1,(INPUT)),MF=(E,REENTAR)  OPEN INPUT DATA SET000G 32700016
         TM    DCBOFLGS,X'10'                WAS OPEN SUCCESSFUL        33000016
         BZ    NOTOK                         NO,BRANCH                  33300016
         CLI   REENTAR+33,X'00'         VALID DCB FIELDS           000G 33600016
         BNE   INVALD                        NO, BRANCH                 33900016
         XC    EXITADDR+2(2),EXITADDR+2 INITIALIZE COMPARE NUMBER  000G 34200016
         BAL   NSIRG,PROC                    GET A RECORD               34500016
         LA    WK3,78                        NUMBER OF BYTES TO BE PRO- 34800016
         STH   WK3,EXITADDR                  CESSED IS 78          000G 35100016
         ST    INPT,ALIGN               SAVE INPUT POINTER         000G 35400016
         LA    RCRG,0                        SET RETURN CODE OF ZERO    35700016
         L     SAVREG,4(SAVREG)              POP UP SAVE AREA           36000016
         L     14,12(SAVREG)                 RESTOR REGISTER FOURTEEN   36300016
         SR    PARM00,PARM00                 SET REGISTER ZERO TO ZERO  36600016
         RETURN (1,12),T,RC=(15)             RETURN TO CALLING PROGRAM  36900016
         EJECT                                                          37200016
*  THIS SECTION PERFORMS ADDITIONAL INITIALIZATION NECESSARY FOR EACH * 37500016
*  SECONDARY ENTRY;IT DETERMINES IF THE ENTIRE CARD IMAGE HAS BEEN    * 37800016
*  PROCESSED.                                                         * 38100016
COMP     LR    TRANS,PARM1                   SAVE OUTPUT AREA ADDRESS   38400016
         L     INPT,ALIGN               RESTORE INPUT POINTER      000G 39000016
         CLI   DOUBLE+7,X'20'     COULD SYSUT2 BE OPENED ?       A48756 39050021
         BE    NOTOK              NO, CLOSE UT1 AND UT2          A48756 39100021
         CLI   DOUBLE+6,X'01'     SYSUT2 DCB PARAM ERROR ?       A48756 39150021
         BE    INVALD             YES, CLOSE UT1 AND UT2         A48756 39200021
         CLC   EXITADDR(2),ZERO         ALL CARD IMAGES PROCESSED  000G 39300016
         BNE   COMP1                         NO,BRANCH TO COMPLETE      39600016
         BAL   NSIRG,PROC                    ELSE, GET ANOTHER RECORD   39900016
         TM    ISSWITCH,X'FF'      SYSUT2 OPEN ?                 A48756 39930021
         BNO   NOTOPEN             NO, SKIP PUT                  A45167 39960021
         MVI   ISSWITCH,X'0F'      YES,PREVENT PUT NEXT TIMES    A45167 39990021
         TM    DCBAR2+DCBRECFM-IHADCB,X'40'  RECFM VAR OR UND ?  A45167 40020021
         BNO   *+10                NO, SKIP LRECL UPDATE         A45167 40050021
         MVC   DCBAR2+DCBLRECL-IHADCB(2),0(INPT) GET LRECL       A45167 40080021
         PUT   DCBAR2              PUT FOR OBTAINING POINTER     A45167 40110021
         LR    TRANS,PARM1         SAVE UPDATED POINTER          A45167 40140021
NOTOPEN  EQU   *                                                 A45167 40170021
         MVC   EXITADDR+4(2),0(INPT)                               000G 40200016
         LH    WK7,EXITADDR+4           LOAD ACTUAL LENGTH         000G 40500016
         LA    WK3,76                        BYTES TO BE PROCESSED = 76 40800016
         LA    INPT,2(INPT)                  STEP INPUT POINTER BY TWO  41100016
         B     MAX                           BRANCH TO CHECK LIMIT      41400016
         SPACE                                                          41700016
*  THIS SECTION DETERMINES IF THE NUMBER OF BYTES TO BE PROCESSED IS  * 42000016
*  EQUAL TO 1,OR IS GREATER THAN OR EQUAL TO 2.IF THE LATTER IS TRUE  * 42300016
*  THE 2 BYTE FIELD IS PROCESSED IF IT DOESN'T CONTAIN ZEROS.         * 42600016
COMP1    CLC   EXITADDR(2),TWO          ARE 2 OR MORE BYTES LEFT   000G 42900016
         BL    LAST                          NO,BRANCH                  43200016
         CLC   0(2,INPT),ZERO                IS THIS END OF INPUT       43500016
         BE    FINI                          YES,BRANCH                 43800016
         MVC   EXITADDR+4(2),0(INPT)                               000G 44100016
         LH    WK7,EXITADDR+4           SAVE LENGTH                000G 44400016
         LH    WK3,EXITADDR             SUBTRACT TWO FROM NUMBER   000G 44700016
         LA    INPT,2(INPT)                  STEP INPUT POINTER BY TWO  45000016
         SH    WK3,TWO                        OF BYTES TO BE PROCESSED  45300016
         LTR   WK3,WK3                       MORE BYTES TO BE PROCESSED 45600016
         BNE   MAX                          YES,BRANCH                  45900016
         BAL   NSIRG,PROC                    GET ANOTHER RECORD         46200016
         LA    WK3,78                        BYTES TO BE PROCESSED = 78 46500016
         B     MAX                           BRANCH TO CHECK LIMIT      46800016
         SPACE                                                          47100016
*  THIS SECTION PROCESSES THE LAST BYTE OF THE CARD IMAGE AS DETER-   * 47400016
*  MINED ABOVE IF IT IS NON-ZERO                                      * 47700016
LAST     SR    WK7,WK7            CLEAR REG BEFORE INSERTING CHAR  7060 47900017
         IC    WK7,0(INPT)        LOAD FIRST BYTE OF LENGTH        7060 48100017
         SLL   WK7,8                                                    48300016
         BAL   NSIRG,PROC                    GET NEXT RECORD            48600016
         IC    WK7,0(0,INPT)                 LOAD SECOND BYTE OF LENGTH 48900016
         STH   WK7,EXITADDR+4           SAVE ACTUAL LENGTH         000G 49200016
         LA    WK3,77                        BYTES TO BE PROCESSED = 77 49500016
         LA    INPT,1(INPT)                  STEP INPUT PTR BY 1        49800016
*  THIS SECTION DETERMINES WHETHER THE LENGTH OF THE RECORD TO BE     * 50100016
*  TRANSMITTED EXCEEDS MAXIMUM NUMBER OF CHARACTERS TO BE TRANSMITTED * 50400016
MAX      STH   WK3,EXITADDR                                        000G 50700016
         CR    PARM0,WK7                     LENGTH EXCEED LIMIT        51000016
         BL    EXCEED                        YES,BRANCH                 51300016
*  THIS SECTION DETERMINES IF THE ENTIRE RECORD IS CONTAINED ON THIS  * 51600016
*  CARD IMAGE. IF NOT,THE CHARACTERS CONTAINED ARE TRANSMITTED.       * 51900016
CHECK    CR    WK3,WK7                       ENTIRE RECORD PRESENT      52200016
         BNL   ENUF                          YES,BRANCH                 52500016
         BCTR  WK3,0                         MOVE EXACT NUMBER OF BYTES 52800016
         EX    WK3,MOVE                       LEFT FOR OUTPUT           53100016
         AH    TRANS,EXITADDR           INCREMENT OUTPUT POINTER   000G 53400016
         SH    WK7,EXITADDR             DECREMENT RECORD LENGTH    000G 53700016
         BAL   NSIRG,PROC                    GET ANOTHER RECORD         54000016
         LA    WK3,78                        BYTES TO BE PROCESSED = 78 54300016
         STH   WK3,EXITADDR                                        000G 54600016
         B     CHECK                                                    54900016
         SPACE                                                          55200016
*  THIS SECTION TRANSMITS AN ENTIRE RECORD FOR OUTPUT AND MODIFIES THE* 55500016
*  APPROPRIATE POINTERS AND LENGTH SPECIFIER                          * 55800016
ENUF     BCTR  WK7,0                                                    56100016
         EX    WK7,MOVE                      MOVE CHARACTERS FOR OUTPUT 56400016
         LA    WK7,1(WK7)                                               56700016
         SR    WK3,WK7                      DECREMENT BYTES LEFT BY     57000016
         STH   WK3,EXITADDR                  NUMBER MOVED          000G 57300016
         AR    INPT,WK7                     STEP INPUT POINTER          57600016
         ST    INPT,ALIGN               SAVE INPUT POINTER              58200016
         LA    RCRG,0                        SET RETURN CODE OF ZERO    58500016
         CLI   ISSWITCH,X'0F'      SYSUT2 DCB OPEN ?             A45167 58510021
         BNE   NOPUT               NO, DO NOT PUT                A45167 58520021
         CLC   EXITADDR(2),ONE     ONE BYTE LEFT ON LAST CARD ?  A45167 58530021
         BH    NOINPUT             NO, MORE, STORE PTR AND PUT   A45167 58540021
         BE    PARTINPT            YES, ONE BYTE LEFT            A45167 58550021
         BAL   NSIRG,PROC          NO BYTE LEFT, GET ANOTH RCRD         58560021
         LA    WK3,78              LOAD NBR OF BYTES TO PROCESS  A45167 58570021
         B     ENDINPUT            PUT A RECORD                  A45167 58580021
PARTINPT IC    WK3,0(INPT)         SAVE LAST BYTE                A45167 58590021
         BAL   NSIRG,PROC          GET ANOTHER RECORD            A45167 58600021
         BCTR  INPT,0              RESET PTR ONE BACK            A45167 58610021
         STC   WK3,0(INPT)         STORE SAVED BYTE              A45167 58620021
         LA    WK3,79              LOAD NBR OF BYTES TO PROCESS  A45167 58630021
ENDINPUT STH   WK3,EXITADDR        SAVE NBR OF BYTES TO PROCESS  A45167 58640021
         ST    INPT,ALIGN          SAVE PTR TO INPUT DATA        A45167 58650021
NOINPUT  CLC   0(2,INPT),ZERO     END OF INPUT ?                OY01167 58660016
         BE    FINI               YES, STOP PROCESSING          OY01167 58663016
         TM    DCBAR2+DCBRECFM-IHADCB,X'40'  RECFM VARIABLE ?   OY01167 58666016
         BNO   *+10                NO, SKIP LRECL UPDATE         A45167 58670021
         MVC   DCBAR2+DCBLRECL-IHADCB(2),0(INPT) GET LRECL       A45167 58680021
         PUT   DCBAR2              PUT FOR OBTAINING POINTER     A45167 58690021
         B     COMP                PROCESS NEXT LOGICAL RECORD   A45167 58700021
NOPUT    EQU   *                                                 A45167 58710021
         L     SAVREG,4(SAVREG)              POP UP SAVE AREA           58800016
         L     14,12(0,SAVREG)              RESTORE REGISTER FOURTEEN   59100016
         RETURN (1,12),T,RC=(15)            RETURN TO CALLING PROGRAM   59400016
*  THIS SECTION SETS THE APPROPRIATE RETURN CODES IN REGISTER 15, AND * 59700016
*  BRANCHES TO CLOSE THE DATASET AND RETURN                           * 60000016
NOTOK    LA    RCRG,X'20'                    SET RETURN CODE OF 20;     60300016
         B     OUT                            OPEN UNSUCCESSFUL         60600016
EXCEED   LA    RCRG,X'12'                    SET RETURN CODE OF 12;RE-  60900016
         B     OUT                            CORD LENGTH EXCEEDS LIMIT 61200016
FINI     LA    RCRG,8                        SET RETURN CODE OF 8;      61500016
         B     OUT                            END OF INPUT              61800016
INVALD   LA    RCRG,X'24'                    SET RETURN CODE OF    24   62100016
         B     OUT                            INVALID DCB FIELDS        62400016
         DS    0F                                                       62700016
LIST     DC    X'85'                                                    63000016
         DC    AL3(ISIEXT)                                              63300016
         SPACE                                                          63600016
*  THIS INSTRUCTION TRANSMITS INFORMATION FROM THE INPUT AREA TO THE  * 63900016
*  OUTPUT AREA. THE LENGTH IS SPECIFIED IN THE EXECUTE STATEMENT      * 64200016
MOVE     MVC   0(0,TRANS),0(INPT)            MOVE CHARACTERS TO OUTPUT  64500016
         EJECT                                                          64800016
*                                                                     * 65100016
*  THIS IS THE INPUT DCB EXIT ROUTINE, IN WHICH THE BLOCKSIZE         * 65400016
*  IS VERIFIED TO BE A MULTIPLE OF EIGHTY(80).  AN INDICATOR IS SET TO* 65700016
*  DESIGNATE THE INVALIDITY OF THE TWO FIELDS.                        * 66000016
*                                                                     * 66300016
ISIEXT   XC    REENTAR+33(1),REENTAR+33                            000G 66600016
TBLKSI   CLC   DCBBLKSI(2),ZERO              BLOCKSIZE ZERO?            66900016
         BE    MAKE80                        YES,BRANCH                 67200016
         SR    WK6,WK6                                                  67500016
          LH    WK7,DCBBLKSI                 LOAD BLOCKSIZE             67800016
         D     WK6,EIGHTY                    IS BLOCKSIZE A MULTIPLE    68100016
         LTR   WK6,WK6                        OF EIGHTY                 68400016
         BE    CONTIN                        YES,BRANCH                 68700016
         MVI   REENTAR+33,X'FF'         SET INVALID INDICATOR      000G 69000016
         B     CONTIN                                                   69300016
MAKE80   MVC   DCBBLKSI(2),EIGHTY+2          SET BLOCKSIZE TO EIGHTY    69600016
CONTIN   RETURN                                                         69900016
         SPACE 2                                                        70200016
*  THIS SECTION MAKES A RECORD AVAILABLE FOR PROCESSING AND CHECKS FOR* 70500016
*  SEQUENTIAL INPUT. IF THE INPUT IS OUT OF SEQUENCE,THE INPUT DATASET* 70800016
*  IS 'CLOSED',AND A RETURN IS MADE TO THE CALLING PROGRAM WITH A RE- * 71100016
*  TURN CODE OF 16. OTHERWISE,THE ADDRESS OF THE INPUT AREA+2,IS RE-  * 71400016
*  TURNED IN 'INPT' TO THE ADDRESS SPECIFIED IN 'NSIRG'.              * 71700016
PROC     GET   DCBAR1                  GET A RECORD                000G 72000016
         CLC   0(2,PARM1),EXITADDR+2    IS INPUT IN SEQUENCE       000G 72300016
         BNE   NOTSEQ                        NO,BRANCH                  72600016
         LH    WK2,EXITADDR+2                                      000G 72900016
         LA    WK2,1(WK2)                    INCREMENT SEQUENCE NUMBER  73200016
         STH   WK2,EXITADDR+2                FOR COMPARE BY ONE    000G 73500016
         LA    INPT,2(PARM1)                 SAVE ADDRESS OF INPUT+2    73800016
         BR    NSIRG                         RETURN WITH INPUT ADDRESS  74100016
EREXIT   SYNADAF ACSMETH=QSAM                                    A31442 74200020
         MVC   MESSAGE+8(79),49(PARM1)  MOVE SYNAD MSG TO BUFFER A52028 74250021
         SYNADRLS                                                A31442 74300020
         LA    RCRG,4                  LOAD RETURN CODE=4        A31442 74400020
         B     OUT                            SYNAD ERROR               74700016
NOTSEQ   LA    RCRG,X'16'                    INPUT OUT OF SEQUENCE      75000016
OUT      STC   RCRG,REENTAR+32          SAVE RETURN CODE           000G 75300016
         MVC   REENTAR(ZERO-CLOSEL),CLOSEL   MOVE L FORM OF CLOSE  000G 75600016
*                                               TO WORKAREA        000G 75900016
         CLOSE (DCBAR1),MF=(E,REENTAR) CLOSE INPUT DATA SET             76200016
         CLI   REENTAR+32,X'20'         NO GOOD OPEN             A41745 76210021
         BE    BYPASS                   NO OPEN SO BYPASS FREEP  A41745 76220021
         FREEPOOL DCBAR1                FREEPOOL INPUT BUFFER    A41745 76250021
BYPASS   IC    RCRG,REENTAR+32                                   A41745 76500021
         L     SAVREG,4(SAVREG)              POP UP SAVE AREA           76800016
         RETURN (14,12),T,RC=(15)           RETURN TO CALLING PROGRAM   77100016
*  CONSTANTS                                                            77400016
OPENL    OPEN  (,INPUT),MF=L                                       000G 77700016
CLOSEL   CLOSE (,DISP),MF=L                                        000G 78000016
ZERO     DC    1H'0'               ** CONSTANTS USED TO CHECK THE       78300016
TWO      DC    1H'2'               ** NUMBER OF BYTES TO BE PROCESSED   78600016
ONE      DC    H'1'                TEST BYTE                     A45167 78700021
EIGHTY   DC    F'80'                                                    78900016
         SPACE                                                          79200016
INDCB    DCB   DSORG=PS,MACRF=GL,DDNAME=SYSUT1,                        *79500016
               RECFM=FB,LRECL=80,EXLST=LIST,                           *79800016
               EODAD=FINI,SYNAD=EREXIT                                  80100016
LABEL    DS    0F                      DUMMY LABEL                 000G 80400016
         DCBD  DSORG=(PS)                                               80700016
IEBWORK  DSECT                                                          81000016
SAVE1    DS    1F                                                       81300016
SAVE1BK  DS    1F                                                       81600016
SAVE1FD  DS    1F                                                       81900016
         DS    15F                                                      82200016
SAVE2    DS    1F                                                       82500016
SAVE2BK  DS    1F                                                       82800016
SAVE2FD  DS    1F                                                       83100016
         DS    15F                                                      83400016
SAVE3    DS    1F                                                       83700016
SAVE3BK  DS    1F                                                       84000016
SAVE3FD  DS    1F                                                       84300016
         DS    15F                                                      84600016
*IEHISR WORKAREA                                                        84900016
ISINVOK  DS    F                        ADDRESS OF PARAMETER LIST AT    85200016
*                                          INVOCATION                   85500016
ISPRINT  DS    2F                       DDNAME FOR SYSPRINT             85800016
ISUT1    DS    2F                       DDNAME FOR SYSUT1               86100016
ISUT2    DS    2F                       DDNAME FOR SYSUT2               86400016
ISCOMP   DS    H                        COMPLETION CODE                 86700016
ISMESS   DS    H                        MESSAGE NUMBER                  87000016
ISPAGE   DS    H                        PAGE NUMBER FOR SYSPRINT        87300016
ISCNTRL  DS    CL1                      CONTROL BYTE                    87600016
* MASKS FOR TESTING CONTROL BYTE - ISCONTRL                             87900016
ISSWC    EQU   X'80'                    COPY                            88200016
ISSWU    EQU   X'40'                    UNLOAD                          88500016
ISSWL    EQU   X'20'                    LOAD                            88800016
ISSWP    EQU   X'10'                    PRINT                           89100016
ISSWN    EQU   X'08'                    NO CONVERT REQUESTED            89400016
ISSWPI   EQU   X'04'                    PRINT INDEX                     89700016
ISSWPP   EQU   X'02'                    PRINT PRIME                     90000016
ISSWPO   EQU   X'01'                    PRINT OVERFLOW                  90300016
ALLMASK  EQU   X'07'                                                    90600016
ISSWITCH DS    CL1                      EXTRANEOUS SWITCHES             90900016
FIRSTTM  EQU   X'80'                    FIRST TIME SWITCH               91200016
*                                                                       91500016
XCTLAREA DS    2F                                                       91800016
WHICH    DS    2F                                                       92100016
ALIGN    DS    1F                                                       92400016
DOUBLE   DS    1D                                                       92700016
EXITADDR DS    2F                                                       93000016
REENTAR  DS    9F                                                       93300016
DCBAR1   DS    64F                                                      93600016
DCBAR2   DS    64F                                                      93900016
MESSAGER DS    CL1                                                      94200016
MESSAGE  DS    CL120                                                    94500016
RECADDR  DS    1F                                                  000G 94800016
         DS    0D                                                       95100016
QISAM    DS    CL250                    UNLOADED DCB WORK AREA          95400016
HEADERC  DS    CL1                                                      95700016
HEADERT  DS    CL144                    HEADER AREA                     96000016
TARGET   DS    2F                       DATE HOLD AREA                  96300016
PAGENO   DS    1F                       HOLD PAGE NUMBER FOR A HEADING  96600016
PAGEPTR  DS    1F                       HOLD POINTER TO WHERE PAGE      96900016
*                                       NUMBER IS TO BE PLACED          97200016
         END                                                            97500016
