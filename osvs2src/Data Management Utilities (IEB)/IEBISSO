 TITLE 'IEBISSO-IEBISAM UTILITY PROGRAM-UNLOAD SUBROUTINE'              00200016
*STATUS: CHANGE LEVEL                                                   00400016
*FUNCTION/OPERATION: SUPPLIED WITH A RECORD ADDRESS AND THE LENGTH OF * 00600016
*   INFORMATION CONTAINED AT THIS ADDRESS(IN BYTES),PRODUCES 80 BYTE  * 00800016
*   CARD IMAGES WITH THE FIELDS DEFINED IN OUTPUT. A POINTER TO THE   * 01000016
*   CURRENT OUTPUT ADDRESS AND THE NUMBER OF BYTES REMAINING TO BE    * 01200016
*   FILLED IN THIS 80 BYTE AREA ARE MAINTAINED.                       * 01400016
*ENTRY POINTS: IEHISSI-TO PROCESS A RECORD FOR OUTPUT  BAL 14,IEHISSO * 01600016
*   DATA IS PASSED BY LOCATION                                        * 01800016
*INPUT: ALL ZEROS IN REGISTER 0 UPON INITIAL ENTRY                    * 02000016
*   AND THE ADDRESS OF THE PARAMETER LIST IN REGISTER 1.              * 02200016
*   A TWO BYTE RECORD LENGTH IN REGISTER 0 AND A RECORD ADDRESS IN    * 02400016
*   REGISTER 1, AND A RECORD OF LENGTH SPECIFIED BY REGISTER ZERO AT  * 02600016
*   THE ADDRESS SPECIFIED BY REGISTER 1.                              * 02800016
*   PARAMETER LIST                                                    * 03000016
*    GUARD    F     CONTAINS ADDRESS OF COMMON WORK AREA              * 03200016
*    KEYADR   F     CONTAINS KEY ADDRESS OR  X'FF' IN HIGH ORDER BYTE * 03400016
*    LONG     CL1   CONTAINS KEY LENGTH                               * 03600016
*    CPYDCB   CL1   DCB UNLOADED INDICATOR                            * 03800016
*                                                                     * 04000016
*   REGISTER 13 MUST CONTAIN  THE ADDRESS OF THE CALLING PROGRAM'S    * 04200016
*   SAVE AREA,REGISTER 14 MUST CONTAIN THE RETURN ADDRESS IN THE      * 04400016
*   CALLING PROGRAM,AND REGISTER 15 MUST CONTAIN THE ADDRESS OF THIS  * 04600016
*   MODULE                                                            * 04800016
*OUTPUT: 80 BYTE CARD IMAGES WITH THE FOLLOWING FIELDS                * 05000016
*   SEQUENCE NUMBER: A TWO BYTE NUMBER STARTING AT ZERO FOR THE FIRST * 05200016
*   CARD IMAGE AND INCREMENTED BY ONE FOR EACH SUCCEEDING CARD IMAGE. * 05400016
*   LENGTH: A TWO BYTE FIELD SPECIFYING THE NUMBER OF BYTES OF INFOR- * 05600016
*   MATION BETWEEN THIS LENGTH FIELD AND THE NEXT.                    * 05800016
*   INFORMATION: THIS FIELD CONTAINS THE INFORMATION FROM A GIVEN     * 06000016
*   RECORD. IT IS SEGMENTED(IF NECESSARY) ON SEQUENTIAL CARD IMAGES.  * 06200016
*   THE LENGTH FIELD DETERMINES ITS SEGMENTATION.                     * 06400016
*   END OF OUTPUT: THIS FIELD OF 0,1,OR 2 BYTES OF ZEROS IS CONTAINED * 06600016
*   ONLY ON THE LAST CARD IMAGE.                                      * 06800016
*EXTERNAL ROUTINES; IEBISU - UNLOAD RTN.GETRECORD ADDRESS AND         * 07000016
*   LENGTH.                                                           * 07200016
*EXITS-NORMAL:  WHEN AN ENTIRE RECORD HAS BEEN PROCESSED (LENGTH      * 07400016
*   IN REGISTER 0), A RETURN IS MADE TO THE CALLING PROGRAM BY A      * 07600016
*   RETURN MACRO WITH A RETURN CODE OF 0 IN REGISTER 15.              * 07800016
*     -ERROR: IF AN EXIT TO THE OUTPUT DCB SYNAD ROUTINE WAS TAKEN    * 08000016
*   A RETURN CODE OF FOUR(4) IS LOADED IN REGISTER 15,BEFORE A RETURN * 08200016
*   IS MADE TO THE CALLING PROGRAM.                                   * 08400016
*   IF THE OPEN FOR THE OUTPUT DATA SET WAS NOT SUCCESSFULLY COMPLETED* 08600016
*   A RETURN CODE OF EIGHT(8) IS LOADED IN REGISTER 15.               * 08800016
*   IF THE BLKSIZE FIELD OF THE OUTPUT DCB WAS INCORRECT A RETURN     * 09000016
*   CODE OF 12 IS LOADED INTO REGISTER 15.                            * 09200016
*TABLES/WORK AREAS: SAVE AREA 72 BYTES TO STORE REGISTERS             * 09400016
*   SEQ = SEQUENCE NUMBER WORK AREA                                   * 09600016
*   KEEP = SAVE AREA FOR CURRENT OUTPUT AREA ADDRESS                  * 09800016
*   SAVE = BYTES REMAINING IN CARD IMAGE WORK AREA                    * 10000016
*ATTRIBUTES-REENTRANT                                              000G 10200016
*NOTES:  BASE REGISTER    = BASERG                                    * 10400016
*        SAVE REGISTER =  13                                          * 10600016
*        PARAMETER REGISTERS:                                         * 10800016
*         PARM0=0 ,PARM1=1                                            * 11000016
*        RETURN CODE REGISTER = 15                                    * 11200016
*        RETURN REGISTER = 14                                         * 11400016
*        WORK REGISTERS                                               * 11600016
*         WK2            WK6                                          * 11800016
*         WK3            WK7                                          * 12000016
*REGISTER ASSIGNMENTS                                                   12200016
PARM00   EQU   0                                                        12400016
PARM1    EQU   1        *REGISTERS* REGISTER OF EVEN-ODD PAIR           12600016
WK2      EQU   2                   WORK REGISTER                        12800016
WK3      EQU   3                   BYTES LEFT IN OUTPUT-WORK REGISTER   13000016
PARM0    EQU   4                                                        13200016
RETRG    EQU   5                   RETURN REGISTER FOR OPERTN SECTION   13400016
WK6      EQU   6                   WORK REGISTER FOR OPERTN SECTION     13600016
WK7      EQU   7                   WORK REGISTER                        13800016
TRANS    EQU   8                   CURRENT RECORD ADDRESS REGISTER      14000016
OUTPT    EQU   9                   CURRENT OUTPUT AREA ADDRESS REGISTER 14200016
BASREG   EQU   10                  BASE REGISTER                        14400016
WORKAREA EQU   11                      WORKAREA REGISTER           000G 14600016
WK12     EQU   12                            RECORD KEY ADDRESS         14800016
SAVREG   EQU   13                                                       15000016
RCRG     EQU   15                                                       15200016
*                                                                     * 15400016
IEBISSO  CSECT                                                          15600016
*2821     014600,016600,018400-018600,019000-019600,020000-020200, 000G 15800016
*         020800,021200,021600,022000,024400-024600,025000-025400, 000G 16000016
*         025800,026800,027800,028200,028600-029200,029600,030000, 000G 16200016
*         030400,031600,032600-033000,033600,034400-035000,035600, 000G 16400016
*         038000-038200,039200,040800,041200,044000-044200,        000G 16600016
*         045600-045800,046200,046600,047200-047400,048000,048400, 000G 16800016
*         050800,051800,052200,052600-053000,054800,056400,        000G 17000016
*         059600-059800,060200,062200,063200-065000,               000G 17200016
* WITH RELEASE 16 THE ENTIRE MODULE WAS REPLACED                   000G 17400016
*  WITH RELEASE 16 THIS MODULE WAS MADE REENTRANT BY USING A       000G 17600016
*DSECT NAMED IEBWORK WHICH WAS DESIGNED AS A COMMON WORKAREA.      000G 17800016
*  THIS SECTION PERFORMS THE INITIALIZATION NECESSARY FOR EACH ENTRY  * 18000016
*A576100-578000                                                  A41745 18050021
         SAVE  (14,12),T,SSO-66-V1           SAVE REGISTERS             18200016
         USING IEBISSO,BASREG                ESTABLISH BASE REGISTER    18400016
         LR    BASREG,RCRG                   ESTABLISH BASE REGISTER    18600016
         LTR   PARM00,PARM00           IS THIS INITIAL ENTRY       000G 18800016
         BNE   SKIP                    NO, BRANCH                  000G 19000016
         LR    WORKAREA,PARM1          PICK UP WORK AREA POINTER   000G 19200016
         USING IEBWORK,WORKAREA                                    000G 19400016
SKIP     ST    SAVREG,SAVE2BK          SET BACK CHAIN POINTER      000G 19600016
         LA    SAVREG,SAVE2            UPDATE TO NEW SAVE AREA     000G 19800016
         LA    WK2,SAVE3               SET UP FORWARD CHAIN POINTER000G 20000016
         ST    WK2,SAVE2FD                TO NEXT SAVE AREA        000G 20200016
         BNE   COMPRE                       NO,BRANCH                   20400016
*                                                                     * 20600016
*  THIS SECTION SAVES THE PARAMETER LIST ADDRESS STORES KEYLEN FOR UN-* 20800016
*BLOCKED RECORDS, SETS THE UNBLOCKED RECFM INDICATOR, MOVES THE       * 21000016
*  DDNAME FROM ISUT2 FIELD OF COMMON AREA TO THE OUTPUT DCB,OPENS THE * 21200016
*  OUTPUT DATASET, SAVES THE APPROPIATE POINTERS ANS SETS RETURN CODE * 21400016
*                                                                     * 21600016
         CLI   DOUBLE,X'FF'            UNBLOCKED RECORDS           000G 21800016
         BNE   COMAREA             NO,BRANCH                            22000016
         MVC   EXITADDR+5(1),DOUBLE+4   STORE KEY LENGTH           000G 22200016
         MVI   EXITADDR+6,X'02'        SET UNBLOCKED INDICATOR     000G 22400016
COMAREA  MVC   DCBAR2(OPENL-OUTDCB),OUTDCB                         000G 22600016
         USING IHADCB,WK2                                          000G 22800016
         LA    WK2,DCBAR2                                          000G 23000016
         MVI   REENTAR+33,X'00'                                    000G 23200016
         MVC   DCBDDNAM(8),ISUT2       MOVE DDNAME FROM ISUT2      000G 23400016
         MVC   REENTAR(CLOSEL-OPENL),OPENL   MOVE OPENL TO WORKAREA000G 23600016
         OPEN  (DCBAR2,(OUTPUT)),MF=(E,REENTAR)   YES, OPEN OUTPUT 000G 23800016
         TM    DCBOFLGS,X'10'                WAS OPEN SUCCESSFUL        24000016
         BZ    NOTOK                         IF NOT,BRANCH              24200016
         CLI   REENTAR+33,X'00'        VALID DCB FIELDS            000G 24400016
         BNE   HERE                          NO, BRANCH                 24600016
ALRITE   XC    EXITADDR(2),EXITADDR    SET SEQUENCE NUMBER TO 0    000G 24800016
         LA    WK3,78                        NUMBER OF BYTES LEFT IN    25000016
         STH   WK3,EXITADDR+2              OUTPUT AREA EQUAL 78    000G 25200016
         BAL   RETRG,OPERTN                                             25400016
         ST    OUTPT,ALIGN             SAVE OUTPUT POINTER         000G 25600016
         LR    PARM1,WK2                     LOAD ADDRESS OF OUTPUT DCB 25800016
         LA    RCRG,0                        SET RETURN CODE OF ZERO    26000016
         LR    PARM00,RCRG                   RETURN ZERO IN REGISTER 0  26200016
         L     SAVREG,4(SAVREG)             POP UP SAVE AREA            26400016
         L     14,12(0,SAVREG)               RESTORE REGISTER FOURTEEN  26600016
         RETURN (2,12),T,RC=(15)            RETURN TO CALLING PROGRAM   26800016
*  THIS SECTION DETERMINES IF THERE IS ANY SPACE LEFT IN THE OUTPUT   * 27000016
*  AREA. IF NOT,THE NEXT AVAILABLE OUTPUT AREA ADDRESS IS ACQUIRED.   * 27200016
*  ALL POINTERS AND LENGTH SPECIFIERS,SAVED UPON EXIT,ARE RESTORED.   * 27400016
COMPRE   LR    TRANS,PARM1                   SAVE RECORD ADDRESS        27600016
         LR    PARM0,PARM00                                             27800016
         CLI   DOUBLE+5,X'FF'          END OF INPUT DATA           000G 28000016
         BE    ERROR1                   YES,BRANCH CLOSE OUTPUT    000G 28200016
         BE    ERROR+4                       YES,BRANCH ,CLOSE OUTPUT   28400016
         L     WK12,DOUBLE             LOAD KEY ADDRESS            000G 28600016
         MVC   EXITADDR+5(1),DOUBLE+4   GET KEY LENGTH             000G 28800016
         L     OUTPT,ALIGN             RESTORE OUTPUT POINTER      000G 29000016
         LA    WK7,2(PARM0)                  SAVE LENGTH PLUS 2 BYTES   29200016
         CLC   EXITADDR+2(2),ZERO      IF SPACE LEFT IN OUTPUT     000G 29400016
         BNE   HOWMCH                         AREA,BRANCH               29600016
*                                            ELSE,GET NEXT AVAILABLE    29800016
         BAL   RETRG,OPERTN                   OUTPUT AREA ADDRESS       30000016
         LA    WK3,78                        RESET BYTES LEFT IN OUTPUT 30200016
         STH   WK3,EXITADDR+2              AREA TO 78              000G 30400016
         SPACE                                                          30600016
*  THIS SECTION DETERMINES THE AMOUNT OF ROOM LEFT,PUTS THE RECORD    * 30800016
*  LENGTH IN THE OUTPUT AREA AND STEPS THE OUTPUT POINTER.            * 31000016
         EJECT                                                          31200016
HOWMCH   CH    WK7,EXITADDR+2          IF ENOUGH ROOM FOR INFOR-   000G 31400016
         BNH   ENUF                           MATION AND LENGTH,BRANCH  31600016
         CLC   EXITADDR+2(2),TWO       IF ONLY ONE BYTE LEFT IN    000G 31800016
         BL    ONE                            OUTPUT AREA,BRANCH        32000016
         STH   PARM0,ALIGN                                         000G 32200016
         MVC   0(2,OUTPT),ALIGN        MOVE LENGTH TO OUTPUT AREA  000G 32400016
         LH    PARM0,ALIGN                                         000G 32600016
         LH    WK3,EXITADDR+2                                      000G 32800016
         SH    WK3,TWO                       DECREMENT NUMBER OF BYTES  33000016
         STH   WK3,EXITADDR+2              LEFT BY TWO             000G 33200016
         LA    OUTPT,2(OUTPT)                ADD TWO TO OUTPUT POINTER  33400016
         CLI   DOUBLE+5,X'11'          DCB FIELDS UNLOADED         000G 33600016
         BE    INFORM                        NO  BRANCH                 33800016
         CLI   EXITADDR+6,X'02'        UNBLOCKED RECORDS           000G 34000016
         BNE   INFORM                        NO,BRANCH                  34200016
         LTR   WK3,WK3                       ANY MORE BYTES LEFT?       34400016
         BZ    KACTION                       NO,BRANCH                  34600016
*  THIS SECTION MOVES THE RECORD KEY TO THE OUTPUT AREA FOR UNBLOCKED * 34800016
*RECORDS:WHERE RKP=0, TECHNIQUE SAME AS FOR DATA PORTIONS.            * 35000016
KEYCOMP  LH    WK6,EXITADDR+4          LOAD KEY LENGTH             000G 35200016
         CR    WK3,WK6                       ENOUGH ROOM FOR KEY?       35400016
         BL    GETMOR                        NO,BRANCH                  35600016
         BCTR  WK6,0                                                    35800016
         EX    WK6,MVEKEY                    MOVE KEY TO OUTPUT         36000016
         SH    PARM0,EXITADDR+4        DECREMENT TOTAL LENGTH      000G 36200016
         AH    OUTPT,EXITADDR+4        STEP OUTPUT POINTER         000G 36400016
         SH    WK3,EXITADDR+4          DECREMENT BYTES LEFT        000G 36600016
         LTR   WK3,WK3                       ANY BYTES LEFT             36800016
         BZ    MORE                          NO,BRANCH                  37000016
         STH   WK3,EXITADDR+2                                      000G 37200016
         B     FIND                          YES,BRANCH                 37400016
GETMOR   BCTR  WK3,0                                                    37600016
         EX    WK3,MVEKEY                    MOVE 'SAVE' BYTES OF KEY   37800016
         SH    WK6,EXITADDR+2          DECREMENT KEY LENGTH        000G 38000016
         STH   WK6,EXITADDR+4                                      000G 38200016
         SH    PARM0,EXITADDR+2        DECREMENT TOTAL LENGTH      000G 38400016
         AH    WK12,EXITADDR+2         STEP KEY ADDRESS            000G 38600016
KACTION  BAL   RETRG,OPERTN                  GET NEXT OUTPUT ADDRESS    38800016
         LA    WK3,78                                                   39000016
         STH   WK3,EXITADDR+2          SET SAVE = 78               000G 39200016
*  THIS INSTRUCTION MOVES THE RECORD KEY TO OUTPUT; THE LENGTH OF     * 39400016
*WHICH IS SPECIFIED IN THE REGISTER OF AN EXECUTE INSTRUCTION         * 39600016
         B     KEYCOMP                                                  39800016
MVEKEY   MVC   0(0,OUTPT),0(WK12)            MOVE KEY TO OUTPUT         40000016
INFORM   LTR   WK3,WK3                       ANY SPACE                  40200016
         BZ    MORE                           LEFT,BRANCH               40400016
         SPACE                                                          40600016
*  THIS SECTION MOVES ENOUGH BYTES FROM THE RECORD TO FILL THE OUTPUT * 40800016
*  AREA AND MODIFIES THE OUTPUT POINTER AND RECORD LENGTH.            * 41000016
CONTIN   BCTR  WK3,0                         MOVE 'SAVE' BYTES TO       41200016
         EX    WK3,MOVE                      FILL OUTPUT AREA           41400016
         AH    TRANS,EXITADDR+2        STEP RECORD POINTER         000G 41600016
         SH    PARM0,EXITADDR+2        DECREMENT LENGTH BY 'SAVE'  000G 41800016
*  THIS SECTION GETS NEXT AVAILABLE OUTPUT ADDRESS AND RESETS 'SAVE'    42000016
*                                            GET ADDRESS OF NEXT AVAIL- 42200016
MORE     BAL   RETRG,OPERTN                   ABLE OUTPUT AREA          42400016
         LA    WK3,78                        RESET BYTES LEFT IN        42600016
         STH   WK3,EXITADDR+2               OUTPUT AREA TO 78      000G 42800016
*  THIS SECTION MOVES IN THE ENTIRE RECORD OF INFORMATION,IF THERE IS * 43000016
*  ENOUGH ROOM IN THE OUTPUT AREA                                       43200016
FIND     CR    WK3,PARM0                     ENOUGH ROOM                43400016
         BL    CONTIN                        NO,GO MOVE IN 'SAVE' BYTES 43600016
         LR    WK3,PARM0                     YES,                       43800016
         BCTR  WK3,0                                                    44000016
         EX    WK3,MOVE                      MOVE IN INFORMATION        44200016
         LH    WK3,EXITADDR+2                                      000G 44400016
         SR    WK3,PARM0                     DECREMENT NUMBER OF BYTES  44600016
         STH   WK3,EXITADDR+2            LEFT BY NUMBER MOVED (LL) 000G 44800016
         AR    OUTPT,PARM0                   STEP OUTPUT POINTER        45000016
         B     CHECK                                                    45200016
*  THIS SECTION INSERTS THE HIGH ORDER BYTE OF THE RECORD LENGTH IN   * 45400016
*  LAST BYTE OF OUTPUT AREA;LOW ORDER BYTE IN THE NEXT AVAILABLE OUT- * 45600016
*  PUT AREA AND MODIFIES THE APPROPIATE POINTERS AND LENGTH SPECIFIER * 45800016
ONE      SRDL  PARM0,8                       INSERT HIGH ORDER BYTE OF  46000016
         STC   PARM0,0(0,OUTPT)               RECORD LENGTH FOR OUTPUT  46200016
         SRDL  PARM0,8                                             1620 46400016
         LR    PARM0,RETRG                                              46600016
         SRL   PARM0,16                                            1620 46800016
         BAL   RETRG,OPERTN                                             47000016
         STC   PARM0,0(0,OUTPT)              INSERT LOW ORDER BYTE      47200016
         LA    OUTPT,1(OUTPT)               STEP OUTPUT POINTER BY ONE  47400016
         LA    WK3,77                        RESET NUMBER OF BYTES LEFT 47600016
         STH   WK3,EXITADDR+2            IN OUTPUT AREA TO 77      000G 47800016
         CLI   EXITADDR+6,X'02'        UNBLOCKED RECORDS           000G 48000016
         BE    KEYCOMP                       YES,BRANCH                 48200016
         B     FIND                          NO,BRANCH                  48400016
         SPACE 2                                                        48600016
*  THIS SECTION MOVES AN ENTIRE RECORD TO THE OUTPUT AREA AND MODIFIES* 48800016
*  THE OUTPUT AREA LENGTH SPECIFIER AND THE OUTPUT AREA POINTER. IT   * 49000016
*  ALSO MOVES RECORD LENGTH TO OUTPUT AREA.                           * 49200016
ENUF     STH   PARM0,ALIGN             MOVE LENGTH TO OUTPUT AREA  000G 49400016
         MVC   0(2,OUTPT),ALIGN                                    000G 49600016
         LA    OUTPT,2(OUTPT)                ADD TWO TO OUTPUT POINTER  49800016
         CLI   EXITADDR+6,X'02'        UNBLOCKED RECORDS           000G 50000016
         BNE   EASYMVE                       NO,BRANCH                  50200016
         LH    WK6,EXITADDR+4          GET KEYLEN                  000G 50400016
         BCTR  WK6,0                                                    50600016
         EX    WK6,MVEKEY                    MOVE KEY TO OUTPUT         50800016
         SH    PARM0,EXITADDR+4        DECREMENT TOTAL LENGTH      000G 51000016
         AH    OUTPT,EXITADDR+4        STEP OUTPUT POINTER         000G 51200016
EASYMVE  BCTR  PARM0,0                       MOVE ENTIRE RECORD TO      51400016
         EX    PARM0,MOVE                     OUTPUT AREA               51600016
         LH    WK3,EXITADDR+2          DECREMENT BYTES LEFT BY     000G 51800016
         SR    WK3,WK7                        THE BYTES OF INFORMATION  52000016
         STH   WK3,EXITADDR+2                MOVED PLUS TWO.       000G 52200016
         AR    OUTPT,PARM0                  STEP OUTPUT POINTER         52400016
         LA    OUTPT,1(OUTPT)                BY LENGTH OF RECORD        52600016
         SPACE                                                          52800016
*  THIS  SECTION PUTS 1 OR 2 BYTES OF ZEROS IN THE OUTPUT AREA AT THE * 53000016
*  END OF EACH FIELD OF INFORMATION,AND SETS A RETURN CODE OF ZERO.   * 53200016
CHECK    CH    WK3,UNO                       ONE BYTE LEFT IN OUTPUT    53400016
         BE    END                           NO,BRANCH                  53600016
         BL    END+6                         POSSIBLE END,BRANCH        53800016
         MVC   0(2,OUTPT),ZERO               DESIGNATE POSSIBLE END OF  54000016
         B     END+6                         OUTPUT BY HALFWORD OF ZERO 54200016
END      MVC   0(1,OUTPT),ZERO                OR BY ONE BYTE OF ZERO    54400016
         ST    OUTPT,ALIGN             SAVE OUTPUT POINTER         000G 54600016
         LA    RCRG,0                   RETURN CODE=0;OPERATION OKAY    54800016
         B     GOBACK                   BRANCH,TO RETURN                55000016
         SPACE                                                          55200016
*  THIS SECTION SETS RETURN CODES DIFFERENT FROM 0.                   * 55400016
NOTOK    MVI   REENTAR+32,X'08'        RETURN CODE=8 OPEN NOT GOOD 000G 55600016
         B     ERROR1                                              000G 55800016
HERE     MVI   REENTAR+32,X'12'        WRONG DSORG OR BLKSIZE      000G 56000016
         B     ERROR1                                              000G 56200016
ERROR    MVI   REENTAR+32,X'04'        RETURN CODE=4 SYNAD RTN     000G 56400016
         SYNADAF ACSMETH=QSAM,PARM1=DCBAR2,PARM2=(0)               000G 56600016
         MVC   MESSAGE+8(79),49(PARM1)  MOVE IN MESSAGE            000G 56800016
         SYNADRLS                                                  000G 57000016
ERROR1   MVC   REENTAR(ZERO-CLOSEL),CLOSEL   MOVE L FORM OF CLOSE  000G 57200016
*                                               TO WORKAREA        000G 57400016
         CLOSE (DCBAR2),MF=(E,REENTAR)   CLOSE OUTPUT              000G 57600016
         CLI   REENTAR+32,X'08'         OPEN NO GOOD             A41745 57610021
         BE    BYPASS                   NO OPEN SO BYPASS FREEP  A41745 57620021
         FREEPOOL DCBAR2                FREEPOOL OUTPUT BUFFER   A41745 57650021
BYPASS   IC    RCRG,REENTAR+32         LOAD RETURN CODE          A41745 57800021
GOBACK   L     SAVREG,4(SAVREG)             POP UP SAVE AREA            58000016
         RETURN (14,12),T,RC=(15)           RETURN TO CALLING PROGRAM   58200016
         SPACE                                                          58400016
*                                                                     * 58600016
*  THIS IS THE OUTPUT DCB EXIT ROUTINE.   THE BLOCKSIZE               * 58800016
*  IS VERIFIED TO BE A MULTIPLE OF EIGHTY(80).  AN INDICATOR IS SET TO* 59000016
*  DESIGNATE THE INVALIDITY OF THE TWO FIELDS.                        * 59200016
*                                                                     * 59400016
DCBRTN   XC    REENTAR+33(1),REENTAR+33  CLEAR INDICATOR           000G 59600016
TBLKSI   CLC   DCBBLKSI(2),ZERO             BLOCKSIZE FIELD ZERO        59800016
         BE    SET80                         YES,BRANCH                 60000016
         SR    WK6,WK6                                                  60200016
         LH    WK7,DCBBLKSI                  LOAD BLOCKSIZW             60400016
         D     WK6,EIGHTY                    IS BLOCKSIZE A MULTIPLE    60600016
         LTR   WK6,WK6                        OF EIGHTY?                60800016
         BE    CONTINUE                                                 61000016
         MVI   REENTAR+33,X'FF'        SET INVALID INDICATOR       000G 61200016
         B     CONTINUE                                                 61400016
SET80    MVC   DCBBLKSI(2),EIGHTY+2          SET BLOCKSIZE TO EIGHTY    61600016
CONTINUE RETURN                                                         61800016
*  THIS INSTRUCTION MOVES INFORMATION FROM THE RECORD TO THE OUTPUT   * 62000016
*  AREA;THE NUMBER OF CHARACTERS TO BE MOVED IS SPECIFIED IN THE      * 62200016
*  REGISTER OF THE EXECUTE STATEMENT WHICH USES THIS INSTRUCTION.     * 62400016
MOVE     MVC   0(0,OUTPT),0(TRANS)      MOVE INFORMATION TO OUTPUT      62600016
         EJECT                                                          62800016
*  THE FOLLOWING SECTION OF CODE OUTPUTS ONE CARD IMAGE AND SUPPLIES  * 63000016
*  AN ADDRESS OF THE NEXT AVAILABLE OUTPUT AREA. IT ALSO MOVES THE    * 63200016
*  TWO BYTE SEQUENCE NUMBER TO THE OUTPUT AREA,INCREMENTS THE SEQUENCE* 63400016
*  NUMBER BY ONE FOR THE FOLLOWING CARD IMAGE,AND STEPS THE POINTER TO* 63600016
*  THE OUTPUT AREA BY TWO. THE RESULTING ADDRESS IS RETURNED IN 'OUT- * 63800016
*  PT' TO THE ADDRESS SPECIFIED IN THE REGISTER NAMED 'RETRG'.        * 64000016
OPERTN   PUT   DCBAR2                  OUTPUT CARD IMAGE           000G 64200016
         MVC   0(2,PARM1),EXITADDR     MOVE SEQUENCE NUMBER        000G 64400016
         LH    WK6,EXITADDR                                        000G 64600016
         LA    WK6,1(WK6)                                               64800016
         STH   WK6,EXITADDR            INCREMENT NUMBER BY ONE     000G 65000016
REAP     LA    OUTPT,2(PARM1)               STEP OUTPUT POINTER BY TWO  65200016
         BR    RETRG                                                    65400016
         DS    0F                                                       65600016
VALID    DC    X'85'                                                    65800016
         DC    AL3(DCBRTN)                                              66000016
         SPACE                                                          66200016
OUTDCB   DCB   DSORG=PS,MACRF=PL,DDNAME=SYSUT2,                        *66400016
               RECFM=FB,LRECL=80,SYNAD=ERROR,EXLST=VALID                66600016
         SPACE                                                          66800016
*CONSTANTS                                                              67000016
OPENL    OPEN  (,OUTPUT),MF=L                                      000G 67200016
CLOSEL   CLOSE (,DISP),MF=L                                        000G 67400016
ZERO     DC    1H'0'               **                                   67600016
UNO      DC    1H'1'                *COMPARE CONSTANTS                  67800016
TWO      DC    1H'2'               **                                   68000016
EIGHTY   DC    F'80'                                                    68200016
         DCBD  DSORG=(PS)                                               68400016
IEBWORK  DSECT                                                          68600016
SAVE1    DS    1F                                                       68800016
SAVE1BK  DS    1F                                                       69000016
SAVE1FD  DS    1F                                                       69200016
         DS    15F                                                      69400016
SAVE2    DS    1F                                                       69600016
SAVE2BK  DS    1F                                                       69800016
SAVE2FD  DS    1F                                                       70000016
         DS    15F                                                      70200016
SAVE3    DS    1F                                                       70400016
SAVE3BK  DS    1F                                                       70600016
SAVE3FD  DS    1F                                                       70800016
         DS    15F                                                      71000016
*IEHISR WORKAREA                                                        71200016
ISINVOK  DS    F                        ADDRESS OF PARAMETER LIST AT    71400016
*                                          INVOCATION                   71600016
ISPRINT  DS    2F                       DDNAME FOR SYSPRINT             71800016
ISUT1    DS    2F                       DDNAME FOR SYSUT1               72000016
ISUT2    DS    2F                       DDNAME FOR SYSUT2               72200016
ISCOMP   DS    H                        COMPLETION CODE                 72400016
ISMESS   DS    H                        MESSAGE NUMBER                  72600016
ISPAGE   DS    H                        PAGE NUMBER FOR SYSPRINT        72800016
ISCNTRL  DS    CL1                      CONTROL BYTE                    73000016
* MASKS FOR TESTING CONTROL BYTE - ISCONTRL                             73200016
ISSWC    EQU   X'80'                    COPY                            73400016
ISSWU    EQU   X'40'                    UNLOAD                          73600016
ISSWL    EQU   X'20'                    LOAD                            73800016
ISSWP    EQU   X'10'                    PRINT                           74000016
ISSWN    EQU   X'08'                    NO CONVERT REQUESTED            74200016
ISSWPI   EQU   X'04'                    PRINT INDEX                     74400016
ISSWPP   EQU   X'02'                    PRINT PRIME                     74600016
ISSWPO   EQU   X'01'                    PRINT OVERFLOW                  74800016
ALLMASK  EQU   X'07'                                               000G 75000016
ISSWITCH DS    CL1                      EXTRANEOUS SWITCHES             75200016
FIRSTTM  EQU   X'80'                    FIRST TIME SWITCH               75400016
*                                                                       75600016
XCTLAREA DS    2F                                                       75800016
WHICH    DS    2F                                                       76000016
ALIGN    DS    1F                                                       76200016
DOUBLE   DS    1D                                                       76400016
EXITADDR DS    2F                                                       76600016
REENTAR  DS    9F                                                       76800016
DCBAR1   DS    64F                                                      77000016
DCBAR2   DS    64F                                                      77200016
MESSAGER DS    CL1                                                      77400016
MESSAGE  DS    CL120                                                    77600016
RECADDR  DS    1F                                                  000G 77800016
         DS    0D                                                       78000016
QISAM    DS    CL250                    UNLOADED DCB WORK AREA          78200016
HEADERC  DS    CL1                                                      78400016
HEADERT  DS    CL144                    HEADER AREA                     78600016
TARGET   DS    2F                       DATE HOLD AREA                  78800016
PAGENO   DS    1F                       HOLD PAGE NUMBER FOR A HEADING  79000016
PAGEPTR  DS    1F                       HOLD POINTER TO WHERE PAGE      79200016
*                                       NUMBER IS TO BE PLACED          79400016
         END                                                            79600016
