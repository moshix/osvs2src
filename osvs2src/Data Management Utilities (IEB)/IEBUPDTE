UPD1     TITLE 'IEBUPDTE INITIALIZATION CSECT- IEBUPDTE'                00020000
*TITLE-IEBUPDTE-IEBUPDTE. INITIALIZATION CSECT                     2039 00040014
*                                                                     * 00060000
*STATUS- CHANGE LEVEL 001                                             * 00080000
*                                                                     * 00100000
*FUNCTION/OPERATION- THIS MODULE IS ABLE TO CREATE AND UPDATE         * 00120000
*   SYMBOLIC LIBRARIES WHICH ARE EITHER PHYSICAL SEQUENTIAL OR        * 00140000
*   PARTITIONED DATA SETS CONTAINED ON DIRECT ACCESS OR TAPE VOLUMES. * 00160000
*                                                                     * 00180000
*   THIS MODULE HANDLES FIXED OR FIXED-BLOCKED RECORDS. IT HAS THE    * 00200000
*   ABITLITY TO HANDLE RECORD OF LOGICAL RECORD LENGTHS UP TO EIGHTY  * 00220000
*   BYTES. IF BLOCKSIZE AND/OR LOGICAL RECORD LENGTH ARE NOT GIVEN    * 00240000
*   BY OPEN TIME, UNBLOCKED RECORDS OF 80 BYTES IS ASSUMED AND A      * 00260000
*   WARNING MESSAGE IS GIVEN ON THE LOG OUT DEVICE.                   * 00280000
*                                                                     * 00300000
*   THIS MODULE CAN-                                                  * 00320000
*                                                                     * 00340000
*   1. CREATE A NEW PARTITIONED OR PHYSICAL SEQUENTIAL DATA SET       * 00360000
*                                                                     * 00380000
*   2. SEQUENCE THE NEW DATA SET OR RESEQUENCE AN OLD ONE             * 00400000
*                                                                     * 00420000
*   3. REPRODUCE OR REPLACE A MEMBER OR A DATA SET                    * 00440000
*                                                                     * 00460000
*   4. PRODUCE A PARTITIONED DATA SET FROM A PHYSICAL SEQUENTIAL      * 00480000
*   MASTER OR THE CONVERSE.                                           * 00500000
*                                                                     * 00520000
*   4. ADD A MEMBER TO A PARTITIONED DATA SET.                        * 00540000
*                                                                     * 00560000
*   5. ASSIGN AN ALIAS NAME TO ANY MEMBER UPDATED                     * 00580000
*                                                                     * 00600000
*   6. CHANGE RECORDS IN A DATA SET                                   * 00620000
*                                                                     * 00640000
*   7. HANDLE RECORDS WITH NON-STANDARD SEQUENCE NUMBER FIELDS AND    * 00660000
*   LENGTHS OF SEQUENCE NUMBERS FROM 1 TO 8 CHARACTERS.               * 00680000
*                                                                     * 00700000
*   8. UPDATE ONLY SPECIFIED PORTION OF DATA SET RECORDS.             * 00720000
*                                                                     * 00740000
*   9. UPDATE INPLACE DATA SETS OR MEMBERS ON DIRECT ACCESS VOLUMES.  * 00760000
*                                                                     * 00780000
*ENTRY POINTS- ENTERED FROM THE OPERATING SYSTEM AT 'IEBUPDTE'.       * 00800000
*                                                                     * 00820000
*INPUT- SYSIN WHICH CONSISTS OF CONTROL CARDS AND LOGICAL DATA        * 00840000
*   RECORDS FOR INSERTION OR REPLACEMENT, THE INPUT DATA SET.         * 00860000
*                                                                     * 00880000
*       SYSUT1 WHICH DEFINES THE 'OLD MASTER' DATA SET WHICH MAY BE   * 00900000
*   PARTITIONED OR PHYSICAL SEQUENTIAL IN ORGANIZATION.               * 00920000
*                                                                     * 00940000
*OUTPUT-SYSPRINT WHICH CONTAINS DIAGNOSTIC MESSAGES, THE CONTENTS OF  * 00960000
*   SYSIN AND-IF THE 'LIST=ALL' CONDITION IS PRIMED- THE ENTIRE       * 00980000
*   'NEW MASTER' IS LISTED ON THE DEVICE DESIGNATED.                  * 01000000
*                                                                     * 01020000
*       SYSUT2 WHICH DEFINES THE 'NEW MASTER' DATA SET. IT WILL BE A  * 01040000
*   COMPOSITE OF THE SYSIN AND SYSUT1 DATA SETS. IT CAN BE EITHER     * 01060000
*   PARTITIONED OR PHYSICAL SEQUENTIAL IN ORGANIZATION.               * 01080000
*                                                                     * 01100000
*EXITS- NORMAL- A RETURN IS GIVEN TO THE SCHEDULAR MODULE OF THE      * 01120000
*   OPERATING SYSTEM. CONDITION CODE RETURNED ON A SUCCESSFUL RUN     * 01140000
*   WILL BE ZERO.                                                     * 01160000
*                                                                     * 01180000
*EXITS- ERROR- IF AN ERROR OCCURS, A CODE IS STORED, A MESSAGE        * 01200000
*   GIVEN TO THE USER EXPLAINING THE ERROR CONDITION AND A            * 01220000
*   RETURN TO THE OPERATING SYSTEM SCHEDULAR MODULE IS GIVEN.         * 01240000
*   RETURN CODE GIVEN ARE-                                            * 01260000
*   00 NORMAL TERMINATION OF A SUCCESSFUL RUN                         * 01280000
*   04 ERROR WITHIN A MEMBER- WILL TRY TO PROCESS NEXT MEMBER IF PODS * 01300000
*   12 UNRECOVERABLE ERROR- TERMINATION OF JOB.                       * 01320000
*   16 ERROR IN LABEL HANDLING IN USERS ROUTINE- TERMINATION OF JOB   * 01340000
*                                                                     * 01360000
*EXTERNAL ROUTINES- I/O HANDLED AT READ/WRITE LEVEL, GETMAIN, LINK,   * 01380000
*   BLDL, FIND, STOW USED.                                            * 01400000
*                                                                     * 01420000
*TABLES/WORK AREAS-                                                   * 01440000
*   -KEYTAB- LIST OF VALID KEY WORDS USED ON CONTROL CARDS            * 01460000
*   -COMDTABL- LIST OF VALID COMMAND WORDS USED ON CONTROL CARDS      * 01480000
*   -SWITCHRD- BUFFER CONTAINING CONTROL CARD AND SWITCHES USED       * 01500000
*   BY CARD SCAN ROUTINE.                                             * 01520000
*   -LOGOUTAR- BUFFER WHICH IS USED BY LOG OUT ROUTINE FOR MESSAGES.  * 01540000
*   -STARTAB- TABLE OF SWITCHES WHICH ARE RESET TO ZERO UPON ENTRY    * 01560000
*   TO MODULE. ALL SWITCHES NEEDING REINITIALIZATION SHOULD BE PUT    * 01580000
*   INTO THIS TABLE.                                                  * 01600000
*                                                                     * 01620000
*ATTRIBUTES- SERIAL REUSABLE, BLOCK LOADS                               01640000
*                                                                     * 01660000
*********************************************************************** 01670017
*FUNCTION/OPERATION OF THIS CSECT--IEBUPDTE--                      UL0H 01680017
*        1.    GETS CORE FOR THE COMMUNICATION REGION IEBUPCON     UL0H 01690017
*        2.    GIVES CONTROL TO THE CSECT IEBUPNIT TO INITIALIZE   UL0H 01700017
*              DCB'S AND THE COMMUNICATION REGION.                 UL0H 01710017
*        3.    THE DCB'S ARE MOVED INTO THIS CSECT                 UL0H 01720017
*        4.    CONTROL IS THEN PASSED TO IEBUPDTE2                 UL0H 01730017
*********************************************************************** 01740017
         EJECT                                                          01780000
*     SYMBOLIC REGISTER NOTATION   GR0 - GR15                           01800000
GR0      EQU   0                                                        01820000
GR1      EQU   1                                                        01840000
GR2      EQU   2                                                        01860000
GR3      EQU   3                                                        01880000
GR4      EQU   4                       REGISTER RESERVED FOR CSECT1     01900000
GR5      EQU   5                                                        01920000
GR6      EQU   6                                                        01940000
GR7      EQU   7                                                        01960000
GR8      EQU   8                                                        01980000
GR9      EQU   9                                                        02000000
GR10     EQU   10                                                       02020000
GR11     EQU   11                                                       02040000
GR12     EQU   12                                                       02060000
GR13     EQU   13                                                       02080000
GR14     EQU   14                                                       02100000
GR15     EQU   15                                                       02120000
         SPACE 1                                                        02140000
IEBUPDTE CSECT                                                          02160000
*1132059200,077400,078400,080800,081400,082000,094400,095000,      BS0H 02162017
*105400-105600,112200,115400                                       BS0H 02164017
*1132016800-017600,023430-023480,025200-082000,082600-104600       UL0H 02166017
*1132109000-111800,112200,112600-113000,113400-125000              UL0H 02168017
*                                                                A38775 02170021
         SPACE 2                                                        02172617
*********************************************************************** 02173217
****************************     W A R N I N G     ******************** 02173817
*        REGISTER 12 IS USED TO CONTAIN THE ADDRESS OF THE COM-    UL0H 02174417
*     MUNICATION REGION---DO NOT USE ALTER REGISTER IN ANY CSECT.  UL0H 02175017
*                                                                  UL0H 02175617
*        REGISTER 4 IS THE BASE REGISTER FOR THE EXIT ROUTINE      UL0H 02176217
*     CSECT,IEBUPXIT.DO NOT ALTER THIS REGISTER IN ANY CSECT.      UL0H 02176817
*                                                                  UL0H 02177417
*********************************************************************** 02178017
         SPACE 2                                                      * 02178617
         SPACE 1                                                        02300000
         EXTRN CTERROUT                                            UL0H 02303017
         EXTRN CTHDREXT                                            UL0H 02306017
         EXTRN CTTRLEXT                                            UL0H 02309017
         EXTRN CTDCBEX                                             UL0H 02312017
         EXTRN OMEOD                                               UL0H 02315017
         EXTRN  OMERROUT                                           UL0H 02318017
         EXTRN OMHDREXT                                            UL0H 02321017
         EXTRN OMTRLEXT                                            UL0H 02324017
         EXTRN OMDCBEX                                             UL0H 02327017
         EXTRN EODCT                                               UL0H 02330017
         EXTRN NMERROUT                                            UL0H 02333017
         EXTRN NMHDREXT                                            UL0H 02336017
         EXTRN NMTRLEXT                                            UL0H 02339017
         EXTRN NMDCBEX                                             UL0H 02342017
         SAVE  (14,12)                                             UL0H 02345017
         BALR  GR2,0                                               UL0H 02348017
         USING AHEAD,GR2                                           UL0H 02351017
         USING IEBUPCON,GR12           POINTER TO DSECT AREA            02360000
AHEAD    LR    GR10,GR1                LOAD PARAMETER INTO STABLE REG10 02380000
         LA    GR0,DSECTSIZ            GET SIZE OF DSECT                02400000
         GETMAIN  R,LV=(GR0)                                            02420000
         LR    GR12,GR1                POINTER TO DSECT AREA            02440000
         XC    IEBUPCON(256),IEBUPCON  CLEAR IEBUPCON BEFORE USE A38775 02450021
         XC    IEBUPCON+1*256(256),IEBUPCON+1*256                A38775 02452021
         XC    IEBUPCON+2*256(256),IEBUPCON+2*256                A38775 02454021
         XC    IEBUPCON+3*256(256),IEBUPCON+3*256                A38775 02456021
         XC    IEBUPCON+4*256(256),IEBUPCON+4*256                A38775 02458021
         XC    IEBUPCON+5*256(256),IEBUPCON+5*256                A38775 02458421
         XC    IEBUPCON+6*256(256),IEBUPCON+6*256                A38775 02458821
         XC    IEBUPCON+7*256(256),IEBUPCON+7*256                A38775 02459221
         XC    IEBUPCON+8*256(232),IEBUPCON+8*256                A38775 02459621
         ST    GR1,DSECTLOC            SAVE ADDRESS OF COMMON AREA      02460000
         ST    GR13,SAVE13             SAVE SETTING FOR SUPERVISOR      02480000
         LA    GR13,SAVEREG            ADDRESS OF THIS CSECT SAVE AREA  02500000
         LR    GR1,GR10                 RELOAD PARM LIST ADDRESS   UL0H 02600017
         L     GR15,INIT                ADDRESS OF INITIALIZE MODULUL0H 02700017
         BALR  GR14,GR15                GIVE CONTROL TO IEBUPNIT   UL0H 02800017
         L     GR10,DCBCT               ADDRESS OF SYSIN DCB       UL0H 02900017
         LA    GR11,DCBCTR             ADDRESS OF SYSIN DCB        UL0H 03000017
         ST    GR11,DCBCT              POINTER TO SYSIN DCB        UL0H 03100017
         BAL   GR14,COMPINIT            MOVE SYSIN DCB             UL0H 03200017
         L     GR10,DCBOM               ADDRESS OF OM DCB          UL0H 03300017
         LA    GR11,DCBOMR             ADDRESS OF SYSUT1 DCB       UL0H 03400017
         L     GR9,36(GR10)            ADDR OF DCB EXIT LIST       UL0H 03500017
         MVC   OMEXITS(52),0(GR9)       MOVE ALL  EXIT LISTS       UL0H 03600017
         ST    GR11,DCBOM              POINTER TO SYSUT1 DCB       UL0H 03700017
         BAL   GR14,COMPINIT            MOVE  O.M. DCB             UL0H 03800017
         L     GR10,DCBNM               ADDRESS OF N.M. DCB        UL0H 03900017
         LA    GR11,DCBNMR             ADDRESS OF SYSUT2 DCB       UL0H 04000017
         ST    GR11,DCBNM              POINTER TO SYSUT2 DCB       UL0H 04100017
         BAL   GR14,COMPINIT            MOVE N.M. DCB              UL0H 04300017
OPENINPT MVI   BLOKSW,X'0'        INITIALIZE BLOCKSIZE FLAG        UL0H 04500017
         OPEN DCBCTR              OPEN SYSIN DCB                   UL0H 04800017
         L     GR1,DCBCT          ADDRESS OF SYSIN DCB             UL0H 05100017
         LA    GR5,SYSINNM        ADDRESS OF DD NAME               UL0H 05400017
         TM    BOFLGS(GR1),BIT10  WAS THERE AN OPEN ERROR          UL0H 05700017
         BZ    BADOPEN            IF YES,PRINT ERROR AND QUIT      UL0H 06000017
         CLI   BLOKSW,X'0'        IS BLOCKSIZE A MULTIPLE OF 80    UL0H 06300017
         BNE   BADOPEN1           IF NOT PUT OUT MESSAGE AND QUIT  UL0H 06600017
         CLI   LBLRETCD,BIT10     USER ASKED TO TERMINATE          UL0H 06900017
         BE    USERTERM           IF SO, DO SO                     UL0H 07200017
         LA    GR1,INPUT          ADDRESS OF INPUT BUFFER          UL0H 07500017
         ST    GR1,READINAP       SAVE BUFFER ADDRESS              UL0H 07800017
ENTRNEXT L     GR15,PROCESS            ADDRESS OF MAIN PROCESSING CSECT 08220000
         BR    GR15                    ENTER NEXT STAGE- NO RETURN      08240000
COMPINIT MVC   SAVEREG+20(3),37(11)                                UL0H 08250017
         MVC   0(98,GR11),0(GR10)                                  UL0H 08260017
         MVC   37(3,GR11),SAVEREG+20                               UL0H 08270017
         BR    GR14                                                UL0H 08280017
         SPACE 2                                                        08320817
BADOPEN1 CLOSE  (DCBCTR,LEAVE)                                     UL0H 08321617
         FREEPOOL  DCBCTR                                          UL0H 08322417
BADOPEN  LA    GR7,SYSINNM        DD NAME FOR MESSAGE              UL0H 08324817
         LA    GR11,NTOPMSG       NOT OPEN MSG CONSTANT            UL0H 08325617
ALMOSTZ  LA    GR15,12            CONDITION CODE                   UL0H 08326417
         LA    GR14,CLOSENOW      PREPARE TO QUIT AFTER MESSAGE    UL0H 08327317
         CH    GR15,HICONCDE      HAS HIGHER CON CODE BEEN STORED  UL0H 08328217
         BNH   FINERRT            IF SO, USE THAT CODE             UL0H 08329117
         STH   GR15,HICONCDE      IF NOT, RETURN CON CODE OF 12    UL0H 08330017
FINERRT  L     GR15,PRIMEWRT      ADDRESS OF IEBUPLOG         2    UL0H 08330917
         BR    GR15               PRINT MESSAGE AND RETURN HERE    UL0H 08331817
CLOSENOW MVI   QUITALL,X'0E'      FLAG AS LAST MESSAGE AND TERMINATUL0H 08332717
         L     GR15,PRIMEWRT      ADDRESS OF IEBUPLOG              UL0H 08333617
         BR    GR15               IEBUPLOG WILL RETURN TO SYSTEM   UL0H 08334517
USERTERM MVI   CTEXITS+8,X'85'    FLAG DCB DCB EXIT AS LAST        UL0H 08335417
         LA    GR11,USRTRMSG      PRINT USER REQ TERMINATION MSG   UL0H 08336317
         MVI   HICONCDE,X'10'     CON CODE=USER TERMINATION        UL0H 08337217
         LA    GR14,CLOSENOW      RETURN TO TERMINATE PROCESSING   UL0H 08338117
         B     FINERRT                                             UL0H 08339017
*                                                                       08340000
*        DCB EXIT LISTS FOR SYSIN, SYSUT1 AND SYSUT2 DATA SETS.    UL0H 08360017
*                                                                       08380000
         DS    0F                                                       08400000
OMEXITS  DC    X'01'                                               UL0H 08480017
         DC    AL3(OMHDREXT)                                       UL0H 08560017
         DC    X'03'                                               UL0H 08640017
         DC    AL3(OMTRLEXT)                                       UL0H 08720017
         DC    A(0)                                                UL0H 08800017
         DC    X'85'                                               UL0H 08880017
         DC    AL3(OMDCBEX)                                        UL0H 08960017
         DC    X'07'                                               UL0H 09040017
         DC    AL3(0)                                              UL0H 09120017
NMEXITS  DC    X'02'                                               UL0H 09200017
         DC    AL3(NMHDREXT)                                       UL0H 09280017
         DC    X'04'                                               UL0H 09360017
         DC    AL3(NMTRLEXT)                                       UL0H 09440017
         DC    X'00'                                               UL0H 09520017
         DC    AL3(0)                                              UL0H 09600017
         DC    X'85'                                               UL0H 09680017
         DC    AL3(NMDCBEX)                                        UL0H 09760017
         DC    X'07'                                               UL0H 09840017
         DC    AL3(0)                                              UL0H 09920017
CTEXITS  DC    X'00'                                               UL0H 10000017
         DC    AL3(CTHDREXT)                                       UL0H 10080017
         DC    X'00'                                               UL0H 10160017
         DC    AL3(CTTRLEXT)                                       UL0H 10240017
         DC    X'85'                                               UL0H 10320017
         DC    AL3(CTDCBEX)                                        UL0H 10400017
         EJECT                                                          10480000
*   SYSIN DCB   CONTROL CARD INPUT     REFERRED TO AS CT                10500000
         SPACE 1                                                        10520000
DCBCTR   DCB   DSORG=PS,MACRF=(GL),DDNAME=SYSIN,RECFM=FB,LRECL=80, BS0H*10540017
               EODAD=EODCT,EXLST=CTEXITS,SYNAD=CTERROUT,BUFNO=1    BS0H 10560017
         EJECT                                                          10580000
*   SYSUT1 DCB  OLD MASTER             REFERRED TO AS OM                10600000
         SPACE 1                                                        10620000
DCBOMR   DCB   DSORG=PO,MACRF=R,DDNAME=SYSUT1,NCP=1,               2039C10640014
               EXLST=OMEXITS,EODAD=OMEOD,SYNAD=OMERROUT                 10660000
         EJECT                                                          10680000
*   SYSUT2 DCB  NEW MASTER             REFERRED TO AS NM                10700000
         SPACE 1                                                        10720000
DCBNMR   DCB   DSORG=PO,MACRF=W,DDNAME=SYSUT2,NCP=1,               2039C10740014
               EXLST=NMEXITS,SYNAD=NMERROUT                             10760000
         SPACE 1                                                        10780000
         EJECT                                                          10800000
         SPACE 1                                                        10820000
         SPACE 1                                                        10840000
         SPACE 1                                                        10860000
*   CONSTANTS FOR INITIALIZATION ROUTINE                                10880000
SAVEREG  DS    18F                     SAVE AREA USED BY READ/WRITE     11200000
DSECTLOC DC    F'0'                    COMMON AREA ADDRESS JUST IN CASE 11240000
CTERROR  DC    V(CTERROUT)                                         UL0H 11250017
PRIMEWRT DC    V(IEBUPLOG)                                         UL0H 11260017
         SPACE 1                                                        11280000
PROCESS  DC    V(IEBUPDT2)             ENTRY POINT PROCESSING CSECT     11300000
         SPACE 1                                                        11340000
INIT     DC    V(IEBUPNIT)                                         UL0H 11380017
BIT10    EQU   X'10'                                               UL0H 11420017
         SPACE 1                                                        11480000
         EJECT                                                          11760000
         IEBUPCON                                                       11780000
         DCBD                                                           11880017
BOFLGS  EQU   DCBOFLGS-IHADCB                                           11980017
         SPACE 2                                                        12080017
         END   IEBUPDTE                                                 12520000
