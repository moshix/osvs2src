         TITLE 'IEBVDM - DIRECTORY MERGE SUBROUTINE OF IEBCOPY'         00200019
IEBVDM   CSECT                                                          00400019
*D082300                                                         A37901 00403000
*C827000                                                         A41780 00405000
*C045800,716800                                                  A44144 00405400
*A045820,716700-716720,717300,720100                             A44144 00405800
*A433300-433400                                                 YA01719 00405902
*A471500                                                       @ZA01673 00406102
*********************************************************************** 00406302
*TITLE - IEBVDM - IEBCOPY DIRECTORY MERGE                             * 00412019
*                                                                     * 00418019
*STATUS - CHANGE LEVEL 001                                            * 00424019
*                                                                     * 00430019
*FUNCTION/OPERATION - MERGES THE INPUT DIRECTORY ENTRIES WITH THE     * 00436019
*        OUTPUT DIRECTORY ENTRIES                                     * 00442019
*ENTRY POINTS - IEBVDM                                                * 00448019
*                                                                     * 00454019
*INPUT - INPUT DIRECTORY ENTRIES                                      * 00460019
*        OUTPUT DIRECTORY ENTRIES                                     * 00466019
*                                                                     * 00472019
*OUTPUT - A MERGED OUTPUT DIRECTORY                                   * 00478019
*        THE NUMBER OF BLOCKS IN THE OUTPUT DIRECTORY                 * 00484019
*                                                                     * 00490019
*EXITS - NORMAL - RETURN TO CALLER                                    * 00496019
*                                                                     * 00502019
*EXTERNAL ROUTINES - IEBDV4,AN ENTRY POINT IN IEBDV1                  * 00508019
*                  IEBDRD(FOR READING IN OUTPUT + INPUT DIRECTORY ENT-* 00514019
*                  RIES)                                              * 00520019
*                  IEBMGD -GETS AN OUTPUT DIRECTORY ENTRY             * 00526019
*                  IEBMGI                                             * 00532019
*                  IEBVMS                                             * 00538019
*TABLES/WORK AREA - I/O BUFFERS                                       * 00544019
*                  AREAS WHERE INPUT + OUTPUT DIRECTORY ENTRIES HAVE  * 00550019
*                  BEEN RETAINED                                      * 00556019
*ATTRIBUTES - SERIALLY REUSEABLE                                      * 00562019
*********************************************************************** 00568019
TWNTY    EQU   20                 OFFSET FROM BEGINNING OF BLK TO COUNT 00600019
TW2      EQU   22                 OFFSET FROM BEG. OF BLK TO 1ST ENTRY  00800019
FF       EQU   X'FF'                                                    01000019
OPEN     EQU   X'10'              MASK USED FOR TESTING DCBOFLGS        01200019
R0       EQU   0                                                        01400019
R1       EQU   1                                                        01600019
R2       EQU   2                                                        01800019
R3       EQU   3                                                        02000019
R4       EQU   4                                                        02200019
R5       EQU   5                                                        02400019
R6       EQU   6                                                        02600019
R7       EQU   7                                                        02800019
R8       EQU   8                                                        03000019
R9       EQU   9                                                        03200019
R10      EQU   10                                                       03400019
R11      EQU   11                                                       03600019
R12      EQU   12                                                       03800019
R13      EQU   13                                                       04000019
R14      EQU   14                                                       04200019
R15      EQU   15                                                       04400019
SAV8     EQU   8                  SAVE AREA DISPLACEMENT                04410019
SAV4     EQU   4                  SAVE AREA DISPLACEMENT                04420019
FLGSET   EQU   X'00'              FLAG SETTING                          04430019
LEN1     EQU   1                  LENGTH OR DISPLACEMENT OF 1           04440019
LEN2     EQU   2                  LENGTH OR DISPLACEMENT OF 2           04450019
LEN3     EQU   3                  LENGTH OR DISPLACEMENT OF 3           04460019
LEN4     EQU   4                                                        04470019
LEN8     EQU   8                  LENGTH OR DISPLACEMENT OF 8           04480019
DISP4    EQU   4                  DISPLACEMENT OR LENGTH OF 4           04490019
LEN254   EQU   254                LENGTH OF 254                         04500019
DISP0    EQU   0                  0 DISPLACEMENT                        04510019
LEN11    EQU   11                 LENGTH OF 11                          04520019
LEN27    EQU   27                 LENGTH OF 27                          04530019
LEN26    EQU   26                 LENGTH OF 26                          04540019
LEN10    EQU   10                 LENGTH OF 10                          04550019
LEN7     EQU   7                  LENGTH OF 7                           04560019
MSGN     EQU   X'47'              OFFSET IN MSG FOR NAME                04570019
RTCDE    EQU   C'8'               RETURN CODE                    A44144 04580000
RTCD12   EQU   X'FC'              RETURN CODE                    A44144 04582000
ZERO     EQU   0                  VALUE OF ZERO                  S20201 04590020
         EJECT                                                          04600019
         SAVE  (14,12),,*                                               04800019
         BALR  R11,R0             BASE REG                              05000019
         USING *,R11               ESTABLISH ADDRESSABILITY             05200019
         USING IEBMCA,4                                                 05300019
         LA    R12,SV6                                                  05400019
         ST    R12,SAV8(R13)      SAVE ADDRESS OF CALLED ROUTINES SAVE  05600019
*                                  AREA                                 05800019
         ST    R13,SAV4(R12)      SAVE ADDRESS OF CALLERS SAVE AREA     06000019
         LR    R13,R12                                                  06200019
         OI    FLG7,DM            INDICATE THAT DIR MERGE IS BEING DONE 06400019
         NI    WSFLAG,FF-USERTTR       ASSURE NOTELIST/USER TTR SWITCH  06660019
*                                        IS OFF                         06720019
         NI    FLG4,FF-LE         ASSURE THAT LAST INPUT ENTRY BIT OFF  06800019
         MVI   FLG5,FLGSET        RESET FLAGS                           06900019
         XC    BC(LEN2),BC        INITIALIZE BC TO 0                    07000019
         XC    DMSW(LEN1),DMSW    RESET INTERNAL SWITCHES               07100019
         LA    R15,LEN1                                                 07200019
         STH   R15,DBCNT          INIT OUTPUT DIRECTORY BLK CT TO 1     07400019
         LA    R15,TW2            INITIAL VALUE FOR DISPLACEMENT        07600019
         STH   R15,DISPO          INITIALIZE DISPLACEMENT TO 22         07800019
         MVI   UT4SCTOR,ZERO      SET UT4 SECTOR VALUE TO 0      S20201 07820020
*                                   (USED ONLY IF ON AN RPS      S20201 07840020
*                                   DEVICE)                      S20201 07860020
         MVI   OUTSCTOR,ZERO      SET OUTPUT SECTOR VALUE TO 0   S20201 07880020
*                                   (USED ONLY IF ON AN RPS      S20201 07900020
*                                   DEVICE)                      S20201 07920020
         L     R15,VIEBMGI                                              08000019
TRNI     EQU   *                                                        08100019
         BALR  R14,R15            GET FIRST IDE                         08200019
         TM    SWITCH1,MGINODE    WAS NO IDE RETURNED BY IEBMGI         08206019
         BZ    GOTIDE1            NO - AN IDE WAS RETURNED AND IS POIN- 08212019
*                                 TED TO BY 'IDEAD'                     08218019
         NI    SWITCH1,FF-MGINODE YES - RESET 'NO IDE' SWITCH           08224019
         TM    FLG4,LE            WAS THIS THE ONLY/LAST IDE TO  A37901 08228000
*                                 BE MERGED                      A37901 08232000
         BZ    TRNI               NO - TRY FOR ANOTHER ONE              08236019
         OI    DMSW,E             YES - INDICATE THAT THERE ARE NO MORE 08242019
*                                 INPUT DE'S TO BE MERGED               08248019
GOTIDE1  EQU   *                                                        08254019
         XC    OUTDS1(LEN8),OUTDS1 SET TTRN IN AREA TO ZEROES           08294019
         MVC   OUTDS1+DISP4(LEN2),TRBALBUF SET TRK BAL FOR OUTPUT DIR   08334019
         TM    COMDCDSW,COMPRESS  IS A COMPRESS BEING DONE              08400019
         BO    NULLOUT            IF NO OUTPUT DATA SET, SET UP TO      08800019
*                                 PROCESS INPUT ONLY                    09000019
         TM    STATUS,STAT5       IS OUTPUT NULL                        09200019
         BO    NULLOUT            IF SO PROCESS INPUT ONLY              09400019
         TM    FLG6,OIC           ALL OUTPUT IN CORE                    09600019
         BO    NULLOUT            YES, DONT NEED TO READ ODE'S          09800019
         TM    FLG4,ODALT         NEED TO READ OUTPUT FROM SYSUT4       10000019
         BO    WROUT              YES- SETUP TO READ FROM UT4           10200019
         LA    R6,OUTDCB          ESTABLISH PTR TO DCB TO BE USED       10400019
*                                 WHEN READING OUTPUT DIR               10600019
         ST    R6,READDCB                                               10800019
         NI    STATUS,STAT7+STAT8                                       11000019
         OI    STATUS,STAT2+STAT6 INDIC  MERGING DIR FROM OUTPUT D.S.   11200019
         LA    R5,UT4DCB                                                11400019
         ST    R5,WRTDCB          ESTABLISH UT4DCB AS DCB TO BE USED    11600019
*                                 WHEN WRITING MERGED DIRECTORY         11800019
         OI    FLG4,ODALT         SET INDICATION OF WHERE OUTPUT DIREC- 11850019
*                                 TORY IS TO BE READ FROM FOR NEXT INDD 11900019
*                                 PROCESSING                            11950019
         MVC   UT4DS1(LEN8),SYS4TTR RE-INIT TTRN AND TRK BALANCE AREA   12050019
*                                 IF DCB NOT YET OPEN, DOESN'T MATTER   12200019
         NI    TAG,TAG5+TAG6                                            12400019
         OI    TAG,TAG3           INDIC WRITING TO SYSUT4 DS            12600019
         USING IHADCB,R5                                                12800019
         TM    DCBOFLGS,OPEN      IS SYSUT4 DCB OPEN                    13000019
         BO    SETRDNXT           YES - SET READ AND WRITE BFR PTRS     13200019
         DROP  R5                                                       13300019
         L     R15,VIEBM04        ADDR OF E.P. IN MAIN PROGRAM TO CALL  13380019
*                                 IEBDV4 TO OPEN/VALIDATE SYSUT4 DCB    13460019
         BALR  R14,R15            EXIT TO E.P.                          13540019
IEBRTM   EQU   *                  RETURN POINT IN IEBVDM FROM MAIN PGM  13620019
IEBVDRTM EQU   IEBRTM                                                   13660019
         ENTRY IEBVDRTM                                                 13730019
SETRDNXT MVC   READNEXT+LEN1(LEN3),BEGFST1  SET BFR ADDR FOR READ       13830019
         MVC   ENDWRT(DISP4),END2ND2   END OF WRT BFR                   13930019
         L     R7,BEGFST2                                               14300019
         XC    TW2(LEN254,R7),TW2(R7)  CLEAR AREA TO BLD 1ST OUTPUT BLK 14400019
         ST    R7,WRTNEXT         SET POINTER FOR IEBDWR                14500019
         MVI   DISP0(R7),FLGSET        SET F=0                          14550019
         ST    R7,BLOCK           SET INTERNAL POINTER TO 1ST BLOCK     14600019
         TM    FLG6,ODIC          ANY ODE'S IN CORE                     14800019
         BO    SOPTR              IF SO, LOCATE 1ST SAVED BLOCK         15000019
         SR    R0,R0              SET TTR 0 FOR BEGIN OF DIR TO BE READ 15200019
         L     R1,READDCB         ADDR OF DCB                           15400019
         LA    R2,FDAD(R1)        POINT TO DCBFDAD FIELD FOR MBBCCHHR   15600019
         L     R15,VTTOCC         CONVERT TTR TO MBBCCHHR               15800019
         BALR  R14,R15            LINK TO ROUTINE                       16000019
RDOUT    EQU   *                                                        16100019
         NI    SENSE,SENSE7+SENSE8 RESET POSSIBLE UNWANTED BITS         16200019
         L     R15,VIEBDRD        ADDR OF READ ROUTINE                  16300019
         BALR  R14,R15            LINK TO READ OUT DIR INTO I/O BFR     16400019
GOBFR    L     R15,VIEBMGD                                              16800019
         BALR  R14,R15            OBTAIN PTR TO 'NEXT' ODE IN BFR       17000019
         B     CKDEPTR            THIS INSTRUCTION WILL BE EXECUTED IF  17200019
*                                 IEBMGD DOES NOT RETURN A PTR TO THE   17400019
*                                 ODE                                   17600019
TME      TM    DMSW,E             ANY INPUT DE'S TO COMPARE AGAINST     17800019
         BO    DOOUT              IF NOT, JUST PUT THIS ODE INTO THE    18000019
*                                 OUTPUT DIRECTORY                      18200019
SETCOMP  L     R2,IDEAD           ADDR OF IDE                           18400019
         L     R6,DEPTR           ADDR OF ODE                           18600019
         CLC   DISP0(LEN8,R2),DISP0(R6) COMPARE INPUT MBRNAME VS OUTPUT 18700019
*                                       NAME                            18800019
         BE    TOIC               IF SAME, GET NEXT ODE                 19000019
         BNL   DOOUT              IF OUTPUT LOW MERGE IT                19200019
SETI     OI    DMSW,I             INDIC IDE BEING MERGED                19400019
* NOTE - AT THIS POINT, R2 POINTS TO THE DE TO BE MERGED                19600019
ADDADE   SR    R3,R3                                                    19800019
         IC    R3,LEN11(R2)       GET 'C' FIELD FROM THIS DE            20000019
*                                 NEED TO ISOLATE NUMBER OF USER HALFWD 20200019
         SLL   R3,LEN27                                                 20400019
         SRL   R3,LEN26           MPLY BY 2 FOR NBR OF USER BYTES       20600019
         LA    R3,TWLV(R3)        ADD THE FIXED MINIMUM LENGTH TO THE   20800019
*                                 NUMBER OF USER DATA BYTES TO CALC.    21000019
*                                 TOTAL LENGTH OF THIS DE               21200019
BCUPD    LH    R6,BC              GET BYTE COUNT OF CURR BLK BEING BLT  21400019
         AR    R6,R3              ADD LENGTH OF CURR DE                 21600019
         CH    R6,TWO54           DOES BYTE COUNT EXCEED 254            21800019
         BH    NEWBLK             IF SO, FINISH UP CURRENT BLK          22000019
         LH    R8,DISPO           GET CURRENT VALUE OF DISPLACEMENT     22200019
         A     R8,BLOCK           ADD TO STARTING ADDR OF DB            22400019
         BCTR  R3,R0              DECREMENT LENGTH OF DE BY 1 FOR EX    22600019
         EX    R3,ADDENT                                                22800019
         MVC   OLDKEY(LEN8),DISP0(R2)  SAVE NAME OF DE JUST MOVED       23000019
         ST    R8,ADLAST          SAVE ADDR OF LAST DE MERGED IN OUTDIR 23200019
         STH   R6,BC              SAVE UPDATED BYTE-COUNT               23300019
         LA    R6,TW2(R6)         ADD 22 TO CURR VALUE OF BC            23400019
         STH   R6,DISPO           SET AS NEW VALUE OF DISPO             23600019
         TM    DMSW,I             WAS AN IDE JUST MERGED                23800019
         BZ    CKR                IF NOT, SEE IF THIS WAS LAST ODE      24000019
         NI    DMSW,FF-I          ELSE RESET IDE-MERGED SWITCH          24200019
         L     R15,TABAD                                                24400019
         LA    R14,LEN10                                                24600019
         SR    R15,R14            POINT TO CTLTAB ENTRY FOR THIS MEMBER 24800019
         MVC   LEN2(LEN8,R15),OLDKEY PUT NAME OF IDE MERGED INTO CTL-   24900019
*                                 TAB ENTRY FOR PRINTING BY IEBVTM      25000019
         OI    LEN1(R15),JSTCPD   INDIC NEED TO PRINT THIS NAME IN MSG  25100019
SETBIT4  OI    DISP0(R15),SEBIT4  SET 'DONT-COPY' FLAG IN CTLTAB ENTRY  25200019
         TM    DISP0(R15),SEBIT1  IS THIS A NEWNAME ENTRY               25300019
         BZ    TF4LE              NO, TEST FOR MORE IDE'S TO MERGE      25400019
         L     R14,NNTABAD        GET POINTER TO CURRENT ENTRY IN       25460019
*                                 NNPTRTAB (SET BY IEBMGI)              25520019
         LA    R15,DISP4                                                25540019
         SR    R14,R15            POINT TO CURRENT NNPTRTAB SLOT        25560019
         L     R15,DISP0(R14)     POINT TO RELATED OLDNAME CTLTAB ENTRY 25660019
         B     SETBIT4            GO SET 'DONT-COPY' IN OLDNAME ENTRY   25900019
TF4LE    EQU   *                                                        26000019
         TM    FLG4,LE            ANY MORE IDE'S                        27200019
         BO    SETE               IF NOT, SET INDICATOR 'E'             27400019
         L     R15,VIEBMGI                                              27600019
         BALR  R14,R15            OBTAIN POINTER TO NEXT IDE            27800019
         TM    SWITCH1,MGINODE    WAS NO IDE RETURNED BY IEBMGI         27820019
         BZ    GOTIDEN            NO - AN IDE WAS RETURNED AND IS POIN- 27840019
*                                 TED TO BY 'IDEAD'                     27860019
         NI    SWITCH1,FF-MGINODE YES - RESET 'NO IDE' SWITCH           27880019
         B     TF4LE              SEE IF ANY MORE IDE'S TO BE TRIED FOR 27900019
GOTIDEN  EQU   *                                                        27920019
         TM    DMSW,R             ANY ODE'S                             28000019
         L     R2,IDEAD           POINT TO NXT IDE IN CASE NEED IT      28100019
         BO    SETI               IF NOT, PUT THIS IDE INTO OUT DIR     28200019
         B     SETCOMP            ELSE COMPARE IDE TO ODE               28400019
WROUT    LA    R6,OUTDCB          ESTAB PTR TO DCB TO BE WRITTEN TO     28600019
         ST    R6,WRTDCB                                                28800019
         LA    R5,UT4DCB          ESTAB PTR TO DCB TO READ FROM         29000019
         ST    R5,READDCB                                               29200019
         NI    FLG4,FF-ODALT      RESET INDICATOR - OUTPUT DIRECTORY    29250019
*                                 WILL BE READ FROM OUTPUT DATA SET     29300019
*                                 WHEN PROCESSING NEXT INDD             29350019
         NI    STATUS,STAT7+STAT8                                       29400019
         OI    STATUS,STAT3+STAT6 INDIC READING FROM SYSUT4 AND MERGING 29600019
         NI    TAG,TAG5+TAG6                                            29800019
         OI    TAG,TAG1           INDIC WRTG OUTPUT DIRECTORY TO OUT DS 30000019
         MVC   FDAD(LEN8,R5),UT4FDAD RESET MBBCCHHR OF BEG OF UT4 DATA  30200019
         B     SETRDNXT           SET POINTERS FOR I/O ROUTINES         30400019
DOOUT    L     R2,DEPTR           POINT TO ODE TO BE MERGED             30600019
         B     ADDADE             GO TO MERGE IT                        30800019
NEWBLK   L     R8,BLOCK           PT TO OUTPUT BLK                      31000019
         ST    R8,WKA1            SAVE ADDR OF THIS BLK IN CASE NO MORE 31200019
         LH    R9,BC              GET CT OF NBR BYTES USED IN THIS DB   31400019
         LA    R9,LEN2(R9)        ADD 2 TO ACCOUNT FOR LENGTH OF COUNT  31600019
         STH   R9,BC              SET TO MOVE UPDTD CT INTO DIR BLOCK   31800019
         MVC   TWNTY(LEN2,R8),BC  MOVE TOTAL LENGTH INTO DIRECTORY BLK  32000019
         MVC   TWLV(LEN8,R8),OLDKEY SET UP KEY OF THIS DIRECTORY BLOCK  32200019
         MVC   KLLOC(LEN3,R8),KLDL  SET UP KEY-LENGTH AND DATA-LENGTH   32400019
         LH    R15,DBCNT          ADD 1 TO CURR NBR OUT BLKS BUILT      32800019
         LA    R15,LEN1(R15)                                            33000019
         CH    R15,DIRBCNT        IS THERE SPACE FOR AT LEAST 1 MORE    33200019
         BH    TOOMANY            IF NOT, OUT OF ALLOCATED DIR SPACE    33400019
         STH   R15,DBCNT          ELSE SAVE UPDATED BLOCK-COUNT         33600019
         LA    R8,BLKLEN(R8)      POINT TO NEXT POSSIBLE BLOCK          33800019
         ST    R8,BLOCK           SAVE NEW PTR IN CASE SPACE IN BFR     34000019
         LA    R8,BLKLEN(R8)      CALC, WHERE NXT BLK WOULD END         34200019
         C     R8,ENDWRT          SPACE FOR NEXT BLK                    34400019
         BNL   SETREC             IF NOT SEE IF MORE BFR SPACE AVAIL    34600019
INITNXT  XC    BC(LEN2),BC        RESET BYTE COUNT TO 0                 34700019
         L     R8,BLOCK           RESET POINTER TO ADDR OF NEXT BLOCK   34830019
         MVI   DISP0(R8),FLGSET   SET  F=0                              34880019
         XC    TW2(LEN254,R8),TW2(R8)  CLEAR NEXT BLOCK                 34930019
         LA    R15,TW2            RESET DISPLACEMENT TO 22              35000019
         STH   R15,DISPO                                                35200019
         B     BCUPD              NOW GO PUT NXT DE INTO OUTPUT BLK     35400019
CKR      TM    DMSW,R             ANY MORE ODE'S                        35600019
         BZ    TOIC               IF SO GET NXT ODE TO BE MERGED        35800019
         L     R2,IDEAD           POINT TO IDE IN CASE NEED TO MERGE    36000019
         TM    DMSW,E             ANY MORE IDE'S TO BE MERGED           36200019
         BZ    SETI               YES - GO MERGE NXT IDE                36400019
         B     SETFF              ELSE SET 'FF' RECORD INTO LAST BLK    36600019
SETE     OI    DMSW,E             INDIC NO MORE IDE'S TO BE MERGED      36800019
         TM    DMSW,R             ANY MORE ODE'S TO BE MERGED           37000019
         BZ    DOOUT              IF SO, GET ADDR OF NXT ONE TO MERGE   37200019
SETFF    LH    R7,BC              GET CURR NBR BYTES IN LAST O.D. BLOCK 37400019
         LA    R7,DLEOD(R7)       ADD 12 FOR 'FF'S                      37600019
         L     R8,BLOCK                                                 37800019
         CH    R7,TWO54           ENOUGH ROOM FOR FF'S IN THIS BLOCK    38000019
         BH    CKALLOC            IF NOT, SEE IF ALLOCATION FOR ANOTHER 38100019
*                                 BLOCK IS AVAILABLE                    38200019
         LR    R9,R8              SAVE POINTER TO START OF BLOCK        38400019
         AH    R8,DISPO           ELSE CALC ADDR TO PUT FF'S AT         38600019
         MVI   DISP0(R8),FF                                             38700019
         MVC   LEN1(LEN7,R8),DISP0(R8) MOVE NME OF 'FF' INTO OUTPUT DIR 38800019
         MVC   TWLV(LEN8,R9),DISP0(R8) SET KEY TO THIS BLOCK TO 'FF'    38900019
         MVC   KLLOC(LEN3,R9),KLDL     SET KEY LENGTH AND DATA LENGTH   39000019
         LA    R7,LEN2(R0,R7)     ADD 2 TO ACCOUNT FOR LENGTH OF        39003019
*                                    COUNT                              39006019
         CLC   DBCNT(LEN2),DIRBCNT     ARE FF'S BEING PLACED IN LAST    39010019
*                                   DIRECTORY BLOCK                     39020019
         BNE   NOPAM1             NO, GO AROUND                         39030019
         STH   R7,BPAMOUT+DIRCTDCB  STORE BYTE COUNT IN BPAM DCB        39040019
NOPAM1   EQU   *                                                        39050019
         STH   R7,WKA1            SET BYTE-COUNT INTO AREA TO MOVE FROM 39500019
         MVC   TWNTY(LEN2,R9),WKA1 MOVE BYTE-COUNT INTO DIR BLOCK       39600019
TSTTAG3  TM    TAG,TAG3           IS OUT DIR BEING WRITTEN TO SYSUT4    39800019
         BZ    SETLREP            IF NOT, GO TO WRT THE OUTPUT DIR.     40000019
         LR    R15,R9             SAVE ADDR OF LAST FULL BLOCK IN BFR   40100019
         LA    R9,BLKLEN(R9)      ADD 276 TO CURRENT ADDR OF BLOCK      40200019
         LA    R8,TWLV(R9)        ADD 12 TO ACCOMMODATE AN EOF RECORD   40400019
         C     R8,ENDWRT          ENOUGH ROOM IN BFR FOR THE EOF        40600019
         BL    MV0                IF SO, SET UP AN EOF RECORD           40800019
         OI    DMSW,RETEOD        ELSE INDICATE NEED TO SET UP/WRT EOF  41000019
         MVI   DISP0(R15),LASTREC+ENDPROC  SET FLGS INTO BFR FOR IEBVWS 41100019
SETTOWRT  EQU  *                                                        41200019
         MVC   READNEXT(DISP4),WRTNEXT SET POINTER FOR USE BY IEBVWS    41800019
         L     R15,VIEBVWS        ROUTINE TO SET UP OUTPUT MBBCCHHR'S   42400019
         BALR  R14,R15            GO SET UP AND WRT MERGED OUTPUT DIR   42600019
         TM    DMSW,RETEOD        NEED TO WRITE SEPARATE EOF RECORD     42800019
         BO    SETUPEOF           YES - GO SET UP  EOF RECORD           43000019
DONE     EQU   *                                                        43020019
         TM    FLG4,ODALT         WAS DIRECTORY JUST WRITTEN TO SYSUT4  43040019
         BO    DMTO4              YES - SEE IF I/O ERR OCCURRED TO UT4  43060019
         MVC   ERPLMFD(LEN8),OUTFDAD SET PTR TO MOST RECENT MBBCCHHR OF 43110019
*                                 AN EOF OF MEMBER DATA FOR WHEN MERGED 43160019
*                                 DIRECTORY IS ON THE OUTPUT DATA SET   43180019
         MVC   ERPTTR(DISP4),OUTPTTR SAME FOR TTRN OF LAST MEMBER'S EOF 43200019
         MVC   ERPTB(LEN2),OUTPTTR+DISP4  SAME FOR TRACK BALANCE        43220019
DONE1A   EQU   *                                                        43240019
         NI    FLG4,FF-NEWODS     RESET BIT TO INDICATE THAT THE OUTPUT 43260019
*                                 DATA SET HAS BEEN WRITTEN TO, I.E.,   43280019
*                                 IS NOT UNCHANGED.                     43300019
DONE1    EQU   *                                                        43320019
         MVC   MSG1+LEN1(LEN1),ZROS   CLEAR MSG CODE MAYBE USED YA01719 43330002
*                                         IN IGG019FT           YA01719 43340002
         L     R13,SV6+SAV4       ADDR OF SAVE AREA +4                  43360019
         RETURN (14,12)           RETURN TO CALLER                      43400019
DMTO4    EQU   *                                                        43420019
         MVC   ERPLMFD4(LEN8),OUTFDAD  SET PTR TO LATEST MBBCCHHR OF    43480019
*                                 AN EOF OF MEMBER DATA FOR WHEN MERGED 43540019
*                                 DIRECTORY IS ON THE SYSUT4 DATA SET   43560019
         MVC   ERPTTR4(DISP4),OUTPTTR  SAME TTRN OF LAST MEMBER'S EOF   43580019
         MVC   ERPTB4(LEN2),OUTPTTR+DISP4  SAME FOR TRACK BALANCE       43600019
         B     DONE1A             GO TO EXIT TO CALLER AFTER RESETTING  43620019
*                                 'NEWODS' TO INDIC OUTPUT DS WRITTEN   43640019
MV0      XC    KLLOC(LEN3,R9),KLLOC(R9) SET KEY/DATALENGTH TO 0 FOR EOF 43740019
SETLREP  MVI   DISP0(R9),LASTREC+ENDPROC  SET INDIC FOR IEBVWS USE      43840019
         B     SETTOWRT           GO WRT THRU LAST BLOCK AND/OR EOF     44200019
CKALLOC  EQU   *                                                        44220019
         LH    R15,DBCNT          GET COUNT OF NBR OF BLOCKS BUILT      44240019
         LA    R15,LEN1(R15)           ADD 1                            44260019
         CH    R15,DIRBCNT        IS AT LEAST 1 MORE BLOCK ALLOCATED    44280019
         BH    TOOMANY            IF NOT, CLOBBER LAST BLOCK BUILT      44300019
         STH   R15,DBCNT          ELSE RETAIN UPDATED BLOCK COUNT       44320019
         B     MVNM               GO COMPLETE PREVIOUS BLOCK WITHOUT    44340019
*                                 PUTTING IN 'FF'S                      44360019
SETUPEOF MVC   WRTNEXT(DISP4),BEGFST2  SET UP WHERE TO WRITE FROM FOR   44440019
*                                      IEBDWR                           44520019
         L     R9,WRTNEXT                                               44600019
         XC    LEN1(LEN11,R9),LEN1(R9)                                  44800019
         NI    DMSW,FF-RETEOD     RESET EOD-NEEDED INDICATOR            45000019
         B     SETLREP            EOD RECORD SET UP                     45200019
MVNM     MVC   TWLV(LEN8,R8),OLDKEY    MOVE NAME OF LAST DE INTO KEY    45400019
         LH    R7,BC              NBR BYTES USED IN THIS (LAST) BLOCK   45600019
         LA    R7,LEN2(R7)        ADD 2 FOR LENGTH OF COUNT AREA        45800019
         STH   R7,WKA1                                                  46100019
         MVC   TWNTY(LEN2,R8),WKA1 SAVE NO. IN COUNT FIELD OF OUTPUT BL 46160019
         MVC   KLLOC(LEN3,R8),KLDL SET KEYLENGTH AND DATALENGTH         46220019
         LR    R9,R8              SAVE ADDR OF THIS BLOCK               46300019
         LA    R8,BLKLEN(R8)      LOCATE BEGIN NXT POSSIBLE BLK         46400019
         LA    R6,BLKLEN(R8)                                            46600019
         C     R6,ENDWRT          ROOM IN BUFFER FOR LAST BLK           46800019
         BL    CLEAR              IF SO, SET UP DIR BLK WITH FF'S       47000019
         MVI   DISP0(R9),LASTREC+ENDPROC INDIC LAST REC TO BE PROCESSED 47100019
         MVC   READNEXT(DISP4),WRTNEXT SET POINTER FOR IEBWSU  @ZA01673 47150002
         L     R15,VIEBVWS                                              47200019
         BALR  R14,R15            ELSE GO WRT DIR TO (BUT NOT THRU)     47400019
*                                 FF'S RECORD, SINCE IT IS NOT YET SET  47600019
*                                 UP                                    47800019
FAKEFFS  EQU   *                                                        47900019
         MVC   WRTNEXT(DISP4),BEGFST2  SET UP WHERE TO WTE FROM FOR DWR 48000019
         L     R9,WRTNEXT                                               48200019
XC1      EQU   *                                                        48300019
         MVI   DISP0(R9),FLGSET   SET F BYTE = 0                 A33294 48350020
         XC    TW2(LEN254,R9),TW2(R9)  CLEAR LAST BLOCK                 48400019
         MVI   TWLV(R9),FF                                              48600019
         MVC   TWLV+LEN1(LEN7,R9),TWLV(R9)  SET KEY TO FF'S             48800019
         MVC   TW2(LEN8,R9),TWLV(R9)  MOVE FF'S INTO NAME FIELD         49000019
         MVC   KLLOC(LEN3,R9),KLDL  SET KEY-LENGTH AND DATA-LENGTH      49200019
         MVC   TW2-LEN2(LEN2,R9),FRTEEN  SET LENGTH FIELD IN DE TO 14   49400019
         CLC   DBCNT(LEN2),DIRBCNT     ARE FF'S BEING PLACED IN LAST    49420019
*                                   DIRECTORY BLOCK                     49440019
         BNE   NOPAM2             NO, GO AROUND                         49460019
         LA    R15,FAKELEN        LOAD LENGTH OF BLOCK                  49480019
         STH   R15,BPAMOUT+DIRCTDCB  STORE IN BPAM DCB                  49500019
NOPAM2   EQU   *                                                        49520019
         B     TSTTAG3            CK WHERE TO WRT MERGED OUT DIR        49600019
CLEAR    LR    R9,R8              GET PTR TO NXT BLOCK                  49800019
         B     XC1                GO SET UP FF'S RECORD                 50000019
NULLOUT  MVC   BLOCK+LEN1(LEN3),BEGFST1  INIT PTR TO 1ST BLK TO BUILD   50200019
         L     R8,BLOCK                                                 50400019
         XC    TW2(LEN254,R8),TW2(R8)  CLEAR AREA FOR 1ST BLK           50600019
         ST    R8,WRTNEXT         INITIALIZE WHERE TO START WRT'G       50800019
         MVI   DISP0(R8),FLGSET        SET  F=0                         50900019
         MVC   ENDWRT(DISP4),ENDFST1  SET PTR WHERE TO STOP WRITING     51000019
         LA    R6,OUTDCB                                                51200019
         ST    R6,WRTDCB          INDIC DCB TO WRITE TO                 51400019
         NI    TAG,TAG5+TAG6      RESET ALL BUT POSSIBLE RDBACK-CK BITS 51600019
         OI    TAG,TAG1           INDIC WRT'G OUT DIR TO OUTPUT DATASET 51800019
         NI    COMDCDSW,FF-COMPRESS    ASSURE THAT BIT INDICATING COM-  51860019
*                                 PRESS-IN-PLACE IS OFF                 51920019
         TM    FLG6,OIC           WERE ALL ODE'S RETAINED IN CORE       51940019
         BO    SOPTR              IF SO, SET TO FIND 1ST ODE IN CORE    51960019
SETRI    OI    DMSW,R+I           INDICATE NO ODES AND DOING AN IDE     52000019
         TM    DMSW,E             ANY INPUT DE'S TO BE MERGED           52050019
         BO    FAKEFFS            NO - SET UP A FAKE END-OF-DIRECTORY   52100019
*                                 BLOCK                                 52150019
         L     R2,IDEAD           POINT TO IDE TO BE PUT OUT            52200019
         B     ADDADE             GO BEGIN TO BUILD OUTPUT DIRECTORY    52400019
SOPTR    L     R2,SVFSTO          POINT TO 1ST OUT DIR BLK SAVED INCORE 54000019
         ST    R2,ODEPTR          SET INTERNAL PTR                      54200019
         MVC   DBKEY(LEN8),TWLV(R2)    SAVE KEY                         54400019
         CLI   TW2(R2),FF         1ST ENTRY NAME = FF                   54600019
         BE    SETRI              YES - SET TO MERGE ONLY INPUT         54800019
         ST    R2,BLKAD           SAVE ADDR OF BLOCK                    55000019
         LA    R2,TW2(R2)         POINT TO 1ST ENTRY                    55200019
         ST    R2,DEPTR           SAVE POINTER TO ODE                   55400019
         B     TME                TEST TO SEE IF ANY IDE'S              55600019
CKDEPTR  CLC   DEPTR(LEN4),ZROS   WAS END OF OUTPUT DIRECTORY REACHED   55800019
         BNE   RDOUT              NO, JUST END OF BFR.  GO RD MORE ODES 56000019
SETR     OI    DMSW,R             YES, INDIC NO MORE ODES TO MERGE      56200019
         TM    DMSW,E             ANY MORE IDE'S TO BE MERGED           56400019
         L     R2,IDEAD           SET UP REG IN CASE THERE ARE MORE     56600019
         BZ    SETI               YES, GO PUT AN IDE INTO MERGED OUTPUT 56800019
         B     SETFF              ELSE SET UP MBRNAME OF FF'S           57000019
TOIC     TM    FLG6,OIC+ODIC      ALL OUTPUT DE'S IN CORE               57200019
         BZ    GOBFR              NO, GET ADDR OF NXT ODE IN I/O BFR    57400019
         L     R5,DEPTR                                                 57600019
         CLC   DISP0(LEN8,R5),DBKEY  IS THIS LAST ENTRY IN OUT DIR BLK  57800019
         BE    BMP1               IF SO, CK FOR ANOTHER BLOCK IN CORE   58000019
         SR    R6,R6              ELSE COMPUTE LENGTH OF THIS DE        58200019
         IC    R6,LEN11(R5)                                             58400019
         SLL   R6,LEN27                                                 58600019
         SRL   R6,LEN26                                                 58800019
         LA    R6,TWLV(R6)                                              59000019
         AR    R6,R5              ADD ADDR OF DE TO LENGTH FOR ADDR NXT 59200019
         CLI   DISP0(R6),FF       IS NEXT DE NAMED FF'S                 59400019
         BE    SETR               IF SO, DONE WITH OUTPUT DIRECTORY     59600019
         ST    R6,DEPTR           ELSE SAVE ADDR OF NXT DE              59800019
TSTEON   TM    DMSW,E             ANY MORE IDE'S                        60000019
         BO    DOOUT              IF NOT, GO MERGE THIS ODE             60200019
         B     SETCOMP            ELSE COMPARE IDE VS. ODE NAMES        60400019
BMP1     L     R6,BLKAD                                                 60600019
         C     R6,SVLSTO          IS THIS THE LAST BLOCK IN CORE        60800019
         BNE   MORBLKS            NO, LOCATE NEXT BLOCK                 61000019
         TM    FLG6,OIC           WERE ALL ODE'S SAVED IN CORE          61200019
         BO    SETR               YES - NO MORE ODE'S TO LOOK AT        61400019
         NI    FLG6,FF-ODIC       NO, RESET INDICATOR THAT SOME ARE IN  61600019
*                                 CORE - THESE HAVE BEEN PROCESSED AND  61800019
*                                 IT IS NOW NECESSARY TO READ INTO THE  62000019
*                                 I/O BUFFER                            62200019
         L     R6,READDCB         POINT TO WHICHEVER DCB IS TO BE READ  62400019
         USING IHADCB,R6                                                62600019
         MVC   DCBFDAD(LEN8),OUTDAD1 MOVE MBBCCHHR OF 1ST UNRETANED BLK 62800019
         SR    R7,R7                                                    63000019
         IC    R7,DCBFDAD+LEN7                                          63200019
         BCTR  R7,R0              SUBTRACT 1 FROM 'R' OF MBBCCHHR -SRCH 63400019
         STC   R7,DCBFDAD+LEN7                                          63600019
         B     RDOUT              GO TO READ OUTPUT DIRECTORY INTO BFR  63800019
MORBLKS  LA    R6,BLKLEN(R6)      POINT TO NXT BLOCK IN CORE            64000019
         ST    R6,BLKAD           SAVE ITS ADDRESS                      64200019
         MVC   DBKEY(LEN8),TWLV(R6)    SAVE KEY OF THIS BLOCK           64400019
         LA    R5,TW2(R6)         PT TO 1ST ENTRY IN BLOCK              64600019
         CLI   DISP0(R5),FF       NAME OF 1ST ENTRY = 'FF'              64800019
         BE    SETR               YES - DONE WITH OUTPUT DIR            65000019
         ST    R5,DEPTR           NO - WANT TO USE 1ST ENTRY NAME       65200019
         B     TSTEON             GO CHECK FOR NEED TO COMPARE WITH IDE 65400019
SETREC   L     R8,WKA1            GET ADDR OF LAST COMPLETE BLOCK TO BE 65600019
*                                 WRITTEN                               65800019
         MVI   DISP0(R8),LASTREC+ENDPROC  INDIC LST REC TO BE PROCESSED 66000019
         L     R10,READNEXT       SAVE PTR WHERE TO READ                66200019
         MVC   READNEXT(DISP4),WRTNEXT  SET UP STARTING ADDR FOR VWS    68200019
         L     R15,VIEBVWS        ADDR OF RTNE TO PREPARE FOR WRITING   70200019
         BALR  R14,R15            LINK TO SET UP MBBCCHHR'S AND WRITE   70400019
         OI    DMSW,DIRWRT        INDICATE THAT SOME OF THE DIRECTORY   70460019
*                                 HAS NOW BEEN WRITTEN                  70520019
         ST    R10,READNEXT       RESTORE PTR WHERE TO READ             70600019
         MVC   BLOCK(DISP4),WRTNEXT  RESET PTR TO NXT BLOCK TO BE BUILT 70800019
         B     INITNXT            SET TO BUILD 1ST BLOCK IN NEXT BFR    71000019
TOOMANY  MVI   MSG1+LEN1,NORMOD   MSG CODE - NO MORE SPACE IN OUT DIR   71100019
         MVI   MSG1,LASTMSG+PBIT  SET INDICATORS FOR IEBVMS             71260019
         MVI   VTMFLG1,UNUSEND    INDICATE UNUSUAL END OF OPERATION     71330019
         MVI   PARAMS,NAME        INDICATE NAME TO BE PUT INTO MSG TEXT 71400019
         L     R15,NXTINDS        POINT TO INDD NAME                    71500019
         MVC   NAMEFLD(LEN8),LEN2(R15) PUT NAME INTO PARAMETER AREA     71560019
         MVI   NAMEDISP,MSGN      OFFSET IN MSG TEXT FOR NAME           71620019
         CLI   RCBUF,RTCDE        IS RETURN CODE 8 OR MORE       A44144 71670000
         BNL   DONTSET            YES- DO NOT CHANGE IT          A44144 71672000
         MVI   RCBUF,RTCDE        SET RETURN CODE TO 8           A44144 71680000
DONTSET  EQU   *                                                 A44144 71730000
         OI    FLG7,SUPPRMSG      SET INDICATOR TO SUPPRESS PRINTING    71740019
*                                 MEMBERNAME MESSAGE(S)                 71760019
         L     R15,VIEBVMS        ADDR OF MSG WRITER                    71780019
         BALR  R14,R15            LINK TO WRITE ERROR MESSAGE           71800019
         TM    FLG4,ODALT         WAS THIS MERGE BEING DONE TO UT4      71820019
         BZ    TSTDIRW            NO, SEE IF ANY WAS WRITTEN YET        71840019
         NI    FLG4,FF-ODALT      YES, RESET SYSUT4 INDICATOR           71860019
         B     DONE1              NOW EXIT TO IEBVTM VIA MAIN PGM       71880019
TSTDIRW  TM    DMSW,DIRWRT        WAS ANY OF THIS DIRECTORY WRITTEN YET 71900019
         BZ    DONE1              NO, EXIT TO IEBVTM VIA MAIN PGM       71920019
         TM    STATUS,STAT5       WAS OUTPUT DIRECTORY NULL BEFORE MRGE 71940019
         BZ    TSTOIC             IF NOT, SEE IF OUTPUT BLOCKS IN CORE  71960019
         MVI   MSG1+LEN1,PLAMPID  YES, SET UP ADDITIONAL MSG CODE       71980019
         MVI   MSG1,LASTMSG       INDIC LAST MSG IN LIST                72000019
         MVI   RCBUF,RTCD12       SET HIGHEST RETURN CODE        A44144 72010000
         L     R15,VIEBVMS        ADDR OF MSG WRITER                    72020019
         BALR  R14,R15            LINK TO WRITE ERROR MESSAGE           72040019
         MVI   TWLV(R8),FF        SET KEYNAME TO FF'S                   72200019
         MVC   TWLV+LEN1(LEN7,R8),TWLV(R8)                              72400019
         MVI   DISP0(R8),LASTREC+ENDPROC  FLAG FOR IEBVWS AND IEBDWR    72600019
         L     R15,ADLAST                                               72800019
         MVC   DISP0(LEN8,R15),TWLV(R8)  SET NAME OF LAST ENTRY IN THIS 73000019
*                                 BLOCK TO FF'S                         73200019
         LA    R7,LEN8(R0,R8)     * CALCULATE AMOUNT OF SPACE *         73210019
         SR    R15,R7             *  USED IN LAST DB =        *         73220019
*                                 * ADLAST - (BLOCK+20-12)    *         73230019
         STH   R15,BPAMOUT+DIRCTDCB  STORE IN BPAM DCB                  73240019
         ST    R15,WKA1                                                 73260019
         MVC   TWNTY(LEN2,R8),WKA1  UPDATE COUNT FIELD                  73270019
         MVC   READNEXT(DISP4),WRTNEXT  SET POINTER FOR USE BY IEBVWS   73300019
         L     R15,VIEBVWS        WRT TRUNCATED OUTPUT DIRECTORY        73400019
         BALR  R14,R15            LINK TO ROUTINE                       73500019
         B     DONE1              NOW GO TO EXIT TO IEBVTM VIA MAIN PGM 73620019
TSTOIC   EQU   *                                                        73640019
         TM    FLG6,OIC           ARE ALL OUTPUT DIR BLKS IN CORE       73660019
         BO    REWRTOUT           IF SO, THEY CAN BE RE-WRITTEN BACK TO 73680019
*                                 THE OUTPUT DATA SET DIRECTORY         73700019
         OI    FLG4,ODALT         SET BIT TO CAUSE THE PREVIOUS VALID   73720019
*                                 OUTPUT DIRECTORY TO BE COPIED FROM    73740019
*                                 SYSUT4 BY THE IEBVTM MODULE           73760019
         MVC   OUTFDAD(LEN8),ERPLMFD4  RESET POINTERS AND TRK BAL WHICH 73780019
*                                 WERE VALID WHEN UT4 WAS LAST USED     73800019
         MVC   OUTPTTR(DISP4),ERPTTR4                                   73820019
         MVC   OUTPTTR+DISP4(LEN2),ERPTB4                               73840019
         B     DONE1              GO TO EXIT TO IEBVTM VIA MAIN PGM     73860019
REWRTOUT EQU   *                                                        73880019
         MVC   READNEXT(DISP4),SVFSTO  POINT TO FST SAVED-IN-CORE OUTPT 73900019
*                                 DIRECTORY BLOCK TO BE WRITTEN BACK    73920019
         MVC   WRTNEXT(DISP4),SVFSTO                                    73940019
         L     R1,SVLSTO          POINT TO LAST SAVED-IN-CORE BLOCK     73960019
         MVI   DISP0(R1),LASTREC+ENDPROC  ASSURE THAT THIS BLK IS FLAG- 73980019
*                                 GED AS THE LAST ONE TO BE PROCESSED   74000019
*                                 AND WRITTEN                           74020019
         XC    OUTDS1(LEN8),OUTDS1  RESET OUTPUT TTRN TO ZERO (DIRECT)  74040019
         MVC   OUTDS1+DISP4(LEN2),TRBALBUF  RESET TRK BAL FOR DIR WRITE 74060019
         L     R15,VIEBVWS                                              74080019
         BALR  R14,R15            LINK TO SET UP AND REWRITE OUTPUT DIR 74100019
         B     DONE1              GO TO EXIT TO IEBVTM VIA MAIN PGM     74120019
         EJECT                                                          74200019
ADDENT   MVC   DISP0(LEN1,R8),DISP0(R2) EXEC TO MOVE A DE TO THE OUTPUT 74400019
*                                 DIR BLK BEING BUILT                   74600019
TWO54    DC    H'254'             MAX LENGTH-2 OF 1 DIRECTORY BLOCK     74800019
FRTEEN   DC    H'14'              FOR DL FIELD OF DE CONTAINING FF ONLY 74900019
KLDL     DC    X'080100'          KEY LENGTH AND DATA LENGTH FOR A      75000019
*                                 DIRECTORY BLOCK                       75200019
KLLOC    EQU   9                  OFFSET FROM START OF A BLK TO KEYLEN  75400019
TWLV     EQU   12                 OFFSET FROM START OF A BLK TO KEY     75600019
FAKELEN  EQU   14                 LENGTH OF FF'S DIRECTORY              75660019
*                                   ENTRY + COUNT                       75720019
BLKLEN   EQU   276                LENGTH OF 1 ENTIRE BLOCK -            75800019
*                                 FLG, MBBCCHHR, KEYLEN, DATALEN, KEY,  76000019
*                                 AND DIRECTORY BLOCK                   76200019
DLEOD    EQU   12                 FOR DATA LENGTH OF EOD RECORD         76400019
DISPO    DC    H'22'              THE DISPLACEMENT FROM THE BEGINNING   80200019
*                                 OF A DIRECTORY BLOCK TO WHICH         80400019
*                                  ENTRIES ARE TO BE ADDED              80600019
         DS    0F                                                       81300019
PATCHLEN EQU   (*-IEBVDM)/20      LENGTH OF 5 PER CENT PATCH AREA       82000019
PATCH    DC    XL(PATCHLEN)'00'   5 PER CENT PATCH AREA          A41780 82700000
         EJECT                                                          83600019
         DCBD  DSORG=PS                                                 83800019
FDAD     EQU   DCBFDAD-IHADCB     OFFSET IN DCB TO DCBFDAD              83900019
DIRCTDCB EQU   DCBDIRCT-IHADCB    OFFSET IN DCB TO DIRECTORY BYTE       83930019
*                                 COUNT USED IN LAST BLOCK              83960019
         EJECT                                                          84000019
IEBMCA   DSECT                                                          84200019
         IEBMCA                                                         84400019
         END                                                            84600019
