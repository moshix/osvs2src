         TITLE 'IECTATEN - BTAM LOCAL 3270 FIRST LEVEL ATTN RTN'        00600002
IECTATEN CSECT                                                          01200002
         SPACE 3                                                        01210002
*********                                                               01250002
*                                                                       01250102
*      THIS MODULE HAS BEEN PARTIALLY RE-WRITTEN FOR OS/VS2-REL2        01400002
*                                                                       01400102
*********                                                               01450002
*                                                                       01650002
*        YA02144                   YM4074                               01660002
*        YA03953                   YM5698                               01670002
*        YA03957                   YM6997                               01680002
*        ZA02340  (11/22/74)                                            01690002
*                                                                       01692002
*********                                                               01700002
         SPACE 3                                                        01750002
* STATUS- CHANGE LEVEL 000                                              01800002
*                                                                       02400002
* FUNCTION-HANDLE ALL ATTENTION INTERRUPTS FOR LOCAL 3270 DEVICES IN    03000002
*    OS/BTAM.  SCHEDULE THE SECOND LEVEL ATTENTION RTN. (IGG019PG) IF   03600002
*    A READ IS TO BE STARTED.  UPDATE ATTENTION INFORMATION FOR THE     04200002
*    BTAM LINE GROUP.                                                   04800002
*                                                                       05400002
* ENTRY POINT- THE ROUTINE IS ENTERED AT IECTATEN FROM THE I/O INTER-   06000002
*    RUPT SUPERVISOR WHENEVER ATTENTION STATUS OCCURS FROM A LOCAL 3270 06600002
*    DEVICE.                                                            07200002
*                                                                       07800002
* INPUT- REGISTERS 7, 10, AND 14 ARE ESTABLISHED BY IOS                 08400002
*    REGISTER  7 - THE ADDRESS OF THE UCB FOR THE DEVICE                09000002
*    REGISTER 10 - ADDRESS OF THE ENTRY POINT OF AEE2                   09600002
*    REGISTER 14 - RETURN POINT ADDRESS                                 10200002
*                                                                       10800002
* OUTPUT- UPDATED ATTENTION FLAGS AND COUNT IN THE LINE GROUP'S UCB'S.  11400002
*    AN INITIALIZED IQE IF THE SECOND LEVEL ATTENTION RTN IS SCHEDULED. 12000002
*                                                                       12600002
* EXTERNAL REFERENCES- ASYNCHRONOUS EXIT EFFECTOR, STAGE 2 (AEE2)       13200002
*                                                                       13800002
* EXITS,NORMAL- RETURN TO IOS WITH ATTENTION INFORMATION UPDATED        14400002
*                                                                       15000002
* EXITS,ERROR- RETURN TO IOS WITHOUT UPDATING ATTENTION INFORMATION     15600002
*                                                                       16200002
* TABLES/WORK AREAS- NONE                                               16800002
*                                                                       17400002
* ATTRIBUTES- NUCLEUS RESIDENT, DISABLED, SUPERVISOR PROTECT KEY,       18000002
*    READ ONLY.                                                         18600002
*                                                                       19200002
* CHARACTER CODE DEPENDENCY- N/A                                        19800002
*                                                                       20400002
* NOTES- THIS ROUTINES LOGS ATTENTION INTERRUPTS RECEIVED FROM LOCAL    21000002
*    3270 DEVICES ON OPEN BTAM LINE GROUPS.  IF AN OUTSTANDING READ     21600002
*    INITIAL EXISTS FOR THE LINE GROUP AND THE DEVICE IS ABLE TO        22200002
*    PROCESS READ INITIALS, THE SECOND LEVEL ATTENTION ROUTINE IS       22800002
*    SCHEDULED USING AEE2 TO START THE READ.  ATTENTION INFORMATION     23400002
*    IN THE LINE GROUP'S UCB'S IS UPDATED AS REQUIRED.                  24000002
*                                                                       24600002
         EJECT                                                          25200002
*/*IECTATEN: CHART */                                                   25220002
*/* HEADER                                                              25240002
*/*IECTATEN - BTAM FIRST LEVEL ATTENTION ROUTINE FOR 3270 DEVICES   */  25260002
*/* E ENTRY FROM IOS */                                                 25280002
*/* M SAVE REGS AND ESTABLISH ADDRESSABILITY */                         25300002
*/* D (YES,CHKTYPE,NO,) USER'S SPACE ADDRESSABLE? */                    25310002
*/* P RESTORE IOS'S REGISTERS */                                        25312002
*/* R RETURN TO IOS+4 */                                                25314002
*/*CHKTYPE: D (YES,,NO,RTNTOIOS) IS DEVICE A 3270 DEVICE? */            25320002
*/*OPENCHK1: D (YES,CHKCSW,NO,RTNTOIOS) ATTN HANDLING IRB ADDRESS       25340002
*/*NON-ZERO? */                                                         25342002
*/*RTN2IOS: P TURN OFF BUSY,NOT READY, AND CU BUSY IN UCBFL1 */         25530002
*/* P RESTORE IOS'S REGISTERS */                                        25532002
*/* R RETURN TO IOS+4 (INTERCEPT BIT UNCHANGED) */                      25534002
*/*RTNTOIOS: P RESTORE IOS'S REGISTERS */                               25540002
*/* R RETURN TO IOS */                                                  25560002
*/*CHKREADY: D (NO,,YES,CHKFUTH) OPEN FAIL ON THIS DEVICE (UCBGRAF) ?   25570002
*/* */                                                                  25572002
*/* D (YES,FLGERCMP,NO,RTNTOIOS) DE WITH ERROR (UCBGCB) ? */            25574002
*/*CHKFUTH: D (YES,,NO,NFUTHCHK) I/O QUEUED FOR THIS DEVICE (UCBFL1) ?  25576002
*/**/                                                                   25578002
*/* D (YES,FLGERCMP,NO,RTNTOIOS) DE WITH ERROR (UCBGCB) ? */            25578402
*/*NFUTHCHK: P SET READY FLAG (UCBGRAF) */                              25578802
*/* P GET IQE ADDRESS FROM IRB AND RECHAIN IQE'S */                     25579202
*/* P GET ADDRESS OF MASTER UCB */                                      25579602
*/* D (YES,,NO,NOSETRDY) USING BTAM'S READY ROUTINE IGG019UP ? */       25579702
*/* P (,LNRDYSET) MARK LINE READY (MTRGRAF) */                          25579802
*/*NOSETRDY: P TURN OFF LINE READY (MTRGRAF) */                         25579902
*/*LNRDYSET: P SET RLN AND DEB ADDRESS AS IQE PARAMETERS */             25583202
*/* S AEE2:SCHEDULE IQE FOR READY ROUTINE */                            25585202
*/* P TURN OFF OPEN FAIL FLAG FOR DEVICE (UCBGRAF) */                   25585602
*/* D (YES,FLGERCMP,NO,RTN2IOS) DE WITH ERROR (UCBGCB) ? */             25586002
*/*FLAGRDY: P FLAG INTERUPT DURING OPEN (UCBGRAF) */                    25586402
*/* D (YES,FLGERCMP,NO,RTNTOIOS) DE WITH ERROR (UCBGRAF) ? */           25588402
*/*CHKCSW: D (YES,OTHRSTS,NO,) ATTN, DE, AND/OR CUE ONLY STATUS? */     25594402
*/*FLGERROR: P SET IOB INTERCEPT FLAG IN UCBFL1 */                      25594802
*/* D (YES,,NO,FLGERCMP) DEVICE END STATUS ? */                         25595202
*/* P SET DE WITH ERROR (UCBGCB) */                                     25595602
*/* D (NO,,YES,OPENCHK) DEVICE PREVIOUSLY NOT READY (UCBGRAF) ? */      25595702
*/*FLGERCMP: P (,RTN2IOS) TURN OFF DE WITH ERROR (UCBGCB) */            25596002
*/*OTHRSTS: D (YES,PROCATTN,NO,) ATTN STATUS PRESENTED? */              25596402
*/* D (YES,,NO,RTNTOIOS) DEVICE END STATUS ? */                         25598002
*/*OPENCHK: D (YES,,NO,NXTCHK) DEVICE READY (UCBGRAF) ? */              25598402
*/* D (YES,FLGERCMP,NO,RTNTOIOS) DE WITH ERROR (UCBGCB) ? */            25598802
*/*NXTCHK: D (NO,,YES,FLAGRDY) OPEN IN PROGRESS (UCBGRAF) ? */          25648802
*/* D (NO,,YES,CHKREADY) READYQ SPECIFIED IN DCB (UCBRDYQ) ? */         25658802
*/* D (YES,FLGERCMP,NO,RTNTOIOS) DE WITH ERROR (UCBGCB) ? */            25668802
*/*PROCATTN: D (NO,,YES,RTN2IOS) I/O OPERATION QUEUED FOR THIS DVC? */  25669202
*/*ACPTATN: D (NO,CHKRLN,YES,SETATNFL) SKIP FLAG ON (UCBGCB) ? */       25669602
*/*SETATNFL: P (,RTN2IOS) SET ATTENTION FLAG IN UCBGCB */               25669702
*/*CHKRLN: P OBTAIN ADDRESS OF MASTER UCB */                            25669802
*/*RDTIPND: D (NO,,YES,NOSCHED) PROCESSING DEVICE READY (MTRGRAF) ? */  25669902
*/* D (NO,,YES,SCHEDIQE) READ INITIAL PENDING (MTRGCB) ? */             25670002
*/*NOSCHED: D (NO,,YES,RTN2IOS) ATTENTION FLAG ON (UCBGCB) ? */         25670102
*/* P (,SETATNFL) INCREMENT ATTENTION COUNT IN MASTER UCB BY 1          25670202
*/*(MTRATNCT) */                                                        25670302
*/*SCHEDIQE: P GET IQE FROM RBNEXAV QUEUE IN IRB */                     25672602
*/* P STORE UCB ADDRESS IN IQE */                                       25673302
*/* P SET RBNEXAV TO ZERO IN IRB */                                     25674002
*/* P SET READ INITIAL PENDING FLAG OFF (MTRGCB) */                     25676002
*/* S (,RTN2IOS) AEE2:SCHEDULE 2ND LEVEL ATTN RTN */                    25678002
*/*IECTATEN: END */                                                     25680002
         EJECT                                                          25700002
*********************************************************************** 25750002
*                                                                       25760002
*         REGISTER DEFINITION                                           25770002
*                                                                       25780002
*********************************************************************** 25790002
         SPACE 5                                                        25792002
PARMREG  EQU   1                    PARAMETER REGISTER                  25800002
AEEREG   EQU   12                   ADDRESS OF AEE2           LD Y02947 26400002
R12      EQU   12                                             LD Y02947 26450002
SAVEREG  EQU   13                   ADDRESS OF SAVE AREA                27000002
R13      EQU   13                                             LD Y02947 27050002
RTNREG   EQU   14                   RETURN POINT REGISTER               27600002
R14      EQU   14                                             LD Y02947 27650002
EPREG    EQU   15                   ENTRY POINT REGISTER                28200002
*                                                                       28800002
BASEREG  EQU   11                   BASE REG FOR ATT RTN      LD Y02947 29400002
IRBREG   EQU   9                    POINTER TO IRB                      30000002
MSTREG   EQU   8                    BASE REGISTER FOR MASTER UCB        30600002
UCBREG   EQU   7                    BASE REGISTER FOR DEVICE UCB        31200002
IQEREG   EQU   6                    POINTER TO IQE                      31800002
CVTREG   EQU   5                    POINTER TO CVT            LD Y02947 31850002
ASCBREG  EQU   5                    POINTER TO ASCB           LD Y02947 31860002
DEBREG   EQU   4                  ADDR OF DEB FROM MSTRUCB     @YA02128 31870002
IOSBREG  EQU   4                    POINTER TO OISB           LD Y02947 31900002
SRBREG   EQU   3                    POINTER TO SRB            LD Y02947 31950002
WORK2    EQU   SRBREG                                        LD YA02128 32000002
UCBEXTRG EQU   2                    POINTER TO UCB EXTENSION  LD YM0160 32400002
WORK1    EQU   2                    WORK REGISTER                       33000002
         SPACE 5                                                        34200002
*********************************************************************** 34250002
*                                                                       34300002
*         EQUATIONS                                                     34350002
*                                                                       34400002
*********************************************************************** 34450002
         SPACE 5                                                        34500002
ALLON    EQU   X'FF'                ONE BYTE MASK-ALL BITS ON           34800002
ATNSTAT  EQU   X'80'                ATTENTION STATUS                    37200002
DESTAT   EQU   X'04'                DEVICE END STATUS                   37800002
CUESTAT  EQU   X'20'                CONTROL UNIT END STATUS  LD YA02144 37850002
IQEPARAM EQU   4                    DISPLACEMENT TO IQEPARAM            38400002
RBNEXAV  EQU   96                   DISPLACEMENT TO IRB POINTER TO IQE  39000002
IOBNTCPT EQU   X'10'                UCB IOB INTERCEPT FLAG              40800002
DVC3277  EQU   X'09'                DEVICE TYPE FOR 3277 DISPLAY        42000002
DVC3286  EQU   X'0B'                DEVICE TYPE FOR 3286 PRINTER        43200002
IQELNGTH EQU   16                 LENGTH OF AN IQE             @YA02128 43250002
IQELINK  EQU   0                  DISPL. TO LK FILD IN IEQ   LD YA03953 43300000
*                                                                       43800002
DEERR    EQU   X'40'              DEVICE END PLUS ERROR        @YA02128 44450002
IGG019UP EQU   X'10'              USING BTAM'S READY ROUT      @YA02128 44500002
LINERDY  EQU   X'01'              MTR FLAG FOR READY IN GROUP  @YA02128 44550002
OPENFAIL EQU   X'02'              OPEN FAILED FOR DEVICE       @YA02128 44600002
OPENINIT EQU   X'80'              OPEN IN PROGRESS FLAG        @YA02128 44650002
OPENRDY  EQU   X'40'              DEV INTERRUPT DURING OPEN    @YA02128 44700002
READY    EQU   X'20'              GRAF FLAG READY              @YA02128 45650002
RDYNDONE EQU   X'04'              PROCESSING READY             @YA02128 45700002
RLN1     EQU   X'01'                RLN FOR RLN 1                       46200002
ONE      EQU   1                                                        46600002
TWO      EQU   2                                              LD Y02947 46650002
FOUR     EQU   4                                                        46700002
ZERO     EQU   0                                              LD Y02947 46710002
A12      EQU   12                                             LD Y02947 46750002
X28      EQU   X'28'                                        LD @ZA02340 46760002
         EJECT                                                          46800002
******                                                                  46850002
*              ENTRY CODE & ADDRESSABILITY                              47400002
******                                                                  47410002
         SPACE 3                                                        47450002
         STM   ZERO,EPREG,ZERO(R13) SAVE REGISTERS            LD Y02947 47700002
         BALR  BASEREG,ZERO         ESTABLISH ADDRSSBLTY FOR  LD Y02947 52200002
         USING *,BASEREG            ATTENTION RTN CSECT                 52800002
         B     CONTIN               BRANCH AOUND MOD ID        @YA02128 52850002
         DC    CL8'IECTATEN'        MODULE ID                 LD Y02947 52900002
         DC    C' 4322'             DATE LAST CHANGE        LD @ZA02340 52950002
         DC    C'&SYSDATE'          DATE LAST ASSEMBLY      LD @ZA02340 52954002
PATCH    DC    10F'0'               PATCH AREA                          52960000
CONTIN   DS    0H                                              @YA02128 53000002
         USING UCB,UCBREG           ESTABLISH ADDRESSABILITY FOR UCB    53400002
         USING IOSB,IOSBREG         ADDRESSABILITY FOR IOSB   LD Y02947 53500002
         SPACE 5                                                        53550002
******                                                                  53552002
*         CHECK ADDRESSABILITY OF ATTN RTN.                             53554002
*         IF RUNNING UNDER THE MASTER SCHEDULER ADDRESSABILITY          53556002
*         RETURN TO IOS+4 IN ORDER TO BE RESCHEDULED UNDER THE          53558002
*         USER ADDRESSABILITY.                                          53558402
******                                                                  53558802
         USING SRB,SRBREG                                     LD Y02947 53558902
         USING ASCB,ASCBREG                                   LD Y02947 53559002
         LR    IOSBREG,PARMREG      GET IOSB ADDRESS          LD Y02947 53559202
         L     SRBREG,IOSSRB        GET SRB ADDRESS           LD Y02947 53559602
         L     ASCBREG,SRBASCB      GET ADDRESS OF ASCB       LD Y02947 53559702
         L     UCBREG,IOSUCB        ADDRESS OF UCB            LD Y02947 53561902
         L     UCBEXTRG,UCBEXTPT    ADDR OF COMMON UCB EXT    LD YM0160 53563002
         USING UCBCMEXT,UCBEXTRG    ADDRESSABILITY FOR...     LD YM0160 53565002
*                                   COMMON UCB EXTENSION.     LD YM0160 53565402
         CLC   UCBASID(TWO),ZEROS   IS THIS AN...             LD YM6997 53565800
*                                   UNSOLICITED ATTENTION?    LD YM6997 53566200
         BE    RTNTOIOS             YES, RETURN               LD YM6997 53566300
*                                                             LD YM6997 53566400
         CLC   ASCBASID(TWO),UCBASID IS ATTN RTN RUNNING      LD Y02947 53566602
*                                   UNDER USER ADDRESSABILITY LD Y02947 53569902
         DROP  ASCBREG                                                  53573202
         DROP  SRBREG                                                   53575202
         BE    CONTINUE             BRANCH IF YES             LD Y02947 53583202
         MVC   IOSASID(TWO),UCBASID MOVE USER MEMORY ID TO    LD YM0160 53583602
*                                   IOSB FOR IOS.             LD YM0160 53584002
         LM    ZERO,EPREG,ZERO(R13) AND RETURN TO             LD Y02947 53585202
         B     FOUR(R14)            IOS+4                     LD Y02947 53585602
         SPACE 5                                                        53586002
******                                                                  53586702
*         ADDRESS OF AEE STAGE 2 FROM CVT                               53600002
******                                                                  53650002
CONTINUE EQU   *                                              LD Y02947 53650402
         USING CVT,CVTREG           ADDRESSABILITY FOR CVT    LD Y02947 53652002
         L     CVTREG,CVTPTR        ADDRESS OF CVT            LD Y02947 53660002
         L     AEEREG,CVT0EF00      ADDRESS OF AEE STAGE 2    LD Y02947 53670002
         DROP  CVTREG                                         LD Y02947 53680002
         SPACE 5                                                        53700002
*              VALIDATE DEVICE                                          54000002
         SPACE 3                                                        54050002
CHKTYPE  CLI   UCBTBYT3,UCB3DISP   GRPHS DEVICE CLASS IN UCB  LD Y02947 54600002
         BNE   RTNTOIOS             NO, RETURN TO IOS                   55200002
         CLI   UCBTBYT4,DVC3277    IS DEVICE 3270 DEVICE      LD Y02947 55800002
         BL    RTNTOIOS            NO, RETURN TO IOS                    56400002
         CLI   UCBTBYT4,DVC3286    MAYBE, CHECK FURTHER       LD Y02947 57000002
         BH    RTNTOIOS            NO, RETURN TO IOS                    57600002
         SPACE 3                                                        57650002
*              CHECK OPEN STATUS                                        58200002
         SPACE 3                                                        58250002
OPENCHK1 L     IRBREG,UCBIRB        GET ADDRESS OF IRB         @YA02128 58800002
         LA    IRBREG,0(IRBREG)   CLEAR GRAF FLAGS             @YA02128 58850002
         LTR   IRBREG,IRBREG        IRB ADDR ESTABLISHED BY OPEN        59400002
         BZ    RTNTOIOS             NO, IGNORE ATTENTION    LD @ZA02340 60000002
         TM    UCBFL1,X28          RQE ADDR PRESENT         LD @ZA02340 60010002
         BZ    CHKCSW              NO, ASYNCHRONOUS         LD @ZA02340 60020002
*                                  YES, RETURN TO IOS...    LD @ZA02340 60030002
*                                  ...SINCE THIS IS...      LD @ZA02340 60040002
*                                  ...ENDING STATUS.        LD @ZA02340 60042002
         SPACE 3                                                        60050002
*              EXIT CODE                                                60600002
         SPACE 3                                                        60650002
RTNTOIOS EQU   *                                              LD Y02947 61200002
         LM    ZERO,EPREG,ZERO(R13) LOAD REGISTER             LD Y02947 61850002
         BR    RTNREG                                         LD Y02947 61900002
         SPACE 3                                                        61950002
*                                 ASYNCHRONOUS READY           @YA02128 62000002
CHKREADY EQU   *                  READY QUEUE HAS BEEN         @YA02128 62050002
*                                 INITIALIZED                  @YA02128 62100002
         TM    UCBGRAF,OPENFAIL   DID OPEN FOR DEVICE FAIL ?   @YA02128 62150002
         BO    CHKFUTH            NO, RETURN TO IOS            @YA02128 62200002
         TM    UCBGCB,DEERR       DE WITH ERROR ?              @YA02128 62250002
         BO    FLGERCMP           YES, SET ERROR               @YA02128 62300002
         B     RTNTOIOS           NO, RETURN                   @YA02128 62350002
CHKFUTH  EQU   *                  CHECK FURTHER                @YA02128 62400102
*                                                                       62410100
*              6 LINES DELETED                               LD YA03957 62420100
*                                                                       62430100
         OI    UCBGRAF,READY      SET READY FLAG               @YA02128 62446002
         L     IQEREG,RBNEXAV(IRBREG) GET ADDR OF READY QUEUE  @YA02128 62448002
         MVC   RBNEXAV(FOUR,IRBREG),0(IQEREG) CHAIN NEXT IQE   @YA02128 62448402
         XC    0(FOUR,IQEREG),0(IQEREG) REMOVE IQE FROM CHAIN  @YA02128 62448802
         XR    WORK1,WORK1        CLEAR EVEN REG               @YA02128 62449202
         IC    WORK2,UCBRLN       PICK UP RLN OF THIS DEVICE   @YA02128 62449602
         CLI   UCBRLN,RLN1        IS THIS THE MASTER UCB ?     @YA02128 62449702
         BE    MSTADDOK           YES, USE THE ADDRESS         @YA02128 62449802
         L     MSTREG,UCBCTLNK    NO, GET ADDR OF MASTER UCB   @YA02128 62449902
         SPACE 3                                                        62450002
         LA    MSTREG,0(MSTREG)   CLEAR THE HIGH BYTE          @YA02128 62499902
         B     MSTUCBOK           GO AND CHECK                 @YA02128 62509902
MSTADDOK EQU   *                  SET UP TO CHECK MASTER UCB   @YA02128 62519902
         LR    MSTREG,UCBREG      PICK UP MASTER UCB ADDR      @YA02128 62529902
MSTUCBOK EQU   *                  MSTREG CONTAINS ADDRESS      @YA02128 62539902
         USING MSTRUCB,MSTREG     TELL ASSEMBLER               @YA02128 62541902
         TM    MTRGRAF,IGG019UP   USING BTAM'S ?               @YA02128 62543902
         BNO   NOSETRDY           NO, DON'T SET LINERDY        @YA02128 62545902
         OI    MTRGRAF,LINERDY    INDICATE DEVICE READY        @YA02128 62547902
         B     LNRDYSET           BYPASS TURN OFF              @YA02128 62548302
NOSETRDY EQU   *                  TURN OFF MTR LINE READY      @YA02128 62548702
         NI    MTRGRAF,ALLON-LINERDY TURN OFF LINE READY       @YA02128 62549102
LNRDYSET EQU   *                  MTR GRAF FLAGS SET           @YA02128 62549502
         L     DEBREG,MTRDEBAD    GET DEB ADDRESS FOR GROUP    @YA02128 62549602
         DROP  MSTREG             DON'T NEED MASTER ANYMORE    @YA02128 62549702
         LA    DEBREG,0(DEBREG)   CLEAR HIGH BYTE              @YA02128 62549802
         SLL   WORK2,24           PUT RLN IN HIGH BYTE         @YA02128 62599802
         OR    DEBREG,WORK2       PUT RLN IN PARM              @YA02128 62609802
         ST    DEBREG,IQEPARAM(IQEREG) STORE RLN/DEBADDR       @YA02128 62619802
         LNR   PARMREG,IQEREG     PARM = COMPLEMENT OF         @YA02128 62629802
*                                 IQE ADDRESS                  @YA02128 62639802
         LR    EPREG,AEEREG       ENTRY POINT TO AEE2          @YA02128 62641802
         BALR  RTNREG,EPREG       GO TO AEE2 TO SCHEDULE       @YA02128 62643802
*                                 ASYNCHRONOUS DEVICE          @YA02128 62645802
*                                 READY ROUTINE                @YA02128 62647802
         NI    UCBGRAF,ALLON-OPENFAIL TURN OFF OPENFAIL FLAG   @YA02128 62648202
         TM    UCBGCB,DEERR       ERROR WITH DE STAT ?         @YA02128 62648602
         BO    FLGERCMP           YES, SET ERROR               @YA02128 62649002
         B     RTNTOIOS           RETURN TO IOS+4              @YA02128 62649402
FLAGRDY  EQU   *                  DEVICE INTERRUPT IN OPEN     @YA02128 62649502
         OI    UCBGRAF,OPENRDY    SET FLAG INT DURING OPEN     @YA02128 62649602
         TM    UCBGCB,DEERR       ERROR WITH DE STAT ?         @YA02128 62649702
         BO    FLGERCMP           YES, SET ERROR               @YA02128 62699702
         B     RTNTOIOS           RETURN TO IOS                @YA02128 62709702
*              CHECK CSW STATUS OTHER THAN ATTN,DE AND CUE   LD YA02144 62749702
CHKCSW   TM    IOSTSA,ALLON-ATNSTAT-DESTAT-CUESTAT           LD YA02144 63000002
         BZ    OTHRSTS              NO, CHECK FURTHER                   63600002
FLGERROR EQU   *                                              LD YM5698 64200000
         TM    IOSTSA,DESTAT      DEVICE END ?                 @YA02128 64250002
         BNO   PROCATTN           SINCE ERROR, TRY TO...     LD YA03957 64300000
*                                 TO SCHE 2ND LEVEL SO...    LD YA03957 64320000
*                                 USER CAN BE AWARE OF.      LD YA03957 64330000
         OI    UCBGCB,DEERR       SET DE WITH ERROR FLAG       @YA02128 64350002
         TM    UCBGRAF,OPENFAIL   NOT PREVIOUSLY READY ?       @YA02128 64400002
         BO    OPENCHK            YES, PROCESS READY           @YA02128 64450002
FLGERCMP EQU   *                  RETURN WITH ERROR            @YA02128 64500002
         NI    UCBGCB,ALLON-DEERR TURN OFF DE WITH ERROR       @YA02128 64550002
         B     RTNTOIOS             RETURN TO IOS             LD Y02947 64800002
OTHRSTS  TM    IOSTSB,ALLON         ERROR STATUS IN BYTE2     LD Y02947 65400002
         BNZ   FLGERROR             YES, FLAG ERROR IN UCB              66000002
         TM    IOSTSA,ATNSTAT       ATTENTION STATUS ON       LD Y02947 66600002
         BO    PROCATTN           YES, GO PROCESS IT           @YA02128 67200002
         TM    IOSTSA,DESTAT      DEVICE END STATUS ?          @YA02128 67250002
         BZ    RTNTOIOS           NO, RETURN TO IOS            @YA02128 67300002
OPENCHK  EQU   *                  SEE IF OPEN IN PROGRESS,     @YA02128 67350002
*                                 OR IF OPEN COMPLETE          @YA02128 67400002
         TM    UCBGRAF,OPENRDY+READY UCB ALREADY READY         @YA02128 67450002
         BZ    NXTCHK             NO, CHECK FURTHER            @YA02128 67500002
         TM    UCBGCB,DEERR       ERROR STAT ?                 @YA02128 67550002
         BO    FLGERCMP           YES, SET ERROR               @YA02128 67600002
         B     RTNTOIOS           NO, RETURN                   @YA02128 67650002
NXTCHK   EQU   *                  CHECK FURTHER                @YA02128 67700002
         TM    UCBGRAF,OPENINIT   IS OPEN IN PROGRESS ?        @YA02128 67750002
         BO    FLAGRDY            YES, FLAG INT DURING OPEN    @YA02128 67760002
         L     IRBREG,UCBRDYQ     GET ADDRESS OF READY IRB     @YA02128 67770002
         LA    IRBREG,0(IRBREG)   CLEAR HIGH BYTE              @YA02128 67780002
         LTR   IRBREG,IRBREG      READY QUEUE INITIALIZED ?    @YA02128 67790002
         BNZ   CHKREADY           YES, SEE IF NEED SCHEDULE    @YA02128 67792002
         TM    UCBGCB,DEERR       ERROR WITH STAT ?            @YA02128 67794002
         BO    FLGERCMP           YES, SET ERROR               @YA02128 67796002
         B     RTNTOIOS           NO, RETURN                   @YA02128 67798002
*                                                                       68450002
PROCATTN EQU   *                  PROCESS THE ATTENTION        @YA02128 68450102
*                                                                       68460100
*              2 LINES DELETED                               LD YA03957 68500100
*                                                                       68550100
ACPTATN  TM    UCBGCB,UCBSKPFG      SKIP FLAG ON              LD Y02947 69000002
         BZ    CHKRLN               NO, CHECK FOR PENDING READ          69600002
SETATNFL OI    UCBGCB,UCBATRCD      SET ATT. FLAG IN GCB      LD Y02947 70200002
         B     RTNTOIOS             RETURN TO IOS             LD Y02947 70800002
*                                                                       71400002
CHKRLN   CLI   UCBRLN,RLN1          UCB RLN 1 FOR LINE GROUP            72000002
         BE    RLNISONE             YES, UCB = MASTER UCB               72600002
         L     MSTREG,UCBCTLNK      NO, GET ADDRESS OF MASTER UCB       73200002
         USING MSTRUCB,MSTREG       & ESTABLISH ADDRESSABILITY          73800002
*                                                                       74400002
RDTIPND  EQU   *                  CHECK STATUS OF UCB          @YA02128 74450002
         TM    MTRGRAF,RDYNDONE   PROCESSING READY ?           @YA02128 74500002
         BO    NOSCHED            YES, DON'T SCHEDULE          @YA02128 74550002
         TM    MTRGCB,UCBRIPND      READ INITIAL PENDING                75000002
         BO    SCHEDIQE             YES, SCHEDULE 2ND LEVEL ATTN RTN    75600002
NOSCHED  EQU   *                  WON'T SCHEDULE               @YA02128 75650002
         TM    UCBGCB,UCBATRCD      IF ATT. FLAG ALREADY ON,  LD Y02947 76200002
         BO    RTNTOIOS             RETURN TO IOS             LD Y02947 76800002
         SR    WORK1,WORK1                                              77400002
         IC    WORK1,MTRATNCT       INCREMENT ATTENTION COUNT IN        78000002
         LA    WORK1,ONE(WORK1)     MASTER UCB BY 1                     78600002
         STC   WORK1,MTRATNCT                                           79200002
         B     SETATNFL             SET ATTENTION FLAG IN GCB           79800002
*              SCHEDULE 2ND LEVEL ATTENTION ROUTINE                     80400002
SCHEDIQE L     IQEREG,RBNEXAV(IRBREG) GET ADDRESS OF IQE                81000002
         ST    UCBREG,IQEPARAM(IQEREG) STORE ADDRESS OF UCB IN IQE      81600002
         MVC   RBNEXAV(FOUR,IRBREG),IQELINK(IQEREG) UNCHAIN  LD YA03953 82200000
*                                   1ST AVAILABLE IQE        LD YA03953 82250000
         LNR   PARMREG,IQEREG       PARM = COMPLEMENT OF IQE ADDRESS    82800002
         LR    EPREG,AEEREG         EP = AEE2                           83400002
         BALR  RTNREG,EPREG         GO TO AEE2 TO SCHEDULE IQE          84000002
         NI    MTRGCB,ALLON-UCBRIPND TURN RD PENDING FLAG OFF LD Y02947 84600002
         B     RTNTOIOS             RETURN TO IOS             LD Y02947 85200002
RLNISONE LR    MSTREG,UCBREG        TRANSFER UCB ADDRESS TO MASTER      85800002
         B     RDTIPND              UCB BASE AND CHECK FOR PENDING READ 86400002
*                                                                       87000002
ZEROS    DC    H'0'                 HALF WORD OF ZEROS       LD YA03953 87050000
*                                                                       87100000
         EJECT                                                          88200002
ASCB     IHAASCB                                              LD Y02947 88260002
         EJECT                                                          88300002
CVT      DSECT                                                LD Y02947 88800002
         CVT                                                  LD Y02947 88850002
         EJECT                                                          88900002
         IECDIOSB                                             LD Y02947 88950002
         EJECT                                                          89045202
SRB      IHASRB                                               LD Y02947 89045602
         EJECT                                                          89046002
UCB      DSECT                                                LD Y02947 89050002
         IEFUCBOB                                             LD Y02947 89100002
         EJECT                                                          89110002
MSTRUCB  DSECT                                                          89120002
MTRATNCT EQU   MSTRUCB+(UCBATNCT-UCBOB)                                 89130002
MTRGCB   EQU   MSTRUCB+(UCBGCB-UCBOB)                                   89140002
MTRGRAF  EQU   MSTRUCB+(UCBGRAF-UCBOB)                                  89142002
MTRDEBAD EQU   MSTRUCB+(UCBRLN-UCBOB)                                   89144002
         SPACE 5                                                        89150002
         END                                                            89400002
