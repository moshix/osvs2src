         TITLE 'IECTSVC - BTAM LOCAL 3270 SUPPORTING SVC LOGIC'         00100002
IECTSVC  CSECT                                                          00300002
*                                                                       00310002
*                                                                       00350002
*        MAINTENANCE ACTIVITY                                           00360002
*                                                                       00370002
*        YA02130                                                        00372002
*        YA02174                                                        00374002
*        YA02128                                                        00380002
*        YA02451                                                        00390002
*        YA03231                                                        00392002
*        YA03253                                                        00394002
*                                                                       00396002
*        ZA00537  (11/25/74)        ZA02332  (11/25/74)                 00398003
*        ZA03587  (03/19/75)        AZ08050  (01/29/76)                 00458003
*        ZA14028  (09/14/76)                                            00518000
*                                                                       00580002
         SPACE 5                                                        00590002
         ENTRY IECTRDIL,IECTATRL,IECTCHSL,IECTCHAL,IECTRSTL             00600002
         EJECT                                                          00900002
* STATUS- CHANGE LEVEL 000                                              01200002
*                                                                       01500002
* FUNCTION- 1. TEST FOR AN OUTSTANDING ATTENTION ON A LINE GROUP AND    01800002
*    QUEUE THE READ INITIAL IF NO ATTENTION EXISTS (IECTRDIL).          02100002
*                                                                       02400002
*         2. RESET ATTENTION STATUS FLAG FOR A DEVICE AND UPDATE THE    02700002
*    LINE GROUP'S ATTENTION COUNT (IECTATRL).                           03000002
*                                                                       03300002
*         3. SET THE SKIP FLAG FOR A DEVICE TO INHIBIT PERFORMING READ  03600002
*    INITIAL OPERATIONS ON THAT DEVICE (IECTCHSL).                      03900002
*                                                                       04200002
*         4. RESET THE SKIP FLAG FOR A DEVICE TO PERMIT PERFORMING READ 04500002
*    INITIAL OPERATIONS ON THAT DEVICE (IECTCHAL).                      04800002
*                                                                       05100002
*         5. DEQUEUE A QUEUED READ INITIAL OPERATION (IECTRSTL).        05400002
*                                                                       05700002
* ENTRY POINTS- 1. IECTRDIL FROM SVC 116 IF THE ROUTING CODE WAS 0.     06000002
*                                                                       06300002
*         2. IECTATRL FROM SVC 116 IF THE ROUTING CODE WAS 1.           06600002
*                                                                       06900002
*         3. IECTCHSL FROM SVC 116 IF THE ROUTING CODE WAS 2.           07200002
*                                                                       07500002
*         4. IECTCHAL FROM SVC 116 IF THE ROUTING CODE WAS 3.           07800002
*                                                                       08100002
*         5. IECTRSTL FROM SVC 116 IF THE ROUTING CODE WAS 4.           08400002
*                                                                       08700002
* INPUT-  1. REGISTER 15 CONTAINS THE ROUTING CODE ON ENTRY TO SVC 116. 09000002
*         2. REGISTER  0 CONTAINS THE ADDRESS OF THE DECB FOR IECTRSTL  09300002
*    AND ZERO IN ALL OTHER CASES                                        09600002
*         3. REGISTER  1 CONTAINS THE ADDRESS OF THE UCB FOR THE RLN    09900002
*    SPECIFIED OR IMPLIED                                               10200002
*         4. REGISTER 14 CONTAINS THE ADDRESS OF THE TYPE 1 SVC EXIT    10500002
*    ROUTINE.                                                           10800002
*                                                                       11100002
* OUTPUT- 1. UPDATED ATTENTION FLAGS IN THE UCB'S FOR THE DEVICE AND    11400002
*    LINE GROUP.                                                        11700002
*         2. A RETURN CODE IN REGISTER 15.                              12000002
*                                                                       12300002
* EXITS,NORMAL- BR 14 TO THE TYPE 1 SVC EXIT ROUTINE.                   12600002
*                                                                       12900002
* EXITS,ERROR-  BR 14 TO THE TYPE 1 SVC EXIT ROUTINE.                   13200002
*                                                                       13500002
* TABLES/WORK AREAS- NONE                                               13800002
*                                                                       14100002
* ATTRIBUTES- NUCLEUS RESIDENT, DISABLED, SUPERVISOR PROTECT KEY, READ  14400002
*    ONLY, TYPE 1 SVC INITIATED FUNCTION.                               14700002
*                                                                       15000002
         EJECT                                                          15003002
         EJECT                                                          15246002
* CHARACTER CODE DEPENDENCY- N/A                                        15300002
*                                                                       15600002
* NOTES- THIS ROUTINE MANIPULATES ATTENTION HANDLING FLAGS IN THE UCB'S 15900002
*    FOR LOCAL 3270 DEVICES SUPPORTED UNDER BTAM.  USER REGISTERS 2-14  16200002
*    ARE SAVED BY SVC FLIH AND RESTORED ON RETURN TO THE USER.          16500002
*                                                                       16800002
         EJECT                                                          16803002
*/*IECTSVC: CHART */                                                    16806002
*/* HEADER                                                              16809002
*/*LOGIC MODULE FOR ESR ROUTING CODES 0 THROUGH 4                       16812002
*/*BTAM 3270 ATTENTION HANDLING SUPPORTING SVC                      */  16815002
*/*IECTRDIL: E ENTRY FROM ESR */                                        16818002
*/* M ESTABLISH ADDRESSABILITY AND SAVE RETURN ADDR */                  16821002
*/* S UCBCHK: CHECK UCB ADDRESS FOR VALIDITY */                         16824002
*/* D (YES,,NO,RC8) UCB VALID? */                                       16827002
*/* D (YES,,NO,RC0) ATTENTION COUNT IS ZERO? */                         16830002
*/* D (YES,,NO,CONTRDTI) BTAM READY PROCESSING ? */                     16832002
*/* D (YES,RDYPROC,NO,) READY IN LINE GROUP ? */                        16834002
*/*CONTRDTI: P SET READ INITIAL PENDING FLAG */                         16834402
*/* P (,RC4) SAVE RLN OF INITIALIZED IOB */                             16836002
*/*RDYPROC: P SET READ INITIAL PENDING FLAG */                          16838002
*/* P (,RCC) SAVE RLN IN MASTER, SET READY NOT DONE IN MASTER */        16838402
*/*IECTATRL: E ENTRY FROM ESR */                                        16839002
*/* M ESTABLISH ADDRESSABILITY AND SAVE RETURN ADDR */                  16842002
*/* S UCBCHK: CHECK UCB ADDRESS FOR VALIDITY */                         16845002
*/* D (YES,,NO,RC4) UCB VALID? */                                       16848002
*/* D (YE>,,NO,RDYCHKT) ATTN FLAG SET ? */                              16850002
*/* D (YES,RDYCHKT,NO,) PROCESSING READY ? */                           16852002
*/* P RESET THE ATTENTION FLAG */                                       16854002
*/* D (NO,,YES,RC0) SKIP FLAG SET? */                                   16857002
*/* P (,RC0) DECREMENT ATTENTION COUNT FOR LINE GROUP BY 1 */           16860002
*/*RDYCHKT: D (YES,,NO,RC4) USING BTAM'S READY ROUTINE ? */             16862002
*/*RDYCHK: D (YES,,NO,RC0) READY NOT DONE FLAG ON ? */                  16864002
*/* L (,RC0) INVOKE POST TO RESETPL THE READ */                         16866002
*/*IECTCHSL: E ENTRY FROM ESR */                                        16870602
*/* M ESTABLISH ADDRESSABILITY AND SAVE RETURN ADDR */                  16874302
*/* S UCBCHK: CHECK UCB ADDRESS FOR VALIDITY */                         16878002
*/* D (YES,,NO,RC8) UCB VALID? */                                       16881702
*/* D (NO,,YES,RC0) SKIP FLAG SET? */                                   16885402
*/* P SET THE SKIP FLAG */                                              16889102
*/* D (YES,,NO,RC0) ATTENTION FLAG SET */                               16892802
*/* P (,RC0) DECREMENT ATTENTION COUNT FOR LINE GROUP BY 1 */           16896502
*/*IECTCHAL: E ENTRY FROM ESR */                                        16900202
*/* M ESTABLISH ADDRESSABILITY AND SAVE RETURN ADDR */                  16903902
*/* S UCBCHK: CHECK UCB ADDRESS FOR VALIDITY */                         16907602
*/* D (YES,,NO,RC8) UCB VALID? */                                       16911302
*/* D (YES,,NO,RC0) SKIP FLAG SET? */                                   16915002
*/* P RESET THE SKIP FLAG */                                            16918702
*/* D (YES,,NO,RC0) ATTENTION FLAG SET? */                              16922402
*/* D (NO,,YES,SCHEDIQE) READ INITIAL PENDING? */                       16926102
*/* P (,RC0) INCREMENT ATTENTION COUNT FOR LINE GROUP BY 1 */           16929802
*/*SCHEDIQE: P RESET READ INITIAL PENDING AND ATTENTION FLAGS */        16933502
*/* P OBTAIN AND INITIALIZE IQE */                                      16937202
*/* L (,RC0) INVOKE AEE2 TO SCHEDULE READ */                            16940902
*/*IECTRSTL: E ENTRY FROM ESR */                                        16944602
*/* M ESTABLISH ADDRESSABILITY AND SAVE RETURN ADDR */                  16948302
*/* S UCBCHK: CHECK UCB ADDRESS FOR VALIDITY */                         16952002
*/* D (0,,4,RC8,8,RCC) TEST RETURN CODE */                              16955702
*/*RDTITST: D (YES,,NO,RC4) READ INITIAL PENDING? */                    16959402
*/* P RESET READ INITIAL PENDING FLAG */                                16963102
*/* P CLEAR INITIALIZED RLN FIELD */                                    16966802
*/* L (,RC0) POST USER'S ECB COMPLETE WITH X'48' CODE */                16970502
*/*RC0: P SET RETURN CODE OF 0 */                                       16974202
*/* R BR TO TYPE I SVC EXIT RTN */                                      16977902
*/*RC4: P SET RETURN CODE OF 4 */                                       16981602
*/* R BR TO TYPE I SVC EXIT RTN */                                      16985302
*/*RC8: P SET RETURN CODE OF 8 */                                       16989002
*/* R BR TO TYPE I SVC EXIT RTN */                                      16992702
*/*RCC: P SET RETURN CODE OF X'0C' */                                   16996402
*/* R BR TO TYPE I SVC EXIT RTN */                                      17000102
*/*IECTSVC: END */                                                      17003802
*/*UCBCHK: CHART */                                                     17007502
*/* HEADER                                                              17011202
*/*IECTSVC UCB VALIDITY CHECK SUBROUTINE                            */  17014902
*/*UCBCHK: E ENTRY */                                                   17018602
*/* M ESTABLISH ADDRESSABILITY FOR SUBROUTINE */                        17022302
*/* M ESTABLISH ADDRESSABILITY FOR DEVICE UCB */                        17026002
*/* D (YES,,NO,VCRC08) UCBID = X'FF'? */                                17029702
*/* D (YES,,NO,VCRC04) IS DEVICE A 3270 DEVICE? */                      17033402
*/* D (YES,,NO,VCRC08) IRB ADDRESS INITIALIZED? */                      17037102
*/* D (NO,,YES,RLNISONE) DEVICE UCB = MASTER UCB? */                    17040802
*/* M OBTAIN ADDRESS OF MASTER UCB FROM DEVICE UCB */                   17044502
*/*CHKMSTR: D (YES,,NO,VCRC08) UCBID OF MASTER UCB = X'FF'? */          17048202
*/* D (YES,,NO,VCRC04) IS MASTER DEVICE A 3270 DEVICE? */               17051902
*/* D (YES,,NO,VCRC08) IRB ADDR IN MASTER UCB NON-ZERO? */              17055602
*/* D (YES,,NO,VCRC08) BOTH UCB IRB ADDRESSES THE SAME? */              17059302
*/* D (YES,,NO,VCRC08) RLN IN MASTER UCB = 1? */                        17063002
*/*VCRC00:  P SET RETURN CODE OF ZERO */                                17066702
*/* R RETURN */                                                         17070402
*/*VCRC04: P SET RETURN CODE OF FOUR */                                 17074102
*/* R RETURN */                                                         17077802
*/*VCRC08: P SET RETURN CODE OF EIGHT */                                17081502
*/* R RETURN */                                                         17085202
*/*RLNISONE: P (,VCRC00) SET MASTER UCB = DEVICE UCB */                 17088902
*/*UCBCHK: END */                                                       17092602
         EJECT                                                          17096302
PARMREG2 EQU   0                    SECOND PARAMETER REGISTER           17100002
PARMREG1 EQU   1                    FIRST PARAMETER REGISTER            17400002
CVTREG   EQU   3                    POINTER TO CVT                      17700002
TCBREG   EQU   4                   POINTER TO TCB                       18000002
RTNREG   EQU   14                   RETURN POINT REGISTER               18300002
EPREG    EQU   15                   ENTRY POINT REGISTER                18600002
*                                                                       18900002
BASEREG  EQU   5                    BASE REGISTER FOR MAIN ROUTINE      19200002
SUBASREG EQU   10                   BASE REGISTER FOR SUB-ROUTINE       19500002
UCBREG   EQU   9                    BASE REGISTER FOR DEVICE UCB        19800002
MSTREG   EQU   8                    BASE REGISTER FOR MASTER UCB        20100002
IRBREG   EQU   7                    POINTER TO IRB                      20400002
IQEREG   EQU   6                    POINTER TO IQE                      20700002
*                                                                       21000002
SAVEREG  EQU   12                   SAVE REGISTER FOR REGISTER 14       21300002
WORKREG  EQU   2                    WORK REGISTER                       21600002
POSTCODE EQU   10                  COMPLETION CODE REG FOR POST         21900002
POSTECB  EQU   11                  ECB ADDR REG FOR POST                22200002
POSTTCB  EQU   12                  TCB ADDR REG FOR POST                22500002
ONEBYTE  EQU   8                   EIGHT BITS IN A BYTE     LD @ZA00537 22550002
*                                                                       22800002
DVC3277  EQU   X'09'                DEVICE TYPE FOR 3277 DISPLAY        23700002
DVC3286  EQU   X'0B'               DEVICE TYPE FOR 3286 PRINTER         24000002
*              X'0A'               DEVICE TYPE FOR 3284 PRINTER         24300002
READY    EQU   X'20'               HAS COME READY FLAG         @YA02128 25210002
LINERDY  EQU   X'01'               MTR DEVICE IN GROUP READY   @YA02128 25220002
RDTIACTV EQU   X'08'               READ INIT REQ OUTSTANDING    YA01033 25300002
RDYNDONE EQU   X'04'               INDICATE READY NOT DONE     @YA02128 25350002
RLN1     EQU   X'01'                RLN FOR RLN 1 OF LINE GROUP         25500002
IGG019UP EQU   X'10'               USING BTAM EXIT ROUT        @YA02128 26450002
*                                   (CVT0PT01)                          27000002
ZERO     EQU   X'00'                COMPARE VALUE OF 0                  27300002
ONE      EQU   X'01'                INCREMENT VALUE OF 1                27600002
FOUR     EQU   X'04'                RETURN CODE OF 4                    27900002
EIGHT    EQU   X'08'                RETURN CODE OF 8                    28200002
TWELVE   EQU   X'0C'                RETURN CODE OF X'0C'                28500002
X58      EQU   X'58'                DISPLACEMENT TI 1ST IOB  LD YA02451 28550002
SIXTEEN  EQU   X'10'               RETURN CODE X'10'           @YA02128 28550102
         EJECT                                                          28800002
         USING UCB,UCBREG           ADDRESSABILITY FOR DEVICE UCB       29100002
         USING MSTRUCB,MSTREG       AND MASTER UCB                      29400002
         USING RBBASIC,IRBREG                                 LD Y02947 29450002
         USING CVT,CVTREG                                     LD Y02947 29460002
         USING IQESECT,IQEREG                                 LD Y02947 29500002
         SPACE 5                                                        29550002
*        READ INITIAL ATTENTION TEST                                    29700002
         SPACE 3                                                        29750002
IECTRDIL LR    SAVEREG,RTNREG       SAVE RETURN ADDRESS                 30000002
         BALR  BASEREG,0            ESTABLISH BASE REGISTER             30300002
         USING *,BASEREG            AND ADDRESSABILITY                  30600002
         B     BEGIN1              BRANCH AROUND ID         LD @ZA02332 30650002
         DC    C' IECTSVC'          MODULE IDENTIFIER          @YA02128 30700003
         DC    C'** MVS *'                                     @ZA02332 30730003
         DC    C'&SYSDATE'         DATE LAST ASSEMBLY       LD @ZA02332 30770002
PATCH1   DC    20X'00'             PATCH AREA FOR IECTRDIL  LD @ZA02332 30800002
BEGIN1   DS    0F                                           LD @ZA02332 30850002
*                                                                       30900002
         BAL   RTNREG,UCBCHK        CHECK UCB ADDRESS FOR VALIDITY      31200002
         LTR   EPREG,EPREG          UCB VALID                           31500002
         BNZ   RC8                  NO, RETURN WITH RETURN CODE OF 8    31800002
*                                                                       32100002
         OI    MTRGCB,RDTIACTV     READ INIT REQ OUTSTANDING    YA01033 32200002
         CLI   MTRATNCT,ZERO        IS ATTENTION COUNT ZERO             32400002
         BNE   RC0                  NO, RETURN WITH RETURN CODE OF 0    32700002
*                                                                       33000002
         TM    UCBGRAF,IGG019UP    BTAM READY PROCESSING ?     @YA02128 33050002
         BNO   CONTRDTI            NO, DO READ TI              @YA02128 33100002
         TM    MTRGRAF,LINERDY     COME READY ?                @YA02128 33150002
         BO    RDYPROC             YES, PROCESS READY          @YA02128 33200002
CONTRDTI EQU   *                   COMPLETE RDTI               @YA02128 33250002
         OI    MTRGCB,UCBRIPND      SET READ INIT.PEND. FLAG  LD Y02947 33300002
         MVC   MTRINRLN(ONE),UCBRLN  MOVE RLN OF INITIALIZED LINE       33600002
         B     RC4                  RETURN WITH RETURN CODE OF 4        33900002
RDYPROC  EQU   *                   DEVICE READY                @YA02128 33950002
         OI    MTRGCB,UCBRIPND     SET PENDING FLAG            @YA02128 34000002
         MVC   MTRINRLN(ONE),UCBRLN MOVE RLN OF INIT LINE      @YA02128 34050002
         OI    MTRGRAF,RDYNDONE    SET NOT DONE IN UCB         @YA02128 34100002
         B     RCC                 GIVE RETURN 0C              @YA02128 34150002
         DROP  BASEREG                                                  34200002
         EJECT                                                          34500002
*                                                                       34550002
*        RESET ATTENTION FLAG                                           34800002
         SPACE 3                                                        34850002
IECTATRL LR    SAVEREG,RTNREG       SAVE RETURN ADDRESS                 35100002
         BALR  BASEREG,0            ESTABLISH BASE REGISTER             35400002
         USING *,BASEREG            AND ADDRESSABILITY                  35700002
ATRLBASE EQU   *                   ALLOW ADDRESSABILITY     L5 @ZA03587 35702003
         B     BEGIN2              BRANCH AROUND PATCHAREA  LD @ZA02332 35710002
PATCH2   DC    20X'00'             PATCH AREA FOR IECTATRL  LD @ZA02332 35720002
BEGIN2   DS    0F                                           LD @ZA02332 35730002
*                                                                       36000002
         BAL   RTNREG,UCBCHK        CHECK UCB ADDRESS FOR VALIDITY      36300002
         LTR   EPREG,EPREG          UCB VALID                           36600002
         BNZ   RC4                  NO, GIVE RETURN CODE OF 4           36900002
*                                                                       37200002
         TM    UCBGCB,UCBATRCD      ATTENTION FLAG SET        LD Y02947 37500002
         BZ    RDYCHKT              NO, SEE IF READY           @YA02128 37800002
         TM    MTRGRAF,RDYNDONE    READY NOT DONE FLAG ?       @YA02128 37850002
         BO    RDYCHKT             YES, PROCESS READY          @YA02128 37900002
         XI    UCBGCB,UCBATRCD      YES, RESET THE ATT. FLAG  LD Y02947 38100002
*                                                                       38400002
         TM    UCBGCB,UCBSKPFG      SKIP FLAG SET             LD Y02947 38700002
         BO    RC0                  YES, GIVE RETURN CODE OF 0          39000002
         SR    WORKREG,WORKREG                                          39300002
         IC    WORKREG,MTRATNCT     DECREMENT ATTENTION COUNT           39600002
         BCTR  WORKREG,0            FOR LINE GROUP BY 1                 39900002
         STC   WORKREG,MTRATNCT                                         40200002
         B     RC0                  GIVE RETURN CODE OF 0               40500002
RDYCHKT  EQU   *                   SEE IF BTAM                 @YA02128 40510002
         TM    UCBGRAF,IGG019UP    USE BTAMS ?                 @YA02128 40520002
         BNO   RC0                 NO, GIVE RETURN CODE 0      @YA02128 40530002
RDYCHK   EQU   *                   CHECK FOR READY             @YA02128 40550002
         TM    MTRGRAF,RDYNDONE    NOT DONE FLAG ?             @YA02128 40600002
         BZ    RC0                 NO, GIVE RETURN CODE 0      @YA02128 40650002
         LTR   PARMREG2,PARMREG2   DECB ADDR IN REG ?       L5 @ZA03567 40700003
         BZ    RC8                 IF NO,SET RET CODE TO 8  L5 @ZA03567 40750003
         LR    SUBASREG,BASEREG    ESTABLISH NEW BASE          @YA02128 40752002
         DROP  BASEREG             RELEASE OLD BASE            @YA02128 40802002
         USING ATRLBASE,SUBASREG   AND USE NEW BASE            @YA02128 40852002
         LA    BASEREG,RSTLBASE   ESTABLISH NEW ADDRESSABILITY @YA02128 40902002
         B     POST48              GO POST READ                @YA02128 41002002
         DROP  SUBASREG            DROP ADDRESSABILITY         @YA02128 41052002
         EJECT                                                          41100002
*                                                                       41150002
*        INHIBIT READ INITIALS ON A DEVICE                              41400002
         SPACE 3                                                        41450002
IECTCHSL LR    SAVEREG,RTNREG       SAVE RETURN ADDRESS                 41700002
         BALR  BASEREG,0            ESTABLISH BASE REGISTER             42000002
         USING *,BASEREG            AND ADDRESSABILITY                  42300002
         B     BEGIN3              BR AROUND PATCH AREA     LD @ZA02332 42350002
PATCH3   DC    20X'00'             PATCH AREA FOR IECTCHSL  LD @ZA02332 42400002
BEGIN3   DS    0F                                           LD @ZA02332 42450002
*                                                                       42600002
         BAL   RTNREG,UCBCHK        CHECK UCB ADDRESS FOR VALIDITY      42900002
         LTR   EPREG,EPREG          UCB VALID                           43200002
         BNZ   RC8                  NO, GIVE RETURN CODE OF 8           43500002
*                                                                       43800002
         TM    UCBGCB,UCBSKPFG      SKIP FLAG SET             LD Y02947 44100002
         BO    RC0                  YES, GIVE RETURN CODE OF 0          44400002
         OI    UCBGCB,UCBSKPFG      NO, SET THE SKIP FLAG     LD Y02947 44700002
*                                                                       45000002
         TM    UCBGCB,UCBATRCD      ATTENTION FLAG SET        LD Y02947 45300002
         BZ    RC0                  NO, GIVE RETURN CODE OF 0           45600002
         SR    WORKREG,WORKREG                                          45900002
         IC    WORKREG,MTRATNCT     DECREMENT ATTENTION COUNT           46200002
         BCTR  WORKREG,0            FOR LINE GROUP BY 1                 46500002
         STC   WORKREG,MTRATNCT                                         46800002
         B     RC0                  GIVE RETURN CODE OF 0               47100002
         DROP  BASEREG                                                  47400002
         EJECT                                                          47450002
*                                                                       47700002
*        PERMIT READ INITIALS ON A DEVICE                               48000002
         SPACE 3                                                        48050002
IECTCHAL LR    SAVEREG,RTNREG       SAVE RETURN ADDRESS                 48300002
         BALR  BASEREG,0            ESTABLISH BASE REGISTER             48600002
         USING *,BASEREG            AND ADDRESSABILITY                  48900002
         B     BEGIN4              BR AROUND PATCH AREA     LD @ZA02332 48950002
PATCH4   DC    20X'00'             PATCH AREA FOR IECTCHAL  LD @ZA02332 49000002
BEGIN4   DS    0F                                           LD @ZA02332 49050002
*                                                                       49200002
         BAL   RTNREG,UCBCHK        CHECK UCB ADDRESS FOR VALIDITY      49500002
         LTR   EPREG,EPREG          UCB VALID                           49800002
         BNZ   RC8                  NO, GIVE RETURN CODE OF 8           50100002
*                                                                       50400002
         TM    UCBGCB,UCBSKPFG      SKIP FLAG SET             LD Y02947 50700002
         BZ    RC0                  NO, GIVE RETURN CODE OF 0           51000002
         XI    UCBGCB,UCBSKPFG      YES, RESET THE SKIP FLAG  LD Y02947 51300002
*                                                                       51600002
         TM    UCBGCB,UCBATRCD      ATTENTION FLAG SET        LD Y02947 51900002
         BZ    RC0                  NO, GIVE RETURN CODE OF 0           52200002
*                                                                       52500002
         TM    MTRGCB,UCBRIPND      READ INITIAL PENDING      LD Y02947 52800002
         BO    SCHEDIQE             YES, SCHEDULE 2ND LEVEL ATTN. RTN   53100002
*                                   TO DO READ                          53400002
         SR    WORKREG,WORKREG      NO, INCREMENT ATTENTION COUNT       53700002
         IC    WORKREG,MTRATNCT     FOR LINE GROUP BY 1                 54000002
         LA    WORKREG,ONE(WORKREG)                                     54300002
         STC   WORKREG,MTRATNCT                                         54600002
         B     RC0                  GIVE RETURN CODE OF ZERO            54900002
*                                                                       55200002
SCHEDIQE XI    MTRGCB,UCBRIPND      RESET RD INIT.PEND. FLAG  LD Y02947 55500002
         XI    UCBGCB,UCBATRCD      RESET ATTENTION FLAG      LD Y02947 55800002
         L     IQEREG,RBNEXAV       GET ADDRESS OF IQE        LD Y02947 56100002
         XC    RBNEXAV(FOUR),RBNEXAV  CLEAR IQE LINK          LD Y02947 56400002
         ST    UCBREG,IQEPARAM      STORE UCB ADDRESS IN IQE  LD Y02947 56700002
         LNR   PARMREG1,IQEREG      PARM = COMPLEMENT OF IQE ADDR       57000002
         L     EPREG,CVT0EF00       EP = AEE2                 LD Y02947 57300002
         BALR  RTNREG,EPREG         SCHEDULE IQE                        57600002
         B     RC0                  GIVE RETURN CODE OF 0               57900002
         EJECT                                                          57950002
*                                                                       58200002
*        DEQUEUE PENDING READ INITIAL                                   58500002
         SPACE 3                                                        58550002
IECTRSTL LR    SAVEREG,RTNREG       SAVE RETURN ADDRESS                 58800002
         DROP  BASEREG                                                  59100002
         BALR  BASEREG,0            ESTABLISH BASE REGISTER             59400002
         USING *,BASEREG            AND ADDRESSABILITY                  59700002
RSTLBASE EQU   *                   ALLOW ADDRESSABILITY     L5 @ZA03587 59702003
         B     BEGIN5              BR AROUND PATCH AREA     LD @ZA02332 59710002
PATCH5   DC    20X'00'             PATCH AREA FOR IECTRSTL  LD @ZA02332 59720002
BEGIN5   DS    0F                                           LD @ZA02332 59730002
*                                                                       60000002
         BAL   RTNREG,UCBCHK        CHECK UCB ADDRESS FOR VALIDITY      60300002
         B     RCTABLE(EPREG)       TEST RETURN CODE                    60600002
RCTABLE  B     RDTITST             GO TEST READ INITIAL     LD @ZA02332 60900002
         B     RC8                  4, INVALID DVC- GIVE RC OF 8        61200002
         B     RCC                  8, INVALID UCB- GIVE RC OF X'0C'    61500002
*                                                                       61800002
*              14 LINES DELETED FOR ----->                  LD @ZA02332 61850002
*                                                                       62098802
RDTITST  TM    MTRGCB,RDTIACTV     READ INIT REQ OUTSTANDING?   YA01033 62100002
         BZ    RC10                RC=16                       @ZA14028 62200000
         TM    MTRGCB,UCBRIPND     READ INIT PENDING?         LD Y02947 62300002
         BZ    RC4                  NO, GIVE RETURN CODE OF 4           62400002
*                                   (OPERATION IN PROGRESS)             62700002
POST48   EQU   *                                             LD YA02174 62750002
         NI    MTRGCB,X'FF'-RDTIACTV-UCBRIPND     YES,        LD Y02947 63000002
*                                  RESET READ INIT PENDING      YA01033 63200002
*                                  AND OUTSTANDING FLAGS        YA01033 63400002
         TM    MTRGRAF,IGG019UP    BTAM READY ?                @YA02128 63400102
         BNO   CONTPOST            NO, DON'T DO READY CHECK    @YA02128 63450002
         TM    MTRGRAF,RDYNDONE    READY PROCESSING            @YA02128 63500002
         BNO   CONTPOST            NO, DON'T RESET FLAGS       @YA02128 63550002
         NI    MTRGRAF,X'FB'       TURN OFF READY NOT DONE     @YA02128 63560002
         NI    UCBGRAF,X'DB'       TURN OFF READY FLAG         @YA02128 63570002
         L     EPREG,MTRDEBAD      GET DEB ADDRESS             @YA02128 63580002
         LA    EPREG,0(EPREG)      CLEAR HIGH BYTE             @YA02128 63590002
         XR    POSTECB,POSTECB     CLEAR RLN REG               @YA02128 63592002
         IC    POSTECB,16(EPREG)   GET NUMBER EXTENTS          @YA02128 63594002
         XR    POSTCODE,POSTCODE   CLEAR INDEX REGISTER        @YA02128 63596002
READYLP  EQU   *                   CHECK UCB'S FOR READY       @YA02128 63598002
         L     PARMREG1,32(POSTCODE,EPREG) POINT TO UCB        @YA02128 63598402
         TM    28(PARMREG1),READY  COME READY                  @YA02128 63598802
         BO    CONTPOST            YES, DON'T TURN OFF         @YA02128 63599202
*                                  LINE READY IN MASTER        @YA02128 63599602
         LA    POSTCODE,4(POSTCODE) BUMP INDEX                 @YA02128 63599702
         BCT   POSTECB,READYLP     CHECK IF MORE TO DO         @YA02128 63599802
         NI    MTRGRAF,X'FE'       TURN OFF LINE READY         @YA02128 63599902
CONTPOST EQU   *                   CONTINUE THE POST           @YA02128 63649902
*                                                                       63650002
         MVI   MTRINRLN,ZERO        CLEAR INITIALIZED RLN               63699902
*        SAVE CONTENTS OF REG 12 ACROSS CALL TO POST                    63900002
*                                                                       63950002
         LR    PARMREG1,SAVEREG    REG 1 IS SAVED BY POST               64200002
*                                                                       64250002
*        SET UP LINKAGE TO POST -- NON-STANDARD                         64500002
*                                                                       64550002
         L     POSTCODE,COMPCODE   REG 10 = COMPLETION CODE             64800002
         LR    POSTECB,PARMREG2    REG 11 = ECB ADDR         LD YA03231 64850002
         LA    POSTECB,ZERO(POSTECB) CLEAR HIGH ORDER BYTE   LD YA03231 64900002
         LR    POSTTCB,TCBREG      REG 12 = TCB ADDR                    65400002
         L     EPREG,CVT0PT01      EP = POST                  LD Y02947 65700002
         BALR  RTNREG,EPREG        POST USER'S ECB COMPLETE             66000002
         LR    SAVEREG,PARMREG1    RESTORE REG 12                       66300002
         B     RC0                  GIVE RETURN CODE OF 0               66600002
         DROP  BASEREG                                                  66900002
         EJECT                                                          67200002
*                                                                       67250002
*        RETURN CODES SUBROUTINE                                        67300002
*                                                                       67350002
RC0      SR    EPREG,EPREG          SET REGISTER 15 = 0                 67500002
         LR    RTNREG,SAVEREG       EXIT RTN ADDRESS IN 14              67800002
         BR    RTNREG               BRANCH TO TYPE 1 EXIT RTN.          68100002
*                                                                       68400002
RC4      LA    EPREG,FOUR           SET RC = 4                          68700002
         LR    RTNREG,SAVEREG       EXIT RTN ADDR TO REG 14             69000002
         BR    RTNREG               BRANCH TO TYPE 1 EXIT RTN.          69300002
*                                                                       69600002
RC8      LA    EPREG,EIGHT          SET RC = 8                          69900002
         LR    RTNREG,SAVEREG       EXIT RTN ADDR TO REG 14             70200002
         BR    RTNREG               BRANCH TO TYPE 1 EXIT RTN.          70500002
*                                                                       70800002
RCC      LA    EPREG,TWELVE         SET RC = X'0C'                      71100002
         LR    RTNREG,SAVEREG       EXIT RTN ADDR TO REG 14             71400002
         BR    RTNREG               BRANCH TO TYPE 1 EXIT RTN.          71700002
*                                                                       72000002
RC10     EQU   *                                               @ZA14028 72010000
         LR    WORKREG,PARMREG2     LOAD DECB ADDR             @ZA14028 72030000
         SLL   WORKREG,ONEBYTE      DECB ADDR IN HI BYTE       @ZA14028 72050000
         LA    EPREG,SIXTEEN        RC=X'10'                   @ZA14028 72070000
         OR    EPREG,WORKREG        DECB ADDR IN REG 15        @ZA14028 72090000
         LR    RTNREG,SAVEREG       EXIT ROUT ADDR IN REG 14   @ZA14028 72110000
         BR    RTNREG               BR TYPE1 EXIT ROUT         @ZA14028 72130000
         EJECT                                                          72160002
*                                                                       72200002
*        UCB VALIDITY CHECK ROUTINE                                     72300002
         SPACE 3                                                        72350002
UCBCHK   BALR  SUBASREG,0           ADDRESSABILITY FOR                  72600002
         USING *,SUBASREG           VALIDITY SUBROUTINE                 72900002
         LR    UCBREG,PARMREG1      ADDRESSABILITY FOR UCB              73200002
*                                                                       73500002
         CLI   UCBID,UCBSTND        UCBID = X'FF'             LD Y02947 73800002
         BNE   VCRC08               NO, GIVE RETURN CODE OF 8           74100002
*                                                                       74400002
         CLI   UCBTBYT3,UCB3DISP    IS DEVICE A GRAPHICS DEV. LD Y02947 74700002
         BNE   VCRC04               NO, GIVE RETURN CODE OF 4           75000002
         CLI   UCBTBYT4,DVC3277    DEVICE 3277 DISPLAY        LD Y02947 75300002
         BL    VCRC04              NO, GIVE RETURN CODE OF 4            75600002
         CLI   UCBTBYT4,DVC3286    DEV. ANY OTHER 3270 DEVICE LD Y02947 75900002
         BH    VCRC04              NO, GIVE RETURN CODE OF 4            76200002
*                                                                       76500002
CHKIRB   L     IRBREG,UCBIRB        GET IRB ADDRESS                     76800002
         LA    IRBREG,0(IRBREG)    CLEAR GRAF FLAGS            @YA02128 76850002
         LTR   IRBREG,IRBREG        IS IT ZERO                          77100002
         BZ    VCRC08               YES, GIVE RETURN CODE OF 8          77400002
         CLI   UCBRLN,RLN1          DEVICE UCB = MASTER UCB             77700002
         BE    RLNISONE             YES, SET MASTER UCB BASE            78000002
         L     MSTREG,UCBCTLNK      GET ADDRESS OF MASTER UCB           78300002
*                                                                       78600002
CHKMSTR  CLI   MTRUCBID,UCBSTND     UCBID OF MASTER UCB       LD Y02947 78900002
         BNE   VCRC08               NO, GIVE RETURN CODE OF 8           79200002
*                                                                       79500002
         CLI   MTRDVCLS,UCB3DISP    DEVICE A GRAPHICS DEVICE  LD Y02947 79800002
         BNE   VCRC04               NO, GIVE RETURN CODE OF 4           80100002
         CLI   MTRUNTYP,DVC3277     DEVICE 3277 DISPLAY                 80400002
         BL    VCRC04              NO, GIVE RETURN CODE OF 4            80700002
         CLI   MTRUNTYP,DVC3286    DEVICE ANY OTHER 3270 DEVICE         81000002
         BH    VCRC04              NO, GIVE RETURN CODE OF 4            81300002
*                                                                       81600002
CHKIRB1  L     WORKREG,MTRIRB       GET IRB ADDRESS                     81900002
         LA    WORKREG,0(WORKREG)  CLEAR GRAF FLAGS            @YA02128 81950002
         LTR   WORKREG,WORKREG      IS IT ZERO                          82200002
         BZ    VCRC08               YES, GIVE RETURN CODE OF 8          82500002
         CR    WORKREG,IRBREG       EQUAL TO ADDRESS FROM DEVICE UCB    82800002
         BNE   VCRC08               NO, GIVE RETURN CODE OF 8           83100002
         CLI   MTRRLN,RLN1          RLN = 1                             83400002
         BNE   VCRC08               NO, GIVE RETURN CODE OF 8           83700002
*                                                                       84000002
VCRC00   SR    EPREG,EPREG          YES, GIVE RETURN CODE OF 0          84300002
         BR    RTNREG               AND RETURN                          84600002
*        INVALID DEVICE TYPE                                            84900002
VCRC04   LA    EPREG,FOUR           GIVE RETURN CODE OF 4               85200002
         BR    RTNREG               AND RETURN                          85500002
*        INVALID CONTROL BLOCK                                          85800002
VCRC08   LA    EPREG,EIGHT          GIVE RETURN CODE OF 8               86100002
         BR    RTNREG              AND RETURN                           86400002
*                                                                       86700002
RLNISONE LR    MSTREG,UCBREG        SET MASTER UCB = DEVICE UCB         87000002
         B     VCRC00               MASTER UCB OK. GIVE RETURN CODE 0   87300002
*                                                                       87600002
         DS    0F                                                       87900002
COMPCODE DC    X'48000000'         COMPLETION CODE FOR RESETPL          88200002
*                                                                       88500002
IN       EQU   8                                                        88600002
         EJECT                                                          88800002
CVT      DSECT                                                          88810002
         CVT                                          LD Y02947         88820002
         EJECT                                                          88820402
         DCBD  DSORG=BX                                      LD YA02451 88822402
         EJECT                                                          88822802
         IECTDEBX                                            LD YA02451 88824002
         EJECT                                                          88826002
         IECTIOBX                                            LD YA02451 88828002
         EJECT                                                          88830002
         IHAIQE                                                         88850002
         EJECT                                                          89099602
         IHARB                                                          89099702
         EJECT                                                          89099802
UCB      DSECT                                                          89099902
         IEFUCBOB                                                       89149902
         EJECT                                                          89151902
MSTRUCB  DSECT                                                          89153902
MTRUCBID EQU   MSTRUCB+(UCBID-UCBOB)                                    89155902
MTRDVCLS EQU   MSTRUCB+(UCBDVCLS-UCBOB)                                 89157902
MTRUNTYP EQU   MSTRUCB+(UCBUNTYP-UCBOB)                                 89158302
MTRATNCT EQU   MSTRUCB+(UCBATNCT-UCBOB)                                 89158702
MTRGCB   EQU   MSTRUCB+(UCBGCB-UCBOB)                                   89159102
MTRIRB   EQU   MSTRUCB+(UCBIRB-UCBOB)                                   89159502
MTRGRAF  EQU   MTRIRB                                                   89166202
MTRINRLN EQU   MSTRUCB+(UCBINRLN-UCBOB)                                 89173102
MTRRLN   EQU   MSTRUCB+(UCBRLN-UCBOB)                                   89179802
MTRDEBAD EQU   MTRRLN                                                   89186502
         SPACE 5                                                        89193202
         END                                                            89199902
