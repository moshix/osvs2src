         TITLE 'IGG0194P - BTAM OPEN LOAD 2 FOR LOCAL 3270'             00100002
IGG0194P CSECT                                                          00200002
*                                                              @YA02128 00250002
*              FOLLOWING CHANGES APPLIED:                      @YA02128 00300002
*                                                              @YA02128 00360002
* A602500-603500,A604000                                     LD YA03239 00370000
* D603000-603500,D604100                                      LD YM7701 00380000
*                                                                       00400002
*        YA03230  (12/1/74)                                             00450002
*        ZA00543  (11/15/74)        ZA00544  (11/15/74)                 00460003
*        ZA02328  (11/15/74)        AZ04872  (10/06/75)                 00470003
*        AZ06392  (10/06/75)        AZ08051  (01/29/76)                 00480003
*        AZ12151  (07/29/76)                                            00490000
*                                                                       00500003
*                                                                       00550002
* STATUS - CHANGE LEVEL 000                                             00600002
*                                                                       00800002
* FUNCTION/OPERATION - THIS MODULE VERIFIES THAT ALL DEVICES IN THE     01000002
*    LINE GROUP ARE LOCAL 3270 DEVICES.  A BUFFER POOL WILL BE OBTAINED 01200002
*    AND INITIALIZED IF REQUESTED.  IOB'S WILL BE OBTAINED AND          01400002
*    INITIALIZED.  AN ERASE WRITE WILL BE DONE TO EACH DEVICE IN THE    01600002
*    LINE GROUP TO INITIALIZE IT.  THE CONTROL BLOCK CHAINS REQUIRED    01800002
*    FOR ATTENTION HANDLING WILL BE INITIALIZED.                        02000002
*                                                                       02200002
* ENTRY POINTS - THIS ROUTINE IS ENTERED AT IGG0194P VIA AN XCTL FROM   02400002
*    IGG0193M, THE FIRST BTAM OPEN LOAD.  IT MAY ALSO BE RE-ENTERED     02600002
*    FROM THE RELOOP PORTION OF THIS ROUTINE IF IT DETERMINES THAT      02800002
*    THERE IS ANOTHER DATA CONTROL BLOCK TO BE OPENED.                  03000002
*                                                                       03200002
* INPUT - THE REGISTERS 5, 6, 7, AND 8 ARE THE INPUT, AS FOLLOWS:       03400002
*    5 - THE ADDRESS OF THE FIRST ENTRY IN THE DCB PARAMETER LIST       03600002
*    6 - THE ADDRESS OF THE BEGINNING OF THE WHERE-TO-GO TABLE          03800002
*    7 - THE ADDRESS OF THE CURRENT ENTRY IN THE DCB PARAMETER LIST     04000002
*    8 - THE ADDRESS OF THE CURRENT EXECUTOR'S ENTRY IN THE WHERE-TO-GO 04200002
*        TABLE                                                          04400002
*                                                                       04600002
* OUTPUT - THE REGISTERS 7 AND 8 WILL BE POSITIONED AT THE NEXT ENTRIES 04800002
*    IN THE DCB PARAMETER LIST AND THE WHERE-TO-GO TABLE.               05000002
*        SOME OF THE DCB AND UCB EXTENSION FIELDS WILL HAVE BEEN        05200002
*    INITIALIZED.                                                       05400002
*        THIS EXECUTOR'S ENTRY (OR ENTRIES) IN THE WHERE-TO-GO TABLE    05600002
*    WILL HAVE BEEN UPDATED TO IDENTIFY LOAD 3 FOR 3270 (IGG0194Q).     05800002
*                                                                       06000002
* EXTERNAL ROUTINES - GETMAIN, REGISTER FORM (SVC 10)                   06200002
*    FREEMAIN, REGISTER FORM (SVC 10)                                   06400002
*    ABEND (SVC 13)                                                     06600002
*    EXCP (SVC 0)                                                       06800002
*    WAIT (SVC 1)                                                       07000002
*    DOM (SVC 87)                                                       07100002
*    PURGE (SVC 16)                                                     07200002
*    WTOR (SVC 35)                                                      07400002
*    XCTL (SVC 7)                                                       07600002
*                                                                       07800002
* EXITS, NORMAL - THIS ROUTINE EXITS VIA XCTL TO THE MODULE IDENTIFIED  08000002
*    BY THE NEXT NON-ZERO ENTRY IN THE WHERE-TO-GO TABLE, NAMELY        08200002
*    IGG0194Q (BTAM OPEN EXECUTOR - LOAD 3 FOR 3270).                   08400002
*                                                                       08600002
* EXITS, ERROR - THIS ROUTINE EXITS VIA ABEND FOR THE FOLLOWING ERROR   08800002
*    CONDITIONS:                                                        09000002
*    00095000  INCOMPATIBLE DEVICES ARE DEFINED ARE DEFINED TO BE ON    09200002
*              THE SAME LINE GROUP.                                     09400002
*                                                                       09600002
* TABLE/WORK AREAS - THE WHERE-TO-GO TABLE CONTAINS THE ID AND TTR OF   09800002
*    OPEN EXECUTOR LOAD 3 FOR 3270.  THE DCB PARAMETER LIST CONTAINS    10000002
*    THE ADDRESS OF EACH DCB SPECIFIED IN THE OPEN MACRO.               10200002
*                                                                       10400002
* ATTRIBUTES - THIS ROUTINE IS EXECUTED IN THE TRANSIENT AREA AS        10600002
*    ENABLED, PRIVILEDGED, AND REENTRANT.                               10800002
*                                                                       11000002
         EJECT                                                          11003002
*/*IGG0194P: CHART */                                                   11006002
*/* HEADER                                                              11009002
*/*BTAM OPEN LOAD 2 FOR LOCAL 3270 DEVICES                          */  11012002
*/*IGG0194P: E ENTRY FROM IGG0193M VIA XCTL */                          11015002
*/* M ESTABLISH ADDRESSABILITY FOR DCB AND DEB DSECTS */                11018002
*/*GETUCB: D (YES,,NO,ABEND95) ALL DVCS IN LINE GROUP 3270 DVCS */      11021002
*/* M SUPPRESS DYNAMIC BUFFERING */                                     11024002
*/*GETIOB: P STORE IOB SIZE IN DCB */                                   11027002
*/* P COMPUTE SIZE OF IOB POOL */                                       11030002
*/* L GETMAIN FOR IOB'S */                                              11033002
*/* P STORE IOB POOL BASE ADDRESS IN DCB */                             11036002
*/* M SET UCB INDEX VALUE TO ZERO */                                    11039002
*/*INITIOBS: P CLEAR CORE FOR ONE IOB */                                11042002
*/* P INITIALIZE IOBFLAG1, IOBSTART, IOBDCBPT, AND IOBUCBX */           11045002
*/* M INCREMENT THE UCB INDEX VALUE BY 1 */                             11048002
*/* D (YES,,NO,INITIOBS) ALL IOB'S INITIALIZED? */                      11051002
*/* D (YES,,NO,INITERMS) OLT REQUESTED? */                              11054002
*/* P GET IRB POINTER AREA AND STORE ADDRESS IN DEBIRBAD */             11057002
*/*INITERMS: M GET NUMBER OF DVCS, ADDR OF DEB UCB LIST AND IOB POOL */ 11060002
*/*INITLP: M GET ADDRESS OF UCB */                                      11063002
*/* P SAVE OLTEP EXECUTING FLAG FROM GCB */                             11066002
*/* P CLEAR ATTENTION HANDLING FIELDS IN UCB EXTENSION */               11069002
*/* P CLEAR THE GRAPHICS STATUS BYTE IN THE UCB */                      11071002
*/* D (NO,,YES,RLN1) IOBUCBX = 0? */                                    11072002
*/* P STORE POINTER TO MASTER UCB */                                    11075002
*/*STRLN: P INCREMENT UCB INDEX BY 1 TO OBTAIN RLN AND STORE IN UCB */  11078002
*/* D (NO,,YES,BYPASSIO) OLTEP EXECUTING ON DEVICE? */                  11081002
*/* P CREATE ERASE WRITE CHANNEL PROGRAM IN IOB CPA */                  11084002
*/* L GETMAIN FOR WTOR AREA AND ECB */                                  11087002
*/* P STORE ADDRESS OF ECB IN IOB */                                    11090002
*/*EXCP: L ISSUE EXCP TO DO WRITE */                                    11093002
*/* M CLEAR TIMER LOOP COUNTER */                                       11096002
*/*TIMELOOP: L STIMER WAIT FOR 1/2 SECOND */                            11099002
*/* D (YES,,NO,TIMLPCHK) ECB POSTED COMPLETE? */                        11102002
*/*CHKECB: D (NO,,YES,RLSECORE) COMP CODE = X'7F'? */                   11105002
*/* D (YES,,NO,CHKERR) COMP CODE = X'44'? */                            11108002
*/* M (,EXCP) CLEAR ECB */                                              11111002
*/*TIMLPCHK: D (NO,,YES,PURGE) HAVE WAITED FOR 30 SEC */                11114002
*/* M (,TIMELOOP) INCR TIMER LOOP COUNTER BY 1 */                       11117002
*/*PURGE: P BUILD PURGE PARM LIST IN WTOR AREA */                       11120002
*/* L PURGE I/O OPERATION REQUEST */                                    11123002
*/* D (YES,,NO,WRTMSG) REQUEST QUIESCING? */                            11126002
*/* L (,CHKECB) WAIT FOR OPERATION TO COMPLETE */                       11129002
*/*CHKERR: D (YES,WRTMSG,NO,) ERROR WAS SIO CC=3? */                    11131002
*/* D (YES,,NO,SETERR) ERROR WAS INTERVENTION REQUIRED ? */             11133002
*/* P SET OPEN FAILED FLAG IN GRAPHICS STATUS BYTE IN UCB */            11133402
*/*DVCIRMSG: M INITIALIZE WTOR PARM LIST, ECB, AND REPLY AREA */        11133802
*/* P STORE DEVICE NAME IN MESSAGE */                                   11134202
*/* L ISSUE WTOR */                                                     11134602
*/* P SAVE MESSAGE ID FROM WTOR */                                      11134702
*/* L WAIT FOR REPLY */                                                 11134802
*/* L RELEASE MESSAGE */                                                11134902
*/* D (YES,SETERR,NO,) IS REPLY POST ? */                               11145902
*/* D (YES,,NO,SPECCHK) IS REPLY CONT ? */                              11155902
*/* M (,EXCP) CLEAR COMPLETION CODE IN ECB */                           11157902
*/*SPECCHK: D (YES,,NO,DVCIRMSG) IS REPLY DROP ? */                     11158202
*/* P (,SETERR) SET FORCE BRANCH TO SETERR ON INTERVENTION REQUIRED */  11158602
*/*WRTMSG: M INITIALIZE WTOR PARM LIST, ECB, AND REPLY AREA */          11159002
*/* P STORE DEVICE NAME IN MESSAGE */                                   11169002
*/* L ISSUE WTOR */                                                     11180002
*/* P SAVE MESSAGE ID FROM WTOR */                                      11191002
*/* L WAIT FOR REPLY */                                                 11202002
*/* L RELEASE MESSAGE */                                                11213002
*/* D (NO,,YES,SETERR) IS REPLY 'POST'? */                              11224002
*/* D (YES,,NO,WRTMSG) IS REPLY 'CONT'? */                              11235002
*/* M (,EXCP) CLEAR COMPLETION CODE IN ECB */                           11246002
*/*BYPASSIO: P SET OLTEP EXECUTING FLAG IN IOB */                       11257002
*/*SETERR: P FLAG INITIALIZATION FAILURE IN IOB */                      11268002
*/*RLSECORE: L FREEMAIN WTOR AREA */                                    11279002
*/* M CLEAR POINTER TO WTOR AREA AND TIMER LOOP COUNT */                11290002
*/* M INCR POINTERS TO NEXT IOB AND UCB POINTER */                      11301002
*/* D (YES,,NO,INITLP) ALL TERMINALS DONE? */                           11312002
*/* R GO TO END-OF-LOAD CODE */                                         11323002
*/*RLN1: P STORE ADDRESS OF DEB IN UCB */                               11334002
*/* P (,STRLN) SAVE ADDRESS OF MASTER UCB */                            11345002
*/*ABEND95: R ABEND WITH SYSTEM 095 CODE */                             11356002
*/*IGG0194P: END */                                                     11367002
         EJECT                                                          11378002
RPARM2   EQU   0                    PARAMETER REGISTER                  11389002
RPARM1   EQU   1                    PARAMETER REGISTER                  11400002
RW1      EQU   2                    WORK REGISTER                       11600002
RCORE    EQU   3                    ADDRESS OF GETMAIN CORE             11800002
RUCB     EQU   4                    UCB ADDRESS                         12000002
RPAR     EQU   5                    ADDRESS OF DCB PARAMETER LIST       12200002
RWTG     EQU   6                    ADDRESS OF WTG TABLE                12400002
RPARC    EQU   7                    ADDRESS OF CURRENT DCB ENTRY        12600002
RWTGC    EQU   8                    ADDRESS OF CURRENT WTG ENTRY        12800002
RTIOT    EQU   9                    ADDRESS OF THE TASK I/O TABLE       13000002
RDCB     EQU   10                   DCB ADDRESS                         13200002
RDEB     EQU   11                   DEB ADDRESS                         13400002
RBASE    EQU   12                   BASE REGISTER                       13600002
RW2      EQU   13                   WORK REGISTER                       13800002
RGMUCB   EQU   13                   MASTER UCB REG             @ZA06392 13900003
RPARM4   EQU   14                   PARAMETER REGISTER                  14000002
RPARM3   EQU   15                   PARAMETER REGISTER                  14200002
*                                                                       14400002
RW3      EQU   RTIOT                ALTERNATE                           14600002
RW4      EQU   RPARM3                   ASSIGNMENTS                     14800002
RW5      EQU   RDCB                         FOR WORK REGISTERS          15000002
*                                                                       15200002
*                                                                       15400002
ALLON    EQU   X'FF'                MASK OF ALL ONE'S                   15600002
ANRIOBSZ EQU   88                   3270 IOB SIZE                       15800002
ATNCNT   EQU   26                   DISP TO ATTN CNT FIELD IN UCB       16000002
ATNXTNSN EQU   14                   LENGTH OF ATTN HANDLING SECTION     16200002
*                                      OF UCB EXTENSION                 16400002
COMPOK   EQU   X'7F'                NORMAL COMPLETION CODE              16600002
DEBATV   EQU   28                  DISP TO DEB VECTOR TO APPNDG TAB     16700002
DVC3277  EQU   X'09'                DEVICE TYPE FOR 3277 DISPLAY        16800002
DVC3286  EQU   X'0B'                DEVICE TYPE FOR 3286 PRINTER        17000002
*              DEVICE TYPE FOR 3284 PRINTER IS X'0A'                    17200002
DYNBUF   EQU   X'08'                DCB DYNAMIC BUFFERING FLAG          17600002
EIGHT    EQU   8                                                        17800002
EQ       EQU   8                    BC MASK FOR EQUALITY                18000002
ERROR    EQU   X'41'                PERMANENT ERROR OCCURED             18200002
FIVE     EQU   5                                                        18400002
FLAG1    EQU   X'42'               IOB IOS FLAGS - CMND CHAINING        18460002
*                                       I/O REQUESTS UNRELATED          18520002
FOUR     EQU   4                    ADDR INCR OF 4                      18600002
GCB      EQU   27                   DISP TO GCB FIELD IN UCB            18800002
GRAF     EQU   28                  DISP TO GRAPHICS STATUS     @YA02128 18800102
GRPHCDVC EQU   X'10'                GRAPHIC DEVICE CLASS                19000002
INITERR  EQU   X'80'                INITIALIZATION ERROR FLAG IN IOB    19200002
INTERCPT EQU   X'44'                IOB INTERCEPTED                     19400002
INTREQ   EQU   X'40'               INTERVENTION REQUIRED SENS  @YA02128 19450002
IOBOLTEP EQU   X'42'                SECONDARY OLTEP EXECUTING FLAGS     19600002
LINK     EQU   36                   DISP TO CONTROL BLOCK LINK-1        19800002
MAXINTVL EQU   59                  MAX VALUE OF TIME LOOP CTR           20000002
MSGDVCAD EQU   21                   DISP TO DEVICE ADDRESS IN WTOR      20200002
*                                      EXPASION                         20400002
OLT      EQU   X'10'                DCB BIT FOR EROPT=T                 20600002
OLTEP    EQU   X'80'                PRIMARY OLTEP EXECUTING FLAG        20800002
ONE      EQU   1                                                        21000002
OPENINIT EQU   X'80'               OPEN IN PROGRESS FLAG       @YA02128 21050002
OPENFAIL EQU   X'02'               OPEN FAILED GRAF FLAG       @YA02128 21120002
OPENMSG  EQU   X'08'               FLAG BYPASS INT REQ MSG     @YA02128 21170002
PLOFF    EQU   4                    OFFSET OF DCB ENTRIES               21200002
POSTBIT  EQU   X'40'               ECB POST BIT                         21400002
PTRSZ    EQU   16                  SIZE OF IRB POINTER AREA             21500002
PRGCTL   EQU   X'94'               PURGE CONTROL BYTE                   21600002
RDYQBTM  EQU   X'02'                RDYQ CODED IN DCB          @ZA12151 21700000
RLN      EQU   36                   DISP TO RLN FIELD IN UCB            21800002
SIOCC3   EQU   X'30'                MASK TO TEST SIO CC = 3 IN IOB      22000002
PURGEOPT EQU   X'A0'               OPTION BYTE FOR PURGE    LD @ZA02328 22050003
SIX      EQU   6                                                        22200002
TENAND4  EQU   14                                                       22300002
THREE    EQU   3                                                        22400002
TIMELNG  EQU   4                                               @ZA08051 22500003
TWELVE   EQU   12                                                       22600002
TWO      EQU   2                                                        22800002
UCBDVCLS EQU   18                   DISP TO DEVICE CLASS FIELD IN UCB   23000002
UCBDVCTP EQU   19                   DISP TO DEVICE TYPE FIELD IN UCB    23200002
UCBNAME  EQU   13                   DISP TO DEVICE NAME IN UCB          23400002
WAOFF    EQU   32                   OFFSET OF FIRST WTG ENTRY FROM      23600002
*                                       BEGINNING OF TABLE              23800002
WGOFF    EQU   8                    OFFSET OF WTG ENTRIES               24000002
WTGPATH  EQU   30                  OFFSET TO PATH BITS         @YA02128 24050002
ZERO     EQU   X'00'                                                    24200002
*              ENTRY AND ADDRESSABILITY CODE                            24400002
         BALR  RBASE,0              ESTABLISH BASE REGISTER             24600002
         USING *,RBASE                                                  24800002
         USING IHADCB,RDCB                                              25000002
         USING IECTDEB,RDEB                                             25200002
         USING WTGENTRY,RWTGC                                           25210002
         B     BEGIN                   BRANCH AROUND ID        @YA02128 25250002
         DC    CL8'IGG0194P'           NAME                    @YA02128 25300002
         DC    C'** MVS *'                                   LD YA03239 25350003
         DC    C'&SYSDATE'         DATE LAST ASSEMBLE       LD @ZA00544 25352003
BEGIN    DS    0H                                              @YA02128 25360002
*                                                                       25400002
         L     RDCB,0(RPARC)        GET DCB ADDRESS                     25600002
         L     RDEB,DCBDEBAD        GET DEB ADDRESS                     25800002
         L     RW4,WTGCORE-ONE     ADDR OF OPEN WORKAREA       @ZA04872 25900003
         USING WORKAREA,RW4                                    @ZA04872 26000003
         XC    DXCCW(TWELVE),DXCCW CLEAR PURGE PARM LIST       @ZA04872 26100003
         ST    RDEB,DXCCW          SAVE DEB ADDR FOR PURGE     @ZA04872 26200003
         L     RDEB,DEBATV(RDEB)  GET ADDR OF APPENDAGE TABLE  @ZA04872 26260003
*                                   FOR BASE OF DSECT          @ZA04872 26320003
*                                                                       26570002
*              CHECK ALL UCB'S IN LINE GROUP FOR 3270 DEVICES           26600002
*                                                                       26650002
         SR    RW1,RW1                                                  26800002
         IC    RW1,DEBNMEXT         GET NUMBER OF DEVICES IN LINE GROUP 27000002
         SR    RW2,RW2              INITIALIZE UCB TABLE INDEX TO ZERO  27200002
*                                                                       27400002
GETUCB   L     RUCB,DEBUCBAD(RW2)   GET ADDRESS OF UCB                  27600002
         CLI   UCBDVCLS(RUCB),GRPHCDVC  DEVICE A GRAPHICS DEVICE        27800002
         BNE   ABEND95              NO, ABEND                           28000002
         CLI   UCBDVCTP(RUCB),DVC3277   DEVICE A 3270 DEVICE            28200002
         BL    ABEND95              NO, ABEND                           28400002
         BE    NEXTUCB              YES, CHECK NEXT UCB                 28600002
         CLI   UCBDVCTP(RUCB),DVC3286   MAYBE                           28800002
         BH    ABEND95              NO, ABEND                           29000002
*                                                                       29200002
NEXTUCB  LA    RW2,FOUR(RW2)        INCR TO NEXT ADDRESS IN TABLE       29400002
         BCT   RW1,GETUCB           CHECK NEXT IF ALL NOT CHECKED       29600002
*                                                                       29800002
         NI    DCBBFTEK,ALLON-DYNBUF  SUPRESS DYNAMIC BUFFERING         30000002
*                                                                       30200002
*              CONSTRUCT THE IOB POOL                                   30400002
GETIOB   LA    RW1,ANRIOBSZ         GET THE IOB SIZE                    30600002
         STC   RW1,DCBEIOBX         STORE IOB SIZE IN DCB               30800002
         LR    RPARM1,RW1                                               31000002
         SR    RW2,RW2                                                  31200002
         IC    RW2,DEBNMEXT         GET NUMBER OF IOB'S                 31400002
         MR    RPARM2,RW2           GET TOTAL CORE REQUIRED             31600002
         LR    RPARM2,RPARM1        LOAD PRODUCT INTO REG 0             31800002
         O     RPARM2,IOBPOOL       OR IN SUBPOOL NUMBER                32000002
         L      RW4,WTGCORE-ONE                                  Y02947 32050002
         USING WORKAREA,RW4                                      Y02947 32100002
         MODESET KEYADDR=DXUKEY,WORKREG=9                        Y02947 32150002
         GETMAIN  R,LV=(0)          GET CORE FOR THE IOB'S              32200002
*                                                                       32400002
         USING IECTIOB,RCORE                                            32600002
         LR    RCORE,RPARM1         GET ADDRESS OF IOB POOL             32800002
         SR    RCORE,RW1            SUBTRACT IOB SIZE                   33000002
         LR    RUCB,RCORE           SAVE IOB BASE ADDR           Y02947 33200002
         LA    RDCB,0(RDCB)         CLEAR HIGH BYTE OF DCB ADDRESS      33400002
         SR    RW3,RW3              INITIALIZE COUNTER FOR UCB INDEX    33600002
*                                                                       33800002
INITIOBS LA    RCORE,0(RCORE,RW1)   STEP TO BEGINNING OF IOB            34000002
         XC    0(ANRIOBSZ,RCORE),0(RCORE)  CLEAR IOB                    34200002
         MVI   IOBFLAG1,FLAG1      SET IOS FLAGS                        34400002
         LA    RW4,IOBCPA           GET ADDR OF CHANNEL PROG AREA       34600002
         ST    RW4,IOBSTART             & STORE IT IN IOBSTART          34800002
         L     RW4,WTGCORE-ONE      ADDRESS OF OPEN WORKAREA     Y02947 34850002
         MVC   IOBDCBPT,DXUDCBAD    ADDR OF USER'S DCB TO IOB    Y02947 34900002
         STC   RW3,IOBUCBX          STORE UCB INDEX                     35200002
         LA    RW3,ONE(RW3)        INCR UCB INDEX BY 1                  35400002
         BCT   RW2,INITIOBS         DECR & TEST IOB COUNT               35600002
*                                   LOOP IF NOT FINISHED                35800002
*                                                                       36000002
*              GET POINTERS FOR IRB IF ON-LINE TEST REQUESTED           36200002
         TM    DCBERROP,OLT         OLT REQUESTED                       36400002
         BNO   INITERMS             NO, GO TO INITIALIZE TERMINALS      36600002
         LA    RPARM2,PTRSZ        NUMBER OF BYTES FOR POINTERS         36800002
         O     RPARM2,IOBPOOL       OR IN SUBPOOL NUMBER                37000002
         GETMAIN  R,LV=(0)          GET CORE FOR POINTERS               37200002
         MODESET EXTKEY=DATAMGT     BACK TO DATA MGT KEY         Y02947 37250002
         ST    RPARM1,DEBIRBAD-1    STORE ADDRESS IN DEB                37400002
         ST    RUCB,DCBIOBAD        IOB ADDR TO 'COPY' DCB       Y02947 37450002
         L     RW4,WTGCORE-ONE                                   Y02947 37500002
         MODESET KEYADDR=DXUKEY,WORKREG=9   ASSUME USER KEY      Y02947 37550002
         XC    0(PTRSZ,RPARM1),0(RPARM1)  CLEAR AREA                    37600002
         B     CONTINUE                                       LD Y02947 37650002
*                                                                       37800002
*              INITIALIZE THE ATTENTION FIELDS IN THE UCB (EXCEPT THE   38000002
*              IRB ADDRESS AND UNLOCK AND CLEAR THE TERMINALS           38200002
*                                                                       38250002
INITERMS EQU   *                                              LD Y02947 38300002
         MODESET EXTKEY=DATAMGT                               LD Y02947 38350002
         ST    RUCB,DCBIOBAD       IOB ADDR TO 'COPY' DCB     LD Y02947 38360002
         L     RW4,WTGCORE-ONE     ADDR OF OPEN WORKAREA      LD Y02947 38370002
         MODESET KEYADDR=DXUKEY,WORKREG=9                     LD Y02947 38380002
CONTINUE EQU   *                                              LD Y02947 38390002
         SR    RW3,RW3                                                  38400002
         IC    RW3,DEBNMEXT         GET NO. OF DEVICES IN  LINE GROUP   38600002
         LA    RDEB,DEBUCBAD        GET ADDR OF DEB UCB LIST            38800002
         DROP  RDEB                END DEB ADDRESSABILITY               39000002
         L     RCORE,DCBIOBAD       GET BASE ADDR OF IOB POOL           39200002
INITLP   EQU   *                                                        39400002
         L     RUCB,0(RDEB)         GET ADDR OF UCB FOR DEVICE          39600002
         LA    RCORE,ANRIOBSZ(RCORE)  GET ADDR OF IOB FOR DEVICE        39800002
         IC    RW1,GCB(RUCB)          SAVE GCB FIELD                    40000002
         MODESET EXTKEY=ZERO          NEED KEY 0 TO STORE IN UCB Y02947 40050002
         XC    ATNCNT(ATNXTNSN,RUCB),ATNCNT(RUCB)  CLEAR ATTN-HANDLING  40200002
*                                         FIELDS IN UCB EXTENSION       40400002
         STC   RW1,GCB(RUCB)          RESTORE GCB TO UCB                40600002
         NI    GCB(RUCB),OLTEP     CLEAR ALL GCB EXCEPT OLTEP-EXEC      40800002
*                                         FLAG                          41000002
         NI    GRAF(RUCB),ZERO     RESET GRAPHICS STATUS       @YA02128 41000102
         OI    GRAF(RUCB),OPENINIT FLAG OPEN IN PROGRESS       @YA02128 41000202
         SR    RW1,RW1                                                  41200002
         IC    RW1,IOBUCBX            GET UCB INDEX FROM IOB            41400002
         LTR   RW1,RW1                IS IT 0, I.E. IS RLN = 1          41600002
         BZ    RLN1                   YES, SET UP MASTER UCB PTR TO DEB 41800002
         ST    RGMUCB,LINK(RUCB) NO, STORE PNTR TO MASTER UCB  @ZA06392 42000003
STRLN    LA    RW1,ONE(RW1)           COMPUTE RLN FROM UCB INDEX        42200002
         STC   RW1,RLN(RUCB)          AND STORE IN UCB                  42400002
*                                                                       42600002
         L     RW4,WTGCORE-ONE        ADDR OF OPEN WORKAREA   LD Y02947 42610002
         MODESET KEYADDR=DXUKEY,WORKREG=2    ASSUME USER KEY     Y02947 42650002
         TM    GCB(RUCB),OLTEP        OLTEP EXECUTING ON DEVICE         42800002
         BO    BYPASSIO               YES, DO NOT DO INITIALIZATION     43000002
*                                                                       43200002
         MVC   IOBCPA(EIGHT),WRTCCW   MOVE ERASE WRITE CCW INTO IOB     43400002
         MVC   IOBCPA+EIGHT(ONE),NOPWRT  WCC TO DATA AREA   LD @ZA00543 43450003
         LA    RW1,IOBCPA+EIGHT          ADR OF DATA AREA   LD @ZA00543 43600003
         O     RW1,IOBCPA             ADD COMMAND CODE TO HIGH BYTE     43800002
         ST    RW1,IOBCPA                                               44000002
         LA    RPARM2,WTOARLNG        GET LENGTH OF WTOR AREA           44200002
         O     RPARM2,IOBPOOL         SET POOL NUMBER                   44400002
         GETMAIN  R,LV=(0)          GET CORE FOR IT                     44600002
         ST    RPARM1,IOBECBPT      STORE ADDRESS IN IOB                44800002
         XC    0(WTOARLNG,RPARM1),0(RPARM1)  CLEAR AREA                 45000002
         MODESET EXTKEY=DATAMGT    DATA MGT KEY               LD Y02947 45050002
         LR    RPARM1,RUCB         SAVE REG 4                 LD Y02947 45100002
         L     RUCB,WTGCORE-ONE    WORK AREA ADDRESS          LD Y02947 45150002
         USING WTG,RWTG            WTG ADDRESSABILITY         LD Y02947 45160002
         L     RW4,WTGPREFX        WTG PREFIX ADDRESS         LD Y02947 45170002
         STM   RPARM2,RPARM4,IECREGSV-IECPREFX(RW4) SAVE REG  LD Y02947 45180002
         IECRES INIT,DCBCOPY=FRWKAR                                     45190002
         LR    RW4,RWTG            REG SAVEAREA ADDR          LD YM5668 45192000
         LM    RPARM2,RPARM4,IECREGSV-IECPREFX(RW4) REST REG  LD Y02947 45194002
         LR    RUCB,RPARM1         RESTORE REG 4              LD Y02947 45196002
         USING WORKAREA,RW4        WORKAREA ADDRESSABILITY    LD Y02947 45198402
         L RW4,WTGCORE-ONE         WORK AREA ADDRESS          LD Y02947 45198802
         MODESET KEYADDR=DXUKEY,WORKREG=1   USER KEY          LD Y02947 45199202
*                                                                       45200002
EXCP     EQU   *                                                 Y02947 45250002
         DROP  RW4                                               Y02947 45300002
         LA    RPARM1,IOBFLAG1      GET ADDRESS OF IOB                  45400002
         EXCP  (1)                  DO I/O OPERATION                    45600002
*                                                                       45800002
         MVI   IOBINCAM+1,ZERO     CLEAR LOOP CTR                       46000002
         MVC   IOBERINF(TIMELNG),TIME   MOVE TIME VALUE        @ZA08051 46050003
*                                                                       46100002
TIMELOOP STIMER WAIT,BINTVL=IOBERINF   WAIT FOR .5 SEC      LD @ZA00544 46200003
         L     RW1,IOBECBPT        GET ADDR OF ECB                      46400002
         TM    0(RW1),POSTBIT      HAS ECB BEEN POSTED                  46600002
         BZ    TIMLPCHK            NO, SEE IF TIME INTERVAL UP          46800002
*                                                                       47000002
CHKECB   EQU   *                                                        47200002
         L     RW1,IOBECBPT         GET ADDRESS OF ECB                  47400002
         CLI   0(RW1),COMPOK        NORMAL COMPLETION                   47600002
         BE    RLSECORE             YES, RELEASE GETMAIN AREA           47800002
*                                                                       48000002
         CLI   0(RW1),INTERCPT      IOB INTERCEPTED                     48200002
         BNE   CHKERR               NO, CHECK CAUSE OF ERROR            48400002
         MVI   0(RW1),ZERO          YES, CLEAR COMPLETION CODE          48600002
         B     EXCP                 RE-ISSUE OPERATION                  48800002
*                                                                       49000002
TIMLPCHK CLI   IOBINCAM+1,MAXINTVL LOOP CTR AT MAX VALUE                49200002
         BE    PURGE               YES, PURGE I/O           LD @ZA02328 49250003
         SR    RW1,RW1                                                  49600002
         IC    RW1,IOBINCAM+1      GET LOOP CTR                         49800002
         LA    RW1,ONE(RW1)        INCR CTR BY 1                        50000002
         STC   RW1,IOBINCAM+1      AND STORE BACK IN IOB                50200002
         B     TIMELOOP            WAIT FOR 0.5 SEC MORE                50400002
*                                                                       50600002
PURGE    EQU   *                                            LD @ZA02328 50610003
         MODESET EXTKEY=DATAMGT    DATA MANAGEMENT KEY      LD @ZA02328 50620003
         L     RW4,WTGCORE-ONE     WORKAREA ADDRESSABILITY  LD @ZA02328 50630003
         USING WORKAREA,RW4                                             50640002
         MVI   DXCCW,PURGEOPT      HALT I/O OPTION          LD @ZA02328 50642003
         PURGE DXCCW                                        LD @ZA02328 50644003
         L     RW4,WTGCORE-ONE     WORKAREA ADDRESSABILITY  LD @ZA02328 50646003
         MODESET KEYADDR=DXUKEY,WORKREG=15                  LD @ZA02328 50648003
*                                                                       50648402
CHKERR   EQU   *                   CHECK CAUSE OF I/O FAILURE  @YA02128 50650002
         TM    IOBSIOCC,SIOCC3     WAS ERROR SIO CC = 3 (CU    @YA02128 50700002
*                                  NOT OPERATIONAL)            @YA02128 50750002
         BO    WRTMSG              YES, TELL OPERATOR          @YA02128 50800002
         TM    IOBSENS0,INTREQ     INTERVENTION REQUIRED ?     @YA02128 50850002
         BNO   SPECBR              NO, GO FLAG OPEN ERROR      @YA02128 50860002
         MODESET EXTKEY=ZERO                                 LD YA02128 50862002
         OI    GRAF(RUCB),OPENFAIL FLAG OPEN FAILED            @YA02128 50870002
         USING WORKAREA,RW4         WORKAREA ADDRESSABILITY  LD YA02128 50872002
         L     RW4,WTGCORE-ONE      WORKAREA ADDRESS         LD YA02128 50874002
         MODESET KEYADDR=DXUKEY,WORKREG=15                   LD YA02128 50876002
         DROP  RW4                                           LD YA02128 50878002
SPECBR   EQU   *                   TEST TO SEE IF BYPASS MSG   @YA02128 50880002
         TM    WTGPATHS,WTGSPMSG   BYPASS MSG SET ?            @YA02128 50900002
         BO    SETERR              YES, BYPASS INT REQ MSG     @YA02128 50950002
         L     RW1,IOBECBPT        GET ADDR OF GETMAINED AREA  @YA02128 51250002
DVCIRMSG EQU   *                   PUT OUT DEVICE NOT OPER     @YA02128 51300002
         XC    0(EIGHT,RW1),0(RW1) CLEAN ECB AND REPLY AREA    @YA02128 51350002
         LA    RPARM2,FOUR(RW1)    GET ADDRESS OF REPLY AREA   @YA02128 51400002
         MVC   EIGHT(WTODVLNG-EIGHT,RW1),WTODEV MOVE DEV MSG   @YA02128 51450002
         MVC   MSGDVCAD+EIGHT(THREE,RW1),UCBNAME(RUCB) PUT     @YA02128 51500002
*                                  UCB NAME IN MESSAGE         @YA02128 51550002
         LA    RPARM1,EIGHT(RW1)   GET BEGINNING OF WTOR EXP   @YA02128 51600002
         WTOR  ,(RPARM2),,(RW1),MF=(E,(1)) WRITE MESSAGE       @YA02128 51650002
         XR    RPARM1,RW1          EXCHANGE CONTENTS           @YA02128 51700002
         XR    RW1,RPARM1          OF REGISTERS                @YA02128 51750002
         XR    RPARM1,RW1          RPARM1 AND RW1              @YA02128 51800002
         WAIT  ECB=(1)             WAIT FOR REPLY              @YA02128 51850002
         DOM   MSG=(2)             RELEASE MESSAGE             @YA02128 51900002
         L     RW1,IOBECBPT        GET ADDRESS OF WTOR AREA    @YA02128 51950002
         CLC   FOUR(FOUR,RW1),POST IS REPLY 'POST' ?           @YA02128 52000002
         BE    SETERR              YES, FLAG ERROR             @YA02128 52050002
         CLC   FOUR(FOUR,RW1),CONT IS REPLY 'CONT' ?           @YA02128 52100002
         BNE   SPECCHK             NO, CHECK SPECIAL REPLY     @YA02128 52150002
         MVI   0(RW1),ZERO         YES, CLEAR COMPLETION CODE  @YA02128 52200002
         MODESET EXTKEY=ZERO                                 LD YA02128 52202002
         NI    GRAF(RUCB),ALLON-OPENFAIL TURN OFF OPEN  FLAG   @YA02128 52210002
         USING WORKAREA,RW4         WORKAREA ADDRESSABILITY  LD YA02128 52220002
         L     RW4,WTGCORE-ONE      WORKAREA ADDRESS         LD YA02128 52230002
         MODESET KEYADDR=DXUKEY,WORKREG=1                    LD YA02128 52240002
         B     EXCP                AND RETRY I/O               @YA02128 52250002
SPECCHK  EQU   *                   CHECK FOR BYPASS FUTURE     @YA02128 52300002
         CLC   FOUR(FOUR,RW1),DROP IS REPLY 'DROP' ?           @YA02128 52700002
         BNE   DVCIRMSG            NO, INVALID - REISSUE MSG   @YA02128 52750002
         MODESET EXTKEY=DATAMGT                              LD YA02128 52760002
         OI    WTGPATHS,WTGSPMSG   SET BYPASS INT REQ MSG      @YA02128 52800002
         L     RW4,WTGCORE-ONE     WORKAREA ADDRESS          LD YA02128 52850002
         MODESET KEYADDR=DXUKEY,WORKREG=15                   LD YA02128 52860002
         DROP  RW4                                           LD YA02128 52870002
         B     SETERR              AND GO SET OPEN ERROR       @YA02128 52900002
*                                                                       55000002
WRTMSG   XC    0(EIGHT,RW1),0(RW1)  YES, CLEAR ECB AND REPLY AREA       55200002
         LA    RPARM2,FOUR(RW1)     GET ADDRESS OF REPLY AREA           55400002
         MVC   EIGHT(WTOARLNG-EIGHT,RW1),WTOBASE  MOVE SKELETON MSG     55600002
*                                   INTO GETMAIN AREA                   55800002
         MVC   MSGDVCAD+EIGHT(THREE,RW1),UCBNAME(RUCB)  PUT DEVICE      56000002
*                                   NAME INTO MESSAGE                   56200002
         LA    RPARM1,EIGHT(RW1)    GET BEGINNING OF WTOR EXPASION      56400002
         WTOR  ,(RPARM2),,(RW1),MF=(E,(1))   WRITE MESSAGE              56600002
         XR    RPARM1,RW1           EXCHANGE CONTENTS                   56800002
         XR    RW1,RPARM1               OF REGISTERS                    57000002
         XR    RPARM1,RW1                   RPARM1 AND RW1              57200002
*                                                                       57400002
         WAIT  ECB=(1)              WAIT FOR REPLY                      57600002
*                                                                       57800002
         DOM   MSG=(2)              RELEASE MESSAGE                     58000002
*                                                                       58200002
         L     RW1,IOBECBPT         GET ADDRESS OF WTOR AREA            58400002
         CLC   FOUR(FOUR,RW1),POST  REPLY IS 'POST'                     58600002
         BE    SETERR               YES, FLAG ERROR                     58800002
*                                                                       59000002
         CLC   FOUR(FOUR,RW1),CONT  REPLY IS 'CONT'                     59200002
         BNE   WRTMSG               NO, INVALID REPLY - REWRITE MESSAGE 59400002
         MVI   0(RW1),ZERO          CLEAR COMPLETION CODE               59600003
         B     EXCP                 RETRY INITIALIZATION                59800002
*                                                                       60000002
BYPASSIO OI    IOBINCAM,IOBOLTEP    SET OLTEP EXECUTING FLAGS IN IOB    60200002
SETERR   EQU   *                                             LD YA03239 60250000
         TM    DCBRDYI,RDYQBTM       USING RDYQ                @ZA06392 60280003
         BZ    SETERR1              NO FLAG IOB                @ZA06392 60310003
         TM    IOBSENS0,INTREQ        INTERVENTION REQUIRED    @ZA06392 60340003
         BO    NOSETERR             YES DON'T FLAG             @ZA06392 60370003
SETERR1  OI    IOBINCAM,INITERR     FLAG I/O ERROR AT OPEN     @ZA06392 60400003
NOSETERR TM    GCB(RUCB),OLTEP      OLTEP USING DVC            @ZA06392 60450003
         BO    DONORLSE                                         YA01229 60500002
RLSECORE LR    RPARM1,RW1           ADDRESS OF AREA TO BE FREED         60600002
         LA    RPARM2,WTOARLNG      LENGTH OF AREA                      60800002
         O     RPARM2,IOBPOOL       POOL NUMBER                         61000002
         FREEMAIN  R,LV=(0),A=(1)   FREE AREA                           61200002
DONORLSE EQU   *                                                YA01229 61250002
         XC    IOBECBPT(FOUR),IOBECBPT  CLEAR POINTER TO AREA           61400002
         MVI   IOBINCAM+1,ZERO                                          61600002
         LA    RDEB,FOUR(RDEB)      GET POINTER TO NEXT UCB ADDRESS     61800002
         BCT   RW3,INITLP           LOOP IF MORE TERMS TO INITIALIZE    62000002
         B     ENDLOAD              END-OF-LOAD CODE                    62200002
*                                                                       62400002
         USING IHADCB,RDCB                                       Y02947 62450002
RLN1     L     RPARM1,DCBDEBAD      GET ADDRESS OF DEB                  62600002
         ST    RPARM1,LINK(RUCB)    STORE IN LINK FIELD                 62800002
         LR    RGMUCB,RUCB       SAVE ADDRESS OF MASTER UCB    @ZA06392 63000003
         B     STRLN                STORE RLN                           63200002
*                                                                       63400002
*              END-OF-LOAD CODE                                         63600002
ENDLOAD  MODESET EXTKEY=DATAMGT     BACK TO DATA MGT KEY         Y02947 63700002
         MVC   WTGIDTTR(FIVE),LD3F3270   SET UP IDTTR FOR NEXT LOAD     63800002
         USING WORKAREA,RW1                                      Y02947 63850002
         L     RW1,WTGCORE-ONE                                          64000002
*                                                                       64200002
RELOOP   LA    RWTGC,WGOFF(RWTGC)   INCR CURRENT WTG REGISTER           64400002
         LA    RPARC,PLOFF(RPARC)   INCR CURRENT PARM LIST REG TO NEXT  64600002
*                                       ENTRY                           64800002
         CLC   WTGIDTTR(TWO),AMIDCNST  IF NEXT WTG ENTRY REQUIRES THIS  65000002
         BCR   EQ,RBASE             EXECUTOR, RETURN TO BEGINNING       65200002
         CLC   WTGIDTTR(TWO),OPIDCNST   IF NOT, TEST FOR END OF TABLE   65400002
         BNE   RELOOP               NOT END, CHECK NEXT ENTRY           65600002
*                                                                       65800002
         LR    RPARC,RPAR           REINITIALIZE CURRENT PARM REG       66000002
         LA    RWTGC,WAOFF(RWTG)    AND CURRENT WTG REG                 66200002
ZCHECK   CLI   WTGIDTTR,ZERO        IF FIRST BYTE OF CURRENT WTG ENTRY  66400002
         BNE   XCTLRTNE             NON-ZERO, GO TO TRANSFER CONTROL    66600002
         LA    RWTGC,WGOFF(RWTGC)   IF NOT, GET NEXT ENTRY              66800002
         LA    RPARC,PLOFF(RPARC)                                       67000002
         B     ZCHECK               AND CHECK IT                        67200002
*                                                                       67400002
*                                                                       67600002
XCTLRTNE EQU   *                                               @YA02128 67650002
         NI    WTGPATHS,ALLON-WTGSPMSG  TURN OFF BYPASS        @YA02128 67700002
         LA    RPARM3,DXCCW12           POINT TO DOUBLE WORD LIST       67800002
         MVC   WTGMODID(TWO),WTGIDTTR   MOVE ID TO NAME FIELD           68000002
         MVC   TENAND4(THREE,RWTG),TWO(RWTGC)  MOVE TTR TO WTG TABLE    68200002
         XCTL  DE=(RWTG),SF=(E,(15))                                    68400002
*                                                                       68600002
ABEND95  L     RPARM1,ERRCODE                                           68800002
         ABEND (1)                 INCOMPATIBLE DEVICES ON LINE GROUP   69000002
NOPWRT   DC    X'C3'                ERASE WRITE DATA STREAM - WCC ONLY  69200002
*                                     RESET KEYBOARD                    69400002
*                                     RESET MODIFIED DATA TAGS          69600002
OPIDCNST DC    C'0S'                ID OF LAST OPEN LOAD                69800002
AMIDCNST DC    C'4P'                ID OF THIS MODULE                   70000002
TIME     DC    F'50'               TIME INTERVAL OF .50 SEC             70200002
ERRCODE  DC    X'80095000'         ABEND CODE                           70400002
IOBPOOL  DC    X'FA000000'          SUBPOOL NUMBER FOR IOB'S            70600002
CONT     DC    C'CONT'             WTOR REPLY                           70800002
DROP     DC    C'DROP'                                         @YA02128 70850002
POST     DC    C'POST'                  COMPARANDS                      71000002
POOLNUM  DC    X'E6000000'          SUBPOOL FOR WTOR AREA               71200002
WRTCCW   DC    XL8'0500000020240001'  ERASE WRITE CCW SKELETON          71400002
*                                   COMMAND = ERASE WRITE               71600002
*                                   FLAGS = SLI                         71800002
*                                   TP OP CODE = OPEN WRITE             72000002
*                                   BYTE CNT = 1                        72200002
WTOBASE  WTOR  'IEC804A  000 CONTROL UNIT NOT OPERATIONAL. REPLY CONT O*72400002
               R POST',,4,,ROUTCDE=(1,8,10),DESC=2,MF=L                 72600002
WTOARLNG EQU   (*-WTOBASE+8+7)/8*8  LENGTH OF GETMAIN AREA FOR WTOR     72800002
WTODEV   WTOR  'IEC804A  000 DEVICE NOT OPERATIONAL. REPLY CONT, POST O*72850002
               R DROP',,4,,ROUTCDE=(1,8,10),DESC=2,MF=L        @YA02128 72900002
WTODVLNG EQU   (*-WTODEV+8+7)/8*8  LENGTH OF GETMAINED AREA    @YA02128 72950002
PTCHAREA DC    80XL1'00'       PATCH AREA                      @YA02128 72960002
NENT     EQU   1                    NUMBER OF IDTTR'S                   73000002
LENGTH   EQU   NENT*6+6             LENGHT OF IDTTR'S+2+4               73200002
NODBWDS  EQU   (LENGTH+7)/8         NO. OF DBL WDS WITH PADDING         73400002
ORGIDTTR EQU   2048-NODBWDS*8                                  @YA02128 73410002
         ORG   IGG0194P+ORGIDTTR                                        73460002
LD3F3270 DC    C'4Q'                ID FOR THIRD LOAD OF BTAM OPEN      74000002
*                                       FOR 3270                        74200002
         DC    X'00000000'          TTR AND L                           74400002
IDEND    DC    X'0000'              END OF IDTTRL TABLE                 74600002
         CNOP  4,8                  PADDING                             74800002
LOADID   DC    C'019'               OPEN SVC CODE                       75000002
         DC    YL1(X'80'-NODBWDS)  POINTER FOR IEHIOSUP                 75200002
*                                                                       75400002
         EJECT                                                          75410002
CVT      DSECT                                                          75420002
         CVT                                                            75430002
         EJECT                                                          75450002
         DCBD  DSORG=BX,DEVD=BS                                         75600002
         EJECT                                                          75650002
         IECTDEBX                                                       75800002
         EJECT                                                          75850002
         IECTIOBX                                                       76000002
         EJECT                                                          76010002
WORKAREA DSECT                                                   Y02947 76050002
         IECDSECT IOB=NO                                         Y02947 76100002
         EJECT                                                          76150002
         IECDSECS WTG,PREFX,EXPAND=YES                                  76160002
         END                                                            76200002
