         TITLE 'IGG0194Q - BTAM OPEN LOAD 3 FOR LOCAL 3270'             00150002
IGG0194Q CSECT                                                          00200002
*                                                              @YA02128 00210002
*              FOLLOWING CHANGES APPLIED:                      @YA02128 00220002
* C158500                                                    LD YA03953 00232000
*                                                              @YA02128 00240002
*                                                                       00250002
* STATUS - CHANGE LEVEL 000                                             00300002
*                                                                       00350002
* FUNCTION/OPERATION - THIS ROUTINE IS BROUGHT INTO CORE AND EXECUTED   00400002
*    WHEN THE BTAM OPEN LOAD 2 FOR 3270 ISSUES 'XCTL' TO IGG0194Q.      00450002
*    THIS ROUTINE LOADS THE REQUIRED MODULES, STORES THE ID'S OF THE    00500002
*    MODULES AND A COUNT OF MODULES LOADED INTO THE DEB, SETS UP 3270   00550002
*    ATTENTION HANDLING AND SETS UP OLT IF REQUIRED.                    00600002
*                                                                       00650002
* ENTRY POINT - ENTRY IS TO THE FIRST BYTE OF THE MODULE AS A RESULT OF 00700002
*    AN XCTL TO IGG0194Q.                                               00750002
*                                                                       00800002
* INPUT - REGISTERS 5, 6, 7, AND 8 ARE THE INPUT                        00850002
*    5 - BEGINNING OF THE DCB PARAMETER LIST                            00900002
*    6 - BEGINNING OF THE WHERE-TO-GO TABLE                             00950002
*    7 - ADDRESS OF THE CURRENT ENTRY IN THE DCB PARAMETER LIST         01000002
*    8 - ADDRESS OF THE CURRENT ENTRY IN THE WHERE-TO-GO TABLE.         01050002
*                                                                       01100002
* OUTPUT - THE REGISTERS WILL BE POSITIONED TO THE NEXT UNFINISHED      01150002
*    ENTRY IN THE WHERE-TO-GO TABLE.  THE REQUIRED BTAM MODULES ARE     01200002
*    LOADED.  IRB'S AND IQE'S ARE BUILT AS REQUIRED.                    01250002
*                                                                       01300002
* EXTERNAL ROUTINES - ABEND (SVC 13)                                    01350002
*         CIRB (SVC 43)                                                 01400002
*         LOAD (SVC 8)                                                  01450002
*         XCTL (SVC 7)                                                  01500002
*                                                                       01550002
* EXITS, NORMAL - THIS ROUTINE NORMALLY TERMINATES BY LOCATING THE NEXT 01600002
*    NON-ZERO ENTRY IN THE WHERE-TO-GO TABLE AND EXECUTING AN XCTL TO   01650002
*    THAT ROUTINE.                                                      01700002
*                                                                       01750002
* EXITS, ERROR - IF THE VECTOR TABLE IN THE READ/WRITE ROUTINE          01800002
*    (IGG019MA) IS FULL AND ANOTHER ENTRY IS REQUIRED, THE TASK IS      01850002
*    ABENDED WITH A SYSTEM CODE OF 097.                                 01900002
*                                                                       01950002
* TABLES/WORK AREAS - THE DCB PARAMETER LIST CONTAINS THE ADDRESS OF    02000002
*    EACH DCB SPECIFIED IN THE OPEN MACRO.  THE WHERE-TO-GO TABLE       02050002
*    CONTAINS THE ID AND TTR OF THE ROUTINE NEEDED TO PROCESS THE DCB   02100002
*    CORRESPONDING TO THIS ENTRY.  THE IDTTRL TABLE CONTAINS THE ID'S   02150002
*    REQUIRED TO LOAD THE REQUIRED BTAM MODULES.                        02200002
*                                                                       02250002
* ATTRIBUTES - THIS ROUTINE IS EXECUTED IN THE TRANSIENT AREA AS        02300002
*    ENABLED, PRIVELEDGED, AND REENTRANT.                               02350002
*                                                                       02400002
* NOTES - THIS ROUTINE COMPLETES THE OPEN PROCESSING FOR BTAM-SUPPORTED 02450002
*    3270'S.  THEREFORE, THE WHERE-TO-GO ENTRY WILL BE SET TO ZERO.     02500002
*                                                                       02550002
         EJECT                                                          02600002
*/*IGG0194Q: CHART */                                                   02650002
*/* HEADER                                                              02700002
*/*BTAM OPEN LOAD 3 FOR LOCAL 3270 DEVICES                          */  02750002
*/*IGG0194Q: E ENTRY FROM IGG0194P VIA XCTL */                          02800002
*/* M ESTABLISH BASES FOR DCB AND DEB DSECTS */                         02850002
*/* D (NO,,YES,GETIOB) BUFFER POOL PROVIDED BY USER? */                 02900002
*/* D (YES,,NO,GETIOB) ANY BUFFERS REQUESTED? */                        02950002
*/* D (NO,,YES,GETIOB) IS BUFFER LENGTH ZERO? */                        03000002
*/* P ROUND BUFFER LENGTH TO NEXT FULL WORD AND STORE LENGTH IN DCB */  03050002
*/* P COMPUTE TOTAL SIZE OF BUFFERS PLUS BUFFER CONTROL BLOCK */        03100002
*/* L GETMAIN FOR BUFFER POOL */                                        03150002
*/* P STORE ADDRESS OF BUFFER CONTROL BLOCK IN DCB */                   03200002
*/* P INITIALIZE THE BUFFER CONTROL BLOCK AND LINK BUFFERS TOGETHER */  03250002
*/*GETIOB: M OBTAIN ADDRESS OF DEB SUBID FIELD */                       03300002
*/* D (YES,,NO,TESTBPM) OLT'S REQUIRED? */                              03350002
*/* S LOADRTN: LOAD LOCAL 3270 OLT CONTROL RTN */                       03400002
*/* S BLDIRB: BUILD IRB AND IQE'S FOR OLT */                            03450002
*/* P STORE IRB ADDRESS IN IRB POINTER AREA */                          03500002
*/*TESTBPM: D (YES,,NO,LOADCEA) BUFFER MANAGEMENT REQUIRED? */          03550002
*/* S LOADRTN: LOAD BUFFER MANAGEMENT ROUTINE */                        03600002
*/* P STORE ADDRESS OF BUFFER MANAGEMENT ROUTINE IN DEB */              03650002
*/*LOADCEA: S LOADRTN: LOAD LOCAL 3270 CEA/XEA RTN */                   03700002
*/* P STORE ADDRESS OF CEA/XEA IN DEB CEA AND XEA VECTORS */            03750002
*/* S LOADRTN: LOAD BTAM READ/WRITE ROUTINE */                          03800002
*/* P STORE ADDRESS OF READ/WRITE ROUTINE IN DCB */                     03850002
*/* M LOCATE DDM VECTOR TABLE */                                        03900002
*/*RWVDLP: D (YES,,NO,VDLPINDX) VECTOR TABLE ENTRY = ZERO? */           03950002
*/*DDMIN: P STORE INDEX TO VECTOR IN DCB */                             04000002
*/* S LOADRTN: LOAD LOCAL 3270 DDM */                                   04050002
*/* P STORE ADDRESS AND DEVICE CODE IN VECTOR TABLE */                  04100002
*/*ENBLATN: S LOADRTN: LOAD 2ND LEVEL ATTN RTN */                       04150002
*/* S BLDIRB: BUILD IRB & 1 IQE FOR ATTN HANDLING */                    04200002
*/* P STORE IRB ADDRESS IN ALL UCB'S OF LINE GROUP */                   04250002
*/* D (YES,,NO,NORDYQ) READYQ SPECIFIED IN DCB ? */                     04260002
*/* D (YES,BLDRDYRB,NO,) USER PROVIDE ROUTINE ? */                      04270002
*/* S LOADRTN: LOAD BTAM'S READY EXIT ROUTINE */                        04280002
*/* P FLAG USING BTAM'S ROUTINE IN DCB */                               04290002
*/* P STORE ADDRESS OF ROUTINE IN DCB */                                04340002
*/*BLDRDYRB: D (YES,USEUSER,NO,) USER PROVIDE ROUTINE ? */              04390002
*/* S (,RDYIRBDN) BLDIRB: BUILD IRB AND IQE'S FOR BTAM READY */         04490002
*/*USEUSER: S BLDPIRB: BUILD IRB AND IQE'S FOR USER READY */            04540002
*/*RDYIRBDN: P SET GRAF FLAGS AND STORE IRB ADDRESS IN ALL UCB'S */     04590002
*/*ENDLOAD: P CHECK ALL UCB'S FOR READY DURING OPEN */                  04690002
*/*NORDYQ: P CLEAR WHERE-TO-GO ID TO INDICATE END OF OPEN */            04740002
*/* R GO TO STANDARD WHERE-TO-GO CODE */                                04790002
*/*VDLPINDX: D (NO,,YES,DDMIN) VECTOR FOR LOCAL 3270? */                04890002
*/* D (NO,,YES,VDOVRFLO) END OF TABLE ENTRY? */                         05040002
*/* M (,RWVDLP) INCR INDICES FOR NEXT ENTRY */                          05140002
*/*VDOVRFLO: R ABEND WITH SYSTEM 097 CODE */                            05240002
*/*LOADRTN: E ENTRY */                                                  05340002
*/* P CONSTRUCT BLDL LIST ENTRY IN OPEN WORK AREA FROM IDTTRL ENTRY */  05440002
*/* D (NO,,YES,THREERCD) RTN REQUESTED = READ/WRITE RTN? */             05492002
*/*COMPL: D (NO,,YES,LONGLOAD) RTN SIZE EXCEEDS 1024 BYTES? */          05498002
*/* P LENGTH OF FIRST TEXT RCD = SIZE OF ROUTINE */                     05498802
*/*LOAD: L LOAD RTN FROM SYS1.SVCLIB */                                 05499602
*/* P STORE ID IN CURRENT DEB SUBID ENTRY */                            05499802
*/* M INCREMENT POINTER TO NEXT DEB SUBID ENTRY */                      05516602
*/* R RETURN */                                                         05528602
*/*THREERCD: P ADD X'800' TO RTN LENGTH DERIVED FROM IDTTRL*/           05533002
*/*LONGLOAD: P LENGTH OF FIRST TEXT RECORD = 1024 BYTES */              05533202
*/* P (,LOAD) MODIFY LOAD ATTRIBUTES FOR MULTIPLE TEXT RECORDS */       05540802
*/*BLDIRB: E ENTRY */                                                   05543202
*/* P COMPUTE LENGTH OF IRB WORKAREA */                                 05544002
*/* L INVOKE CIRB TO BUILD IRB */                                       05544102
*/*IQEINIT: P INITIALIZE IQE'S AND LINK THEM TOGETHER */                05544202
*/* P SET LINK OF LAST IQE = ZERO */                                    05544302
*/* R RETURN */                                                         05558202
*/*BLDPIRB: E ENTRY */                                                  05568202
*/* P COMPUTE LENGTH OF IRB WORKAREA */                                 05570202
*/* L (,IQEINIT) INVOKE CIRB TO BUILD PROB PROG IRB */                  05570602
*/*IGG0194Q: END */                                                     05571002
         EJECT                                                          05572202
*              REGISTER EQUATES                                         05586102
RPARM2   EQU   0                   SECOND PARAMETER REGISTER            05600002
RPARM1   EQU   1                   FIRST PARAMETER REGISTER             05650002
REVN     EQU   2                   WORK REGISTER- EVEN                  05700002
RODD     EQU   3                   WORK REGISTER- ODD                   05750002
RCORE    EQU   4                   POINTER TO GETMAIN'ED CORE           05800002
RPAR     EQU   5                   START OF PARAMETER LIST              05850002
RWTG     EQU   6                   START OF WTG TABLE                   05900002
RPARC    EQU   7                   CURRENT ENTRY IN PARAMETER LIST      05950002
RWTGC    EQU   8                   CURRENT ENTRY IN WTG TABLE           06000002
RUCB     EQU   9                   POINTER TO UCB                       06050002
RDEB     EQU   10                  POINTER TO DEB                       06100002
RBASE    EQU   11                  BASE REGISTER FOR CSECT              06150002
RDCB     EQU   12                  POINTER TO DCB                       06200002
RW3      EQU   13                  WORK REGISTER                        06250002
RRTRN    EQU   14                  RETURN POINT REGISTER                06300002
RRTN     EQU   14                                                       06350002
REPR     EQU   15                  ENTRY POINT REG                      06400002
*                                                                       06450002
RW1      EQU   2                   ALTERNATE ASSIGNMENTS                06500002
RW2      EQU   3                       FOR WORK REGISTERS               06550002
RSUBID   EQU   9                   POINTER TO NEXT DEB SUB-ID FIELD     06600002
*                                                                       06650002
ANRDEVTP EQU   X'FE'               DCBDEVTP CODE FOR 3270               06700002
CVTAD    EQU   16                                                       06750002
DEBAVT   EQU   28                  DISP TO VECTOR TO DEB APPENDAGE TAB  06800002
EQ       EQU   8                   CC MASK = 8                          06850002
EIGHT    EQU   X'08'                                                    06900002
FIVE     EQU   5                                                        06950002
ONE28    EQU   128                                                      07000002
FOUR     EQU   4                                                        07050002
FREEPOOL EQU   X'04'                FLAG TO CLOSE TO RELEASE BUFFERS    07100002
GRAF     EQU   28                  OFFSET TO GRAPHICS STATUS   @YA02128 07110002
IQEIRB   EQU   8                   DISP TO IRB ADDR FIELD IN IQE        07150002
IQELNGTH EQU   16                  LENGTH OF AN IQE IN BYTES            07200002
IQELNK   EQU   0                   DISP TO LINK FIELD IN IQE            07250002
IQETCB   EQU   12                  DISP TO TCB ADDR FIELD IN IQE        07300002
IRBAD    EQU   28                  DISP TO IRB ADDR IN UCB EXTENSION    07350002
LOADATB  EQU   X'CB'               STANDARD LOAD ATTRIBUTES             07400002
*    REENTERABLE, ONLY LOADABLE, EXECUTABLE, ONLY ONE TEXT RECORD, NO   07450002
*    RLD RECORD, ORIGIN IS ZERO, ENTRY IS ZEROS EERO, AND CANNOT BE     07500002
*    REPROCESSED BY THE LINKAGE EDITOR.                                 07550002
LOADATB1 EQU   X'CA'               LOAD ATTRIBUTES FOR MODULES          07600002
*    LARGER THAN 1024 BYTES - SAME AS LOADATB BUT MORE THAN ONE         07650002
*    TEXT RECORD.                                                       07700002
NDVTRTBL EQU   X'FF'               END OF VECTOR TABLE CODE             07750002
OLT      EQU   X'10'               DCB OLT FLAG                         07800002
ONE      EQU   1                                                        07850002
PLOFF    EQU   4                   OFFSET IN PARM LIST                  07900002
RBNEXAV  EQU   96                  DISP TO IQE POINTER IN IRB           07950002
RDYOFF   EQU   X'EF'               TURN OFF READY IN OPEN FLG  @YA02128 07960002
RDYQ     EQU   32                  UCB OFFSET TO READY IRB AD  @YA02128 07970002
SEVEN    EQU   7                                                        08000002
SIX      EQU   6                                                        08050002
SVCDCB   EQU   84                  DISP TO DCB FOR SYS1.SVCLIB IN CVT   08100002
TENAND4  EQU   14                                                       08150002
THREE    EQU   3                                                        08200002
TWENTY2  EQU   22                                                       08250002
TWENTY4  EQU   24                                                       08300002
TWENTY5  EQU   25                                                       08350002
TWENTY6  EQU   26                                                       08400002
TWENTY7  EQU   27                                                       08450002
TWO      EQU   2                                                        08500002
VDOFFSET EQU   12                  DISP TO VECTOR TABLE IN READ/WRITE   08550002
*                                      RTN                              08600002
WAOFF    EQU   32                  OFFSET OF FIRST WTG ENTRY FROM       08650002
*                                      BEGINNING OF TABLE               08700002
WGOFF    EQU   8                   OFFSET IN WTG TABLE                  08750002
ZERO     EQU   0                                                        08800002
*                                                                       08950002
*                                                                       09000002
         USING IHADCB,RDCB                                              09050002
         USING IECTDEB,RDEB                                             09100002
         USING WTG,RWTG                                          Y02947 09150002
         USING WTGENTRY,RWTGC                                           09160002
         BALR  RBASE,0             ESTABLISH ADDRESSABILITY FOR CSECT   09200002
         USING *,RBASE                                                  09250002
         B     CONTIN                  BRANCH AROUND ID        @YA02128 09260002
         DC    CL8'IGG0194Q'           NAME                    @YA02128 09270002
         DC    XL2'4072'                 AND DATE                       09280000
PATCH    DC    15F'0'                  PATCH AREA            LD YA03953 09282000
CONTIN   DS    0H                                              @YA02128 09290002
         L     RDCB,0(RPARC)       ADDRESSABILITY FOR DCB               09300002
         L     RDEB,DCBDEBAD       GET DEB ADDRESS                      09350002
         L     RDEB,DEBAVT(RDEB)   GET START OF APPENDAGE VECTOR TABLE  09400002
*                                                                       09450002
*                                                                       09500002
*              DETERMINE IF A BUFFER POOL MUST BE OBTAINED              09550002
         CLI   DCBBUFCB+3,ONE      BUFFER POOL PROVIDED BY USER         09600002
         BNE   GETIOB               YES, GO GET IOB'S                   09650002
         CLI   DCBBUFNO,ZERO        NO. ARE ANY BUFFERS REQUESTED       09700002
         BE    GETIOB               NO, BUFFER POOL NOT REQUIRED.       09750002
*                                   GO GET IOB'S                        09800002
         CLC   DCBBUFL(TWO),ZEROS  BUFFER LENGTH EQUAL TO ZERO          09850002
         BE    GETIOB               YES, GO GET IOB'S                   09900002
*                                                                       09950002
*              CONSTRUCT BUFFER POOL                                    10000002
         SR    RW1,RW1                                                  10050002
         IC    RW1,DCBBUFNO         GET NUMBER OF BUFFERS               10100002
         LH    RW2,DCBBUFL          GET BUFFER LENGTH                   10150002
         LA    RW2,THREE(RW2)       ROUND TO                            10200002
         SRL   RW2,TWO                  NEXT HIGHEST                    10250002
         SLL   RW2,TWO                      FULL WORD                   10300002
         STH   RW2,DCBBUFL          STORE ROUNDED LENGTH IN DCB         10350002
         LR    RPARM1,RW1                                               10400002
         MR    RPARM2,RW2           COMPUTE SIZE OF BUFFER AREA         10450002
         LA    RPARM2,EIGHT(RPARM1) ADD 8 FOR BUFFER CONTROL BLOCK      10500002
         O     RPARM2,IOBPOOL       PLACE SUBPOOL NUMBER IN             10550002
*                                   HIGH-ORDER BYTE OF PARM REG         10600002
         L     RW3,WTGCORE-ONE      ADDR OF OPEN WORK AREA       Y02947 10650002
         USING FORCORE,RW3                                       Y02947 10700002
         MODESET KEYADDR=DXUKEY,WORKREG=14    ASSUME USER KEY    Y02947 10750002
         GETMAIN  R,LV=(0)          GET CORE FOR BUFFER POOL            10800002
         LR    RRTN,RPARM1         SAVE ADDR OF GETMAIN'ED AREA  Y02947 10850002
         STH   RW1,FOUR(RPARM1)    STORE NO. OF BUFFERS IN BUFCB        10900002
         STH   RW2,SIX(RPARM1)     STORE SIZE OF BUFFERS IN BUFCB       10950002
*              BUILD BUFFER CHAIN                                       11000002
         LR    RCORE,RPARM1                                             11050002
         LA    RPARM1,EIGHT(RPARM1) GET ADDRESS OF FIRST BUFFER         11100002
         ST    RPARM1,0(RCORE)      STORE ADDRESS OF FIRST BUFFER       11150002
*                                       IN BUFFER CONTROL BLOCK         11200002
TESTBUFC BCT   RW1,NEXTBUF          IF MORE BUFFERS, BRANCH             11250002
         ST    RW1,0(RPARM1)        ZERO LAST LINK FIELD                11300002
         MODESET EXTKEY=DATAMGT    GO BACK TO DATA MGT KEY       Y02947 11350002
         OI    DCBMACRF,FREEPOOL    TELL CLOSE TO FREE BUFFER POOL      11400002
         B     GETIOB               GO GET THE IOB'S                    11450002
*                                                                       11500002
NEXTBUF  LR    RCORE,RPARM1         SAVE ADDRESS OF CURRENT BUFFER      11550002
         ALR   RPARM1,RW2           COMPUTE ADDRESS OF NEXT BUFFER      11600002
         ST    RPARM1,0(RCORE)      STORE ADDRESS OF NEXT BUFFER IN     11650002
*                                       LINK FIELD OF CURRENT BUFFER    11700002
         B     TESTBUFC            LOOP TO PROCESS NEXT BUFFER          11750002
*                                                                       11800002
GETIOB   EQU   *                                                        11850002
         STCM  RRTN,SEVEN,DCBBUFCA STORE BUFFCB ADDRESS          Y02947 11900002
         SR    RSUBID,RSUBID                                            11950002
         IC    RSUBID,DEBNMEXT     GET NO. OF DEVICES IN THE LINE GROUP 12000002
         SLL   RSUBID,TWO          MULT BY 4 TO GET LENGTH OF UCB LIST  12050002
         LA    RSUBID,DEBUCBAD(RSUBID)  GET ADDRESS OF AM AREA          12100002
         IC REVN,DCBOFLGS          SAVE DCB OPEN FLAGS                  12150002
         ST    RSUBID,DCBREAD      SAVE ADDRESS OF ACCESS METHOD AREA   12200002
         STC   REVN,DCBOFLGS       RESTORE DCB OPEN FLAGS               12250002
*                                                                       12300002
         SR    REVN,REVN                                                12350002
         IC    REVN,DEBAMLNG       GET LENGTH OF ACCESS METHOD AREA     12400002
         AR    RSUBID,REVN         ADD TO GET ADDRESS OF SUB-ID'S       12450002
*                                                                       12500002
         TM    DCBERROP,OLT        ON-LINE TESTS REQUIRED               12550002
         BNO   TESTBPM             NO, TEST FOR BUFFER MANAGEMENT RTN   12600002
         L     RCORE,DEBIRBAD-1    GET ADDRESS OF DEB IRB POINTERS      12650002
         LA    RPARM1,OLTCTL       GET ADDRESS OF IDTTRL ENTRY          12700002
         BAL   RRTRN,LOADRTN       LOAD OLT CTL MODULE                  12750002
         SR    RPARM1,RPARM1                                            12800002
         IC    RPARM1,DEBNMEXT     NO. OF IQE'S = NO. OF DEVICES        12850002
         SLA   RPARM1,ONE               & MULTIPLY BY 2                 12900002
*                                  BECAUSE EXISTING BTAM EXPECTS IT     12950002
         BAL   RRTRN,BLDIRB        BUILD IRB AND IQE'S FOR OLT          13000002
         ST    RPARM1,0(RCORE)     STORE IRB ADDRESS IN POINTER         13050002
         B     CONTINUE                                       LD Y02947 13060002
*                                                                       13100002
TESTBPM  EQU   *                                              LD Y02947 13110002
         MODESET EXTKEY=ZERO                                  LD Y02947 13120002
CONTINUE EQU   *                                              LD Y02947 13130002
         CLI   DEBAMLNG,ZERO       BUFFER MANAGEMENT REQUIRED           13150002
         BZ    LOADCEA             NO, GO LOAD CEA/XEA                  13200002
         LA    RPARM1,BPMR         GET ADDRESS OF IDTTRL ENTRY          13250002
         BAL   RRTN,LOADRTN        LOAD BUFFER POOL MANAGEMENT ROUTINE  13300002
         L     REVN,DCBREAD        GET ADDRESS OF AM SECTION IN DEB     13350002
         ST    RPARM2,0(REVN)      STORE ADDR OF ROUTINE IN DEB         13400002
*                                                                       13450002
LOADCEA  LA    RPARM1,CHENDAPP     GET ADDRESS OF IDTTRL ENTRY          13500002
         BAL   RRTN,LOADRTN        LOAD CEA/XEA                         13550002
         ST    RPARM2,DEBCEA       STORE RTN ADDR IN DEB VECTOR TO CEA  13600002
         ST    RPARM2,DEBXCEA          AND XEA                          13650002
*                                                                       13700002
         LA    RPARM1,SIOAPPN          ADDR OF SIO APPENDAGE TTR        13750002
         BAL   RRTN,LOADRTN            LOAD SIO APPENDAGE               13800002
         ST    RPARM2,DEBSIOA          STORE ADDRESS IN DEB PREFIX      13850002
         OI    DEBSIOA,SIOFIX      FLAG PAGE FIX APP IN SIO       3876  13900002
*                                                                       13950002
         LA    RPARM1,RDWRTRTN     GET ADDRESS OF IDTTRL ENTRY          14000002
         BAL   RRTN,LOADRTN        LOAD READ/WRITE ROUTINE              14050002
         IC    REVN,DCBOFLGS       SAVE DCBOFLAGS                       14100002
         ST    RPARM2,DCBREAD      STORE ADDRESS OF READ/WRITE RTN      14150002
         STC   REVN,DCBOFLGS       RESTORE OPEN FLAGS DESTROYED BY      14200002
*                                      PREVIOUS STORE.                  14250002
*                                                                       14300002
         LR    RCORE,RPARM2        GET ADDR OF READ/WRITE RTN           14350002
         LA    RCORE,VDOFFSET(RCORE)    GET BEGINNING OF VECTOR TABLE   14400002
         SR    REVN,REVN           INITIALIZE INDEX COUNTER             14450002
         SR    RODD,RODD           INITIALIZE COMPARAND OF ZERO         14500002
*                                                                       14550002
RWVDLP   C     RODD,0(RCORE)       VECTOR TABLE ENTRY EQUAL ZERO        14600002
         BNZ   VDLPINDX            BRANCH IF FIELD NON-ZERO             14650002
         MVI   TWO(RCORE),ANRDEVTP MAKE FIELD NON-ZERO                  14700002
DDMIN    STC   REVN,DCBDEVTP       STORE INDEX TO VECTOR IN DCB         14750002
         LA    RPARM1,DDM          GET ADDRESS OF IDTTRL ENTRY          14800002
         BAL   RRTN,LOADRTN        LOAD 3270 LOCAL DDM                  14850002
         ST    RPARM2,0(RCORE)     STORE ADDRESS IN VECTOR TABLE        14900002
         MVI   0(RCORE),ANRDEVTP   STORE DEVICE TYPE CODE IN HIGH-ORDER 14950002
         MODESET EXTKEY=DATAMGT    DATAMGT KEY                LD Y02947 15000002
*                                      BYTE                             15050002
         B     ENBLATN             BRANCH TO LOAD 2ND LEVEL ATTN RTN    15100002
*                                                                       15150002
VDLPINDX CLI   0(RCORE),ANRDEVTP   VECTOR FOR ANR                       15200002
         BE    DDMIN               YES, USE IT                          15250002
         CLI   0(RCORE),NDVTRTBL   END OF TABLE                         15300002
         BE    VDOVRFLO            YES, GIVE ABEND                      15350002
         LA    RCORE,FOUR(RCORE)   INCR TO NEXT ENTRY                   15400002
         LA    REVN,ONE(REVN)      BUMP INDEX                           15450002
         B     RWVDLP              CHECK NEXT ENTRY                     15500002
*                                                                       15550002
VDOVRFLO L     RPARM1,ERR6         VECTOR DIRECTORY OVERFLOW ERROR      15600002
         ABEND (1)                                                      15650002
*                                                                       15700002
ENBLATN  LA    RPARM1,ATNRTN2      GET IDTTRL ENTRY                     15750002
         BAL   RRTN,LOADRTN        LOAD SECOND LEVEL ATTN RTN           15800002
         LA    RPARM1,TWO          NO. OF IQE'S = TWO        LD YA03953 15850000
         BAL   RRTN,BLDIRB         BUILD IRB AND IQE FOR ATTENTION      15900002
*                                  HANDLING                             15950002
         LR    RW3,RSUBID          SAVE SUB ID PTR             @YA02128 15960002
         SR    REVN,REVN           INITIALIZE UCB LIST INDEX            16000002
         SR    RODD,RODD                                                16050002
         IC    RODD,DEBNMEXT       INITIALIZE LOOP COUNTER              16100002
         MODESET EXTKEY=ZERO       NEED KEY 0 TO STORE IN UCB    Y02947 16150002
ATNIRBLP L     RUCB,DEBUCBAD(REVN) GET UCB ADDRESS                      16200002
         LA    RPARM1,0(RPARM1)    INSURE HIGH BYTE CLEAR      @YA02128 16210002
         IC    RPARM2,IRBAD(RUCB)  PICK UP GRAF FLAGS          @YA02128 16220002
         SLL   RPARM2,TWENTY4      PUT IN HIGH BYTE            @YA02128 16230002
         OR    RPARM1,RPARM2       SAVE GRAF FLAGS             @YA02128 16240002
         ST    RPARM1,IRBAD(RUCB)  STORE IRB ADDR IN UCB EXTENSION      16250002
         LA    REVN,FOUR(REVN)     INDEX TO NEXT UCB ADDRESS            16300002
         BCT   RODD,ATNIRBLP       LOOP IF NOT FINISHED                 16350002
         L     RDCB,ZERO(RPARC)        POINT TO DCB                     17700002
         MODESET EXTKEY=DATAMGT                                         17750002
         TM    DCBLERB,X'02'       ADDRESS VIA READYQ ?        @YA02128 17760002
         BNO   NORDYQ              NO, DON'T SETUP IRB         @YA02128 17770002
         L     REVN,52(RDCB)       PICK POINTER TO USER        @YA02128 17780002
*                                  SUPPLIED READY ROUTINE      @YA02128 17790002
         LA    REVN,0(REVN)        CLEAR HIGH BYTE             @YA02128 17792002
         LTR   REVN,REVN           ADDRESS SUPPLIED ?          @YA02128 17794002
         BNZ   BLDRDYRB            YES, USE IT                 @YA02128 17796002
         LR    RSUBID,RW3          RESTORE SUB ID PTR          @YA02128 17798002
         OI    DCBLERB,X'01'       FLAG USING BTAM'S           @YA02128 17798402
         LA    RPARM1,READYM       POINT TO IDTTRL             @YA02128 17798802
         BAL   RRTN,LOADRTN        LOAD READYQ - IGG019UP      @YA02128 17799202
         LR    REVN,RPARM2         GET ENTRY POINT             @YA02128 17799602
         LA    REVN,0(REVN)        CLEAR HIGH BYTE             @YA02128 17799702
         O     REVN,DCBLERB        PICK UP FLAGS               @YA02128 17799802
         ST    REVN,DCBLERB        SAVE THE ADDRESS            @YA02128 17799902
         LA    REVN,0(REVN)        CLEAR HIGH BYTE             @YA02128 17816602
BLDRDYRB EQU   *                   BUILD THE IRB               @YA02128 17826602
         LR    RPARM2,REVN         SET EP ADDRESS              @YA02128 17828602
         SR    RPARM1,RPARM1       CLEAR IQE COUNT REG         @YA02128 17830602
         IC    RPARM1,DEBNMEXT     GET NUMB IQE'S - ONE / UCB  @YA02128 17832602
         TM    DCBLERB,X'01'       USING BTAM'S ?              @YA02128 17833002
         BNO   USEUSER             NO, BUILD PP IRB            @YA02128 17833102
         BAL   RRTN,BLDIRB         BUILD A SUPR IRB            @YA02128 17833202
         MODESET EXTKEY=DATAMGT                              LD YA02128 17835202
         B     RDYIRBDN            GO SET UP UCB'S             @YA02128 17838802
USEUSER  EQU   *                   BUILD USER IRB              @YA02128 17840802
         BAL   RRTN,BLDPIRB        GO BUILD THE READY QUEUE    @YA02128 17842802
         MODESET EXTKEY=DATAMGT                              LD YA02128 17842902
RDYIRBDN EQU   *                   IRB IS BUILT                @YA02128 17843202
         SR    REVN,REVN           CLEAR A WORK REGISTER       @YA02128 17843602
         SR    RODD,RODD           CLEAR A WORK REGISTER       @YA02128 17844002
         IC    RODD,DEBNMEXT       INITIALIZE LOOP COUNTER     @YA02128 17844102
RDYIRBLP L     RUCB,DEBUCBAD(REVN) GET UCB ADDRESS             @YA02128 17844202
         MODESET EXTKEY=ZERO                                 LD YA02128 17846102
         ST    RPARM1,RDYQ(RUCB)   STORE IRB ADDRESS IN UCB    @YA02128 17850002
         TM    DCBLERB,X'01'       USING BTAM'S ?              @YA02128 17858202
         BZ    SETUSER             NO, FLAG USER               @YA02128 17868202
         OI    GRAF(RUCB),X'10'    SAY BTAM                    @YA02128 17870202
         B     TYPESET             CONTINUE                    @YA02128 17870602
SETUSER  EQU   *                   INDICATE USER ROUTINE       @YA02128 17871002
         OI    GRAF(RUCB),X'08'    SAY USER                    @YA02128 17871402
TYPESET  EQU   *                   TYPE OF ROUTINE FLAGGED     @YA02128 17871802
         MODESET EXTKEY=DATAMGT                              LD YA02128 17873202
         LA    REVN,FOUR(REVN)     INDEX TO NEXT UCB ADDRESS   @YA02128 17877602
         BCT   RODD,RDYIRBLP       LOOP IF NOT FINISHED        @YA02128 17879002
ENDLOAD  EQU   *                   BTAM OPEN DONE              @YA02128 17880402
         SR    RODD,RODD           CLEAR A WORK REGISTER       @YA02128 17881802
         SR    REVN,REVN           CLEAR A WORK REGISTER       @YA02128 17883202
         IC    RODD,DEBNMEXT       INITIALIZE LOOP COUNTER     @YA02128 17884602
CHKOPRDY EQU   *                   CHECK FOR READY DURING OPEN @YA02128 17886002
         L     RUCB,DEBUCBAD(REVN) GET UCB ADDRESS             @YA02128 17887402
         MODESET EXTKEY=ZERO                                 LD YA02128 17888402
         NI    GRAF(RUCB),X'7F'    TURN OPEN IN PROGRESS       @YA02128 17888802
         TM    GRAF(RUCB),X'40'    READY DURING OPEN ?         @YA02128 17890202
         BNO   SCHEDRET            NO, CONTINUE                @YA02128 17891602
         NI    GRAF(RUCB),X'BF'    TURN OFF OPEN READY         @YA02128 17893002
         SR    RDCB,RDCB           CLEAR EVEN REG              @YA02128 17894402
         LA    RW3,X'58'           PICK UP IOB LENGTH          @YA02128 17895802
         IC    RDCB,36(RUCB)       GET RLN FOR DEVICE          @YA02128 17897202
         MR    RDCB,RDCB           DISP INTO IOB POOL          @YA02128 17898602
         MODESET EXTKEY=DATAMGT                              LD YA02128 17899002
         L     RDCB,0(RPARC)       GET DCB ADDRESS             @YA02128 17900002
         L     RDCB,28(RDCB)       GET ADDRESS OF IOB POOL     @YA02128 17902502
         LA    RDCB,0(RW3,RDCB)    POINT TO THIS RLN'S IOB     @YA02128 17912502
         USING FORCORE,RW3          WORKAREA ADDRESSABILITY  LD YA02128 17912902
         L     RW3,WTGCORE-ONE      WORKAREA ADDRESS         LD YA02128 17913302
         MODESET KEYADDR=DXUKEY,WORKREG=13                   LD YA02128 17913702
         DROP  RW3                                                      17914102
         NI    28(RDCB),X'7F'      TURN OFF OPEN FAILED        @YA02128 17914502
         MODESET EXTKEY=DATAMGT                              LD YA02128 17916502
         L     RDCB,0(RPARC)       RESTORE DCB ADDRESS         @YA02128 17918402
SCHEDRET LA    REVN,FOUR(REVN)     INDEX TO NEXT UCB           @YA02128 17920402
         MODESET EXTKEY=DATAMGT                              LD YA02128 17920802
         BCT   RODD,CHKOPRDY       LOOP IF NOT FINISHED        @YA02128 17922402
NORDYQ   EQU   *                   THIS LOAD FINISHED          @YA02128 17926302
         XC    0(TWO,RWTGC),0(RWTGC) CLEAR ID TO INDICATE DONE @YA02128 17928302
         L     RCORE,4(RWTGC)      WORKAREA ADDRESS           LD Y02947 17930402
         L     REPR,WTGPREFX                                     Y02947 17934302
         STM   RRTN,RDCB,IECREGSV-IECPREFX(REPR) SAVE REGS       Y02947 17938202
         IECRES INIT,DCBCOPY=FRWKAR  REFRESH USER'S DCB FROM 'COPY'     17950002
         LR    REPR,RWTG            REG SAVEAREA ADDR            YM5668 17960002
         LM    RRTN,RDCB,IECREGSV-IECPREFX(REPR)  RESTORE REGS   Y02947 18000002
         XC    0(TWO,RWTGC),0(RWTGC)  CLEAR ID TO INDICATE COMPLETION   18050002
         L     REPR,FOUR(RWTGC)    GET ADDR OF  WORK AREA        Y02947 18100002
         USING FORCORE,REPR                                             18110002
*                                                                       18150002
RELOOP   LA    RWTGC,WGOFF(RWTGC)  INCR CURRENT WTG REGISTER            18200002
         LA    RPARC,PLOFF(RPARC)  INCR CURRENT PARM LIST REGISTER      18250002
         CLC   0(TWO,RWTGC),AMIDCNST   IF THE NEXT WTG ENTRY REQUIRES   18300002
         BCR   EQ,RBASE            THIS EXECUTOR, EXEC MODULE AGAIN     18350002
         CLC   0(TWO,RWTGC),OPIDCNST   END OF TABLE                     18400002
         BNE   RELOOP              NO, CHECK NEXT ENTRY IN TABLE        18450002
*                                                                       18500002
         LR    RPARC,RPAR          REINITIALIZE CURRENT PARM LIST REG   18550002
         LA    RWTGC,WAOFF(RWTG)       AND WTG REGISTER                 18600002
*                                                                       18650002
ZCHECK   CLI   0(RWTGC),ZERO       IF ENTRY NON-ZERO,                   18700002
         BNE   XCTLRTNE                GO TO TRANSFER CONTROL           18750002
         LA    RWTGC,WGOFF(RWTGC)  IF ZERO, GET NEXT ENTRY              18800002
         LA    RPARC,PLOFF(RPARC)                                       18850002
         B     ZCHECK                  AND CHECK IT                     18900002
*                                                                       18950002
XCTLRTNE EQU   *                                                        18960002
         LA    REPR,DXCCW12            POINT TO DOUBLE WORD LIST        19000002
         DROP  REPR                                                     19010002
         MVC   SIX(TWO,RWTG),0(RWTGC)  MOVE ID TO NAME FIELD            19050002
         MVC   TENAND4(THREE,RWTG),TWO(RWTGC)  MOVE TTR TO WTG FIELD    19100002
         MVC   TWENTY2(SEVEN,RWTG),CONATB  RESTORE XCTL ATTRIBUTES      19150002
*                                  & LENGTH                             19200002
         XCTL  DE=(RWTG),SF=(E,(15))                                    19250002
*                                                                       19300002
*                                                                       19350002
*        THIS SUBROUTINE IS USED TO LOAD ROUTINES FOR OPEN              19400002
*  INPUT - REGISTER 1 - ADDR OF IDTTRL ENTRY OF MODULE TO BE LOADED     19450002
*  OUTPUT - REGISTER 0 - ADDR OF THE LOADED ROUTINE                     19500002
*  RETURN ADDR IN REGISTER 14                                           19550002
LOADRTN  MVI   TWENTY2(RWTG),LOADATB  SET STANDARD LOAD ATTRIBUTES      19600002
         MVC   SIX(TWO,RWTG),0(RPARM1)  MOVE ID TO LAST 2 BYTES OF      19650002
*                                  NAME                                 19700002
         MVC   TENAND4(THREE,RWTG),TWO(RPARM1)  MOVE TTR FROM TABLE     19750002
         SR    REVN,REVN                                                19800002
         IC    REVN,FIVE(RPARM1)   GET LENGTH IN DOUBLE WORDS           19850002
         SLL   REVN,3                  AND CONVERT TO BYTES             19900002
         ST    REVN,TWENTY4(RWTG)  STORE LENGTH IN PARM LIST            19950002
         MVC   TWENTY5(TWO,RWTG),TWENTY6(RWTG)  MOVE LENGTH             20000002
         CLC   0(TWO,RPARM1),RDWRTRTN  MOD REQUESTED = READ/WRITE RTN   20050002
         BE    THREERCD            YES, ADJUST LENGTH TO THREE RECORDS  20100002
*                                      TOTAL LENGTH OF MODULE           20150002
CMPL     CLI   FIVE(RPARM1),ONE28  IF MOD MORE THAN 128 DOUBLE WORDS    20200002
         BH    LONGLOAD                (1024 BYTES)  LONG, GO TO        20250002
*                                       SET UP LONG LOAD                20300002
         MVC   TWENTY7(TWO,RWTG),TWENTY5(RWTG)  MOVE LENGTH TO FIELD    20350002
*                                       WHICH INDICATES LENGTH OF       20400002
*                                       FIRST TEXT RECORD               20450002
LOAD     LR    RPARM2,RWTG         POINT TO PARAMETER LIST              20500002
         L     RPARM1,CVTAD        GET ADDRESS OF CVT                   20550002
         L     RPARM1,SVCDCB(RPARM1)   GET ADDR OF DCB FOR SYS1.SVCLIB  20600002
         LR    RODD,RRTN           SAVE RETURN ADDRESS                  20650002
         CLC   SIX(TWO,RWTG),RDWRTRTN  R/W MODULE                YM4057 20660002
         BE    LOADSVC             LOAD R/W ROUTINE FROM SVCLIB  YM4057 20670002
         CLC   SIX(TWO,RWTG),BPMR  BUFF.MNGMT RTN             LD YM5661 20680002
         BE    LOADSVC2            LOAD RTN FROM SVCLIB       LD YM5661 20690002
         LOAD  DE=(0),DCB=(1)                                           20700002
MOVEID   EQU   *                                                 YM4057 20710002
         MVC   0(TWO,RSUBID),SIX(RWTG)  MOVE ID TO DEB                  20750002
         LA    RSUBID,TWO(RSUBID)  INCR TO NEXT SUB-ID IN DEB           20800002
         SR    REVN,REVN                                                20850002
         IC    REVN,DEBNMSUB       GET NO. OF SUBROUTINES LOADED        20900002
         LA    REVN,ONE(REVN)      ADD ONE                              20950002
         STC   REVN,DEBNMSUB       STORE UPDATED COUNT IN DEB           21000002
PAGEFIXR EQU   *                   AOS PAGE FIX FOR APPENDS       3876  21050002
         LR    REVN,RPARM2         TEMP SAVE EP ADDRESS           3876  21100002
         SLL   RPARM1,3            SUBP OUT AND CONV TO BYTES     3876  21150002
         LA    RPARM1,PAGESIZE(RPARM1,REVN) BUMP BY PAGE SIZE     3876  21200002
         N     RPARM2,PAGEBDRY     PUT ON PAGE BOUNDRY            3876  21250002
         SLR   RPARM1,RPARM2       REMOVE EXTRA BYTES             3876  21300002
         SRL   RPARM1,PAGESHFT     GET NUMBER OF PAGES            3876  21350002
         SLL   RPARM1,24           GET SET FOR OR INTO ADDRESS    3876  21400002
         LR    RPARM2,REVN         RESTORE ADDRESS                3876  21450002
         OR    RPARM2,RPARM1       PUT FIX COUNT IN               3876  21500002
         BR    RODD                RETURN TO NSI                        21550002
LOADSVC  LOAD  EPLOC=IGG019MA,DCB=(1)                            YM4057 21560002
         B     MOVEID              MOVE R/W ID                   YM4057 21570002
LOADSVC2 EQU   *                                              LD YM5661 21580002
         LOAD  EPLOC=IGG019MS,DCB=(1)                         LD YM5661 21590002
         B     MOVEID              MOVE BUFF.MNGMT ID         LD YM5661 21592002
         DS    0F                                                Y02947 21600002
PAGEBDRY DC    X'00FFF000'         AOS2 PAGE BOUNDRY MASK         3876  21650002
IGG019MA DC    C'IGG019MA'         R/W MODULE ID                 YM4057 21660002
IGG019MS DC    C'IGG019MS'         BUFF.MNGMT MODULE ID       LD YM5661 21670002
PAGESHFT EQU   12                  AOS2 PAGE SHIFT COUNT          3876  21700002
PAGESIZE EQU   4095                AOS2 PAGE SIZE -1              3876  21750002
*                                                                       21800002
THREERCD OI    TWENTY5(RWTG),EIGHT ADD X'800' TO TTR LENGTH             21850002
LONGLOAD MVC   TWENTY7(TWO,RWTG),CONATB+3  SET LENGTH OF FIRST TEXT     21900002
*                                  RECORD TO 1024 (X'400') BYTES        21950002
         MVI   TWENTY2(RWTG),LOADATB1  MODIFY ATTRIBUTES FOR MORE       22000002
*                                      THAN ONE TEXT RECORD             22050002
         B     LOAD                                                     22100002
*                                                                       22150002
*        THIS SUBROUTINE IS USED TO BUILD AN IRB AND REQUIRED IQE'S     22200002
*        FOR OPEN                                                       22250002
* INPUT - REGISTER 0 - ADDRESS OF THE ROUTINE ENTRY POINT TO BE USED    22300002
*         REGISTER 1 - NUMBER OF IQE'S TO BE BUILT                      22350002
*                                                                       22400002
* OUTPUT - REGISTER 1 - ADDRESS OF THE INITIALIZED IRB                  22450002
*                                                                       22500002
* RETURN ADDRESS PASSED IN REGISTER 14                                  22550002
*                                                                       22600002
BLDPIRB  EQU   *                   BUILD PP IRB                @YA02128 22610002
         LR    REVN,RPARM1         SAVE NO. IQE'S              @YA02128 22620002
         SLL   REVN,1              MULTIPLY BY TWO             @YA02128 22630002
         LA    REVN,ONE(REVN)      ADD ONE FOR LNGTH OF WKAREA @YA02128 22640002
         LR    RW3,RRTN            SAVE RETURN PTR             @YA02128 22642002
         CIRB  EP=(RPARM2),KEY=PP,MODE=PP,STAB=(RE),WKAREA=(REVN),     X22644002
               SVAREA=YES                                      @YA02128 22648002
         B     IRBSETUP            GO SET IT UP                @YA02128 22648402
*                                                              @YA02128 22648802
BLDIRB   LR    REVN,RPARM1         SAVE NO. OF IQE'S                    22650002
         SLL   REVN,1              MULTIPLY BY TWO                      22700002
         LA    REVN,ONE(REVN)      ADD ONE TO GET LENGTH OF WORKAREA    22750002
         LR    RW3,RRTN                IN DOUBLE WORDS                  22800002
         CIRB  EP=(RPARM2),KEY=SUPR,MODE=SUPR,STAB=(RE),WKAREA=(REVN)   22850002
*                                                                       22900002
IRBSETUP EQU   *                                               @YA02128 22910002
         SRL   REVN,1              GET NUMBER OF IQE'S                  22950002
         LA    RODD,RBNEXAV(RPARM1)    GET ADDR OF IRB'S IQE POINTER    23000002
         LR    RRTN,RW3            RESTORE RETURN ADDRESS               23050002
         L     RPARM2,DEBTCBAD-1   GET TCB ADDRESS                      23100002
         LA    RW3,FOUR(RODD)                                           23150002
IQEINIT  EQU   *                                                        23200002
         MODESET EXTKEY=ZERO       KEY ZERO                   LD Y02947 23250002
         ST    RW3,IQELNK(RODD)    STORE ADDR OF NEXT IQE IN CURRENT    23300002
*                                      LINK                             23350002
         ST    RPARM1,IQEIRB(RW3)  STORE IRB ADDR IN IQE                23400002
         ST    RPARM2,IQETCB(RW3)  STORE TCB ADDR IN IQE                23450002
         MVI   IQETCB(RW3),ZERO    CLEAR HIGH ORDER BYTE                23500002
         LR    RODD,RW3            SAVE ADDR OF THIS IQE                23550002
         LA    RW3,IQELNGTH(RW3)   STEP TO NEXT                         23600002
         BCT   REVN,IQEINIT        LOOP IF MORE TO DO                   23650002
         ST    REVN,IQELNK(RODD)   ZERO LINK FIELD OF LAST IQE          23700002
*                                                                       23750002
         BR    RRTN                RETURN TO CALLING POINT              23800002
*                                                                       23850002
*                                                                       23900002
AMIDCNST DC    C'4Q'               ID OF THIS EXECUTOR                  23950002
OPIDCNST DC    C'0S'               ID OF LAST OPEN LOAD                 24000002
         DS    0F                  FORCE FULL WORD ALIGNMENT            24050002
ZEROS    DC    F'0'                WORD OF ZEROS                        24100002
ECB      DC    F'0'                    ECB FOR PAGE FIX                 24150002
IOBPOOL  DC    X'FA000000'         SUBPOOL NO. FOR IOB'S AND BUFFERS    24250002
ERR6     DC    X'80097000'         ABEND CODE FOR VECTOR TABLE OVERFLOW 24300002
CONATB   DC    X'C3780004000400'   XCTL ATTRIBUTES AND LENGTH           24350002
*                                                                       24400002
*        IDTTRL TABLE FOR THE BTAM ROUTINES USED FOR THE LOCAL 3270.    24450002
*    EACH ENTRY CONSISTS OF THE LAST TWO CHARACTERS OF THE MODULE NAME, 24500002
*    THE RELATIVE TRACK AND RECORD ADDRESS OF THE CORRESPONDING LOAD    24550002
*    MODULE ON SYS1.SVCLIB, AND THE LENGTH OF THE MODULE IN DOUBLE      24600002
*    WORDS. THE TTR AND LENGTH ARE INSERTED BY THE IEHIOSUP UTILITY     24650002
*    DURING STAGE 2 OF SYSGEN. IF ANY OF THESE MODULES ARE CHANGED,     24700002
*    REPLACED, OR MOVED, IEHIOSUP MUST BE EXECUTED AGAIN.               24750002
*                                                                       24800002
ONEPAGE  EQU   X'02'    NUMBER OF PAGES A ONE PAGE MODULE MAY SPAN      24850002
SIOFIX   EQU   X'80'    INDICATES EXISTENCE OF PAGE FIX APPENDAGE       24900002
NENT     EQU   8                   NUMBER OF IDTTRL'S          @YA02128 24910002
LENGTH   EQU   NENT*6+6                LENGTH OF IDTTRL'S               24920002
NODBWDS  EQU   (LENGTH+7)/8            DBL WORDS WITH PADDING           24930002
RDWRTRTN DC    C'MA',XL4'0'        READ/WRITE RTN                       24950002
BPMR     DC    C'MS',XL4'0'        BUFFER POOL MANAGEMENT RTN           25000002
CHENDAPP DC    C'PA',XL4'0'        LOCAL 3270 CEA/XEA RTN               25050002
ATNRTN2  DC    C'PG',XL4'0'        LOCAL 3270 SECOND LEVEL ATTN RTN     25100002
DDM      DC    C'PH',XL4'0'        LOCAL 3270 DEVICE DEPENDENT MODULE   25150002
OLTCTL   DC    C'PI',XL4'0'        LOCAL 3270 OLT CONTROL RTN           25200002
READYM   DC    C'UP',XL4'0'        LOCAL 3270 READY ROUTINE    @YA02128 25210002
SIOAPPN  DC    C'LP',XL4'0'            START I/O APPENDAGE              25250002
IDEND    DC    XL2'0'              END OF IDTTRL TABLE                  25350002
         CNOP  4,8                 PADDING                              25400002
LOADID   DC    C'019'              OPEN SVC CODE                        25450002
         DC    YL1(X'80'-NODBWDS)  POINTER FOR IEHIOSUP                 25500002
*                                                                       25550002
*        END OF IDTTRL TABLE                                            25600002
*                                                                       25650002
         DCBD  DSORG=BX,DEVD=BS                                         25700002
         IECTDEBX                                                       25750002
         IECDSECS MAIN,WTG,PREFX,CVT,EXPAND=YES                         25800002
         IHAPSA                                                         25850002
         END                                                            25900002
