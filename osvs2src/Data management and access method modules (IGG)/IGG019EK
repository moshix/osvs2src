         TITLE 'IGG019EK - RPS SIO, PGFX, CE, AND XE APPENDAGES' Y02080 00400002
IGG019EK CSECT                                                          00600020
*0000216000,258000,582000                                        M5885  00650020
*0000426000                                                      M4741  00700020
*0000336000                                                      M6287  00750020
*          VS2 RELEASE 02 DELETIONS/CHANGES                             00760002
*0000000600-004000,272000,278400,279200-281000,340200,341200,    Y02080 00760102
*0000634090,634450-634540,634990,635530-635620,635710-635800,    Y02080 00760202
*0000635890,644000-656000,692000-716000,762000-786000            Y02080 00760302
*0000278800-282000,286000,340400-341000,634020-635920            YM3048 00760502
*0000                                                           YA03681 00761002
*                                                                       00800020
*********************************************************************** 01000020
*                                                                     * 01200020
*                                                                     * 01400020
*                  - - - APPENDAGES - - -                             * 01600020
*                                                                     * 01800020
* MODULE NAME     - IGG019EK                                          * 01820002
*                                                                     * 01840002
* DESCRIPTIVE NAME- RPS SIO, CE, AND XE APPENDAGES                    * 01860002
*                                                                     * 01880002
* COPYRIGHT       - NONE                                              * 01900002
*                                                                     * 01920002
* CHANGE ACTIVITY - SEE DELETIONS/CHANGES FOLLOWING THE CSECT CARD.   * 01940002
*                                                                     * 02000020
* ATTRIBUTES      - REENTRANT, REFRESHABLE, DISABLED, SUPERVISORY     * 02200020
*                                                                     * 02400020
* ENTRY POINTS    - IGG019EK: RPS SIO APPENDAGE                       * 02600020
*                 - IGG019EK+8: RPS CHANNEL END APPENDAGE             * 02800020
*                 - IGG019EK+12: RPS ABNORMAL END APPENDAGE           * 03000020
*                                                                     * 03200020
* NORMAL EXITS    - RETURN TO IOS VIA A BR 14 INSTRUCTION             * 03400020
*                                                                     * 03600020
* ERROR EXITS     - NONE                                              * 03800020
*                                                                     * 04000020
* SVCS ISSUED     - NONE                                              * 04200020
*                                                                     * 04400020
* INPUT PARAMETERS- THE REGISTER SETTINGS WILL BE AS SPECIFIED IN     * 04600020
*                   THE SYSTEM PROGRAMMER'S GUIDE (FORM C28-6550)     * 04800020
*                   UNDER THE HEADING OF 'APPENDAGES'. THE ADDRESS    * 05000020
*                   OF THE USER'S CHANNEL PROGRAM WILL BE OBTAIN-     * 05200020
*                   ED BY REFERENCING THE IOBSTART FIELD OF THE       * 05400020
*                   IOB ADDRESSED BY REGISTER 2. THE RPS WORK         * 05600020
*                   AREA WILL BE LOCATED BY REFERENCING THE DEB-      * 05800020
*                   APPAD FIELD OF THE DEB ADDRESSED BY REGISTER      * 06000020
*                   3. THE UCB WILL BE ADDRESSED USING THE CON-       * 06200020
*                   TENTS OF REGISTER 7.                              * 06400020
*                                                                     * 06600020
* FUNCTIONS       -                                                   * 06800020
*                                                                     * 07000020
*        RPS SIO APPENDAGE:                                           * 07200020
*                                                                     * 07400020
*        1. IS ENTERED FROM IOS JUST PRIOR TO ISSUING THE SIO IN-     * 07600020
*        STRUCTION,                                                   * 07800020
*        2. EFFECTIVELY INSERTS SET SECTOR CCWS AT THE START OF,      * 08000020
*        AND AT APPROPRIATE POINTS WITHIN THE USER'S CHANNEL PRO-     * 08200020
*        GRAM,                                                        * 08400020
*        3. INTERFACES WITH A RESIDENT 'R-TO-THETA CONVERT ROU-       * 08600020
*        TINE' TO OBTAIN APPROPRIATE THETA VALUES,                    * 08800020
*        4. PLACES POINTERS TO THE RPS CHANNEL END AND ABNORMAL       * 09000020
*        END APPENDAGES IN THE AVT IN RPS'S WORK AREA TO SCHEDULE     * 09200020
*        THESE ROUTINES FOR EXECUTION AFTER I/O HAS BEEN PERFORMED.   * 09400020
*                                                                     * 09600020
*        RPS ABNORMAL END APPENDAGE:                                  * 09800020
*                                                                     * 10000020
*        1. IS ENTERED FROM IOS A) BEFORE ATTEMPTING ERROR CORREC-    * 10200020
*        TION WHEN AN I/O ERROR IS DETECTED OR B) AFTER A PERMA-      * 10400020
*        NENT I/O ERROR IS ENCOUNTERED,                               * 10600020
*        2. TESTS THE ECB POINTED TO BY THE IOB TO DETERMINE IF       * 10800020
*        THE ENTRY WAS DUE TO CASE A OR B,                            * 11000020
*        3. IF CASE A (CC=X'7F') RETURNS IMMEDIATELY TO IOS,          * 11200020
*        4. IF CASE B (CC=X'4X') FALLS THROUGH TO THE RPS CHANNEL     * 11400020
*        END APPENDAGE.                                               * 11600020
*                                                                     * 11800020
*        RPS CHANNEL END APPENDAGE:                                   * 12000020
*                                                                     * 12200020
*        1. IS ENTERED A) FROM IOS UPON NORMAL COMPLETION OF AN I/O   * 12400020
*        OPERATION OR B) FROM THE RPS ABNORMAL END APPENDAGE,         * 12600020
*        2. RESTORES THE USERS CHANNEL PROGRAM TO ITS STATE PRIOR     * 12800020
*        TO RPS START I/O APPENDAGE EXECUTION,                        * 13000020
*        3. PLACES POINTERS TO A BR 14 INSTRUCTION IN THE AVT IN      * 13200020
*        RPS'S WORK AREA SO THAT THE RPS CHANNEL END AND ABNORMAL     * 13400020
*        END APPENDAGES WILL NOT BE REEXECUTED UNTIL AFTER THE        * 13600020
*        RPS SIO APPENDAGE HAS PERFORMED ITS FUNCTION.                * 13800020
*                                                                     * 14000020
*********************************************************************** 14200020
*                                                                       14400020
*********************************************************************** 14600020
*    REGISTER AND OTHER EQUATES                                       * 14800020
**********************************************************************  15000020
*                                                                       15200020
FGMR1    EQU   0                        USED IN FREE-& GETMAINS         15400020
REG02    EQU   2                        NOT USED                        15600020
REG09    EQU   9                        NOT USED                        15800020
REG10    EQU   10                       NOT USED                        16000020
REG11    EQU   11                       NOT USED                        16200020
REG12    EQU   12                       NOT USED                        16400020
RETRG    EQU   14                       RETURN REGISTER                 16600020
BASER    EQU   15                       BASE REG. FOR THIS MODULE       16800020
C0       EQU   0                        CONSTANT NUMBER                 17000020
C4       EQU   4                        CONSTANT NUMBER                 17200020
C8       EQU   8                        CONSTANT NUMBER                 17400020
REG13    EQU   13                       CE APPG. WORK REG.              17600020
OAVT     EQU   120                      DISP. TO PTR TO OLD AVT IN W.A. 17800020
RSADS    EQU   96                       DISP. TO REG SAVE AREA          18000020
REG06    EQU   6                        WORK REG FOR SIO                18200020
PARM0    EQU   0                        PARAMETER REGISTER              18400020
PARM2    EQU   2                        PARAMETER REG. FOR CONVERT      18600020
IOBAD    EQU   2                        POINTER TO IOB                  18800020
DEBAD    EQU   3                        POINTER TO DEB                  19000020
HITIC    EQU   5                        ADDR OF HIGHEST TIC             19200020
USRCCW   EQU   7                        USER CHAN.PGM.PTR               19400020
UCBAD    EQU   7                        POINTER TO UCB                  19600020
CVTPTR   EQU   16                       ADDR OF POINTER TO CVT          19800020
RPCCW    EQU   13                       RPS CHAN.PGM. PTR               20000020
REG14    EQU   14                       WORK REG.                       20200020
REG15    EQU   15                       WORK REG.                       20400020
ECBDIS   EQU   4                        DISP. OF ECB IN IOB             20600020
FW       EQU   4                        BYTES IN 1 FULL WORD            20800020
RDS      EQU   4                        DISP TO R IN CCHHR              21000020
LGTH     EQU   22                       DISP TO BYTE COUNT              21200020
DW       EQU   8                        BYTES IN ONE DOUBLE WD.         21400020
DCCHN    EQU   X'C0'                    DATA AND COMMAND CHAIN   M5885  21500020
*                                       BITS                     M5885  21600020
TFW      EQU   12                       BYTES IN 3 FULL WORDS           21800020
SAVWD1   EQU   12                       2ND WORD OF 1ST TIC             22000020
TDW      EQU   16                       BYTES IN 2 DOUBLE WORDS         22200020
IOBST    EQU   16                       DISP. OF IOBSTART FIELD         22400020
D17      EQU   17                       DISPLACEMENT OF 17              22600020
D18      EQU   18                       DISPLACEMENT OF 18              22800020
D19      EQU   19                       DISPLACEMENT OF 19              23000020
SAVWD2   EQU   20                       2ND WORD OF 2ND TIC             23200020
TH1DS    EQU   20                       DISP TO 1ST THETA               23400020
DAMSK    EQU   X'20'                    MASK FOR D.A. BIT IN UCB        23600020
SSCODE   EQU   X'23'                    SET SECTOR COMMAND CODE         23800020
CHPGMDS  EQU   24                       DISP TO CH. PGM. IN RPSWA       24000020
SHFT3    EQU   24                       BITS FOR THREE-BYTE SHIFT       24200020
SIDECD   EQU   X'31'                    SIDE COMMAND CODE               24400020
COMP     EQU   X'7F'                    ECB COMPLETION CODE             24600020
SCRDS    EQU   232                      DISP TO CONVERT ADDR            24800020
BIT256   EQU   X'01'                    BIT WITH VALUE OF 256           25000020
ADRMSK   EQU   X'03'                    MASK FOR LOW TWO BITS           25200020
TICCW    EQU   X'08'                    COMMAND CODE OF TIC             25400020
RPSMSK   EQU   X'10'                    MASK FOR RPS BIT IN UCB         25600020
SECSA    EQU   RSADS+12                 S. A. FOR SECOND 3 REG.S        26000020
*                                                                       26200020
*********************************************************************** 26400020
* THIS SECTION TESTS ENTRY CONDITIONS AND INITIALIZES REGISTERS       * 26600020
*********************************************************************** 26800020
*                                                                       27000020
         USING DEB,DEBAD                                                27400020
         USING IGG019EK,BASER                                           27600020
         B     SIO                      BRANCH AROUND I.D.              27800020
         USING IGG019EK+C4,BASER        CE ADDRESSABILITY        YM3048 28200002
         B     CE                       BRANCH TO CHAN END APPG         28400020
         USING IGG019EK+C8,BASER        XE ADDRESSABILITY        YM3048 28600002
XE       EQU   *                                                        28800002
*                                                                       29000020
*********************************************************************** 29200020
*        ABNORMAL END APPENDAGE                                       * 29400020
*********************************************************************** 29600020
*                                                                       29800020
         TM    ECBDIS(IOBAD),COMP       TEST COMPLETION CODE            30000020
         BO    C0(RETRG)                7F, RETURN TO IOS               30200020
*                                                                       30400020
*********************************************************************** 30600020
*        CHANNEL END APPENDAGE                                        * 30800020
*********************************************************************** 31000020
*                                                                       31200020
CE       EQU   *                                                        31400020
         BALR  BASER,FGMR1              RE ESTABLISH BASE REG           31600020
         USING *,BASER                  USING FOR HERE                  31800020
         L     REG10,IOBST(IOBAD)       GET START OF USER CCW'S         32000020
         LA    REG10,C0(REG10)          CLEAR HIGH ORDER BYTE           32200020
NOADD    EQU   *                                                        32400020
         CLI   C0(REG10),TICCW          IS CCW A 'TIC'                  32600020
         BE    IFTIC                    YES,INCREMENT HITIC             32800020
         CLI   C0(REG10),SSCODE         IS CCW A 'SET SECTOR'           33000020
         BE    IFSS                     YES, MUST BE REMOVED            33200020
         TM    C4(REG10),DCCHN          DATA/COMMAND CHN. BITS ON       33400020
         BZ    ENDCE                    BRANCH NO                M6287  33600020
         AH    REG10,ONEDW              STEP TO NEXT CCW                33800020
         B     NOADD                    SKIP INCREMENT                  34000020
IFTIC    EQU   *                                                        34200020
         L     REG12,C0(REG10)          GET ADDRESS IN TIC              34400020
         LA    REG12,C0(REG12)          CLEAR COMMAND CODE              34600020
         CR    REG12,REG10              IS IT A TIC *-8                 34800020
         BH    FOLLOW                   NO, GO FOLLOW THIS TIC          35000020
         AH    REG10,ONEDW              STEP TO NEXT CCW                35200020
         B     NOADD                    GO ON TO NEXT CCW TEST          35400020
FOLLOW   EQU   *                                                        35600020
         L     REG10,C0(REG10)          LOOK AT NEXT CCW                35800020
         B     NOADD                    GO ON TO NEXT CCW TEST          36000020
IFSS     EQU   *                                                        36200020
         L     REG11,C8(REG10)          TIC POINTS TO ORIG. CCW         36400020
         MVC   C0(C8,REG10),C0(REG11)      ORIGINAL CCW RESTORED        36600020
         ST    REG10,C8(REG10)          YES, POINT TIC TO TIC-8         36800020
         MVI   C8(REG10),TICCW          INSERT COMMAND CODE             37000020
         AH    REG10,TWODW              INCREMENT THRU USER CCWS        37200020
         AH    REG11,DCTDWDW            INCREMENT THRU RPS  CCWS        37400020
         B     NOADD                    GO ON TO NEXT CCW               37600020
ENDCE    EQU   *                                                        37800020
         L     REG10,DEBAPPAD           GET PTR TO RPSWA                38000020
         L     REG13,OAVT(REG10)        GET ADDR OF OLD AVT             38200020
         MVC   TFW(C4,REG10),TFW(REG13) RESTORE OLD CE PTR              38400020
         MVC   TDW(C4,REG10),TDW(REG13) RESTORE OLD XE PTR              38600020
         BR    RETRG                    RETURN TO IOS                   38800020
*                                                                       39000020
*********************************************************************** 39200020
*        START I/O APPENDAGE                                          * 39400020
*********************************************************************** 39600020
*                                                                       39800020
SIO      EQU   *                                                        40000020
         USING IGG019EK,BASER                                           40200020
         TM    D18(UCBAD),DAMSK                                         40400020
         BZ    C0(RETRG)                NO, RETURN TO IOS               40600020
         TM    D17(UCBAD),RPSMSK                                        40800020
         BZ    C0(RETRG)                NO, RETURN TO IOS               41000020
         L     RPCCW,DEBAPPAD           POINTER TO RPS WORK AREA        41200020
         STM   PARM0,REG02,RSADS(RPCCW) SAVE REGISTERS                  41400020
         STM   HITIC,UCBAD,SECSA(RPCCW) SAVE REGISTERS                  41600020
         IC    REG06,D19(UCBAD)                                         41800020
         SLL   REG06,SHFT3              PUT IN HIGH ORDER BYTE          42000020
         L     USRCCW,IOBST(IOBAD)      GET START OF USER'S CCWS        42200020
         LA    USRCCW,0(USRCCW)         CLEAR THE HIGH BYTE     YA03681 42250002
         CLI   C0(USRCCW),SSCODE        1ST CCW A SET SECTOR            42400020
         BE    RPSEND                   YES,RETURN TO IOS        M4741  42600020
         LA    PARM2,TH1DS(RPCCW)       POINTER TO FIRST THETA          42800020
         OR    PARM2,REG06              PUT DEV. CODE IN PARM.REG.      43000020
         LA    RPCCW,CHPGMDS(RPCCW)     POINTER TO RPS CCW CHAIN        43200020
         O     RPCCW,TICCW2             PUT X'08' INTO HIGH BYTE        43400020
         XR    HITIC,HITIC              CLEAR HIGH TIC ADDR REG.        43600020
*                                                                       43800020
*********************************************************************** 44000020
* MODIFY THE CCW CHAINS                                               * 44200020
*********************************************************************** 44400020
*                                                                       44600020
         CLI   C0(USRCCW),SIDECD        1ST CCW A SEARCH ON I.D.        44800020
         BNE   INCREMNT                 NO, SKIP THETA CONVERT          45000020
MODIFY   EQU   *                                                        45200020
         MVC   C0(TDW,RPCCW),C0(USRCCW) MOVE 2 CCWS TO RPS CHAIN        45400020
         L     REG06,C0(USRCCW)         GET SIDE'S DATA ADDRESS         45600020
SCRPREP  EQU   *                                                        45800020
*                                                                       46000020
* THE FOLLOWING INSTRUCTION TESTS THE BYTE COUNT OF THE CCW FOLLOWING   46200020
* THE SIDE AND TIC IN ORDER TO DETERMINE THE KEY AND DATA LENGTH TO     46400020
* BE PASSED TO THE SECTOR CONVERT ROUTINE. CATALOG'S VOLUME CONTROL     46600020
* BLOCKS AND INDICES HAVE DATA LENGTHS OF 256, ALL OTHER I/O OPERATIONS 46800020
* IN CATALOG AND DADSM ARE PERFORMED ON DSCB'S HAVING A MAXIMUM I/O     47000020
* BYTE COUNT OF 148 (96 BYTES DATA, 44 BYTES KEY, AND 8 BYTES COUNT)    47200020
*                                                                       47400020
         TM    LGTH(USRCCW),BIT256      BYTE COUNT = 256 OR MORE        47600020
         BO    LONGBLK                  YES, PASS INDEX/VCB DD&K        47800020
         L     PARM0,DSCBCT             NO, PASS DSCB'S DD & K          48000020
         B     GETR                     SKIP LONGBLK                    48200020
LONGBLK  EQU   *                                                        48400020
         L     PARM0,IVCBCT             INDEX & VOL.CONT.BLK DD&K       48600020
GETR     EQU   *                                                        48800020
         IC    PARM0,RDS(REG06)         PUT R INTO PARAMETER REG.       49000020
         ST    RETRG,SAVWD1(RPCCW)      SAVE RETURN AND BASE AD-        49200020
         ST    BASER,SAVWD2(RPCCW)      *DRESSES IN TICS                49400020
         L     REG15,CVTPTR             CVT ADDRESS                     49600020
         L     REG15,SCRDS(REG15)       SECTOR CONVERT RTN ADDR         49800020
         BALR  REG14,REG15              CONVERT R TO THETA              50000020
         L     RETRG,SAVWD1(RPCCW)      RESTORE                         50200020
         L     BASER,SAVWD2(RPCCW)      REGISTERS                       50400020
         ST    PARM2,C0(USRCCW)         POINT SET SECTOR TO THETA       50600020
         MVI   C0(USRCCW),SSCODE        SET SECTOR COMMAND CODE         50800020
         MVC   FW(FW,USRCCW),SSFLGL     SET FLGS & LENGTH FOR SS        51000020
         ST    RPCCW,DW(USRCCW)         BUILD TIC TO RPS CHAIN          51200020
         CLI   DW(RPCCW),TICCW          IS NEXT RPCCW A TIC             51400020
         BNE   NEXTTIC                  NO, SKIP WORK ON THIS TIC       51600020
         O     USRCCW,TICCW2            PUT X'08' IN HI BYTE            51800020
         ST    RPCCW,DW(RPCCW)          POINT RPCCW+8 TO RPCCW          52000020
NEXTTIC  EQU   *                                                        52200020
         AH    USRCCW,TWODW             POINT TO CCW AFTER TIC          52400020
         ST    USRCCW,TDW(RPCCW)        BUILD TIC TO USER'S CHPGM       52600020
         CLI   C0(USRCCW),TICCW         IS NEXT CCW A TIC               52800020
         BNE   INCREMNT                 NO, SKIP TIC ADJUSTMENT         53000020
         MVC   TDW(C4,RPCCW),C0(USRCCW) POINT RPS TIC TO NEW ADDR       53200020
INCREMNT EQU   *                                                        53400020
         STC   PARM2,TFW(RPCCW)         GET LOW BYTE OF THETA ADR       53600020
         TM    TFW(RPCCW),ADRMSK        HAVE 3 THETA'S BEEN USED        53800020
         BO    FINISH                   YES, FINISH OFF & RETURN        54000020
         AH    RPCCW,DCTDWDW            STEP RPS CCW POINTER            54200020
         AH    PARM2,ONE                STEP THETA'S POINTER            54400020
         TM    FW(USRCCW),DCCHN         MORE CCWS                M6287  54460020
         BZ    FINISH                   BRANCH NO                M6287  54520020
USRSTEP  EQU   *                                                        54600020
         AH    USRCCW,ONEDW             STEP USER CCW POINTER           54800020
*                                                                       55000020
*********************************************************************** 55200020
* MAKE TESTS TO DECIDE NEXT ACTION: MODIFY CHANNEL PROGRAM AT THIS    * 55400020
* POINT, LOOK AT NEXT CCW, OR RETURN ?                                * 55600020
*********************************************************************** 55800020
*                                                                       56000020
         CLI   C0(USRCCW),TICCW         IS CCW A TIC                    56200020
         BNE   NOTTIC                   NO, SKIP TIC WORK               56400020
         L     REG06,C0(USRCCW)         GET TIC DATA ADDRESS            56600020
         CR    HITIC,REG06              ADDR GREATER THAN HITIC         56800020
         BH    USRSTEP                  NO, LOOK AT NEXT CCW            57000020
         LR    HITIC,REG06              UPDATE HITIC                    57200020
         B     USRSTEP                  NOW LOOK AT NEXT CCW            57400020
NOTTIC   EQU   *                                                        57600020
         CLI   C0(USRCCW),SIDECD        IS CCW A SEARCH ON I.D. ?       57800020
         BE    MODIFY                   YES, MODIFY CHANNEL PGM.        58000020
         TM    FW(USRCCW),DCCHN         CMD. OR DATA CHAIN ON    M5885  58200020
         BNZ   USRSTEP                  YES, LOOK AT NEXT CCW           58400020
FINISH   EQU   *                                                        58600020
         L     REG10,DEBAPPAD           GET ADDR OF RPSWA               58800020
         LM    PARM0,REG02,RSADS(REG10) LOAD REGISTERS                  59000020
         LM    HITIC,UCBAD,SECSA(REG10) LOAD REGISTERS                  59200020
         XR    REG09,REG09              ZERO REG .                      59400020
         L     REG11,FW(REG10)          GET ADDR OF THIS APPENDAGE      59600020
         LA    REG11,FW(REG11)          GET ADDR OF CE APPENDAGE YM3048 59800002
         ST    REG11,TFW(REG10)         PUT INTO NEW AVT                60000020
         LA    REG11,FW(REG11)          GET ADDR OF XE APPENDAGE        60200020
         ST    REG11,TDW(REG10)         PUT INTO NEW AVT                60400020
         BR    RETRG                    RETURN TO IOS            M4741  60460020
RPSEND   LM    HITIC,UCBAD,SECSA(RPCCW) LOAD REGISTERS           M4741  60520020
         BR    RETRG                    RETURN TO IOS                   60600020
*                                                                       60800020
*********************************************************************** 61000020
*        CONSTANTS                                                    * 61200020
*********************************************************************** 61400020
*                                                                       61600020
         DS    0F                                                       61800020
TICCW2   DC    X'08000000'              COMMAND CODE OF TIC             62000020
SSFLGL   DC    X'60000001'              FLAG & LENGTH FOR SET SECT.     62200020
DSCBCT   DC    X'00602C00'              DSCB DD & K                     62400020
IVCBCT   DC    X'01000800'              INDEX & VCB DD & K              62600020
ONE      DC    H'1'                     ONE                             62800020
ONEDW    DC    H'8'                     EIGHT                           63000020
TWODW    DC    H'16'                    SIXTEEN                         63200020
DCTDWDW  DC    H'24'                    TWENTY-FOUR                     63400020
*                                                                       63420002
*********************************************************************** 63440002
*        END OF APPENDAGES DELIMITER                                  * 63460002
*********************************************************************** 63480002
*                                                                       63500002
         XCTLTABL BRT=YES,PATCH=0,LENGTH=                        Y02080 63520002
         EJECT                                                   Y02080 63540002
*                                                                       63600020
*        WORK AREA'S DEFINITIONS                                      * 63800020
*********************************************************************** 64000020
*                                                                       64200020
*** DEB AREA DEFINITION                                                 64400002
         DSECT                                                   Y02080 64600002
DEB      DS    0F                       START OF DEB                    65800020
DEBTCBAD DS    F                        TCB ADDRESS                     66000020
DEBAMIND DS    0X                       AM INDICATOR                    66200020
DEBDEBAD DS    F                        NEXT DEB ADDRESS                66400020
         DS    2F                       IRB AND SYS PURGE CHAIN         66600020
DEBNMEXT DS    0X                       NO. OF EXTENTS                  66800020
         DS    2F                       USER PURGE & PURGE ECB ADDR     67000020
DEBDCBAD DS    F                        DCB ADDRESS                     67200020
DEBAPPAD DS    F                        APPENDAGE ADDRESS               67400020
DEBDVMOD DS    0X                       DEVICE MODIFIER MASK            67600020
DEBUCBAD DS    F                        UCB ADDRESS                     67800020
DEBBINUM DS    H                        BIN NUMBER                      68000020
DEBSTRCC DS    H                        START CYLINDER                  68200020
DEBSTRHH DS    H                        START HEAD                      68400020
DEBENDCC DS    H                        END CYLINDER                    68600020
DEBENDHH DS    H                        END HEAD                        68800020
DEBNMTRK DS    H                        NUMBER OF TRACKS                69000020
*                                                                       71800020
*** RPS WORK AREA DEFINITION                                            71900002
RPSWA    DSECT                                                          72000020
RPSWAST  DS    0D                       START OF WORK AREA              72200020
RPEOEPTR DS    F                        EOE APG. POINTER                72400020
RPSIOPTR DS    F                        SIO APG. POINTER                72600020
RPPCIPTR DS    F                        PCI APG. POINTER                72800020
RPCEPTR  DS    F                        CE APG. POINTER                 73000020
RPXEPTR  DS    F                        XE  APG. POINTER                73200020
THETA1   DS    CL1                      THETA VALUE 1                   73400020
THETA2   DS    CL1                      THETA VALUE 2                   73600020
THETA3   DS    CL1                      THETA VALUE 3                   73800020
RPSWASIZ DS    CL1                      SIZE/8 OF RPS WORK AREA         74000020
RPCCW1   DS    D                        1ST USER CCW                    74200020
RPCCW2   DS    D                        2ND USER CCW                    74400020
RPCCW3   DS    D                        TIC BACK TO USER'S C.P.         74600020
RPCCW4   DS    D                        1ST EMBEDDED USER SIDE          74800020
RPCCW5   DS    D                        USER CCW FOLLOWING SIDE         75000020
RPCCW6   DS    D                        TIC BACK TO USER'S C.P.         75200020
RPCCW7   DS    D                        2ND EMBEDDED USER SIDE          75400020
RPCCW8   DS    D                        USER CCW FOLLOWING SIDE         75600020
RPCCW9   DS    D                        TIC BACK TO USER'S C.P.         75800020
REGSVAR  DS    4D                       SAVE AREA FOR SIO REGS          76000020
RPSWAEND EQU   *                        END OF RPS WORK AREA     Y02080 76200002
*                                                                       76400002
         END                                                            78800020
