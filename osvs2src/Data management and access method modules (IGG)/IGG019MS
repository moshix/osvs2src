 TITLE 'IGG019MS - BTAM REQUEST/RELEASE BUFFER ROUTINE'                 00009000
IGG019MS CSECT                                                          00018000
         SPACE 2                                                        00027000
*    STATUS         CHANGE LEVEL 001,   JUNE 2, 1967                    00036000
         SPACE 2                                                        00045000
* FUNCTION -                                                            00054000
*                   TO PROVIDE OR RELEASE A BUFFER FROM OR TO           00063000
*                   THE BUFFER POOL ASSOCIATED WITH A DESIGNATED DCB.   00072000
         SPACE 2                                                        00081000
* ENTRY POINT -                                                         00090000
*                   ENTRY IS TO THE 1ST EXECUTABLE INSTRUCTION          00099000
         SPACE 2                                                        00108000
* INPUT -                                                               00117000
*                   REQUEST                                             00126000
*                        REGISTER O CONTAINS THE REQUEST COUNT IN THE   00135000
*                        LEFTMOST BYTE WITH (0) IN THE RIGHT 3  BYTES   00144000
*                                                                       00153000
*                   RELEASE                                             00162000
*                        REGISTER O CONTAINS THE ADDRESS OF THE FIRST   00171000
*                        BUFFER BEING RELEASED BY THE CALLING PROGRAM.  00180000
*                                                                       00189000
*                   REGISTER 1 CONTAINS A POINTER TO THE CURRENT DCB    00198000
         SPACE 2                                                        00207000
* OUTPUT -                                                              00216000
*                   REQUEST                                             00225000
*                        REGISTER 15 WILL CONTAIN THE ADDRESS OF THE    00234000
*                        FIRST REQUESTED BUFFER IN THE THREE RIGHTHAND  00243000
*                        BYTES.                                         00252000
*                                                                       00261000
*                        THE LEFT MOST BYTE WILL CONTAIN -              00270000
*                        0 - IF ALL REQUESTED BUFFERS ARE AVAILABLE.    00279000
*                        4 - IF THE NUMBER OF AVAILABLE BUFFERS         00288000
*                            IS LESS THAN THE REQUESTED NUMBER. THOSE   00297000
*                            AVAILABLE WILL HAVE BEEN PROVIDED.         00306000
*                        8 - IF NO BUFFERS ARE AVAILABLE.               00315000
*                       12 - IF THE BUFFER CONTROL BLOCK ADDRESS IS     00324000
*                            INVALID (THE ADDRESS IS ZERO OR HAS        00333000
*                            IMPROPER BOUNDARY ALIGNMENT).              00342000
*                                                                       00351000
*                                                                       00360000
*                   RELEASE                                             00369000
*                        REGISTER 15 WILL CONTAIN ZEROS IN THE RIGHT    00378000
*                        HAND THREE BYTES.                              00387000
*                                                                       00396000
*                        THE LEFT MOST BYTE WILL CONTAIN -              00405000
*                        0 - IF ALL BUFFERS HAVE BEEN RETURNED.         00414000
*                        8 - IF NO BUFFERS WERE RETURNED BECAUSE THE    00423000
*                            THE FIRST BUFFER IN THE CHAIN HAD BEEN     00432000
*                            RETURNED PREVIOUSLY (IS THE FIRST BUFFER   00441000
*                            ON THE QUEUE OF AVAILABLE BUFFERS).        00450000
*                       12 - IF THE BUFFER CONTROL BLOCK ADDRESS IS     00500000
*                            INVALID (THE ADDRESS IS ZERO OR HAS        00509000
*                            IMPROPER BOUNDARY ALIGNMENT).              00518000
*                       20 - IF THE COUNT OF BUFFERS BEING              00527000
*                            RELEASED IS GREATER THAN THE TOTAL         00536000
*                            BUFFERS OBTAINED OR THE MAX (256)          00545000
*                                                                       00554000
         SPACE 2                                                        00563000
* EXTERNAL -                                                            00572000
*                   NONE                                                00581000
         SPACE 2                                                        00590000
* EXIT -                                                                00599000
*                   RETURN IS TO THE CALLING ROUTINE, THE RETURN        00608000
*                   ADDRESS BEING IN REGISTER 14.                       00617000
         SPACE 2                                                        00626000
* IN-LINE ROUTINES -                                                    00635000
*                   ENTRY IS FOR A REQUEST OR RELEASE OF A BUFFER.      00644000
*                   WHICH ONE IS DETERMINED BY LOOKING AT THE           00653000
*                   INPUT IN REGISTER O, (SEE INPUT ABOVE)              00662000
*                                                                       00671000
*                   RELEASE                                             00680000
*                        BUFFERS ARE RETURNED TO THE BUFFER POOL.       00689000
*                                                                       00698000
*                   REQUEST                                             00707000
*                        BUFFERS ARE ASSIGNED TO THE REQUEST IF THEY    00716000
*                        ARE AVAILABLE.  A COUNT OF THE NUMBER OF       00725000
*                        AVAILABLE BUFFERS IS MAINTAINED IN BUFFER      00734000
*                        CONTROL BLOCK...                               00743000
         SPACE 2                                                        00752000
*********************************************************************** 00761000
*          AZ11413  (05/26/76) IGG019MS MODULE WAS REWRITTEN TO CORRECT 00770000
*                   THE MVS NON-DISABLED PROBLEM OF UPDATING THE BUFFER 00779000
*                   CONTROL BLOCK (ADDRESS OF 1ST AVAIL BUFFER AND THE  00788000
*                   NUMBER OF AVAIL BUFFERS)..........................  00797000
*********************************************************************** 00806000
         EJECT                                                          00815000
*********************************************************************** 00824000
*                                                                       00833000
*         REGISTER AND MISCELLANEOUS EQUATIONS                          00842000
*                                                                       00851000
*********************************************************************** 00860000
         SPACE 2                                                        00869000
ZEROREG  EQU   0                        CNT OR PTR TO BFR PASSED AS PAR 00878000
DCBREG   EQU   1                        PTR TO DATA CONTROL BLOCK       00887000
TCBREG   EQU   2                        PTR TO TCB                      00896000
CNTREG   EQU   4                        WORK REGISTER                   00905000
BCBREG   EQU   5                        PTR TO BUFFER CONTROL BLOCK     00914000
BUFREG   EQU   7                        PTR BUFFER                      00923000
BUF2REG  EQU   8                        PTR TO NEXT BUFFER              00932000
WORKREG  EQU   11                       WORK REGISTER                   00941000
CVTREG   EQU   11                       PTR TO CVT                      00950000
BASEREG  EQU   12                       BASE REGISTER                   01000000
BRREG    EQU   15                       RETRUN DATA TO REQUESTOR        01010000
         SPACE 5                                                        01020000
DEBNMEXT EQU   16                       OFFSET TO NUMBER OF IOB         01030000
DEBUCBAD EQU   32                       OFFSET TO PTR TO UCB ADRS       01040000
DCBBUFCB EQU   20                       OFFSET TO BCB ADRS              01050000
DCBBUFNO EQU   20                       OFFSET TO NO. OF BUFFERS IN BCB 01059000
DCBBFTEK EQU   32                       OFFSET TO BUFFER TECHNIQUE CODE 01068000
DCBDEBAD EQU   44                       OFFSET TO DEB ADDRESS           01077000
DEBTCBAD EQU   0           OFFSET FROM DEB BASIC OF TCB ADDR.           01086000
ZERO     EQU   0                                                        01095000
BCB      EQU   X'03'                                                    01104000
         EJECT                                                          01113000
         USING *,BASEREG                                                01122000
         USING CVTMAP,CVTREG                                            01131000
         USING TCB,TCBREG                                               01140000
         SAVE  (14,12),,*               SAVE REGISTERS                  01149000
         LR    BASEREG,BRREG            LOAD BASE REGISTER              01158000
         SPACE 3                                                        01167000
*        IGG019MS WILL RUN UNDER THE USER'S KEY                         01176000
         SPACE 3                                                        01185000
*********************************************************************** 01194000
*                                                                     * 01203000
* PERFORM BASIC CHECKS ON BUFFER RELEASE/REQUEST. VERIFY THAT BASIC   * 01212000
* REQUEST CAN BE SERVICED UNDER PRESENT BCB CONDITIONS....            * 01221000
*                                                                     * 01230000
*********************************************************************** 01239000
         L     CVTREG,DCBDEBAD(DCBREG)  GET DEB ADDR                    01248000
         L     CVTREG,DEBTCBAD(CVTREG)  GET TCB ADDR                    01257000
         LA    TCBREG,ZERO(CVTREG)      CLEAR HI BYTE                   01266000
         DROP  CVTREG                                                   01275000
         MODESET EXTKEY=TCB,WORKREG=11   GET USER'S KEY                 01284000
         SPACE 3                                                        01293000
         SR    BRREG,BRREG              CLEAR RETURN REG                01302000
         TM    DCBBUFCB+3(DCBREG),BCB   TEST FOR VALID BCB ADDRESS      01311000
         BNZ   NOBCB                    IF NOT VALID BRANCH             01320000
         L     BCBREG,DCBBUFCB(DCBREG)   LOAD PTR TO BUFFER CTRL BLOCK  01329000
         LA    BCBREG,0(BCBREG)         CLEAR HIGH ORDER BYTE.          01338000
         LTR   BCBREG,BCBREG            TEST FOR BUFFER CONTROL BLOCK   01347000
         BZ    NOBCB               ADDRESS OF ZERO.  IF SO, EXIT.       01356000
         LR    BUFREG,ZEROREG           LOAD PTR TO BFR                 01365000
         LA    BUFREG,0(BUFREG)         ZERO OUT LEFT BYTE              01374000
         LTR   BUFREG,BUFREG            TEST BUFFER ADDRESS             01383000
         BZ    BUFREQ                   IF REQUEST                      01392000
         EJECT                                                          01401000
*********************************************************************** 01410000
*                                                                     * 01419000
* REQUEST FOR RELEASING BUFFERS BACK TO POOL. VERIFY BASIC RELEASE    * 01428000
* LIMITATIONS AND PROCEED ....                                        * 01437000
*                                                                     * 01446000
*********************************************************************** 01455000
         SR    ZEROREG,ZEROREG          GET MAX BUFR CNT FROM DCB       01500000
         ICM   ZEROREG,1,DCBBUFNO(DCBREG) ***                           01510000
         LA    CNTREG,1                 DEFAULT TO REL ONE BUFFER       01520000
         LR    BUF2REG,BUFREG           ***                             01530000
         L     WORKREG,0(BUFREG)        NXT BUF ADDR OR END             01540000
CKNXTBUF LA    WORKREG,0(WORKREG)       CLR HI ORDER BYTE               01550000
         ST    WORKREG,0(BUF2REG)       CLR HI ORDER BYTE OF BUFFER     01558000
         LTR   WORKREG,WORKREG          CHK FOR LAST BUFFER             01566000
         BZ    CHNDONE                  REL CHAIN COMPLETE              01574000
         LA    CNTREG,1(CNTREG)         INCR REL BUFR CNTR              01582000
         CR    CNTREG,ZEROREG           CK FOR BUFR LIMIT               01590000
         BNH   INLIMIT                  BUFR CNT WITHIN LIMITS          01600000
         ICM   BRREG,8,INVBUF           INVALID REL CNT ERROR           01610000
         B     EXIT                     RETURN TO REQUESTOR             01620000
INLIMIT  LR    BUF2REG,WORKREG          LOAD NXT BUFR ADDR              01630000
         L     WORKREG,0(BUF2REG)       CK FOR END OF CHAIN             01640000
         B     CKNXTBUF                 CHK INPUT CHAIN                 01650000
CHNDONE  L     WORKREG,0(BCBREG)        GET NXT ADDR FROM BCB           01660000
TRYAGAIN LA    WORKREG,0(WORKREG)       CLR HI ORDER BYTE               01670000
         ST    WORKREG,0(BUF2REG)       SETUP NEW CHAING PTR            01680000
*********************************************************************** 01690000
*                                                                     * 01700000
* THE COMPARE&SWAP WILL UPDATE THE BCB NEXT BUFFER ADDR AREA OR WILL  * 01710000
* CAUSE THE LOGIC TO START THE BCB CHAIN UPDATE OVER...               * 01720000
*                                                                     * 01730000
*********************************************************************** 01740000
         CS    WORKREG,BUFREG,0(BCBREG) UPDATE BCB CHAIN                01750000
         BNE   TRYAGAIN                 BCB ALTERED, RSTRT UPDATE       01760000
*********************************************************************** 01770000
*                                                                     * 01780000
* THE BCB AVAILABLE BUFFER COUNT MUST NOW BE UPDATED TO REFLECT THE   * 01790000
* ADDITIONAL BUFFERS ADDED TO THE CHAIN...                            * 01800000
*                                                                     * 01810000
*********************************************************************** 01820000
         L     WORKREG,4(BCBREG)        GET CURRENT BUFR CNT            01830000
         SLL   CNTREG,16                SETUP TO INCR BCB CNT           01840000
TRYCNTR  LR    BUF2REG,CNTREG           SETUP CNT IN TEMP REG           01850000
         AR    BUF2REG,WORKREG          INCR CNT IN TEMP REG            01860000
*********************************************************************** 01870000
*                                                                     * 01880000
* THE COMPARE&SWAP WILL CAUSE THE LOGIC TO UPDATE THE BCB AVAILABLE   * 01890000
* BUFFER COUNT FIELD OR START THE COUNT UPDATE OVER...                * 01900000
*                                                                     * 01910000
*********************************************************************** 01920000
         CS    WORKREG,BUF2REG,4(BCBREG) UPDATE CNT IN BCB AREA         01930000
         BNE   TRYCNTR                  CNT ALTERED, START AGAIN        01940000
         B     EXIT                     RELEASE COMPLETED, RETURN       01950000
         EJECT                                                          01960000
*********************************************************************** 01970000
*                                                                     * 01980000
* REQUEST TO OBTAIN BUFFERS FROM POOL. VALIDATE THE BASIC REQUEST AND * 01990000
* PROCESS IF VERIFIED...                                              * 02000000
*                                                                     * 02009000
*********************************************************************** 02018000
BUFREQ   LTR   ZEROREG,ZEROREG          ANY BUFFERS REQUESTED           02027000
         BZ    EXITA                    NO, NONE MADE AVAILABLE         02036000
         SRL   ZEROREG,8                SETUP REQ CNT FOR COMPARE       02045000
         L     WORKREG,4(BCBREG)        CHK BCB FOR AVAILABILITY        02054000
         C     WORKREG,GTLNGTH          ***                             02063000
         BNH   EXITA                    NO BUFFERS AVAILABLE            02072000
TRYUPDCT LR    CNTREG,WORKREG           CK REQ CNT AGAINST BCB CNT      02081000
         SR    CNTREG,ZEROREG           ***                             02090000
         BM    NOTENUF                  INSUFFICIENT BUFFERS            02099000
*********************************************************************** 02108000
*                                                                     * 02117000
* THE COMPARE & SWAP WILL ATTEMPT TO UPDATE THE BCB BUFFER COUNT      * 02126000
* FIELD WITH THE NEW COUNT AFTER SERVICING THIS GETBUFFER REQUEST...  * 02135000
*                                                                     * 02144000
*********************************************************************** 02153000
         CS    WORKREG,CNTREG,4(BCBREG) SETUP NEW BCB CNT VALUE         02162000
         BNE   TRYUPDCT                 BCB CNT ALTERED, TRY AGAIN      02171000
         SRL   ZEROREG,16               SETUP REQ CNT AS CONTROL        02180000
TRYUPBFR LR    CNTREG,ZEROREG           SETUP REQ CNT IN CTRL LOOP      02189000
         L     BUFREG,0(BCBREG)         GET FIRST BCB BUFFER ADDR       02198000
         ICM   BRREG,7,1(BCBREG)        PUT BUFR ADDR IN RC REG         02207000
         LR    BUF2REG,BUFREG           DEFAULT TO SINGLE BUFR          02216000
DECRCNTR BCT   CNTREG,NEXTBUFR          LOOP TO GET ALL BUFFERS         02225000
         L     WORKREG,0(BUF2REG)       GET BCB BUFR ADDR FOR C&S       02234000
*********************************************************************** 02243000
*                                                                     * 02252000
* THE COMPARE & SWAP WILL ATTEMP TO REMOVE THE REQUESTED BUFFERS FROM * 02261000
* THE BCB NEXT BUFFER ADDR...                                         * 02270000
*                                                                     * 02279000
*********************************************************************** 02288000
         CS    BUFREG,WORKREG,0(BCBREG) SETUP NEW BCB BUFFER ADDR       02297000
         BNE   TRYUPBFR                 BCB BUFR ALTERED,TRY AGAIN      02306000
         SR    WORKREG,WORKREG          SETUP LAST ADDR IN CHAIN        02315000
         ST    WORKREG,0(BUF2REG)       ***                             02324000
         B     EXIT                     REQUEST SERVICED, RETURN        02333000
NEXTBUFR L     BUF2REG,0(BUF2REG)       CONTINUE ALONG BUFR CHAIN       02342000
         LTR   BUF2REG,BUF2REG          TEST FOR ALL ZEROS IN BUFR PTR  02351000
         BZ    TRYUPBFR                 START OVER                      02360000
         B     DECRCNTR                 ***                             02369000
*********************************************************************** 02378000
*                                                                       02387000
* IF INSUFFICIENT BUFFERS ARE AVAILABLE FOR THIS REQUEST, THE AVAILABLE 02396000
* BUFFERS WILL BE GIVEN TO THE REQUESTOR....                            02405000
*                                                                       02414000
*********************************************************************** 02423000
NOTENUF  SR    BRREG,BRREG              CLEAR RC REG                    02432000
         C     WORKREG,GTLNGTH          ***                             02441000
         BNH   EXITA                    NO BUFFERS AVAILABLE NOW        02450000
         LR    ZEROREG,WORKREG          GET AVAILABLE BUFFER CNT        02459000
         SRL   ZEROREG,16               SETUP FOR BCB CNT ONLY          02500000
         SLL   ZEROREG,16               ***                             02510000
         ICM   BRREG,8,PARTBUF          INDICATE PARTIAL RC             02520000
         B     TRYUPDCT                 ATTEMPT TO UPDATE CNT           02530000
         EJECT                                                          02540000
NOBCB    ICM   BRREG,8,NOBCBLK          NO BUFFER CONTROL BLOCK         02550000
         B     EXIT                     RETURN TO REQUESTOR             02560000
EXITA    ICM   BRREG,8,NOBUFR           CODE NO BUFFER AVAILABLE        02570000
EXIT     EQU   *                                                        02580000
         MODESET EXTKEY=ZERO                                            02590000
         RETURN (2,12)                  RESTORE REGISTERS               02600000
         SPACE 3                                                        02610000
         DC    C'&SYSDATE'                                              02620000
PATCH    DC    20F'0'                                                   02630000
         SPACE 3                                                        02640000
*********************************************************************** 02650000
*                    PROGRAMING CONSTANTS                             * 02660000
*********************************************************************** 02670000
GTLNGTH  DC    X'0000FFFF'              TST FOR BUFR CNT EQ ZERO        02680000
PARTBUF  DC    X'04'                    RC = SOME BUFERS AVAILABLE      02690000
NOBUFR   DC    X'08'                    RC = NO BUFERS AVAILABLE        02700000
NOBCBLK  DC    X'0C'                    RC = INVALID BCB ADDR           02710000
INVBUF   DC    X'14'                    RC FOR INV BUFR REL CNT         02720000
         EJECT                                                          02730000
CVT      DSECT                                                          02740000
         CVT   LIST=YES                                                 02750000
         EJECT                                                          02760000
         IKJTCB DSECT=YES,LIST=YES                                      02770000
         SPACE 5                                                        02780000
         CNOP  0,8                                                      02790000
         END                                                            02800000
