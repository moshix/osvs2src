         TITLE 'IGG019PG - BTAM LOCAL 3270 SECOND LEVEL ATTN RTN'       00750000
IGG019PG CSECT                                                          00800000
*                                                                       00810000
* A035100,A058980-058999,D059000-060000,A064100,             LD YA02143 00820000
* C065500-069000,D069500-072000,D075000-077000,              LD YA02143 00830000
* C081000-081500,A084100,A100600-100700                      LD YA02143 00840000
* C058998,A059498                                            LD YA03270 00840400
* D064100,A065020-065200,D067600-067700                      LD YA03953 00840800
*                                                                       00842000
*********************************************************************** 00850000
*                                                                       00900000
* STATUS - CHANGE LEVEL 000                                             00950000
*                                                                       01000000
* FUNCTION - STARTS A READ OF MODIFIED DATA TO A 3270 WHICH HAS GIVEN   01050000
*    AN ATTENTION INTERRUPT. SCHEDULED BY THE FIRST LEVEL ATTENTION     01100000
*    ROUTINE IF NEEDED.  ENTERED FROM THE THIRD STAGE OF THE            01150000
*    ASYNCHRONOUS EXIT EFFECTOR (AEE3).                                 01200000
*                                                                       01250000
* ENTRY POINT - THIS ROUTINE IS ENTERED AT IGG019PG FROM AEE3.          01300000
*                                                                       01350000
* INPUT - REGISTER 1 CONTAINS THE ADDRESS OF THE UCB OF THE DEVICED     01400000
*    WHICH CAUSED AN ATTENTION INTERRUPT.                               01450000
*                                                                       01500000
* OUTPUT - AN IOB IS INITIALIZED TO DO A READ MODIFIED AND EXCP IS      01550000
*    ISSUED.  THE UCB ATTENTION HANDLING FIELDS ARE UPDATED AS NEEDED.  01600000
*                                                                       01650000
* EXTERNAL ROUTINES - EXCP (SVC 0)                                      01700000
*                                                                       01750000
* EXITS, NORMAL - WHEN FINISHED, THIS ROUTINE RETURNS TO AEE3.          01800000
*                                                                       01850000
* EXITS, ERROR - RETURN TO AEE3 WITHOUT ISSUING EXCP.                   01900000
*                                                                       01950000
* TABLES/WORK AREAS - THE UCB EXTENSIONS OF THE INTERRUPTING DEVICE     02000000
*    AND RELATIVE LINE 1 OF THE LINE GROUP.  THE IOB INITIALIZED BY     02050000
*    READ INITIAL AND THE IOB FOR THE INTERRUPTING DEVICE.              02100000
*                                                                       02150000
* ATTRIBUTES - THIS ROUTINE IS LOADED BY OPEN. IT EXECUTES ENABLED,     02200000
*    SUPERVISOR PROTECT KEY, PRIVILEDGED, AND REENTRANT.                02250000
*                                                                       02300000
*********************************************************************** 02350000
         EJECT                                                          02400000
*/*IGG019PG: CHART */                                                   02450000
*/* HEADER                                                              02500000
*/*BTAM SECOND LEVEL ATTENTION ROUTINE FOR 3270                     */  02550000
*/* E ENTRY FROM AEE3 */                                                02600000
*/* M ESTABLISH CSECT ADDRESSABILITY AND SAVE RETURN ADDR */            02650000
*/* M ESTABLISH BASES FOR DEVICE AND MASTER UCB DSECTS */               02700000
*/* M ESTABLISH ADDRESSABILITY FOR DEB AND DCB */                       02750000
*/* P OBTAIN ADDRESS OF INITIALIZED IOB */                              02800000
*/* D (YES,EXCP,NO,GETIOB) RLN OF DEVICE IS RLN OF INIALIZED IOB? */    02850000
*/*GETIOB: P OBTAIN ADDRESS OF DEVICE'S IOB */                          02900000
*/* P COPY ECB POINTER AND CHANNEL PROGRAM TO DEVICE'S IOB */           02950000
*/*EXCP: P SAVE ECB CONTENTS IN A NON-VOLATILE REGISTER */              03000000
*/* L INVOKE EXCP TO START I/O OPERATION ON DEVICE */                   03050000
*/* P CLEAR INITIALIZED RLN FIELD IN UCB */                             03100000
*/* P RESTORE PRE-EXCP STATUS OF ECB */                                 03150000
*/* P MOVE RLN OF DEVICE BEING USED TO DECPOLPT */                      03200000
*/* R RETURN TO AEE3 */                                                 03250000
*/*IGG019PG: END */                                                     03300000
         EJECT                                                          03350000
         USING MSTRUCB,MTRBASE                                          03400000
         USING UCB,UCBBASE                                              03450000
         USING DEBNMSUB,DEBREG                                          03500000
         USING IECTIOB,IOBDVC      ADDRESSABILITY FOR IOB    LD YA02143 03510000
         BALR  BASEREG,0           ESTABLISH ADDRESSABILITY FOR CSECT   03550000
         USING *,BASEREG                                                03600000
         B     A                                                        03650000
         DC    CL8'IGG019PG'       MODULE IDENTIFIER                    03700000
         DC    XL2'4072'           DATE OF REVIOSION                    03750000
PATCH    DS    CL48                PATCH AREA                           03800000
A        DS    0H                                                       03850000
         LR    SAVE14,RTRNREG      SAVE REG 14 (RETURN ADDRESS)         03900000
         LR    UCBBASE,PARMREG     GET ADDRESS OF UCB IN DSECT BASE REG 03950000
         CLI   UCBRLN,ONE          RLN = 1                              04000000
         BE    RLN1                YES, BRANCH                          04050000
         L     MTRBASE,UCBCTLNK    NO, GET ADDRESS OF MASTER UCB        04100000
         B     GETDEB              BR TO GET ADDR OF DEB                04150000
RLN1     EQU   *                                                        04200000
         LR    MTRBASE,UCBBASE                                          04250000
GETDEB   EQU   *                                                        04300000
         L     DEBREG,MTRDEBAD     GET ADDR OF DEB FROM MASTER UCB      04350000
         L     DCBREG,ZERO(DEBREG)   POINT TO TCB                       04400000
         USING TCB,DCBREG                                               04450000
         MODESET EXTKEY=TCB,WORKREG=4                           YA01891 04500000
         DROP  DCBREG                                                   04550000
         USING IHADCB,DCBREG                                            04600000
         L     DCBREG,DEBDCBAD-1   GET ADDR OF DCB FROM DEB             04650000
         L     IOBDVC,DCBIOBAD     GET BASE OF IOB POOL                 04700000
         SR    RW3,RW3                                                  04750000
         IC    RW3,DCBEIOBX        GET LENGTH OF IOB                    04800000
         SR    RODD,RODD                                                04850000
         IC    RODD,MTRINRLN       GET RLN OF INITIALIZED IOB           04900000
         MR    REVN,RW3            GET DISPLACEMENT INTO IOB POOL       04950000
         LA    IOBRDTI,0(RODD,IOBDVC)  ADD BASE TO GET ADDR OF          05000000
*                                  INITIALIZED IOB                      05050000
         CLC   UCBRLN(ONE),MTRINRLN  RLN FOR INITIALIZED IOB =          05100000
*                                  RLN OF INTERRUPTING DEVICE           05150000
         BNE   GETIOB              NO, GET ADDRESS OF OTHER IOB         05200000
         LR    IOBDVC,IOBRDTI      YES, IOB FOR DEVICE IS INITIALIZED   05250000
         B     EXCP                IOB.  START I/O                      05300000
GETIOB   EQU   *                                                        05350000
         SR    RODD,RODD           GET RLN OF INTERRUPTING DEVICE       05400000
         IC    RODD,UCBRLN                                              05450000
         MR    REVN,RW3            GET DISP INTO IOB POOL               05500000
         AR    IOBDVC,RODD         ADD BASE OF POOL TO GET ADDRESS OF   05550000
*                                  IOB                                  05600000
         MVC   ECBPT(FOUR,IOBDVC),ECBPT(IOBRDTI)  MOVE ECB ADDR         05650000
         MVC   CPA(CPALNGTH,IOBDVC),CPA(IOBRDTI)  MOVE CHANNEL PROGRAM  05700000
         LA    REVN,CPA(IOBDVC)    GET ADDR OF CPA                      05750000
         ST    REVN,START(IOBDVC)  AND STORE IN IOB                     05800000
EXCP     DS    0H                                            LD YA02143 05850000
**********                                                              05860000
*                                                                       05870000
*    NOTE-- EXCP DESTROYS THE CONTENTS OF THE ECB.  SINCE WAIT MAY      05880000
*    HAVE BEEN ISSUED, THE ECB CONTENTS MUST BE SAVED IF THE WAIT IS    05890000
*    TO BE SATISFIED. THIS IS DONE BY NOT USING THE USER'S DECB FOR     05892000
*    THE EXCP, SINCE IT COULD BE PART OF A MULTIPLE WAIT AND ANOTHER    05892400
*    EVENT MAY COMPLETE AND PROCESS THE DECB DURING THE EXCP.           05892800
*    IGG019PA AND IGG019LP HAVE BEEN CHANGED TO RECOGNIZE THIS          05893200
*    CASE AND GET ADDRESS OF REAL DECB FROM THE WORD AFTER              05893600
*    THE ECB. THIS IS RECOGNIZED BY A X'FF' IN THE FOURTH BYTE WHICH    05893700
*    IS NORMALLY THE DECTYPE FIELD.                                     05893800
*                                                                       05894000
**********                                                              05896000
         L     RZERO,GETPARM       SET SUBPOOL AND LENGTH    LD YA02143 05898000
         GETMAIN R,LV=(0)          GET 8 BYTES FOR AN ECB    LD YA02143 05898400
         LR    ECBSAVE,PARMREG     SAVE ADDRS OF NEW ECB     LD YA02143 05898800
         L     REVN,IOBECBPT       GET ADDRS OF DECB         LD YA02143 05899200
         ST    REVN,FOUR(ECBSAVE)  POINT NEW ECB TO DECB     LD YA02143 05899600
         MVI   FOUR(ECBSAVE),ALLON FLAG AS NOT A REAL DECB   LD YA02143 05899700
         STCM  ECBSAVE,MASKA,IOBECBPT+ONE    SET NEW AREA    LD YA03270 05899800
*                                            AS ECB          LD YA03270 05949800
         LR    PARMREG,IOBDVC      SET FOR EXCP              LD YA02143 05999800
         EXCP  (1)                 START I/O OPERATION                  06400000
         MODESET EXTKEY=ZERO                                    YA01891 06450000
         MVI   MTRINRLN,ZERO       CLEAR INITIALIZED RLN                06500000
         L     REG3,DEBNMSUB       LOAD TCB ADDRSS           LD YA03953 06502000
         USING TCB,REG3            ADDRESSABILITY FOR TCB    LD YA03953 06504000
         MODESET EXTKEY=TCB,WORKREG=14  ASUME USER'S KEY     LD YA03953 06506000
         STCM  REVN,MASKA,IOBECBPT+ONE REPOINT IOB TO DECB   LD YA03953 06510000
         MODESET EXTKEY=ZERO                                 LD YA03953 06520000
         TM    ZERO(ECBSAVE),POSTBIT  WAS ECB POSTED ?       LD YA02143 06550000
         BNO   NOPOST                 NO, SKIP POST          LD YA02143 06600000
         L     RZERO,ZERO(ECBSAVE) YES, REPOST USER          LD YA02143 06650000
         POST  (REVN),(RZERO)      WITH SAME POST CODE       LD YA02143 06700000
NOPOST   EQU   *                                             LD YA02143 06750000
         MODESET EXTKEY=TCB,WORKREG=3 USER KEY FOR FREEMAIN  LD YA02143 06800000
         DROP  REG3                                          LD YA02143 06810000
         L     RZERO,GETPARM       SETUP FOR FREEMAIN        LD YA02143 06850000
         FREEMAIN R,LV=(0),A=(ECBSAVE) FREE THE ECB AREA     LD YA02143 06900000
         MODESET EXTKEY=ZERO                                 LD YA02143 06950000
         XC    POLPT(FOUR,REVN),POLPT(REVN)  CLEAR POLPT FIELD          07250000
         MVC   POLPT+3(ONE,REVN),UCBRLN   MOVE RLN OF DEVICE BEING USED 07300000
*                                  INTO DECB                            07350000
         LR    RTRNREG,SAVE14      GET RETURN ADDR BACK IN REG 14       07400000
         BR    RTRNREG             RETURN TO AEE3                       07450000
         EJECT                                                          07750000
**********                                                              07800000
*                                                                       07850000
*        CONSTANTS                                                      07900000
*                                                                       07950000
**********                                                              08000000
         DS    0F                  GET ON WORD BOUNDARY         YA02050 08050000
GETPARM  DC    X'FA000008'         SUBPOOL 250 FOR 8 BYTES   LD YA02143 08100000
POSTBIT  EQU   X'40'               DEFINE BIT IN ECB         LD YA02143 08150000
**********                                                              08200000
*                                                                       08250000
*        REGISTER DEFINITIONS                                           08300000
*                                                                       08350000
**********                                                              08400000
RZERO    EQU   0                                             LD YA02143 08410000
PARMREG  EQU   1                   PARAMETER REGISTER                   08450000
REVN     EQU   2                   WORK REGISTERS- EVEN/ODD PAIR        08500000
RODD     EQU   3                       FOR MULTIPLICATION               08550000
REG3     EQU   3                   WORK REG                  LD YA02143 08560000
ECBSAVE  EQU   4                   REG TO SAVE ECB CONTENTS             08600000
RW3      EQU   4                   WORK REGISTER                        08650000
MTRBASE  EQU   5                   ADDR OF UCB FOR RLN 1                08700000
UCBBASE  EQU   6                   ADDR OF UCB FOR INTERRUPTING DEVICE  08750000
IOBDVC   EQU   7                   ADDR OF IOB FOR DEVICE               08800000
IOBRDTI  EQU   8                   ADDR OF INITIALIZED IOB              08850000
DEBREG   EQU   9                   ADDRESS OF DEB                       08900000
DCBREG   EQU   10                  ADDRESS OF DCB                       08950000
SAVE14   EQU   11                  REGISTER TO SAVE RETURN PT.          09000000
BASEREG  EQU   12                  BASE REGISTER                        09050000
RTRNREG  EQU   14                  RETURN REGISTER                      09100000
**********                                                              09150000
*                                                                       09200000
*        MISCELLANEOUS EQUATES                                          09250000
*                                                                       09300000
**********                                                              09350000
ALLON    EQU   X'FF'                                                    09400000
CPA      EQU   X'40'               DISP TO START OF CPA IN IOB          09450000
CPALNGTH EQU   24                  LENGTH OF 3270 CPA                   09500000
DCBPT    EQU   X'14'               DISP TO DCB ADDR IN IOB              09550000
ECBPT    EQU   4                   DISP TO ECB ADDR IN IOB              09600000
FOUR     EQU   4                                                        09650000
INCAM    EQU   X'1C'               DISP TO IOBINCAM                     09700000
IOBBUSY  EQU   X'40'               IOBINCAM BUSY BIT                    09750000
ONE      EQU   X'01'                                                    09800000
POLPT    EQU   36                  DISP TO DECPOLPT FIELD IN DECB       09850000
START    EQU   X'10'               DISP OF PTR TO CPA IN IOB            09900000
TWO      EQU   2                                                        09950000
UCBX     EQU   X'20'               DISP TO UCB INDEX IN IOB             10000000
ZERO     EQU   0                                                        10050000
MASKA    EQU   7                   MASK FOR ADDRS PORTION... LD YA02143 10060000
*                                  OF FULL WORD              LD YA02143 10070000
         EJECT                                                          10100000
         DCBD  DSORG=BX,DEVD=BS                                         10150000
         EJECT                                                          10200000
         IECTDEBX                                                       10250000
         EJECT                                                          10300000
         IECTIOBX                                                       10350000
         EJECT                                                          10400000
         IECTUCBX                                                       10450000
         EJECT                                                          10500000
         IKJTCB DSECT=YES,LIST=YES                              XA01891 10550000
         END                                                            10600000
