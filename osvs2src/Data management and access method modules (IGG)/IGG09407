         TITLE 'IGG09407 - STCC ROUTINE'                                97105000
IGG09407 CSECT                                                          97110000
*********************************************************************** 97170000
*                                                                     * 97195000
* STATUS -                                                            * 97200000
*    CHANGE LEVEL 01                                                  * 97205000
*                                                                     * 97210000
* FUNCTION -                                                          * 97215000
*    THIS ROUTINE IS TO SERVE THE MACRO 'STCC'. IT IS TO              * 97220000
*    CHANGE THE STCC BYTES IN TSB (TSBSTCC) TO REFLECT REQUEST OF THE * 97225000
*    MACRO.                                                           * 97230000
*                                                                     * 97235000
*    IF X'00' IS SPECIFIED AS LINE DELETE OR CHARACTER DELETE         * 97240000
*    CHARACTER, THE PREVIOUSLY USED CHARACTER IS RETAINED.  IN THIS   * 97245000
*    CASE, THE FORMER LINE DELETE CHARACTER IS PUT IN REG 0, THE      * 97250000
*    FORMER CHARACTER DELETE CHARACTER IS PUT IN REG 1.               * 97255000
*                                                                     * 97260000
*    IF X'FF' IS SPECIFIED, THE EDIT FUNCTION IS TERMINATED, AND      * 97265000
*    THE PROPER FIELD IS ZEROED OUT.                                  * 97270000
*                                                                     * 97275000
*    IF ATTENTION IS SPECIFIED FOR LINE DELETE, THE TSBATNLD BIT IN   * 97310000
*    TSBSTAT FIELD IS TURNED ON.                                      * 97315000
*                                                                     * 97320000
* ENTRY POINTS -                                                      * 97325000
*         IGG09407 - CONTROL IS RECEIVED FROM MODULE IGC0009D         * 97330000
*         VIA BRANCH ACCORDING ENTRY CODE 7, HODLING THE LOCAL        * 97332002
*         AND THE CMS LOCKS, AND IN SUPERVISOR KEY                    * 97334002
*                                                                     * 97335000
* INPUT -                                                             * 97340000
*    REGISTER 0 CONTAINS PARAMETER                                    * 97370000
**********************************************************************  97371002
* ENTRY CODE     *                 *                 *               *  97372002
**********************************************************************  97373002
*    REGISTER 1 CONTAINS PARAMETER                                    * 97375000
**********************************************************************  97376002
* FLAG           *                 * LINE DELETE     * CHAR DELETE   *  97377002
**********************************************************************  97380002
*    REGISTER 3 CONTAINS CVT ADDRESS                                  * 97385000
*    REGISTER 4 CONTAINS TCB ADDRESS                                  * 97390000
*    REGISTER 5 CONTAINS TSB ADDRESS                                  * 97395002
*    REGISTER 10 CONTAINS THE ADDRESS OF AN ARRAY OF CONSTANTS          97425002
*    REGISTER 12 CONTAINS THE BASE ADDRESS                              97426002
*    REGISTER 14 CONTAINS THE RETURN ADDRESS                            97427002
*                                                                     * 97430000
* OUTPUT -                                                            * 97435000
*    REGISTER 0 HAS THE FORMER LINE DELETE CHARACTER. X'FF' MEANS     * 97440000
*               THERE WAS NO FORMER LINE DELETE CHARACTER             * 97445000
*                                                                     * 97450000
*    REGISTER 1 HAS THE FORMER CHARACTER DELETE CHARACTER. X'FF' MEANS* 97455000
*               THERE WAS NO FORMER CHARACTER SELETE CHARACTER        * 97460000
*                                                                     * 97465000
*    REGISTER 15 HAS RETURN CODE:                                     * 97470000
*           0 - SUCCESSFUL                                            * 97475000
*           8 - CHARACTER REJECTED                                    * 97485000
*                                                                     * 97490000
* EXTERNAL REFERENCES -                                               * 97495000
*         NONE                                                        * 97500000
*                                                                     * 97505000
* EXITS, NORMAL -                                                     * 97510000
*         BR 14 WITH 0 IN REGISTER 15                                 * 97515002
*                                                                     * 97520000
* EXITS, ERROR -                                                      * 97525000
*         BR 14 WITH 4 OR 8 IN REGISTER 15                            * 97530002
*                                                                     * 97535000
* TABLES/WORK AREAS -                                                 * 97540000
*    CONTROL BLOCKS ARE DESCRIBED IN THE DSECTS AT END OF LISTING     * 97545000
*                                                                     * 97550000
* ATTRIBUTES -                                                        * 97555000
*    REENTRANT, REFRESHABLE, SUPERVISOR MODE, ENABLED                 * 97560000
*                                                                     * 97565000
* CHARACTER CODE DEPENDENCY -                                         * 97570000
*    THIS MODULE IS ASSEMBLED IN EBCDIC AND MUST BE REASSEMBLED IF    * 97575000
*    A DIFFERENT CHARACTER SET IS TO BE USED DURING EXECUTION         * 97580000
* NOTES -                                                             * 97585000
*    NONE                                                             * 97590000
*                                                                     * 97595000
*********************************************************************** 97600000
*                                                                     * 97605000
*********************************************************************** 97610000
         SPACE 3                                                        97615000
********                                                                97620000
******** REGISTER EQUATES                                               97625000
********                                                                97630000
RPARM0   EQU   0                        PARAMETER REG 0                 97635000
RWORK0   EQU   0                        WORK REGISTER                   97640000
RPARM1   EQU   1                        PARAMETER REG 1                 97645000
RWORK1   EQU   1                        WORK REGISTER                   97650000
RWORK2   EQU   2                        WORK REGISTER                   97655000
RWORK3   EQU   3                        WORK REGISTER                   97660000
RTSB     EQU   5                        POINTS TO TSB           YS02019 97695002
RBASE    EQU   6                        BASE REGISTER           YS02019 97755002
RWORK4   EQU   8                        WORK REGISTER           YS02019 97757002
RCONST   EQU   10                       POINTS TO PROGRAM       YS02019 97760002
*                                       CONSTANTS               YS02019 97761002
R12      EQU   12                       INTERFACE REGISTER      YS02019 97762002
R13      EQU   13                       REGISTER 13                     97765000
R14      EQU   14                       REGISTER 14                     97770000
RLINK    EQU   14                       LINK REGISTER                   97775000
R15      EQU   15                       REGISTER 15                     97785000
RBRANCH  EQU   15                       BRANCH REGISTER                 97790000
RCODE    EQU   15                       RETURN CODE REGISTER            97795000
         SPACE 2                                                        97800000
********                                                                97805000
******** DISPLACEMENT,LENGTH AND MASK EQUATES                           97810000
********                                                                97815000
BYTE1    EQU   8                        MASK FOR BYTE 1 IN REG.         97820000
BYTE2    EQU   4                        MASK FOR BYTE 2 IN REG.         97825000
BYTE3    EQU   2                        MASK FOR BYTE 3 IN REG.         97830000
BYTE4    EQU   1                        MASK FOR BYTE 4 IN REG.         97835000
BYTE34   EQU   3                        MASK FOR BYTES 3&4 IN REG.      97840000
BYTE234  EQU   7                        MASK FOR BYTES 2&3&4 IN REG.    97845000
D0       EQU   0                        DISPLACEMENT OF 0               97850000
D1       EQU   1                        DISPLACEMENT OF 1               97855000
D2       EQU   2                        DISPLACEMENT OF 2               97860000
D3       EQU   3                        DISPLACEMENT OF 3               97865000
D4       EQU   4                        DISPLACEMENT OF 4               97870000
D5       EQU   5                        DISPLACEMENT OF 5               97875000
D6       EQU   6                        DISPLACEMENT OF 6               97880000
D14      EQU   14                       DISPLACEMENT OF 14              97885000
L1       EQU   1                        LENGTH OF 1                     97890000
L2       EQU   2                        LENGTH OF 2                     97895000
L3       EQU   3                        LENGTH OF 3                     97900000
L4       EQU   4                        LENGTH OF 4                     97905000
L8       EQU   8                        LENGTH OF 8                     97910000
L16      EQU   16                       LENGTH OF 16                    97915000
L20      EQU   20                       LENGTH OF 20                    97920000
L24      EQU   24                       LENGTH OF 24                    97925000
L31      EQU   31                       LENGTH OF 31                    97930000
L32      EQU   32                       LENGTH OF 32                    97935000
RETCODE4 EQU   4                        RETURN CODE OF 4                97940000
RETCODE8 EQU   8                        RETURN CODE OF 8                97945000
RETCODEC EQU   12                       RETURN CODE OF 12               97950000
COMPLETE EQU   X'40'                    TO TEST POST COMPLETE BIT       97960000
CZERO    EQU   0                        TO TEST FOR ZERO                97965000
CONE     EQU   1                        CONSTANT OF 1                   97970000
CTWO     EQU   2                        CONSTANT OF 2                   97975000
CTHREE   EQU   3                        CONSTANT OF 3                   97980000
CFIFTEEN EQU   15                       CONSTANT OF 15                  97985000
CSIXTEEN EQU   16                       CONSTANT OF 16                  97990000
CFOX     EQU   X'FF'                    TO TEST FOR 'FF'                97995000
BLANK    EQU   X'40'                    TO TEST FOR BLANK               98015000
OFF      EQU   X'FF'                    TO TURN BITS OFF                98020000
EXIT     EQU   3                        TO INVOKE EXIT,SVC 3            98025000
IDLE     EQU   X'17'                    TERM. CTL. CHAR. - IDLE         98030000
NL       EQU   X'15'                    TERM. CTL. CHAR. - NEW LINE     98035000
CR       EQU   X'0D'                    TERM. CTL. CHAR. - CARRIAGE RET 98040000
LF       EQU   X'25'                    TERM. CTL. CHAR. - LINE FEED    98045000
HEX18    EQU   X'18'                    TERM. CTL. CHAR. - TWX CHAR.    98050000
HEX20    EQU   X'20'                    TO TEST BIT 2 OF BYTE           98055000
         SPACE 3                                                        98100000
********                                                                98105000
******** ESTABLISH BASE REGISTER AND ADDRESSABILITY                     98110000
********                                                                98115000
         USING *,RBASE                  BASE ADDRESSABILITY             98140000
         LR    RBASE,R12                LOAD BASE REGISTER      YS02019 98145002
         B     MAIN                     BR AROUND ID AND DATE   YS02019 98147002
         DC    C'IGG09407'              MODULE ID               YS02019 98149002
         DC    X'3261'                  DATE - 09/18/73         YM03842 98151002
         USING TSB,RTSB                 TSB ADDRESSABILITY              98155000
         USING CONSTANT,RCONST          CONSTANT ADDRESSABILITY         98195000
********                                                                98205002
******** MAIN FUNCTION OF ROUTINE - MODIFY LINE/CHAR DELETE CHARACTERS  98215002
********                                                                98225002
MAIN     EQU   *                                                YS02019 98245002
         LR    RWORK4,RLINK             SAVE RETURN ADDRESS     YS02019 98295002
         MODESET EXTKEY=TCAM            GET TCAM'S KEY          YS02019 98370002
         SR    RCODE,RCODE              INITIALIZE RETURN CODE          98420000
         SR    RWORK0,RWORK0            CLEAR REGISTER                  98425000
         SR    RWORK2,RWORK2            CLEAR REGISTER                  98430000
         LR    RWORK3,RPARM1            PUT ENTRY PARMS IN WORK REG     98435000
         SR    RWORK1,RWORK1            CLEAR REGISTER                  98440000
         IC    RWORK0,TSBLNDCC          GET OLD LINE DEL CHAR           98445000
         LTR   RWORK0,RWORK0            WAS IT ZERO                     98450000
         BNZ   CHKOLDCD                 IF NOT, CHECK OLD CD CHAR       98455000
         LA    RWORK0,CFOX              YES,SET TO X'FF'                98460000
CHKOLDCD EQU   *                                                        98465000
         IC    RWORK1,TSBCHDCC          GET OLD CHAR DEL CHAR           98470000
         LTR   RWORK1,RWORK1            WAS OLD CH DEL CHAR ZERO        98475000
         BNZ   STORE                    NO,STORE NEW ONES               98480000
         LA    RWORK1,CFOX              YES,SET TO X'FF'                98485000
STORE    EQU   *                                                        98490000
         STH   RWORK3,TSBSTCC           STORE NEW LD/CD CHARS.   A111   98495000
********                                                                98500000
******** CHECK ATTENTION                                                98505000
********                                                                98510000
         TM    TSBSTAT,TSBATNLD         ATTN FOR LD ALREADY SET         98515000
         BZ    NOTON                    NO                              98520000
         ICM   RWORK0,BYTE1,HIGHBIT     INDICATE ATTN LD IN EFFECT      98555000
NOTON    EQU   *                                                        98565000
         SLDL  RWORK2,L1                GET FLAG BIT 0                  98570000
         LTR   RWORK2,RWORK2            IS IT 1                         98575000
         BZ    CHKLD1                   NO,1ST OP NOT SPECIF.    A111   98580000
         SR    RWORK2,RWORK2            CLEAR BIT                       98585000
         SLDL  RWORK2,L1                GET FLAG BIT 1                  98590000
         LTR   RWORK2,RWORK2            ATTN SPECIFIED                  98595000
         BZ    NATN                     NO,NATN                         98600000
ATTN     EQU   *                                                        98605000
         TM    TSBTERM1,TSBCATTN        DOES DEVICE HAVE FEATUREYS02019 98610002
         BO    SETATTN                  YES,GO SET FOR LD               98615000
         LA    RCODE,RETCODE8           SET RETURN CODE OF 8            98620000
         B     CHKLD                    CHECK LINE DELETE               98625000
SETATTN  EQU   *                                                        98630000
         OI    TSBSTAT,TSBATNLD         TURN ON ATTN FOR LD             98635000
         B     CHKLD                    GO CHECK LD                     98640000
NATN     EQU   *                                                        98645000
         NI    TSBSTAT,OFF-TSBATNLD     TURN OFF ATTN FOR LD            98650000
********                                                                98655000
******** CHECK LINE DELETE CHARACTER                                    98660000
********                                                                98665000
CHKLD    EQU   *                                                        98670000
         SRL   RWORK3,L1                RESET FLAG BYTE          A111   98675000
CHKLD1   EQU   *                                                 A111   98680000
         SR    RWORK2,RWORK2            CLEAR BIT                       98685000
         SLDL  RWORK2,L2                GET FLAG BIT 2           A111   98690000
         LTR   RWORK2,RWORK2            LD CHAR. SPECIFIED              98695000
         BP    CKLDZERO                 YES,TEST IT                     98700000
LDZERO   EQU   *                                                        98705000
         STC   RWORK0,TSBLNDCC          RESTORE OLD LD CHAR             98710000
         CLI   TSBLNDCC,CFOX            WAS OLD LD ZERO                 98715000
         BNE   CHKCD                    NO,CHECK CD CHAR                98720000
         B     ZEROLDFD                 YES,RESET TO ZERO               98725000
CKLDZERO EQU   *                                                        98730000
         CLI   TSBLNDCC,CZERO           IS NEW LD ZERO                  98735000
         BNE   CKLDFOX                  NO,CHECK FOR X'FF'              98740000
         B     LDZERO                   NEW LD IS ZERO                  98745000
CKLDFOX  EQU   *                                                        98750000
         CLI   TSBLNDCC,CFOX            IS NEW LD FOX                   98755000
         BE    ZEROLDFD                 YES,SET TO ZERO                 98760000
CHKLDVLD EQU   *                                                        98765000
         CLI   TSBLNDCC,CR              IS NEW LD CARRIAGE RETURN CHAR. 98770000
         BE    LDREJECT                 SET REJECT CODE                 98775000
         CLI   TSBLNDCC,LF              WAS LINE FEED CHAR SPECIFIED    98780000
         BE    LDREJECT                 SET REJECT CODE                 98785000
         CLI   TSBLNDCC,NL              WAS NEW LINE CHAR. SPECIFIED    98790000
         BE    LDREJECT                 SET REJECT CODE                 98795000
         B     CHKCD                    GO CHECK CODE           YS02019 98840002
LDREJECT EQU   *                                                        98895000
         LA    RCODE,RETCODE8           SET RETURN CODE OF 8            98900000
ZEROLDFD EQU   *                                                        98905000
         MVI   TSBLNDCC,CZERO           ZERO FIELD                      98910000
********                                                                98915000
******** CHECK CHARACTER DELETE CHARACTER                               98920000
********                                                                98925000
CHKCD    EQU   *                                                        98930000
         SR    RWORK2,RWORK2            CLEAR BIT                       98935000
         SLDL  RWORK2,L1                GET FLAG BIT 3                  98940000
         LTR   RWORK2,RWORK2            CHAR. DELTE SPECIFIED           98945000
         BNZ   CHKMORE                  YES                             98950000
CDZERO   EQU   *                                                        98955000
         STC   RWORK1,TSBCHDCC          RESTORE OLD CD CHAR             98960000
         CLI   TSBCHDCC,CFOX            WAS OLD CD CHAR ZERO            98965000
         BE    ZEROCDFD                 YES,GO RESET TO ZERO            98970000
         B     RETURN                   NO,RETURN TO CALLER             98975000
CHKMORE  EQU   *                                                        98980000
         CLI   TSBCHDCC,CZERO           IS NEW CD ZERO                  98985000
         BNE   CHKCDFOX                 NO,CHECK FOR X'FF'              98990000
         B     CDZERO                   NEW CD CHAR IS ZERO             98995000
CHKCDFOX EQU   *                                                        99000000
         CLI   TSBCHDCC,CFOX            IS NEW CD CHAR. X'FF'           99005000
         BE    ZEROCDFD                 YES,RESET TO ZERO               99010000
CHKCDVLD EQU   *                                                        99015000
         CLI   TSBCHDCC,CR              IS NEW CD CARRIAGE RETURN       99020000
         BE    CDREJECT                 YES,INVALID                     99025000
         CLI   TSBCHDCC,NL              IS NEW CD NEW LINE CHAR.        99030000
         BE    CDREJECT                 YES,INVALID                     99035000
         CLI   TSBCHDCC,LF              IS NEW CD LINE FEED CHAR.       99040000
         BE    CDREJECT                 YES,INVALID                     99045000
         B     RETURN                   RETURN TO CALLER                99120000
CDREJECT EQU   *                                                        99125000
         LA    RCODE,RETCODE8           SET RETURN CODE OF 8            99130000
ZEROCDFD EQU   *                                                 A111   99135000
         MVI   TSBCHDCC,CZERO           SET TSBCHDCC TO X'00'    A111   99140000
RETURN   EQU   *                                                        99145000
         MODESET EXTKEY=SUPR            GET KEY FOR SETLOCKS    YS02019 99145802
*********************************************************************** 99146602
*        RELEASE CMS AND LOCAL LOCKS                                  * 99147402
*********************************************************************** 99148202
RELEASE  EQU   *                        LABEL FOR SETLOCK       YS02019 99149002
         SETLOCK RELEASE,TYPE=CMS,RELATED=(TSB,IGC0009D)        YS02019 99150002
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=(TSB,IGC0009D)      YS02019 99151002
         LR    RLINK,RWORK4             RESTORE RETURN ADDR     YS02019 99153002
         BR    RLINK                    RETURN                  YS02019 99154002
         DC    9F'0'                    MAINTENANCE PATCH AREA  YS02019 99395002
         SPACE 3                                                        99445002
CONSTANT DSECT                          DEFINES CONSTANTS IN 1ST CSECT  99455000
ZERO     DC    F'0'                     CONSTANT OF 0                   99460000
NINES    DC    F'99999999'              CONSTANT OF 99,999,999          99465000
FIFTEEN  DC    AL1(15)                  CONSTANT OF 15                  99470000
TWO      DC    XL1'02'                  CONSTANT OF 2                   99475000
TEN      DC    H'10'                    CONSTANT OF 10                  99480000
SIXTEEN  DC    AL1(16)                  CONSTANT OF 16                  99485000
ENABLE   DC    X'FF'                    TO ENABLE INTERRUPTS            99490000
SIX      DC    H'6'                     CONSTANT OF 6                   99495000
EIGHT    DC    AL1(8)                   CONSTANT OF 8                   99500000
CP5041   DC    XL3'026D16'              BKSP/UNDERSCORE CHAR. PROMPT    99505000
HIGHBIT  DC    XL1'80'                  TO TEST HIGH ORDER BITS         99510000
CPTWX    DC    XL3'024B0D'              PERIOD/CAR.RETURN CHAR. PROMPT  99515000
TPUT1    DC    XL4'0E000000'            TPUT,HOLD,CONTROL               99520000
TPUT2    DC    XL4'0A000000'            TPUT,CONTROL                    99525000
ONE      DC    AL1(1)                   CONSTANT OF ONE                 99530000
XIDLE    DC    X'17'                    TPUT IDLE CHARACTER     YA00364 99532002
FOX      EQU   ENABLE                   CONSTANT OF 'FF'                99535000
DISABLE  EQU   ZERO                     TO DISABLE INTERRUPTS           99540000
         EJECT                                                          99545002
         IHAPSA                                                         99555002
         EJECT                                                          99565002
         IKJTSB                                                         99580000
         END                                                            99895000
