         TITLE 'IGG0940H - SECOND LOAD OF SVC 94 FOR TCAM/TSO'          00050000
IGG0940H CSECT                                                          00100000
*********************************************************************** 00150000
*                                                                     * 00200000
* MODULE NAME - IGG0940H - SVC 94, SECOND LOAD; ROUTER FOR TCAM/TSO   * 00250000
*                                                                     * 00300000
* STATUS - VERSION 000                                                * 00350000
*                                                                     * 00400000
* FUNCTION - THIS MODULE IS THE TCAM-SPECIFIC CODE FOR SVC 94. ITS    * 00450000
*    FUNCTION IS TO ROUTE CONTROL TO THE PROPER SUPPORT MODULE FOR    * 00500000
*    THE MACRO ISSUED.                                                * 00550000
*                                                                     * 00600000
* ENTRY POINTS -                                                      * 00650000
*         IGG0940H - CONTROL IS RECEIVED FROM IGC0009D IF TCAM/TSO    * 00700000
*                  IS ACTIVE.                                         * 00750000
*         POSTTSB - ROUTINE TO POST THE TSB TO IEDAYP                 * 00800000
*                  LOCAL AND CMS LOCKS MUST BE HELD, MUST BE IN       * 00850000
*                  TCAM'S KEY .                                       * 00900000
*                  R1 = ADDR OF MASK TO BE USED                       * 00950000
*                  R3 = ADDR OF CVT                                   * 01000000
*                  R14 = RETURN ADDR OF CALLER                        * 01050000
*                  R15 = ENTRY ADDRESS                                * 01100000
*                  R6 TO R9 ARE WORK REGISTERS                        * 01150000
*                                                                     * 01200000
* INPUT -  (WHEN ENTERED AT IGG0940H ENTRY POINT)                     * 01250000
*    REG 0 - ENTRY CODE IN HIGH ORDER BYTE, PARAMETERS IN REMAINDER   * 01300000
*    REG 1 - PARAMETERS                                               * 01350000
*    REG 3 - CVT                                                      * 01400000
*    REG 4 - TCB                                                      * 01450000
*    REG 5 - SVRB (UNUSED)                                            * 01500000
*    REG 12 - ENTRY POINT ADDRESS                                     * 01550000
*                                                                     * 01600000
* OUTPUT - RETURN CODE IN REGISTER 15                                 * 01650000
*                                                                     * 01700000
* EXTERNAL REFERENCES -                                               * 01750000
*         QTIP (IKJGGQT1) WITH ENTRY CODE 21 (CLEAR INPUT QUEUE) OR   * 01800000
*         ENTRY CODE 22 (CLEAR OUTPUT QUEUE)                          * 01850000
*                                                                     * 01900000
* EXITS - NORMAL                                                      * 01950000
*         BRANCH TO NEXT CSECT, LOCAL AND CMS LOCKS ARE HELD.         * 02000000
*    REG 0 - ENTRY CODE IN HIGH ORDER BYTE,PARAMETERS IN REMAINDER    * 02050000
*    REG 1 - PARAMETERS                                               * 02100000
*    REG 3 - CVT                                                      * 02150000
*    REG 4 - TCB                                                      * 02200000
*    REG 5 - TSB                                                      * 02250000
*    REG 10 - ADDR OF ARRAY OF CONSTANTS (FOR USE BY OTHER ROUTINES)  * 02300000
*    REG 14 - RETURN ADDRESS                                          * 02350000
*                                                                     * 02400000
* EXITS - ERROR                                                       * 02450000
*         BR 14 WITH RETURN CODE IN REGISTER 15                       * 02500000
*                                                                     * 02550000
* TABLES AND WORK AREAS - SEE DSECTS AT END OF MODULE                 * 02600000
*                                                                     * 02650000
* ATTRIBUTES - REENTRANT                                              * 02700000
*                                                                     * 02750000
* CHARACTER CODE DEPENDENCY - NONE                                    * 02800000
*                                                                     * 02850000
* NOTES - THIS MODULE CONTAINS THE TCAM-SPECIFIC CODE REMOVED IN THE  * 02900000
*         SPLIT OF IGC0009D                                           * 02950000
*                                                                     * 03000000
* CHANGE ACTIVITY -                                                   * 03050000
* A000000-999999                                               @ZZZZZZZ 03100000
*                                                                     * 03150000
*********************************************************************** 03200000
         SPACE 3                                                        03250000
********                                                                03300000
******** REGISTER EQUATES                                               03350000
********                                                                03400000
RPARM0   EQU   0                        PARAMETER REG 0                 03450000
RWORK0   EQU   0                        WORK REGISTER                   03500000
RPARM1   EQU   1                        PARAMETER REG 1                 03550000
RWORK1   EQU   1                        WORK REGISTER                   03600000
RWORK2   EQU   2                        WORK REGISTER                   03650000
RPARM2   EQU   2                        PARAMETER REG 2 FOR OUTPUT      03700000
RCVT     EQU   3                        POINTS TO CVT                   03750000
RTCB     EQU   4                        POINTS TO TCB                   03800000
RTSB     EQU   5                        POINTS TO TSB                   03850000
RWORK6   EQU   6                        WORK REGISTER                   03900000
RTCX     EQU   6                        BASE FOR TCX                    03950000
RTSI     EQU   7                        BASE FOR TSI                    04000000
RWORK7   EQU   7                        WORK REGISTER                   04050000
RWORK8   EQU   8                        WORK REGISTER                   04100000
RWORK9   EQU   9                        WORK REGISTER                   04150000
RWORK10  EQU   10                       WORK REGISTER                   04200000
RCONST   EQU   10                       POINTS TO PROGRAM CONSTANTS     04250000
RWORK11  EQU   11                       WORK REGISTER                   04300000
RBASE    EQU   12                       BASE REGISTER                   04350000
R13      EQU   13                       REGISTER 13                     04400000
R14      EQU   14                       REGISTER 14                     04450000
RLINK    EQU   14                       LINK REGISTER                   04500000
R15      EQU   15                       REGISTER 15                     04550000
RBRANCH  EQU   15                       BRANCH REGISTER                 04600000
RCODE    EQU   15                       RETURN CODE REGISTER            04650000
         SPACE 2                                                        04700000
********                                                                04750000
******** DISPLACEMENT,LENGTH AND MASK EQUATES                           04800000
********                                                                04850000
BYTE1    EQU   8                        MASK FOR BYTE 1 IN REG.         04900000
D2       EQU   2                        DISPLACEMENT OF 2               04950000
L2       EQU   2                        LENGTH OF 2                     05000000
L24      EQU   24                       LENGTH OF 24                    05050000
RETCODE4 EQU   4                        RETURN CODE OF 4                05100000
RETCODE8 EQU   8                        RETURN CODE OF 8                05150000
CZERO    EQU   0                        TO TEST FOR ZERO                05200000
CFOUR    EQU   4                        CONSTANT OF 4                   05250000
CEIGHT   EQU   8                        CONSTANT OF 8                   05300000
TWELVE   EQU   12                       CONSTANT OF 12                  05350000
OFF      EQU   X'FF'                    TO TURN BITS OFF                05400000
QINEC    EQU   21                       QTIP ENTRY CODE/TCLEARQ/INPUT   05450000
QOUTEC   EQU   22                       QTIP ENTRY CODE/TCLEARQ/OUTPUT  05500000
         SPACE 3                                                        05550000
********                                                                05600000
******** DEFINE ALTERNATE ENTRY POINTS                                  05650000
********                                                                05700000
         ENTRY CONST                                                    05750000
         ENTRY POSTTSB                                                  05800000
         SPACE 3                                                        05850000
********                                                                05900000
******** ESTABLISH BASE REGISTERS AND ADDRESSABILITY                    05950000
********                                                                06000000
         BALR  RBASE,RWORK0             ADDRESSABILITY                  06050000
         USING *,RBASE                  FOR THIS CSECT                  06100000
         USING CVT,RCVT                 CVT ADDRESSABILITY              06150000
         USING TCB,RTCB                 TCB ADDRESSABILITY              06200000
IGG0940H IEDHJN AFTERHJN,HJN                                            06250000
         LA    RCONST,CONST             BASE FOR CONSTANTS              06300000
*                                       (PASSED FOR OTHER MODULES)      06350000
*******                                                                 06400000
*******  CHECK ENTRY CODES                                              06450000
*******                                                                 06500000
         CLM   RPARM0,BYTE1,T20         HIGHEST ENTRY CODE              06550000
         BH    INVALID                  CODE IS INVALID                 06600000
         SPACE 2                                                        06650000
TCABTSE  EQU   *                        CHECK FOR TCABEND OR            06700000
*                                       TSEND                           06750000
         CLM   RPARM0,BYTE1,XZERO       IS ENTRY CODE 0                 06800000
         BE    BRANCH1                  YES, BRANCH TO MODULE           06850000
         CLM   RPARM0,BYTE1,T2          IS ENTRY CODE 2                 06900000
         BE    BRANCH1                  YES BRANCH TO MODULE            06950000
         SPACE                                                          07000000
*********************************************************************** 07050000
*        GET POINTER TO TSB                                           * 07100000
*********************************************************************** 07150000
BRANCH   EQU   *                                                        07200000
         L     RWORK6,PSAAOLD-PSA       ADDRESS OF ASCB                 07250000
         USING ASCB,RWORK6              ASCB ADDRESSABILITY             07300000
         L     RTSB,ASCBTSB             ADDRESS OF TSB                  07350000
         DROP  RWORK6                                                   07400000
         USING TSB,RTSB                 TSB IS ADDRESSABLE              07450000
********                                                                07500000
******** BRANCH TO PROPER MODULE                                        07550000
********                                                                07600000
         LR    RWORK2,RPARM0            GET ENTRY CODE                  07650000
         SRL   RWORK2,L24               SHIFT TO LOW ORDER AND          07700000
         SLL   RWORK2,L2                MPY BY 4 (TABLE ENTRY           07750000
*                                       LENGTH)                         07800000
BRNEXT   L     RWORK2,BRTBL(RWORK2)     GET BRANCH ADDRESS              07850000
BRNCH    LR    RBASE,RWORK2             SET UP NEW BASE REG             07900000
         BR    RBASE                    GO TO PROPER ROUTINE            07950000
         SPACE                                                          08000000
*********************************************************************** 08050000
*        FOR TSO/TCAM ENTRY CODES 0 AND 2, REG 5 (RRB) MUST           * 08100000
*        CONTAIN THE RB ADDRESS. THOSE CODES ARE HANDLED HERE         * 08150000
*        TO AVOID THE RESETTING OF REG 5 TO THE TSB ADDRESS           * 08200000
*        NEEDED BY THE OTHER TSO/TCAM ENTRY CODES.                    * 08250000
*********************************************************************** 08300000
BRANCH1  EQU   *                                                        08350000
         LR    RWORK2,RPARM0            GET ENTRY CODE                  08400000
         SRL   RWORK2,L24               SHIFT TO LOW ORDER AND          08450000
         SLL   RWORK2,L2                MPY BY 4 (TABLE ENTRY LENGTH)   08500000
         L     RBASE,BRTBL(RWORK2)      GET BRANCH ADDRESS              08550000
         BR    RBASE                    GO TO PROPER ROUTINE            08600000
         SPACE 2                                                        08650000
INVALID  EQU   *                                                        08700000
         LA    RCODE,RETCODE4           INDICATE INVALID ENTRY CODE     08750000
         BR    R14                      RETURN TO CALLER                08800000
         SPACE                                                          08850000
INVALID8 EQU   *                                                        08900000
         LA    RCODE,RETCODE8           SET RETURN CODE 8 FOR           08950000
         BR    R14                                RETURN                09000000
         EJECT                                                          09050000
*********************************************************************** 09100000
*        POSTTSB ROUTINE - USED TO POST THE TSB TO IEDAYP WHO UPDATES * 09150000
*                          AND/OR DOES TPOSTING OF THE QCB AND/OR TSI * 09200000
*                          THE QCB IS IN TCAMS PRIVATE MEMORY AND NOT * 09250000
*                          DIRECTLY ADDRESSABLE                       * 09300000
*             INPUT = REGISTER 1  = ADDRESSS OF MASK                  * 09350000
*                     REGISTER 3  = ADDRESS OF CVT                    * 09400000
*                     REGISTER 5  = ADDRESS OF TSB TO BE POSTED       * 09450000
*                     REGISTER 6-9 = WORK REGISTERS                   * 09500000
*********************************************************************** 09550000
POSTTSB  EQU   *                                                        09600000
         USING *,RBRANCH                ADDRESSABILITY                  09650000
         USING TSB,RTSB                 TSB ADDRESSABILITY              09700000
         USING CVT,RCVT                 CVT ADDRESSABILITY              09750000
         LM    RWORK6,RWORK7,TSBTPOST   GET CONTENTS OF TSBTPOST        09800000
*                                       FIELD FOR COMPARE               09850000
RETRY    EQU   *                                                        09900000
         LR    RWORK8,RWORK6            COPY 1ST WORD IN REG8           09950000
         LR    RWORK9,RWORK7            COPY 2ND WORD IN REG9           10000000
         O     RWORK8,CZERO(,RWORK1)    OR WITH MASK                    10050000
         N     RWORK8,CFOUR(,RWORK1)    AND IN VALUE                    10100000
         O     RWORK9,CEIGHT(,RWORK1)   OR WITH MASK WORD 2             10150000
         N     RWORK9,TWELVE(,RWORK1)   AND WITH VALUE WORD 2           10200000
         CDS   RWORK6,RWORK8,TSBTPOST   COMPARE & SWAP TSBTPOST         10250000
         BNE   RETRY                    FIELDS NOT EQUAL,RETRY          10300000
         LTR   RWORK6,RWORK6            IS POST PENDING?                10350000
         BM    GOBACK                   YES,SKIP POSTING                10400000
         L     RTCX,CVTAQAVT            GET ADDR OF TCX                 10450000
         USING IEDQTCX,RTCX             TCX ADDRESSABILITY              10500000
         L     RTSI,TCXTSI              GET ADDR OF TSI                 10550000
         USING IEDQTSI,RTSI             TSI ADDRESSABILITY              10600000
         LA    RWORK8,TSITSAP-(QCBSTVTO-IEDQQCB) DUMMY QCB              10650000
*                                        STARTING ADDR                  10700000
         ST    RWORK8,TSBRQCB           STORE IN QCB CHAIN              10750000
         LA    RWORK9,TSBRCB            ADDRESS OF RCB                  10800000
         L     RWORK8,TCXREADY          GET ADDR OF READY QUEUE         10850000
AGAIN    EQU   *                                                        10900000
         ST    RWORK8,TSBLINKA          STORE LINK ADDRESS              10950000
         MVI   TSBPRI,PRIDESTQ          STOR PRIORITY FIELD             11000000
         CS    RWORK8,RWORK9,TCXREADY   SWAP READY QUEUE ADDR           11050000
         BNE   AGAIN                    NOT EQUAL TRY AGAIN             11100000
         EJECT                                                          11150000
*********************************************************************** 11200000
*        XMPOST ROUTINE - USED TO WAKE UP TCAM DISPATCHER TO POST     * 11250000
*                         QCB. XMPOST IS DONE IN KEY ZERO WITH THE    * 11300000
*                         LOCAL AND CMS LOCKS HELD.                   * 11350000
*        REG 10 = ZERO                                                * 11400000
*        REG 11 = ADDR OF TCAM DISPATCHER'S ECB, HIGH ORDER BIT IS 1  * 11450000
*                 TO INDICATED XMPOST                                 * 11500000
*        REG 12 = ADDR OF CVTBRET FOR ERRET(ERROR RETURN)             * 11550000
*        REG 13 = ADDR OF TCAMS ASCB                                  * 11600000
*        REG 14 = RETURN ADDR                                         * 11650000
*        REG 15 = ADDR OF IEAOPT01 - XMPOST ENTRY POINT ROUTINE       * 11700000
*        REG 14 SAVED IN REG 1,REG11 SAVED IN REG 8,REG 10 SAVED IN   * 11750000
*        REG 6,REG 12 SAVED IN REG 7 AND REG 13 SAVED IN REG 9        * 11800000
*********************************************************************** 11850000
         MODESET EXTKEY=SUPR            NEED SUPERVISOR KEY FOR XMPOST  11900000
         LR    RWORK1,R14               SAVE RETURN ADDR                11950000
         LR    RWORK7,RBASE             SAVE REG 12                     12000000
         LA    RBASE,CVTBRET            E.P. IF POST FAILS              12050000
         LR    RWORK8,RWORK11           SAVE REG 11                     12100000
         LR    RWORK9,R13               SAVE REG 13                     12150000
         L     R13,TCXASCB              @ OF TCAM'S ASCB                12200000
         L     RWORK11,TCXAVT           ADDR OF AVT                     12250000
         DROP  RTCX                                                     12300000
         USING IEDQAVTD,RWORK11         ADDRESSABILITY TO AVT           12350000
         LA    RWORK11,AVTOSECB         @ OF TCAM'S ECB TO POST         12400000
         ICM   RWORK11,BYTE1,HIHBIT     HIGH ORDER BIT INDICATES        12450000
*                                       IT IS AN XMPOST                 12500000
         DROP  RWORK11                                                  12550000
         LR    RWORK6,RWORK10           SAVE REG 10                     12600000
         SR    RWORK10,RWORK10          ZERO RWORK10 FOR XMPOST         12650000
         DROP  RBRANCH                                                  12700000
         L     RBRANCH,CVT0PT01         @ OF E.P. FOR XMPOST            12750000
         BALR  R14,RBRANCH              BR TO XMPOST (IEAOPT01)         12800000
         LR    R14,RWORK1               .RESTORE                        12850000
         LR    RWORK10,RWORK6           .ALL                            12900000
         LR    RBASE,RWORK7             .REGISTERS                      12950000
         LR    RWORK11,RWORK8           .PREVIOUSLY                     13000000
         LR    R13,RWORK9               .SAVED                          13050000
         MODESET EXTKEY=TCAM            RESTORE TCAM'S KEY              13100000
GOBACK   EQU   *                                                        13150000
         BR    R14                      RETURN TO CALLER                13200000
         EJECT                                                          13250000
PAT      DC    13F'0'                   MAINTENANCE PATCH AREA          13300000
         SPACE                                                          13350000
         DS    0F                                                       13400000
CONST    EQU   *                                                        13450000
XZERO    DC    F'0'                     CONSTANT OF 0                   13500000
XNINES   DC    F'99999999'              CONSTANT OF 99,999,999          13550000
XFIFTEEN DC    AL1(15)                  CONSTANT OF 15                  13600000
XTWO     DC    XL1'02'                  CONSTANT OF 2                   13650000
XTEN     DC    H'10'                    CONSTANT OF 10                  13700000
XSIXTEEN DC    AL1(16)                  CONSTANT OF 16                  13750000
XENABLE  DC    X'FF'                    TO ENABLE INTERRUPTS            13800000
XSIX     DC    H'6'                     CONSTANT OF 6                   13850000
XEIGHT   DC    AL1(8)                   CONSTANT OF 8                   13900000
XCP5041  DC    XL3'026D16'              BKSP/UNDERSCORE CHAR. PROMPT    13950000
XHIGHBIT DC    XL1'80'                  TO TEST HIGH ORDER BITS         14000000
XCPTWX   DC    XL3'024B0D'              PERIOD/CAR.RETURN CHAR. PROMPT  14050000
         DS    0F                                                       14100000
XTPUT1   DC    XL4'0E000000'            TPUT,HOLD,CONTROL               14150000
XTPUT2   DC    XL4'0A000000'            TPUT,CONTROL                    14200000
XONE     DC    AL1(1)                   CONSTANT OF 1                   14250000
XIDLE    DC    X'17'                    TPUT IDLE CHARACTER             14300000
TPADDR   DC    A(POSTTSB)               TSB POSTING ROUTINE             14350000
TPMASK   DC    AL1(TSBPOSTO+TSBTPQCB,0,0,0) .MASK TO                    14400000
         DC    F'-1'                    .CAUSE IEDAYP                   14450000
         DC    AL1(0,0,QCBTPUT,QCBTPUT) .TO TPOST THE QCB               14500000
         DC    F'-1'                    .TO ITSELF                      14550000
T2       DC    AL1(2)                   CONSTANT OF 2 FOR CLM           14600000
T3       DC    AL1(3)                   CONSTANT OF 3 FOR CLM           14650000
T11      DC    AL1(11)                  CONSTANT OF 11 FOR CLM          14700000
T15      DC    AL1(15)                  CONSTANT OF 15 FOR CLM          14750000
T16      DC    AL1(16)                  CONSTANT OF 16 FOR CLM          14800000
T17      DC    AL1(17)                  CONSTANT OF 17 FOR CLM          14850000
T20      DC    AL1(20)                  CONSTANT OF 20 FOR CLM          14900000
HIHBIT   DC    XL1'80'                  HIGH ORDER BIT SETTING          14950000
         EJECT                                                          15000000
********                                                                15050000
******** BRANCH TABLE FOR FUNCTION SUPPORT ROUTINES                     15100000
********                                                                15150000
         USING IGG0940H+D2,RBASE                                        15200000
         DS    0F                       FULLWORD ALIGNMENT              15250000
BRTBL    EQU   *                                                        15300000
TCABEND  DC    V(IGG09400)              ENTRY CODE 0                    15350000
TCLEARQ  DC    A(IGG09401)              ENTRY CODE 1                    15400000
TSEND    DC    V(IGG09402)              ENTRY CODE 2                    15450000
MSGS     DC    V(IGG09403)              ENTRY CODE 3                    15500000
STBREAK  DC    V(IGG09404)              ENTRY CODE 4                    15550000
STCOM    DC    V(IGG09405)              ENTRY CODE 5                    15600000
STTIMEOU DC    V(IGG09406)              ENTRY CODE 6                    15650000
STCC     DC    V(IGG09407)              ENTRY CODE 7                    15700000
STATTN   DC    V(IGG09408)              ENTRY CODE 8                    15750000
STAUTOLN DC    V(IGG09409)              ENTRY CODE 9                    15800000
STSIZE   DC    V(IGG0940A)              ENTRY CODE 10                   15850000
GTSIZE   DC    V(IGG0940B)              ENTRY CODE 11                   15900000
STAUTOCP DC    V(IGG0940C)              ENTRY CODE 12                   15950000
STAUTOPT DC    V(IGG0940D)              ENTRY CODE 13                   16000000
RTAUTOPT DC    V(IGG0940E)              ENTRY CODE 14                   16050000
ENTRY15  DC    A(INVALID)               ENTRY CODE 15,INVALID           16100000
STCLEAR  DC    V(IGG0940G)              ENTRY CODE 16                   16150000
TSTGTTRM DC    V(IGG09411)              ENTRY CODE 17                   16200000
TSTFSMOD DC    A(INVALID8)              ENTRY CODE 18,INVALID           16250000
TSTLINEN DC    A(INVALID8)              ENTRY CODE 19,INVALID           16300000
TSTTMPMD DC    V(IGG09414)              ENTRY CODE 20                   16350000
         EJECT                                                          16400000
*********************************************************************** 16450000
* TCLEARQ - THE FUNCTION OF THIS SECTION IS TO PERFORM THE TCLEARQ    * 16500000
*           FUNCTION.  DEPENDING UPON THE PARAMETER PASSED, EITHER    * 16550000
*           INPUT OR OUTPUT BUFFER QUEUES ARE CLEARED.  FREED BUFFERS * 16600000
*           ARE REMOVED FROM THE TSB QUEUES AND RETURNED TO THE TIOC  * 16650000
*           FREE QUEUES .                                             * 16700000
*********************************************************************** 16750000
IGG09401 CSECT                                                          16800000
         USING CVT,RCVT                 CVT ADDRESSABILITY              16850000
         USING TCB,RTCB                 TCB ADDRESSABILITY              16900000
         USING TSB,RTSB                 TSB ADDRESSABILITY              16950000
         USING *,RBASE                  BASE ADDRESSABILITY             17000000
********                                                                17050000
******** MAIN LINE TCLEARQ                                              17100000
********                                                                17150000
         LR    RWORK2,RPARM1            SAVE PARM                       17200000
         LTR   RPARM1,RPARM1            TEST ENTRY CODE                 17250000
         BP    PARMBAD                  ONLY NEGATIVE OR ZERO VALID     17300000
         BZ    FREE                     GO PROCESS OUTPUT QUES          17350000
         SPACE 1                                                        17400000
*********************************************************************** 17450000
*        DISALLOW TERMINAL INPUT UNTIL NEXT TGET IN ORDER             * 17500000
*        TO SYNCHRONIZE WITH THE USER.                                * 17550000
*********************************************************************** 17600000
         SPACE 1                                                        17650000
INPUT    EQU   *                                                        17700000
         MODESET EXTKEY=TCAM            GET TCAMS KEY FOR TSB           17750000
*                                       UPDATE                          17800000
         NI    TSBFLG2,OFF-TSBBRKIN     DISALLOW BREAKIN IN PROGRESS    17850000
         OI    TSBSTAT,TSBLWAIT         LOCK KEYBOARD                   17900000
         OI    TSBFLG1,TSBIFLSH         TELL TSINPUT TO FLUSH INPUT     17950000
         OI    TSBFLG3,TSBSPIT          STOP AUTOPROMPTING              18000000
         LR    RWORK10,RLINK            SAVE RETURN REGISTER            18050000
         L     RWORK1,M1                PASS ADDR OF MASK FLD           18100000
         L     RBRANCH,POST             ADDR OF POSTTSB ROUTINE         18150000
         BALR  RLINK,RBRANCH            GO POST TSB                     18200000
         LR    RLINK,RWORK10            RESTORE RETURN REG              18250000
         EJECT                                                          18300000
*********************************************************************** 18350000
*        FREE CMS AND LOCAL LOCKS                                     * 18400000
*********************************************************************** 18450000
         SPACE 1                                                        18500000
FREE     EQU   *                                                        18550000
         LR    RWORK7,RLINK             SAVE RETURN ADDR                18600000
         LR    RWORK10,RBASE            SAVE BASE                       18650000
         MODESET EXTKEY=SUPR            RETURN TO SUPR KEY              18700000
         SETLOCK RELEASE,TYPE=CMS,      FREE CMS LOCK                  X18750000
               RELATED=(TSB,IGC0009D(GETLKS))                           18800000
         SETLOCK RELEASE,TYPE=LOCAL,    FREE LOCAL LOCK                X18850000
               RELATED=(TSB,IGC0009D(GETLKS))                           18900000
         LR    RBASE,RWORK10            RESTORE BASE                    18950000
         LR    RLINK,RWORK7             RESTORE RETURN ADDR             19000000
         SPACE 1                                                        19050000
*********                                                               19100000
********* ISSUE QTIP SVC TO INVOKE MODULE IEDAYAA FOR QUEUE CLEARING    19150000
*********                                                               19200000
         SPACE 1                                                        19250000
         LR    R15,RTSB                 PUT TSB ADDR IN REG 15          19300000
         LTR   RWORK2,RWORK2            TEST ENTRY CODE                 19350000
         BM    QTIPINPT                 ISSUE QTIP FOR INPUT            19400000
         SPACE                                                          19450000
         QTIP  QOUTEC                   QTIP FOR OUTPUT                 19500000
         B     LEAVE                    RETURN TO CALLER                19550000
         SPACE                                                          19600000
QTIPINPT EQU   *                                                        19650000
         QTIP QINEC                     QTIP FOR INPUT                  19700000
LEAVE    EQU   *                                                        19750000
         SR    RCODE,RCODE              SET RETURN CODE ZERO            19800000
         BR    R14                      RETURN TO CALLER                19850000
         SPACE                                                          19900000
PARMBAD  EQU   *                                                        19950000
         LA    RCODE,RETCODE4           INDICATE INVALID ENTRY CODE     20000000
         BR    R14                      RETURN TO CALLER                20050000
         EJECT                                                          20100000
ZAP      DS    9F'0'                    MAINTENANCE PATCH AREA          20150000
         SPACE                                                          20200000
POST     DC    A(POSTTSB)               POSTTSB ADDR                    20250000
MASKD1   DC    AL1(TSBPOSTO+TSBTPQCB,0,0,0) .ALLOW                      20300000
         DC    AL1(255,255,255,255)     .NO                             20350000
         DC    AL1(0,0,QCBNOBUF+QCBTPUT,QCBTPUT)    .TERMINAL           20400000
         DC    AL1(255,255,255,255-QCBNOBUF) .INPUT                     20450000
         SPACE                                                          20500000
M1       DC    A(MASKD1)                ADDR OF MASKD1                  20550000
         SPACE 4                                                        20600000
CONSTANT DSECT                                                          20650000
ZERO     DC    F'0'                     CONSTANT OF 0                   20700000
NINES    DC    F'99999999'              CONSTANT OF 99,999,999          20750000
FIFTEEN  DC    AL1(15)                  CONSTANT OF 15                  20800000
TWO      DC    XL1'02'                  CONSTANT OF 2                   20850000
TEN      DC    H'10'                    CONSTANT OF 10                  20900000
SIXTEEN  DC    AL1(16)                  CONSTANT OF 16                  20950000
ENABLE   DC    X'FF'                    TO ENABLE INTERRUPTS            21000000
SIX      DC    H'6'                     CONSTANT OF 6                   21050000
EIGHT    DC    AL1(8)                   CONSTANT OF 8                   21100000
CP5041   DC    XL3'026D16'              BKSP/UNDERSCORE CHAR. PROMPT    21150000
HIGHBIT  DC    XL1'80'                  TO TEST HIGH ORDER BITS         21200000
CPTWX    DC    XL3'024B0D'              PERIOD/CAR.RETURN CHAR. PROMPT  21250000
TPUT1    DC    XL4'0E000000'            TPUT,HOLD,CONTROL               21300000
TPUT2    DC    XL4'0A000000'            TPUT,CONTROL                    21350000
ONE      DC    AL1(1)                   CONSTANT OF ONE                 21400000
IDLE     DC    X'17'                    TPUT IDLE CHARACTER             21450000
         EJECT                                                          21500000
         IHAASCB                                                        21550000
         EJECT                                                          21600000
         TAVTD                                                          21650000
         EJECT                                                          21700000
CVT      DSECT                                                          21750000
         CVT                                                            21800000
         EJECT                                                          21850000
         TPRIOR                                                         21900000
         EJECT                                                          21950000
         IHAPSA                                                         22000000
         EJECT                                                          22050000
         TQCBD                                                          22100000
         EJECT                                                          22150000
         IKJTCB                                                         22200000
         EJECT                                                          22250000
         TTCXD                                                          22300000
         EJECT                                                          22350000
         IKJTSB                                                         22400000
         EJECT                                                          22450000
         TTSID                                                          22500000
         END                                                            22550000
