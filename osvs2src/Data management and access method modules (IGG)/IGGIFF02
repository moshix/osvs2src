         MACRO                                                          00050002
&LABEL   IGGIFF02 &DEB,&REG,&DEBD,&UCBD,&LANG                           00100002
         EJECT                                                          00102002
* STATUS -- CHANGE LEVEL 0                                              00110002
*                                                                       00120002
* FUNCTION -- GAM FORCE CLOSE MACRO                                     00132002
*     PURPOSE IS TO CLEAN UP IF A CLOSE FOR A GRAPHIC DEVICE            00134002
*     FAILS SO THAT THE DEVICE CAN BE USED AGAIN                        00136002
*     1) NOP IF DEB IS NOT A GAM DEB                                    00140002
*     2) IF A GAM DEB                                                   00142002
*        A. CLEAN UP THE UCBS FOR THE DEB (ZEROS THE                    00142402
*        USE COUNT BYTE, GRAPHIC CONTROL BYTE, AND TASK ENTRY           00144002
*        BLOCK POINTER)                                                 00146002
*        B. FREE THE TASK ENTRY BLOCK (TEB)                             00146402
*                                                                       00148402
* ENTRY POINT -- NONE - GENERATED AS INLINE CODE                        00148802
*                                                                       00149202
* INPUT                                                                 00149602
*     EXECUTION                                                         00149702
*     1) DEB POINTER                                                    00149802
*                                                                       00170302
*     MACRO OPERANDS (POSITIONAL)                                       00193502
*     1) SYMBOL OR NUMBER OF A REGISTER CONTAINING THE DEB              00214002
*        POINTER (2-13)                                                 00234502
*     2) SYMBOL OR NUMBER OF A REGISTER THAT CAN BE USED BY             00255002
*        THE MACRO AS A WORK REGISTER (2-13)                            00275502
*     3) Y OR N (N IS THE DEFAULT)                                      00296002
*        Y, THE CALLER'S CODE CONTAINS THE DEB DSECT                    00316502
*        N, MACRO WILL GENERATE THE DEB DSECT                           00337002
*     4) Y OR N (N IS THE DEFAULT)                                      00357502
*        Y, THE CALLER'S CODE CONTAINS THE UCB DSECT                    00378002
*        N, MACRO WILL GENERATE THE UCB DSECT                           00398502
*     5) PLS OR BAL (BAL IS THE DEFAULT)                                00408502
*        PLS, THE CALLER IS CODED IN PLS                                00419002
*        BAL, THE CALLER IS CODED IN BAL                                00429002
*                                                                       00439002
* OUTPUT                                                                00439502
*     1) CLEANED UP UCB                                                 00460002
*     2) TEB FREED                                                      00480502
*     3) DEB POINTER IN INPUT REG                                       00501002
*     4) CONTENTS OF REGS 0,1,14,15, WORK REG DESTROYED                 00521502
*                                                                       00542002
* EXTERNAL REFERENCES -- FREEMAIN                                       00562502
*                                                                       00583002
* EXITS -- FALL OUT OF INLINE CODE                                      00603502
*                                                                       00624002
* TABLES/WORK AREAS -- DEB(READ-ONLY), UCB                              00644502
*                                                                       00665002
* ATTRIBUTES -- REFRESHABLE                                             00685502
*                                                                       00706002
* NOTES                                                                 00726502
.*    -) THE METHOD OF ADDRESSING THE DEB AND UCB CONTROL               00736502
.*       BLOCKS WAS CHOSEN TO ENABLE GENERATION OF THE SAME             00746502
.*       CODE IN A BAL, PLS/1, OR PLS/2 PROGRAM, AS MUCH                00746902
.*       AS POSSIBLE                                                    00753702
*     1) IF ANY DSECT IS GENERATED BY THIS MACRO, THE CALLER            00760702
*        MUST REESTABLISH HIS CSECT AFTER THE MACRO                     00767502
         SPACE 3                                                        00788002
* REGISTER EQUATES                                                      00808502
IGGR0    EQU   0 -                 REGISTER 0                           00829002
IGGR1    EQU   1 -                 REGISTER 1                           00849502
IGGUCB   EQU   1 -                 UCB POINTER                          00870002
IGGR14   EQU   14 -                REGISTER 14                          00890502
IGGDEBAP EQU   14 -                DEB APPENDAGE POINTER                00911002
IGGDEBIN EQU   14 -                DEB INDEX                            00931502
IGGTEBUC EQU   14 -                TEB USE COUNT                        00952002
IGGR15   EQU   15 -                REGISTER 15                          00972502
IGGTEB   EQU   15 -                TEB POINTER                          00993002
         SPACE 2                                                        01013502
.TSTGDEB ANOP                                                           01034002
* IF THE DEB IS NOT A GRAPHIC DEB, CONTROL IS RETURNED TO THE           01040002
* CALLER                                                                01090002
&LABEL   L     IGGDEBAP,DEBAPPAD-DEBBASIC(&DEB)  GET ADDRESS OF         01140002
*                                       DEB APPENDAGE VECTOR            01150002
*                                       TABLE                           01160002
         CLI   DEBAMTYP-DEBAVT(IGGDEBAP),IGGGAM  Q. GAM                 01170002
         BNE   IGGOUT -            NO, RETURN TO CALLER                 01190002
         SPACE 3                                                        01240002
* CLEAN UP ALL UCB'S ASSOCIATED WITH THE DEB                            01250002
* FREE UP ANY TEB'S NO LONGER IN USE                                    01260002
         SR    &REG,&REG -         CLEAR REG                            01270002
         IC    &REG,DEBNMEXT-DEBBASIC(&DEB) GET THE NUMBER OF           01280002
*                                       UCB(S) ASSOCIATED WITH          01284002
*                                       THE DEB                         01284402
         SPACE                                                          01286002
IGGNXUCB EQU   *                   LOOP THROUGH UCB'S                   01288002
* THE ADDRESSES OF THE UCB'S TO BE RESET ARE PICKED UP FROM THE         01288102
* EXTENTS IN THE GAM DEPENDENT SECTION OF THE DEB                       01288202
* (THE LOOP STARTS WITH THE LAST EXTENT AND WORKS BACK)                 01288302
         LR    IGGDEBIN,&REG -     ESTABLISH ADDRESS OF A UCB           01288402
         BCTR  IGGDEBIN,IGG0 -          TO BE RESET FROM THE            01288802
         SLL   IGGDEBIN,IGGEXSCL -      NEXT EXTENT IN THE              01289202
         L     IGGUCB,DEBBASND-DEBBASIC(IGGDEBIN,&DEB)  DEB             01291202
         AIF   ('&LANG' EQ 'BAL' OR T'&LANG EQ 'O').BALCODE  IF         01293202
.*                                      &LANG IS OMITTED OR BAL         01293602
.*                                      GENERATE CODE FOR BAL           01294002
         AIF   ('&LANG' NE 'PLS').ERRLANG  IF &LANG IS NOT PLS          01295202
.*                                      GENERATE ERROR MSG              01295302
.PLSCODE ANOP                                                           01295602
         L     IGGTEB,UCBTEB-UCBOBS04+UCBDEV(IGGUCB)  GET TEB ADDRESS   01296502
         XC    UCBOPEN-UCBOBS04+UCBDEV(IGG6,IGGUCB),UCBOPEN-UCBOBS04+UC*01299902
               BDEV(IGGUCB) -      CLEAR UCBOPEN, UCBGCB, AND          *01300302
                                        UCBTEB FIELDS                   01300702
         AGO   .CONT                                                    01302702
.BALCODE ANOP                                                           01304702
         L     IGGTEB,UCBTEB-UCBOB(IGGUCB)  GET TEB ADDRESS             01306702
         XC    UCBOPEN-UCBOB(IGG6,IGGUCB),UCBOPEN-UCBOB(IGGUCB)         01309102
*                                       CLEAR UCBOPEN,UCBGCB,           01309502
*                                       AND UCBTEB FIELDS               01309602
.CONT    ANOP                                                           01309702
         LTR   IGGTEB,IGGTEB -     Q. DOES TEB EXIST                    01309802
         BZ    IGGENDLP -          NO TEB, BRANCH OVER                  01311702
*                                       FREEMAIN                        01313602
         L     IGGTEBUC,IGGUSECT(IGGTEB)  GET TEB USE COUNT             01316602
         BCTR  IGGTEBUC,IGG0 -     DECREMENT TEB USE COUNT              01320602
         LTR   IGGTEBUC,IGGTEBUC - Q. IS USE COUNT ZERO                 01322602
         BNZ   IGGENDLP -          NO, BRANCH OVER FREEMAIN             01323002
IGGFRTEB EQU   *                   ISSUE FREEMAIN FOR TEB               01323102
         LR    IGGR1,IGGTEB -      LOAD TEB ADDRESS                     01325002
         L     IGGR0,IGGTEBLN -    LOAD LENGTH AND SP                   01328802
         FREEMAIN R,LV=(0),A=(1) - FREE TEB CORE                        01330802
IGGENDLP EQU   *                                                        01332802
         BCT   &REG,IGGNXUCB -     LOOP THROUGH UCBS                    01333202
         B     IGGOUT -            EXIT                                 01334502
         SPACE 3                                                        01336402
* CONSTANTS                                                             01350402
         DS    0F                                                       01350802
IGGTEBLN DC    AL1(235) -          TEB SUBPOOL                          01351202
         DC    AL3(32) -           TEB LENGTH                           01351602
IGG0     EQU   0 -                 ZERO                                 01351702
IGG6     EQU   6 -                 LENGTH OF UCBOPEN+UCBGCB+UCBTEB      01356202
IGGGAM   EQU   X'08' -             ACCESS METHOD CODE FOR GAM           01361402
IGGEXSCL EQU   X'02' -             EXTENT SCALE VALUE FOR               01365902
*                                       GRAPHIC DEB'S                   01370402
IGGUSECT EQU   16 -                OFFSET OF TEB USE COUNT              01374902
         SPACE 2                                                        01379402
* EXIT                                                                  01383902
IGGOUT EQU *                                                            01388402
         AIF   (T'&DEBD EQ 'O').DEBD  IF &DEBD IS OMITTED OR N          01392902
.*                                      GENERATE DEB DSECT              01397402
         AIF   ('&DEBD' EQ 'Y').TSTUCBD  IF &DEBD = Y, DEB DSECT        01401902
.*                                      IS IN CALLER                    01406402
         AIF   ('&DEBD' NE 'N').ERRDEBD  ANYTHING OTHER THAN Y          01410902
.*                                      OR N IS AN ERROR                01415402
.DEBD    ANOP                                                           01419902
         EJECT                                                          01421902
IGGDEBD  IEZDEB                                                         01429902
.TSTUCBD ANOP                                                           01460302
         AIF   (T'&UCBD EQ 'O').UCBD  IF &UCBD IS OMITTED OR N          01461902
.*                                      GENERATE UCB DSECT              01463902
         AIF   ('&UCBD' EQ 'Y').OUT  IF &UCBD = Y, UCB DSECT            01465902
.*                                      IS IN CALLER                    01467902
         AIF   ('&UCBD' NE 'N').ERRUCBD  ANYTHING OTHER THAN Y          01468302
.*                                      OR N IS AN ERROR                01468702
.UCBD    ANOP                                                           01469102
         EJECT                                                          01469202
IGGUCBD  DSECT                                                          01469502
         IEFUCBOB                                                       01469602
         AGO   .OUT                                                     01469702
.* ERROR MESSAGES                                                       01469902
.ERRLANG ANOP                                                           01471902
         SPACE 3                                                        01473902
         MNOTE 1,'INVALID LANGUAGE OPERAND'                             01475902
         SPACE 3                                                        01477902
         AGO   .BALCODE                                                 01478302
.ERRDEBD ANOP                                                           01479902
         SPACE 3                                                        01489902
         MNOTE 1,'INVALID DEB DSECT OPERAND'                            01519902
         AGO   .TSTUCBD                                                 01569902
.ERRUCBD ANOP                                                           01579902
         SPACE 3                                                        01589902
         MNOTE 1,'INVALID UCB DSECT OPERAND'                            01619902
.OUT     ANOP                                                           01679902
         EJECT                                                          01689902
         MEND                                                           01719902
