*TITLE 'GRAPHIC ATTENTION ANALYSIS ROUTINE'                             00020002
*STATUS:CHANGE LEVEL 0                                                  00040002
*FUNCTION/OPERATION:THIS ROUTINE DETERMINES IF AN ATTENTION HAS OCCURED 00060002
*   BY POLLING BITS 6 AND 7 IN THE INDIVIDUAL GRAPHIC CONTROL BYTES     00080002
*   (GCB).THIS ROUTINE DETERMINES THE TYPE OF ATTENTION THAT OCCURED    00100002
*   BY USE OF GCB BITS 6 AND 7 AND THE SENSE BYTES IN THE UNIT CONTROL  00120002
*   BLOCK(UCB).A UNIQUE RETURN CODE FOR EACH TYPE OF ATTENTION IE.LITE  00140002
*   PEN,KEYBOARD,END ORDER SEQUENCE OR ASYNCH.ERROR IS PUT IN GR#15.    00160002
*   CODES ARE PLACED IN GR#15 FOR PARAMETERS MISSING,WORD 5 IN TABLE    00180002
*   OF ADDRESSES MISSING OR NO ATTENTIONS OCCURED.WHEN RETURN IS        00200002
*   MADE TO THE CALLING PROGRAM THE REST OF THE CODING GENERATED BY     00220002
*   THE ANALYZ MACRO DECODES GR#15 AND EITHER PASSES CONTROL TO A USER  00240002
*   ATTENTION ROUTINE OR GENERATES A RETURN CODE TO PLACE IN GR#15      00260002
*   AS PER THE SRL.REGISTERS 1 THRU 12 ARE SAVED ON ENTRY AND RESTORED  00280002
*   UPON EXIT.SAVE AREA POINTED TO BY GR#13 AS PER CONVENTIONS.         00300002
*ENTRY POINTS:IFFANA ALIAS ANLZ.POLL FOR AN ATTENTION.                  00320002
*       L    15,ADDR                                                    00340002
*       BALR 14,15                                                      00360002
*               X                                                       00380002
*               X                                                       00400002
*   ADR DC   V(ANLZ)                                                    00420002
*               X                                                       00440002
*               X                                                       00460002
*               X                                                       00480002
*INPUT:GR#1 CONTAINS ADR OF PARAMETER LIST WHICH IS CONSTRUCTED AS      00500002
*   FOLLOWS:                                                            00520002
*       ******************                                              00540002
*   LIST* DCBLST ADDRESS *(0)                                           00560002
*       ******************                                              00580002
*       * POINTR ADDRESS *(4)                                           00600002
*       ******************                                              00620002
*       * RESERVED       *(8)                                           00640002
*       ******************                                              00660002
*OUTPUT:GR#0 CONTAINS ADDRESS OF OUTPUT AREA(IN CASE OF MULTIPLE OUTPUT 00680002
*   AREAS DEFINED BY MULTIPLE ANALYZ MACROS IN A SINGLE ASSEMBLY).      00700002
*   OUTPUT AREA IS AS FOLLOWS:                                          00720002
*       ****************                                                00740002
*   OUT * SENSE INFO   * (0)                                            00760002
*       ****************                                                00780002
*       * IX * DCB ADR * (4)                                            00800002
*       ****************                                                00820002
*         IX=VALID USE ONLY FOR MULTIPLE 2260S ASSIGNED TO A SINGLE     00840002
*   DATA CONTROL BLOCK(DCB).GIVES THE NUMBER OF THE UCB ADR EXTENT TO   00860002
*   THE DATA EXTENT BLOCK IE 0,1,2 ETC.                                 00880002
*   SENSE INFO VALID ONLY FOR LITE PEN,END ORDER SEQUENCE AND ASYNCH.   00900002
*   ERROR ATTENTIONS.                                                   00920002
*   GR#15 CONTAINS RETURN CODES AS DESCRIBED IN EXITS NORMAL AND ERROR. 00940002
*   RETURN CODES ARE FOR DECODING BY ANALYZ MACRO EXPANSION.            00960002
*EXTERNAL ROUTINES:N/A                                                  00980002
*EXITS NORMAL:IN ALL CASES CONTROL IS RETURNED TO THE ANALYZ MACRO      01000002
*   EXPANSION CODING IN THE CALLING PROG.WITH A RETURN CODE IN GR#15.   01020002
*   THE RETURN CODES ARE AS FOLLOWS:                                    01040002
*   LITE PEN=00               X                                         01060002
*   KEYBOARD(AN OR PF)=04     X                                         01080002
*   ASYNCHRONOUS ERROR=08     XXCODES ARE IN HEX                        01100002
*   END ORDER SEQUENCE=0C     X                                         01120002
*   NO ATTENTIONS PRESENT=10  X                                         01140002
*   LA  15,RETURN CODE                                                  01160002
*   BR  14                                                              01180002
*EXITS-ERROR:IN ALL CASES CONTROL IS RETURNED TO THE ANALYZ MACRO       01200002
*   EXPANSION CODING IN THE CALLING PROG.WITH A RETURN CODE IN GR#15.   01220002
*   THE RETURN CODES ARE AS FOLLOWS:                                    01240002
*   PARAMETER MISSING=18                      XCODES ARE IN HEX         01260002
*   WORD 5 IN ADDRESS TABLE(POINTR)MISSING=20 X                         01280002
*   LA  15,RETURN CODE                                                  01300002
*   BR  14                                                              01320002
*TABLES/WORK AREAS:A TABLE OF USER ATTENTION ROUTINE ADDRS. AS POINTED  01340002
*   TO BY THE POINTR PARAMETER OF THE ANALYZ MACRO.IT IS CONSTRUCTED AS 01360002
*   FOLLOWS.                                                            01380002
*   **************************                                          01400002
*   * LITE PEN ADR           *(0)                                       01420002
*   **************************                                          01440002
*   * KEYBOARD ADR           *(4)                                       01460002
*   **************************                                          01480002
*   * ASYNCHRONOUS ERROR ADR *(8)                                       01500002
*   **************************                                          01520002
*   * END ORDER SEQUENCE ADR *(12)                                      01540002
*   **************************                                          01560002
*  * OUTPUT AREA ADR         *(16)  OUTPUT AREA CONSTRUCTED AS SHOWN    01580002
*   **************************      IN OUTPUT HEADING ABOVE.            01600002
*ATTRIBUTES:RE-ENTRANT                                                  01620002
*NOTES:THE RETURN CODES AS GENERATED BY THIS ROUTINE ARE FOR INTERNAL   01640002
*   USE ONLY AND ARE NOT THE RETURN CODES LOGICALLY PRESENTED TO THE    01660002
*   CALLING PROBLEM PROGRAM.THE RETURN CODES LOGICALLY PRESENTED  TO    01680002
*   THE PROBLEM PROGRAM ARE AS FOLLOWS:                                 01700002
*   NORMAL RETURN=00                                  X                 01720002
*   NO ATTENTIONS=04                                  X                 01740002
*   ATTENTION OCCURED BUT NO USER ROUTINE PROVIDED 08 XCODES ARE IN HEX 01760002
*   PARAMETER MISSING=0C                              X                 01780002
*   WORD 5 IN ADDRESS TABLE MISSING=10                X                 01800002
*   IT IS THE RESPONSIBILITY OF THE USER ATTENTION ROUTINES TO SAVE AND 01820002
*   RESTORE REGISTERS ONCE CONTROL IS GIVEN TO THEM.                    01840002
*                                                                       01860002
*        REGISTER DEFINITION                                            01880002
REG0     EQU   0                       PARAM REGISTER                   01900002
LISTRG   EQU   1                        POINTER TO PARAMETER LIST       01920002
BASERG   EQU   2                        BASE REGISTER                   01940002
TABREG   EQU   3                        POINTER TO TABLE                01960002
DCBREG   EQU   4                        POINTER TO DCB LIST             01980002
INDXRG   EQU   5                        INDEX REGISTER                  02000002
GBREG1   EQU   6                        WORK REG                        02020002
GBREG2   EQU   7                        WORK REG                        02040002
GBREG3   EQU   8                        WORK REG                        02060002
GBREG4   EQU   9                        WORK REG                        02080002
GBREG5   EQU   10                       WORK REG                        02100002
GBREG6   EQU   11                       WORK REG                        02120002
GBREG7   EQU   12                       WORK REG                        02140002
SAVEREG  EQU   13                      SAVE AREA REGISTER               02160002
RETREG   EQU   14                       RETURN REGISTER                 02180002
BRNCHREG EQU   15                      BRANCH TO ADDRESS REGISTER       02200002
ERCODE   EQU   15                      RETURN CODE REGISTER             02220002
*                                                                       02240002
UCBADR   EQU   32                       CONSTANT TO ACCESS START OF     02260002
*                                       UCB LIST IN DEB                 02280002
DEBADR   EQU   44                      CONSTANT TO ACCESS DEB ADDRESS   02300002
SENSE    EQU   32                       SENSE DATA            LF YM4067 02320002
GCBADR   EQU   27                       DISPLACEMENT CONSTANT TO GIVE   02340002
*                                       GCB BYTE AT END OF UCB          02360002
GASR     EQU   70                      GSERV SVC ID                     02380002
*                                                                       02400002
IFFANA   CSECT                                                          02420002
*C023200                                                      LF YM4067 02422002
* 040600,041400,042200,025800                                    S21016 02430002
         ENTRY ANLZ                                                     02440002
ANLZ     STM   LISTRG,GBREG7,24(SAVEREG) SAVE REGISTERS CONTENTS        02460002
         BALR  BASERG,0                 SET UP BASE REGISTER            02480002
         USING *,BASERG                 DEFINE BASE REGISTER            02500002
RECYCLE  CLC   1(3,LISTRG),ZERO        TEST FOR ZERO IN DCBLST PARAM    02520002
         BC    8,EXIT1                  BRANCH IF ZERO                  02540002
         CLC   5(3,LISTRG),ZERO        TEST FOR ZERO IN POINTR PARAM    02560002
         BE    EXIT6                   BRANCH IF ZERO            S21016 02580002
         L     TABREG,4(0,LISTRG)       LOAD POINTER PARAMETER          02600002
         L     DCBREG,0(0,LISTRG)       LOAD DCB LIST POINTER           02620002
         LA    DCBREG,0(0,DCBREG)      ZERO HIGH ORDER BYTE             02640002
         CLC   9(3,LISTRG),ZERO        IS RESUME PTR ZERO               02660002
         BC    8,PTR0                   BRANCH IF YES                   02680002
         CLC   9(3,LISTRG),1(DCBREG)   IS RESUME PTR GREATER THAN       02700002
*                                       LAST DCBLST ENTRY ADDRESS       02720002
         BC    2,PTR0                   BRANCH IF RESUME PTR GREATER    02740002
         L     DCBREG,8(0,LISTRG)       LOAD RESUME PTR                 02760002
         LA    DCBREG,0(0,DCBREG)      ZERO HIGH ORDER BYTE             02780002
         BC    15,IDCHEK                BRANCH                          02800002
PTR0     LA    DCBREG,4(0,DCBREG)      INCREMENT ADDRESS                02820002
         ST    DCBREG,8(0,LISTRG)       STORE ADDRESS IN RESUME PTR     02840002
IDCHEK   CLC   17(3,TABREG),ZERO       TEST FOR ZERO IN TABLE WORD 5    02860002
         BC    8,EXIT3                  BRANCH IF ZERO                  02880002
BACK     L     GBREG1,0(DCBREG)         LOAD DCB ADDRESS                02900002
         L     GBREG1,DEBADR(GBREG1)    LOAD DEB ADDRESS                02920002
         L     INDXRG,0(DCBREG)         LOAD INDEX FACTOR FOR UCB ADR   02940002
*                                       LOCATED IN DEB                  02960002
         SRL   INDXRG,24                LOCATE INDEX FOR MULTIPLICATION 02980002
         LA    GBREG3,0                 ZERO EVEN REGISTER              03000002
         LA    GBREG4,4                LOAD MULTIPLICAND                03020002
         MR    GBREG3,INDXRG            DETERMINE DEB DISPLACEMENT      03040002
*                                       FACTOR FOR PROPER UCB ADR.      03060002
*                                       PRODUCT IS IN GBREG3 AND GBREG4 03080002
         L     GBREG2,UCBADR(GBREG4,GBREG1)  LOAD UCB ADDRESS           03100002
         TM    GCBADR(GBREG2),X'03'     TEST FOR POSTED GCB             03120002
         BC    5,POSTED                 BRANCH IF EITHER OR BOTH        03140002
*                                       BITS POSTED.                    03160002
         L     GBREG3,0(0,LISTRG)       LOAD DCB LIST PTR               03180002
         L     GBREG3,0(0,GBREG3)      LOAD END OF LIST POINTER         03200002
         LA    GBREG3,0(0,GBREG3)      ZERO HIGH ORDER BYTE             03220002
         CLR   GBREG3,DCBREG           COMPARE PRESENR DCBLST POINTER   03240002
*                                      WITH LAST DCBLST ADDRESS         03260002
         BC    8,LAST1                  BRANCH IF PRESENT EQUAL TO      03280002
*                                       LAST IN LIST                    03300002
         LA    DCBREG,4(0,DCBREG)      INCREMENT POINTER                03320002
COMPAR   L     GBREG6,8(0,LISTRG)      LOAD RESUME PTR                  03340002
         LA    GBREG6,0(0,GBREG6)      ZERO HIGH ORDER BYTE             03360002
         CLR   DCBREG,GBREG6           COMPARE TO SEE IF COMPLETE       03380002
*                                      LIST WAS POLLED                  03400002
         BC    8,EXIT4                  NO GCBS POSTED                  03420002
         BC    15,BACK                                                  03440002
LAST1    L     DCBREG,0(0,LISTRG)       LOAD DCB LIST POINTER           03460002
         LA    DCBREG,4(0,DCBREG)      INCREMENT POINTER                03480002
         BC    15,COMPAR                BRANCH                          03500002
POSTED   LA    GBREG5,4(0,DCBREG)      BUMP PTR TO SERVICE NEXT DCB     03520002
*                                      UPON RE-ENTRY                    03540002
         ST    GBREG5,8(0,LISTRG)      STORE ADR OF NEXT DCBLST ENTRY   03560002
*                                      TO SERVICE IN RESUME PTR         03580002
         L     GBREG5,16(0,TABREG)     LOAD OUTPUT AREA ADDRESS         03600002
         MVC   0(4,GBREG5),SENSE(GBREG2)  MOVE SENSE DATA TO OUTPUT     03620002
         L     GBREG3,0(0,DCBREG)       LOAD DCB ADDRESS                03640002
         ST    GBREG3,4(0,GBREG5)      STORE DCB ADDRESS IN OUTPUT      03660002
         TM    GCBADR(GBREG2),X'01'     TEST FOR KEYBOARD ATTENTION     03680002
         BC    1,TEST0                  BRANCH IF KYBD.ATTN.            03700002
         LA    LISTRG,2                LOAD BIT 6 RESET MASK            03720002
         SLL   LISTRG,24               POSITION BIT MASK                03740002
         AR    LISTRG,DCBREG           ADD DCBLST ADDRESS TO PARAMETER  03760002
         SVC   GASR                    RESET BIT 6 IN GCB               03780002
         LR    REG0,GBREG5             OUTPUT AREA ADDRESS TO REG 0     03800002
         BC    15,NOTKEY                                                03820002
TEST0    LA    LISTRG,1                LOAD BIT 7 RESET MASK            03840002
         SLL   LISTRG,24               POSITION BIT MASK                03860002
         AR    LISTRG,DCBREG           ADD DCBLST ADDRESS TO PARAMETER  03880002
         SVC   GASR                    RESET BIT 7 IN GCB               03900002
         LR    REG0,GBREG5             OUTPUT AREA ADDRESS TO REG 0     03920002
         BC    15,EXIT2                BRANCH                           03940002
NOTKEY   TM    1(GBREG5),X'40'         TEST FOR END OF ORDERS SEQUENCE  03960002
         BC    1,EOS                    BRANCH IF SET                   03980002
         TM    1(GBREG5),X'80'         TEST FOR LITE PEN ATTENTION      04000002
         BC    1,LPA                    BRANCH IF SET                   04020002
         BC    15,EXIT5                BRANCH                           04040002
EXIT1    EQU   *                                                 S21016 04048002
         WTO   'IFF401I ANALYZ FOUND NO POLST ADDRESS IN PARAMETER LISTX04056002
               ',ROUTCDE=11,DESC=7                               S21016 04064002
         LA    ERCODE,24               POLST PARAM MISSING              04072002
         B     LPA+4                   BRANCH                           04080002
EXIT2    LA    ERCODE,4                KYBD                             04100002
         B     LPA+4                   BRANCH                           04120002
EXIT3    EQU   *                                                 S21016 04128002
         WTO   'IFF403I ANALYZ FOUND NO OUTPUT AREA ADDRESS IN ATTENTIOX04136002
               N ROUTINE LIST',ROUTCDE=11,DESC=7                 S21016 04144002
         LA    ERCODE,32               OUTPUT AREA ADDR MISSING  S21016 04152002
         B     LPA+4                   BRANCH                           04160002
EXIT4    LA    ERCODE,16               NONE POSTED                      04180002
         B     LPA+4                   BRANCH                           04200002
EXIT5    EQU   *                                                 S21016 04208002
         WTO   'IFF404I ANALYZ DETERMINED AN ASYNCHRONOUS ERROR ATTENTIX04216002
               ON OCCURRED',ROUTCDE=11,DESC=7                    S21016 04224002
         LA    ERCODE,8                                                 04232002
         B     LPA+4                   BRANCH                           04240002
EXIT6    EQU   *                                                 S21016 04243002
         WTO   'IFF402I ANALYZ FOUND NO ATTENTION ROUTINE LIST',ROUTCDEX04246002
               =11,DESC=7                                        S21016 04249002
         LA    ERCODE,24                                         S21016 04252002
         B     LPA+4                   GO RETURN                 S21016 04255002
EOS      LA    ERCODE,12               EOS                              04260002
         B     LPA+4                   BRANCH                           04280002
LPA      LA    ERCODE,0                LP                               04300002
         LM    LISTRG,GBREG7,24(SAVEREG) RESTORE REGISTERS              04320002
         BCR   15,14                   RETURN                           04340002
         DS    0F                                                       04360002
ZERO     DC    4XL1'00'                 ZERO                            04380002
         CNOP  0,8                                                      04400002
         END                                                            04420002
