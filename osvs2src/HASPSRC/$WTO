         MACRO -- $WTO -- WRITE TO OPERATOR                          R4 IP002000
&NAME    $WTO  &MSG,&L,&MF=,&JOB=YES,&WAIT=YES,&CMB=,&RMT=,&UCM=,    R4CIP004000
               &ROUTE=$LOG,&TYPE=,&CLASS=$NORMAL,&PRI=$ST            R4 IP006000
         LCLA  &E,&F                                                 R4 IP008000
         LCLC  &A,&B,&D                                              R4 IP010000
&A       SETC  '$WTO'              ASSUME CMB=NO                     R4 IP012000
&B       SETC  '&L'                ASSUME L IN PARAMETER LIST        R4 IP014000
&D       SETC  '&ROUTE'            ASSUME LOGICAL ROUTINGS           R4 IP016000
&E       SETA  X'1000'             ASSUME NORMAL DESCRIPTOR CODES    R4 IP018000
         AIF   ('&CMB' NE 'YES').A                                   R4 IP020000
&A       SETC  '$WTOC'             SET CMB=YES                       R4 IP022000
.A       AIF   ('&L' EQ '').AA                                       R4 IP024000
         AIF   ('&L'(1,1) NE '(').B                                  R4 IP026000
.AA      ANOP                                                        R4 IP028000
&B       SETC  '*-*'               SET L DYNAMICALLY                 R4 IP030000
.B       AIF   ('&MF' EQ 'LX').C                                     R4 IP032000
&F       SETA  X'01'               INDICATE SHORT FORM REQUEST       R4 IP034000
.C       AIF   ('&JOB' NE 'YES').D                                   R4 IP036000
&F       SETA  &F+X'04'            SET JOB=YES                       R4 IP038000
.D       AIF   ('&WAIT' EQ 'YES').E                                  R4 IP040000
&F       SETA  &F+X'02'            SET WAIT=NO                       R4 IP042000
.E       AIF   ('&TYPE' NE 'SVC34').F                                R4 IP044000
&F       SETA  &F+X'80'            SET COMMAND PRESENT               R4 IP046000
.F       AIF   ('&UCM' NE 'YES').G                                   R4 IP048000
&F       SETA  &F+X'10'            SET UCMID WILL BE DYNAMICALLY SET R4 IP050000
&D       SETC  '*-*'               NEGATE LOGICAL ROUTINGS           R4 IP052000
&E       SETA  0                   NEGATE DESCRIPTOR CODES           R4 IP054000
         AGO   .H                                                    R4 IP056000
.G       AIF   ('&RMT' NE 'YES').H                                   R4 IP058000
&F       SETA  &F+X'40'            REMOTE WILL BE DYNAMICALLY SET    R4 IP060000
&D       SETC  '*-*'               NEGATE LOGICAL ROUTINGS           R4 IP062000
&E       SETA  0                   NEGATE DESCRIPTOR CODES           R4 IP064000
.H       AIF   ('&MF' EQ 'L').O                                      R4 IP066000
         AIF   ('&MF' EQ 'LX').P                                     R4 IP068000
         $CHEK X,&MSG              NORMAL AND MF=(E,X) REQUIRES MSG  R4 IP070000
&NAME    $DECODE &MSG              R1 POINTS TO MESSAGE DATA         R4 IP072000
         AIF   ('&MF(1)' EQ 'E').I                                   R4 IP074000
         AIF   ('&MF(1)' EQ 'EX').K                                  R4 IP076000
         $CHEK X,&L                NORMAL REQUIRES LENGTH            R4 IP078000
         AIF   ('&L'(1,1) NE '(').HA                                 R4 IP080000
         $DECODE  ,&L              R0=LENGTH                   @OZ25131 IP081000
&F       SETA  &F+X'08'            INDICATE LENGTH IN R0             R4 IP082000
.HA      L     R15,&A              POINT TO SERVICE ROUTINE          R4 IP084000
         BALR  LINK,R15            ENTER SERVICE ROUTINE             R4 IP086000
         DC    AL1(&F,&CLASS+&PRI,&D,&B)                             R4 IP088000
         AGO   .N                                                    R4 IP090000
.I       AIF   ('&L' EQ '').J                                        R4 IP092000
         $DECODE ,&L               SET MSG LENGTH IN R0              R4 IP094000
         O     R0,&MF(2)           SET PARAMETER REGISTER            R4 IP096000
         AGO   .MA                                                   R4 IP098000
.J       L     R0,&MF(2)           SET PARAMETER REGISTER            R4 IP100000
         AGO   .MA                                                   R4 IP102000
.K       AIF   ('&L' EQ '').M                                        R4 IP104000
         AIF   ('&L'(1,1) EQ '(').L                                  R4 IP106000
         MVI   &MF(2)+3,&L         SET MSG LENGTH                    R4 IP108000
         AGO   .M                                                    R4 IP110000
.L       STC   &L(1),&MF(2)+3      SET MSG LENGTH                    R4 IP112000
.M       LA    R0,&MF(2)           POINT TO PARAMETERS               R4 IP114000
.MA      L     R15,&A              POINT TO SERVICE ROUTINE          R4 IP116000
         BAL   LINK,0(,R15)        ENTER SERVICE ROUTINE             R4 IP118000
.N       AIF   ('&WAIT' EQ 'YES' OR '&WAIT' EQ 'NO').END             R4 IP120000
         BE    &WAIT               BRANCH IF $WTO NOT SUCCESSFUL     R4 IP122000
         MEXIT                                                       R4 IP124000
.O       CNOP  0,4                 INSURE FULL WORD ALIGNMENT        R4 IP126000
&NAME    DC    AL1(&F,&CLASS+&PRI,&D,&B)                             R4 IP128000
         MEXIT                                                       R4 IP130000
.P       AIF   ('&CLASS' NE '$DOMACT').Q                             R4 IP132000
&E       SETA  X'4000'             SET ACTION DESCRIPTOR CODES       R4 IP134000
.Q       ANOP                                                        R4 IP136000
&NAME    DC    AL1(&F,&CLASS+&PRI,0,&B),AL2(*-*,&E,&D,0,0)           R4 IP138000
.END     MEND                                                        R4 IP140000
