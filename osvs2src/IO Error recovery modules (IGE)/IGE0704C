         TITLE  'IGE0704C - BSC BUS OUT ERP ROUTINE'                    00650002
IGE0704C CSECT                                                          00700002
*********************************************************************** 00750002
*                                                                     * 00800002
* MODULE NAME:                                                        * 00850002
*    IGE0704C                                                         * 00900002
*                                                                     * 00950002
* DESCRIPTIVE NAME:                                                   * 01000002
*    BTAM BSC BUS OUT ERP                                             * 01050002
*                                                                     * 01100002
* COPYRIGHT:                                                          * 01150002
*    NONE                                                             * 01200002
*                                                                     * 01250002
* STATUS:                                                             * 01300002
*    CHANGE LEVEL 000                                                 * 01350002
*                                                                     * 01400002
* FUNCTION:                                                           * 01450002
*    THIS ROUTINE RECOVERS FROM A BUS OUT ERROR WHICH OCCURRED DURING * 01500002
*    A BSC OPERATION. THE TYPE OF CCW ON WHICH THE ERROR OCCURRED     * 01550002
*    IS DETERMINED AND THE PROPER ERROR RECOVERY IS INITIATED.        * 01600002
*                                                                     * 01650002
* NOTES:                                                              * 01700002
*    DEPENDENCIES:                                                    * 01750002
*        NONE                                                         * 01800002
*    RESTRICTIONS:                                                    * 01850002
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              * 01900002
*    REGISTER CONVENTIONS:                                            * 01950002
*        SEE 'REGISTER DEFINITION' BELOW                              * 02000002
*                                                                     * 02050002
* PATCHLABEL:                                                         * 02100002
*    PACHAREA (52 BYTES)                                              * 02150002
*                                                                     * 02200002
* MODULE TYPE:                                                        * 02250002
*    PROCEDURE                                                        * 02300002
*    PROCESSOR:                                                       * 02350002
*        BAL                                                          * 02400002
*    MODULE SIZE:                                                     * 02450002
*        526 BYTES                                                    * 02500002
*    ATTRIBUTES:                                                      * 02550002
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD.  * 02600002
*                                                                     * 02650002
* ENTRY POINT:                                                        * 02700002
*    IGE0704C                                                         * 02750002
*    PURPOSE:                                                         * 02800002
*        SEE 'FUNCTION'                                               * 02850002
*    LINKAGE:                                                         * 02900002
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   * 02950002
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           * 03000002
*    INPUT:                                                           * 03050002
*        REG 1 = ADDRESS OF IOSB                                      * 03100002
*    OUTPUT:                                                          * 03150002
*        ALTERED CHANNEL PROGRAM                                      * 03200002
*        SETS INDICATOR IN DECERRST FOR SHOULD NOT OCCUR ERROR          03250002
*                                                                     * 03300002
* EXITS-NORMAL:                                                       * 03350002
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         * 03450002
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       * 03500002
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  * 03550002
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       * 03600002
*        LOADS ARE 1000 GREATER.                                      * 03650002
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        * 03700002
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             * 03750002
*                                                                     * 03800002
* EXITS-ERROR:                                                        * 03850002
*    NONE                                                             * 03900002
*                                                                     * 03950002
* EXTERNAL REFERENCES:                                                * 04000002
*    ROUTINES:                                                        * 04050002
*        NONE                                                         * 04100002
*    DATA AREAS                                                       * 04200002
*        IOB (MAPPED BY IEZIOB)                                       * 04250002
*        VIRTUAL CHANNEL PROGRAM                                      * 04300002
*        ERP WORKAREA (MAPPED BY IECDERWA)                            * 04350002
*    CONTROL BLOCKS                                                   * 04400002
*        UCB                                                          * 04450002
*        DCB                                                          * 04500002
*        DECB                                                         * 04550002
*        IOSB         (MAPPED BY IECDIOSB)                            * 04600002
*        CVT          (MAPPED BY CVT)                                 * 04650002
*        IOB          (MAPPED BY IEZIOB)                              * 04700002
*        RQE          (MAPPED BY IECDRQE)                             * 04750002
*                                                                     * 04800002
* TABLES-WORKAREAS:                                                   * 04850002
*    ERP WORKAREA                                                     * 04900002
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   * 04950002
*                                                                     * 05000002
* MACROS:                                                             * 05050002
*    NONE                                                             * 05100002
*                                                                     * 05150002
* CHANGE ACTIVITY:                                                    * 05200002
*    YM02595K (9/20/73)                                               * 05250002
*    YM02594K (9/24/73)                                               * 05260002
*    YM05652K (1/24/74)                                               * 05270000
*    ZA02812  (01/10/75)          AZ03976  (04/24/75)                 * 05280000
*    AZ13205  (09/23/76)                                              * 05290000
*                                                                     * 05300002
*********************************************************************** 05350002
         EJECT                                                          05400002
         USING *,BASEREG                                                05450002
         LR    BASEREG,ERREG5      SET BASE REG                         05500002
         USING RQE,ERREG5                                               05550002
         USING IECTDECB,DECBRG                                          05600002
         USING IHADCB,DCBREG                                            05650002
         USING IOSB,IOSBREG                                             05700002
         USING IOBSTDRD,IOBRG                                           05750002
         USING EWA,EWAREG                                               05800002
         B     BEGIN               BRANCH AROUND ID         LD @ZA02812 05810000
         DC    C'IGE0704C'         MODULE ID                LD @ZA02812 05820000
         DC    C'5010'             DATE LAST CHANGE         LD @ZA02812 05830000
         DC    CL8'&SYSDATE'       DATE LAST ASSEMBLY       LD @ZA02812 05840000
BEGIN    DS    0H                                           LD @ZA02812 05842000
         L     ERREG5,IOSUSE       BASE RQE                             05850002
         L     EWAREG,IOSERP       BASE ERP WORKAREA                    05900002
         L     IOBRG,RQEIOB        BASE IOB                             05950002
         L     DECBRG,IOBECBPT     BASE DECB                            06000002
         L     DCBREG,IOBDCBPT     BASE DCB                             06050002
         L     UCBREG,IOSUCB       BASE UCB                             06100002
         DROP  ERREG5                                                   06150002
         L     CCWREG,IOBFL3       GET IOB CSW                          06200002
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT FLAG BYTE                06250002
         SH    CCWREG,ERRCN8       POINT TO FAILING CCW IN IOB          06300002
         XC    IOBERCCW(L16),IOBERCCW   CLEAR ERP CCW AREA              06350002
         TM    IOBSENS0,BUSOUT     IS THIS A BUSOUT CHECK               06400002
         BO    BSCBO00B            YES BRANCH INTO ROUTINE              06450002
         CLI   ZERO(CCWREG),CCWREAD   DID ERROR OCCUR ON A READ         06500002
         BNE   BSCOR03             NO, GO SET SHOULD NOT OCCUR          06550002
         CLI   IOBWORK,TPOPRENQ    IS THIS READ ENQUIRY                 06600002
         BE    BSCOR06             YES, GO TO RESTART CHAN.PROG.        06650002
         CLI   IOBWORK,TPRDRENQ    IS THIS READ RESP TO ENQ             06700002
         BE    BSCOR04             YES, RESTART W/ PREVIOUS CCW         06750002
         CLI   IOBWORK,TPOPRREN    IS THIS READ RESP TO TEXT            06800002
         BE    BSCOR05             YES, RESTART W/ READ ENQ CCW         06850002
         CLI   IOBWORK,TPOPRTXT    IS THIS READ TEXT                    06900002
         BNE   BSCOR03             NO, GO SET SHOULD NOT OCCUR          06950002
         CLI   L5(DECBRG),DECCONV   CONVERSATIONAL WRITE ?              07000002
         BNL   BSCCCC                                                   07050002
         TM    L4(DECBRG),AUTOPOLL USING AUTOPOLL ?         LD @ZA02812 07060000
         BZ    TSTDB               NO, CHECK DYN. BUFF.     LD @ZA02812 07070000
         CLI   L5(DECBRG),DECTYP1  IS READ INIT ?           LD @ZA02812 07080000
         BNE   TSTDB               NO, CHECK DYN. BUFF.     LD @ZA02812 07090000
         LR    ERREG2,CCWREG       GET READ TXT ADDR           @ZA03976 07090200
         SH    ERREG2,EIGHT        BACK UP TO RD INDEX         @ZA03976 07090400
         L     ERREG4,ZERO(ERREG2) LOAD DATA ADDR              @ZA03976 07090600
         AH    ERREG4,ONE           ADD ONE                    @ZA03976 07090800
         C     ERREG4,ZERO(CCWREG)  ADDR PREV ADJUSTED?        @ZA03976 07091000
         BE    TSTDB                YES, DONT ADJUST AGAIN     @ZA03976 07091200
*        ONE LINE OF CODE DELETED BY APAR ---------------->    @ZA13205 07096000
         ST    ERREG4,ZERO(CCWREG) RESTORE CORRECT DATA...  LD @ZA02812 07098000
*                                  ...ADDR IN READ TXT.     LD @ZA02812 07098400
         LH    ERREG4,L6(CCWREG)   GET CCW BYTE COUNT       LD @ZA02812 07098800
         LA    ERREG4,L1(ERREG4)   ADD ONE                  LD @ZA02812 07099200
         STH   ERREG4,L6(CCWREG)   RESTORE COUNT TO CCW     LD @ZA02812 07099600
TSTDB    TM    DCBBFTEK,BSCDYBUF   WAS DYNAMIC BUFFERING EMPLOYED       07100002
         BO    BSCOR02             YES, INDICATE PERMANENT ERROR        07150002
BSCOR00  LA    ERREG4,DCBBSNAK     ADDRESS OF NAK SPECIAL CHAR.         07200002
         ST    ERREG4,IOBERCCW     STORE ADDRESS IN ERP CCW AREA        07250002
BSCOR00A MVI   IOBERCCW,OPCODWRT   COMMAND CODE INTO ERP CCW            07300002
         MVI   IOBERCCW+L4,FLAG                                         07350002
         MVI   IOBERCCW+L7,COUNTONE   COUNT OF 1 FOR WRITE NAK CCW      07400002
         ST    CCWREG,IOBERCCW+L8   READ TEXT CCW ADDRESS IN TIC        07450002
         MVI   IOBERCCW+L8,BSCTICCC   TIC COMMAND CODE INTO ERP CCW     07500002
         LA    ERREG4,IOBERCCW     GET ADDRESS OF ERP CHANNEL PROG      07600002
         ST    ERREG4,IOBSTART     STORE ADDRESS IN IOB FIELD           07650002
BSCOR01  CLI   IOBERRCT,ERRMAX     RETRY COUNT AT MAXIMUM               07800002
         BNL   BSCOR02             INDICATE PERMANENT ERROR             07850002
         IC    ERREG4,IOBERRCT     UPDATE RETRY COUNT                   07900002
         LA    ERREG4,L1(ERREG4)   TO NEW COUNT                         07950002
         STC   ERREG4,IOBERRCT     STORE IN COUNT FIELD                 08000002
         B     RETRY               ISSUE ERROR EXCP                     08050002
BSCOR02  NI    IOSFLA,L255-IOSERR  INDICATE PERMANENT ERROR             08100002
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB                     08110002
         LA    NAMEREG,ERLOD25     GET ERROR ROUTINE ADDRESS            08150002
XCTLOUT  EQU   *                                                        08200002
         L     ERRETR,CVTPTR       POINT TO CVT                         08250002
         USING CVTMAP,ERRETR                                            08300002
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUTINE                08350002
         DROP  ERRETR                                                   08400002
         BR    ERRETR              XCTL                                 08450002
BSCOR03  OI    DECERRST,DECBSNO    SET SHOULD-NOT-OCCUR FLAG            08500002
         B     BSCOR02             INDICATE PERMANENT ERROR & EXIT      08550002
BSCOR04  SH    CCWREG,ERRCN8       BACK UP TO PREVIOUS CCW              08600002
BSCOR06  ST    CCWREG,IOBSTART     STORE CPA ADDRESS IN DCB FIELD       08650002
         B     BSCOR01                                                  08750002
BSCCCC   SH    CCWREG,ERRCN8       BACK UP TO RD RESP TO TEXT           08800002
         B     TSTDB                                                    08850002
BSCOR05  EQU   *                                                        08900002
         LA    ERREG4,DCBBSENQ     GET ADDRESS OF ENQ CHARACTER         08950002
         ST    ERREG4,IOBERCCW     STORE ENQ ADDRESS IN CCW             09000002
         MVI   IOBERCCW+L5,TPOPWENQ   MOVE IN WRITE TQ TP CODE          09050002
         B     BSCOR00A            GO FINISH CCW AND EXCP               09100002
BSCBO00B EQU   *                                                        09150002
         CLI   ZERO(CCWREG),CCWREAD   WAS CCW FOR A READ OPERATION      09200002
         BE    BSCBO02             CHK. CT. AND RETRY IF POSSIBLE       09250002
         CLI   ZERO(CCWREG),CCWDIAL   CCW FOR A DIAL COMMAND            09300002
         BE    BSCBO03             DISABLE LINE AND DIAL AGAIN          09350002
         CLI   ZERO(CCWREG),CCWPOLL   CCW FOR A POLL COMMAND            09400002
         BE    BSCBO04             RE-EXECUTE CHANNEL PROGRAM           09450002
         CLI   ZERO(CCWREG),CCWRITE   CCW FOR A WRITE COMMAND           09500002
         BNE   BSCBO00             NO, RETRY FAILING COMMAND            09550002
         TM    TPOPCODE(CCWREG),PRIORTXT   CCW PRIOR TO TEXT TRANSFER   09650002
         BZ    BSCBO05             YES, CHECK FOR WRITE EOT CCW         09700002
         TM    TPOPCODE(CCWREG),DURINTXT   CCW DURING TEXT TRANSFER     09750002
         BO    BSCBO02             YES, RETRY FAILING CCW IF CAN        09800002
         TM    TPOPCODE(CCWREG),LASTCOMD   CCW LAST ONE IN CHAN.PROGRAM 09850002
         BZ    BSCBO06             NO, RESTART WITH NEXT CCW            09900002
BSCBO00  ST    CCWREG,IOBSTART     ADDR. OF FAILING CCW IN IOBSTAR      09950002
BSCBO00A CLI   IOBERRCT,ERRMAX     RETRY COUNT AT MAXIMUM               10050002
         BNL   BSCBO01             YES, GO INDICATE A PERM. ERROR       10100002
         IC    ERREG4,IOBERRCT     NO, UPDATE RETRY COUNT BY ONE        10150002
         LA    ERREG4,L1(ERREG4)   AND ISSUE EXCP TO RESTART            10200002
         STC   ERREG4,IOBERRCT     WITH THE FAILING CCW                 10250002
RETRY    EQU   *                                                        10300002
         MVI   EWAIND1,EWAISVCF    SVC 15                      YM05652K 10350000
         L     NAMEREG,TRANSRTN    LOAD NAME                            13000002
         B     XCTLOUT             XCTL                                 13050002
BSCBO02  CLC   COUNT(L2,CCWREG),IOBCSW+L5   RESIDUAL COUNT = ORIGINAL   13300002
         BE    BSCBO00             YES, RETRY FAILING COMMAND           13350002
BSCBO01  NI    IOSFLA,L255-IOSERR  SET PERMANENT ERROR FLAGS            13400002
         NI    IOBFLAG1,L255-IOBERRTN   ALSO ON IOB                     13450002
         LA    NAMEREG,ERLOD25     ADDRESS OF ERROR POST                13500002
         B     XCTLOUT             BRANCH TO XCTL ROUTINE               13550002
BSCBOSNO OI    DECERRST,DECBSNO    SET PERMANENT ERROR FLAG             13600002
         B     BSCBO01             GO TO ERRPOST VIA XCTL ROUTINE       13650002
BSCBO03  XC    IOBERCCW(L12),IOBERCCW   CLEAR OUT NEEDED CCW AREA       13700002
         MVI   IOBERCCW,DISABLE    MOVE DISABLE COMMAND CODE            13750002
         MVI   IOBERCCW+L5,TPDISAB   DSABLE TP CODE IN CCW              13800002
         MVI   IOBERCCW+L7,DISABCNT   COUTN IN CCW                      13850002
         MVI   IOBERCCW+L4,FLAGS   CC & SLI FLAGS INTO CCW              13900002
         ST    CCWREG,IOBERCCW+L8   USER CCW ADDR IN TIC COMMAND        13950002
         MVI   IOBERCCW+L8,TICCOMMD   TIC COMMAND CODE IN CCW           14000002
BSCBO04  LA    ERREG2,IOBCPA       UPDATE IOBSTART TO POINT TO          14050002
         ST    ERREG2,IOBSTART     START OF USER CHANNEL PROG.          14100002
         L     ERREG4,IOBCPA+L8    ADDR. OF USER POLLING ENTRY          14150002
         MVC   IOBCPA+L9(L3),DECENTRY+L1   FAILING ENTRY ADDR TO CCW    14200002
         L     ERREG2,IOBCPA+L8    ADDR. OF FAILING POLL CCW            14250002
         SR    ERREG4,ERREG2       COUNT BETWEEN USER AND FAILED        14300002
         AH    ERREG4,IOBCPA+L14   ADD TOTAL COUNT FROM USER ENTRY      14350002
         STH   ERREG4,IOBCPA+L14   STORE NEW COUNT IN POLL CCW          14400002
         B     BSCBO00A            GO EXECUTE                           14500002
BSCBO05  CLI   IOBWORK,WRITEOT     WRITE EOT CCW                        14550002
         BE    BSCBO00                                                  14600002
BSCBO06  LA    CCWREG,NEXTCCW(CCWREG)   ADDRESS OF NEXT CCW             14650002
         ST    CCWREG,IOBSTART                                          14700002
         B     BSCBO00A                                                 14800002
         EJECT                                                          15450002
**********                                                              15500002
*                                                                       15550002
*        CONSTANTS                                                      15600002
*                                                                       15650002
**********                                                              15700002
TRANSRTN DC    F'9042'             NAME OF BTAM TRANSLATE ROUTINE       15750002
EIGHT    EQU   *                                               @ZA03976 15770000
ERRCN8   DC    H'08'               NO BYTES IN ONE CCW                  15800002
ONE      DC    H'01'                                           @ZA03976 15820000
PACHAREA DC    13C'704C'           PATCH AREA FOR MAINTENANCE           15850002
**********                                                              15900002
*                                                                       15950002
*        MISCELLANEOUS EQUATES                                          16000002
*                                                                       16050002
**********                                                              16100002
TPOPCODE EQU   5                   LOCATION OF TP CODE IN CCW           16200002
L255     EQU   X'FF'               AND MASK                             16250002
DECCONV  EQU   X'0D'               CONVERSATIONAL WRITE                 16300002
CCWREAD  EQU   X'02'               COMMAND CODE FOR READ CCW            16350002
CCWDIAL  EQU   X'29'               COMMAND CODE FOR DIAL                16400002
CCWPOLL  EQU   X'09'               COMMAND CODE FOR POLL                16450002
CCWRITE  EQU   X'01'               COMMAND CODE FOR WRITE               16500002
PRIORTXT EQU   X'F0'               MASK TO SEE IF CCW PRIOR TO TXT      16550002
DURINTXT EQU   X'10'               CCW DURING TEXT TRANSFER             16600002
LASTCOMD EQU   X'80'               LAST CCW IN CHANNEL PROGRAM          16650002
ERRMAX   EQU   7                   MAXIMUM NO RETRIES ALLOWED           16700002
COUNT    EQU   6                                                        16750002
DISABLE  EQU   X'27'               COMMAND CODE FOR DISABLE CCW         16800002
TPDISAB  EQU   X'01'               TP CODE FOR DISABLE CCW              16850002
DISABCNT EQU   X'01'               COUNT FOR DISABLE CCW                16900002
FLAGS    EQU   X'60'               FLAGS FOR DISABLE CCW                16950002
TICCOMMD EQU   X'08'               COMMAND CODE FOR TIC COMMAND         17000002
WRITEOT  EQU   X'02'               TP CODE FOR WRITE EOT                17050002
NEXTCCW  EQU   X'08'                                                    17100002
ERLOD25  EQU   2043                                                     17150002
BUSOUT   EQU   X'20'               BUS OUT BIT IN SENSE BYTE            17350002
DECBSNO  EQU   X'40'               SHOULD NOT OCCUR FLAG IN DECB        17400002
TPOPRENQ EQU   X'0B'               TP CODE FOR READ ENQ                 17450002
TPRDRENQ EQU   X'0C'               TP CODE FOR READ-RESPONSE-ENQ        17500002
TPOPRREN EQU   X'25'               TP OP CODE FOR RD RESP TO TEXT       17550002
TPOPRTXT EQU   X'11'               TP CODE FOR READ TEXT                17600002
BSCDYBUF EQU   X'08'               DYNAMIC BUFFERING FLAG               17650002
ZERO     EQU   X'00'               LENGTH AND DISPLACEMENT              17700002
L1       EQU   1                   LENGTH AND DISPLACEMENT              17750002
L2       EQU   2                   LENGTH AND DISPLACEMENT              17800002
L3       EQU   3                   LENGTH AND DISPLACEMENT              17850002
L4       EQU   4                   LENGTH AND DISPLACEMENT              17900002
L5       EQU   5                   LENGTH AND DISPLACEMENT              17950002
L6       EQU   6                   LENGTH AND DISPLACEMENT              18000002
L7       EQU   7                   LENGTH AND DISPLACEMENT              18050002
L8       EQU   8                   LENGTH AND DISPLACEMENT              18100002
L9       EQU   9                   LENGTH AND DISPLACEMENT              18150002
L12      EQU   12                  LENGTH AND DISPLACEMENT              18200002
L14      EQU   14                  LENGTH AND DISPLACEMENT              18250002
L16      EQU   16                  LENGTH AND DISPLACEMENT              18300002
COUNTONE EQU   X'01'               COUNT FOR WRITE NAK & ENQ CCWS       18350002
OPCODWRT EQU   X'01'               OP-CODE FOR A WRITE COMMAND          18400002
BSCTICCC EQU   X'08'               OP-CODE FOR TIC COMMAND              18450002
TPOPWENQ EQU   X'03'                                                    18500002
FLAG     EQU   X'60'                                                    18550002
AUTOPOLL EQU   X'80'               AUTOPOLL FLG IN DECB     LD @ZA02812 18600000
DECTYP1  EQU   X'01'               READ INIT. OP TYPE...    LD @ZA02812*18650000
                                   ...IN DECB               LD @ZA02812 18660000
**********                                                              18700002
*                                                                       18750002
*        REGISTER DEFINITION                                            18800002
*                                                                       18850002
**********                                                              18900002
ERREG3   EQU   0                   WORK REG                             18950002
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG              19000002
ERREG2   EQU   2                   WORK REG                             19050002
EWAREG   EQU   3                   ERP WORKAREA BASE                    19100002
CCWREG   EQU   4                   CCW ADDRESS REG                      19150002
DECBRG   EQU   5                   DECB BASE                            19200002
DCBREG   EQU   6                   DCB BASE                             19250002
UCBREG   EQU   7                   UCB BASE                             19300002
BASEREG  EQU   8                   PROGRAM BASE                         19350002
ERREG1   EQU   9                   WORK REG                             19400002
IOBRG    EQU   10                  IOB BASE                             19450002
ERREG4   EQU   11                  WORK REG                             19500002
ERRLNK   EQU   12                  LINK REG                             19550002
NAMEREG  EQU   13                  LINK REG                             19600002
ERRETR   EQU   14                  LINK REG                             19650002
ERREG5   EQU   15                  LINK REG/WORK REG                    19700002
         EJECT                                                          19750002
         IECTDECB                                                       19800002
         EJECT                                                          19850002
         DCBD  DSORG=BX,DEVD=BS                                         19900002
         EJECT                                                          19950002
         IECDRQE                                                        20000002
         EJECT                                                          20050002
         IEZIOB                                                         20100002
         EJECT                                                          20150002
CVT      DSECT                                                          20200002
         CVT                                                            20250002
         EJECT                                                          20300002
         IECDERWA                                                       20350002
**********                                                              20400002
*                                                                       20450002
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.        20500002
*                                                                       20550002
**********                                                              20600002
         ORG   EWAIERP                                                  20650002
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K 20700002
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K 20750002
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K 20800002
EWAIND0  DS    B                   FLAG BYTE                            20850002
EWAIVISR EQU   X'10'               V=R REQUEST                          21450002
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K 21500002
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K 21550002
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K 21600002
EWAIND1  DS    B                   FLAG BYTE                            21650002
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K 21700002
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K 21750002
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K 21800002
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K 21810000
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K 21820000
EWAIND2  DS    B                   FLAG BYTE                            21850002
EWAIND3  DS    B                   FLAG BYTE                            21900002
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO     21950002
EWAISAVE DS    2F                  TEMPORARY SAVE AREA                  22000002
EWAIEXIT DS    4F                  ESATE PARAMETER LIST                 22050002
*********  END OF BTAM LOCAL DEFINITIONS **********                     22100002
         EJECT                                                          22750002
         IECDIOSB                                                       22800002
         END                                                            22850002
