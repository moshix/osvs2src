         MACRO                                                          00300019
&NAME    IOSGNIP &FUNC,&PARM,&REG,&VAR,&AUTH=CSECTNME                   00500019
         GBLB    &EXECN                 MULTIPLE EXPANSION SWITCH       00700019
         LCLC  &SUFFIX                  CHARACTER VARIABLE              00760019
&SUFFIX  SETC  '&SYSNDX'(2,4)           SET 3 CHARACTER SYSNDX VALUE    00820019
         AIF   ('&FUNC' EQ 'MINIMAP').MIN010                            00900019
         AIF   ('&FUNC' EQ 'UCBCHM').NIP010                             01200019
         AGO   .IOSNOGO                 UNSUPPORTED FUNCTION MNOTE      01500019
.NIP010  ANOP                                                           01800019
         AIF   ('&AUTH' EQ 'IEAANIP').NIP010A                           02100019
         AGO   .IOSNOGO                 UNAUTHORIZED USE MNOTE          02400019
.NIP010A ANOP                                                           02700019
*********************************************************************** 03000019
***      MACRO DEFINITION - UCBCHM FUNCTION OF IOSGNIP              *** 03300019
***            THIS MACRO, OWNED AND MAINTAINED BY IOS PERSONNEL,   *** 03600019
***            INSERTS THE PATH MASK CREATED BY MP6500 INTO THE     *** 03900019
***            'UCBCHM' FIELD.                                      *** 04200019
***                                                                 *** 04500019
         AIF   (&EXECN EQ 1).NIP101     SKIP DETAIL 2ND TIME THROUGH    04600019
***            FOLLOWING ARE THE PARAMETERS WHICH MUST BE SPECIFIED *** 04800019
***                1.&FUNC-UCBCHM                                   *** 05100019
***                2.&PARM-NUMBER OF A REGISTER WHICH POINTS TO     *** 05400019
***                  THE UCB.                                       *** 05700019
***                3.&REG-SPECIFIES THE NUMBER OF A REGISTER WHICH  *** 06000019
***                  POINTS TO THE PATH MASK BYTE. IF NOT SPECIFIED *** 06300019
***                  THE VAR PARAMETER WILL BE USED TO LOCATE THE   *** 06600019
***                  UCB PATH MASK.                                 *** 06900019
***                4.&VAR-LABEL OF THE BYTE CONTAINING THE UCB PATH *** 07200019
***                  MASK                                           *** 07500019
***      THE KEYWORD PARAMETER 'AUTH=CSECTNME' MUST ALSO BE         *** 07530020
***      SPECIFIED, NOTING THE CSECT NAME INVOKING THIS MACRO.      *** 07560020
.NIP101  ANOP                                                           07600019
*********************************************************************** 07800019
         SPACE 1                                                        08100019
         AIF   ('&REG' NE '').NIP110     USING REGISTER FORM...         08400020
         SPACE 1                                                        08700019
*********************************************************************** 09000019
***            REGISTER DEFINITION                                  *** 09300019
*********************************************************************** 09600019
IECIU&SUFFIX   EQU &PARM                UCB POINTER REGISTER            09700020
         AIF   (&EXECN EQ 1).NIP102     NOT FIRST TIME - SKIP           09800020
&EXECN   SETB  1                         SET FIRST TIME SWITCH.         09900020
*********************************************************************** 10200019
***            UCB DEFINITION                                       *** 10500019
*********************************************************************** 10800019
IECIWCHM EQU   12                       DISP TO PATH MSK IN UCB         11100019
.NIP102  ANOP                                                           11200019
         SPACE 3                                                        11400019
&NAME    MVN   IECIWCHM(1,IECIU&SUFFIX),&VAR INSERT MASK INTO UCBCHM.   11700020
         AGO   .IOSGEND                                                 12000019
.NIP110  ANOP                                                           12300019
         SPACE 1                                                        12600019
*********************************************************************** 12900019
***            THIS IS THE REGISTER FORM OF UCBCHM FUNCTION         *** 13200020
*********************************************************************** 13500019
         SPACE 1                                                        13800019
*********************************************************************** 14100019
***            REGISTER DEFINITION                                  *** 14400019
*********************************************************************** 14700019
IECIW&SUFFIX EQU &REG                  PATH MASK POINTER REGISTER       14770019
IECIU&SUFFIX EQU &PARM                   UCB POINTER REGISTER           14970020
         AIF   (&EXECN EQ 1).NIP103     NOT FIRST TIME - SKIP           15170020
&EXECN   SETB  1                        SET FIRST TIME SWITCH           15370020
*********************************************************************** 15600019
***            UCB DEFINITION                                       *** 15900019
*********************************************************************** 16200019
IECIWCHM EQU   12                       DISP TO PATH MSK IN UCB         16500019
.NIP103  ANOP                                                           16600019
         SPACE 3                                                        16800019
&NAME    MVN   IECIWCHM(1,IECIU&SUFFIX),0(IECIW&SUFFIX) PUT MASK IN UCB 17100020
         MEXIT                           EXIT MACRO                     17400020
.MIN010  ANOP                                                           17700019
         AIF   ('&AUTH' EQ 'IEAANIP').MIN010A                           18000019
         AGO   .IOSNOGO                 UNAUTHORIZED USE MNOTE          18300019
.MIN010A ANOP                                                           18600019
&NAME    B     IECIM010                 SKIP AROUND CONSTANTS           18900019
*********************************************************************** 19200019
*        MACRO DEFINITION - MINIMUM PATH MAP FUNCTION OF IOSGNIP      * 19500019
*              THIS MACRO OWNED AND MAINTAINED BY  I O S  PERSONNEL,  * 19800019
*              USES A GIVEN UCB TO CONSRUCT AN EIGHT (8) BYTE TABLE   * 20100019
*              ENTRY TO CONVEY TO THE CALLER THE POSSIBLE PATHS TO A  * 20400019
*              DEVICE.                                                * 20700019
*                                                                     * 21000019
*              THIS CODE IS GENERATED WHEN 'IOSGNIP' IS INVOKED       * 21300019
*              BY NIP WHERE VARIABLES ARE SET ACCORDING TO THE FORMAT * 21600019
*              BELOW --                                               * 21900019
*        IOSGNIP &FUNC,&PARM,&REG,&VAR            ... WHERE --        * 22200019
*             &FUNC = MINIMAP                                         * 22500019
*             &PARM = A REGISTER CONTAINING THE ADDRESS OF A 2 WORD   * 22800019
*                     AREA IN WHICH THE MINIMAP OF AVAILABLE PATHS    * 23100019
*                     WILL BE BUILT.                                  * 23400019
*              &REG = A REGISTER CONTAINING THE ADDRESS OF THE UCB FOR* 23700019
*                     WHICH PATHS ARE TO BE DETERMINED.               * 24000019
*              &VAR = 0 FOR UNIPROCESSING, OR 1 FOR MULTIPROCESSING.  * 24300019
*                                                                     * 24600019
*        RESTRICTIONS/ASSUMPTIONS --                                  * 24900019
*              1)  IOSGNIP(MINIMAP) WILL EXPAND AS IN-LINE CODE.      * 25200019
*              2)  REGISTERS 11, 12, AND 13 ARE NOT AVAILABLE.        * 25500019
         AIF   (&VAR(1) EQ 0).MIN020   'UNI-PROCESSING'                 25800019
*                                                                     * 26100019
*              FOR A SYSTEM SUPPORTING MULTIPROCESSING, THIS ENTRY    * 26400019
*              TAKES THE FOLLOWING FORM --                            * 26700019
*                                                                     * 27000019
*              *- - IPLED CPU - - - -*- - - - NULL  - - - -*          * 27300019
*              *********************************************          * 27600019
*              * PRIMARY  * ALTERNATE*          *          *          * 27900019
*              *  DEVICE  *  DEVICE  *  00  00  *  00  00  *          * 28200019
*              *   ADDRESS*   ADDRESS*          *          *          * 28500019
*              *********************************************          * 28800019
*              ..2 BYTES...                                           * 29100019
*              .................8 BYTES.....................          * 29400019
         AGO   .MIN030                                                  29700019
.MIN020  ANOP                                                           30000019
*                                                                     * 30300019
*              FOR A UNIPROCESSING SYSTEM, THIS ENTRY TAKES THE       * 30600019
*              FOLLOWING FORM --                                      * 30900019
*                                                                     * 31200019
*              *********************************************          * 31500019
*              * PRIMARY  * ALTERNATE* ALTERNATE* ALTERNATE*          * 31800019
*              *  DEVICE  *  ADDRESS *  ADDRESS *  ADDRESS *          * 32100019
*              *   ADDRESS*    ONE   *    TWO   *    THREE *          * 32400019
*              *********************************************          * 32700019
*              ..2 BYTES...                                           * 33000019
*              .................8 BYTES.....................          * 33300019
.MIN030  ANOP                                                           33600019
*                                                                     * 33900019
*        THE MACRO WILL PASS TWO RETURN CODES VIA REGISTER 15 --      * 34200019
*              0 = ALTERNATE PATHS WERE GENERATED FOR THIS DEVICE.    * 34500019
*              4 = NO ALTERNATE PATHS EXIST.                          * 34800019
*                                                                     * 35100019
*********************************************************************** 35400019
         SPACE 3                                                        35700019
*********************************************************************** 36000019
*          R E G I S T E R    D E F I N I T I O N S                   * 36300019
*********************************************************************** 36600019
         SPACE 1                                                        36900019
IECIMREG EQU   &REG                     UCB POINTER INPUT               37200019
IECIMPRM EQU   &PARM                    NIP WORKAREA POINTER INPUT      37500019
*                                                                       37800019
IECIMW1  EQU   1                        *                               38100019
IECIMW2  EQU   2                        ** WORK REGISTERS               38400019
IECIMW3  EQU   3                        *                               38700019
IECIMW4  EQU   4                        *                               39000019
IECIMW15 EQU   15                       *                               39300019
IECIMUCB EQU   5                        UCB POINTER REGISTER            39600019
IECIMNWA EQU   6                        NIP WORKAREA PTR REGISTER       39900019
IECIMRC  EQU   15                       RETURN CODE REGISTER 15         40200019
         SPACE 2                                                        40500019
*********************************************************************** 40800019
*          U C B    F L A G    D E F I N I T I O N S                  * 41100019
*********************************************************************** 41400019
         SPACE 1                                                        41700019
IECIMALC EQU   1                        UCB ALLOCATION CHANNEL MASK     42000019
IECIMCHA EQU   4                        UCB CHANNEL ADDRESS FIELD       42300019
IECIMUAD EQU   5                        UCB UNIT ADDRESS FIELD          42600019
IECIMFLG EQU   6                        UCB ACTIVITY FLAG 1 FIELD       42900019
IECIMLCI EQU   10                       UCB LOGICAL CHANNEL INDEX       43200019
IECIMAPS EQU   X'01'                    ALTERNATE PATH SWITCH SETTING   43500019
         SPACE 2                                                        43800019
*********************************************************************** 44100019
*          M I S C.   M A S K S   A N D   P O I N T E R S             * 44400019
*********************************************************************** 44700019
         SPACE 1                                                        45000019
IECIMCVT EQU   16                       POINTER TO CVT                  45300019
IECIMLCH EQU   140                      POINTER TO IOS LOGICAL CHAN TBL 45600019
IECIMTCC EQU   6                        TEST CHANNEL CODE OFFSET        45900019
IECIMACU EQU   1                        ALTERNATE CU INDICATOR OFFSET   46200019
*                                       IN TEST CHANNEL PREFIX TABLE... 46500019
IECIMPRP EQU   X'08'                    MASK FOR TCH PREFIX PRIMARY PTH 46800019
IECIMAP1 EQU   X'04'                    PATH INDICATION FOR - ALTER 1   47100019
IECIMAP2 EQU   X'02'                                          ALTER 2   47400019
IECIMAP3 EQU   X'01'                                          ALTER 3   47700019
IECIMNUL EQU   X'00'                    NULL MASK                       48000019
IECIMRC4 EQU   4                        RETURN CODE FOR NO ALTERNATES   48300019
IECIMNXT EQU   2                        INCREMENT FOR TABLES            48600019
IECIMMB1 EQU   0                        OFFSET FOR --  FIRST            48900019
IECIMMB2 EQU   2                        *              SECOND           49200019
IECIMMB3 EQU   4                        *              THIRD            49500019
IECIMMB4 EQU   6                        *              FOURTH MAP BOXES 49800019
IECIMBSY EQU   X'80'                    UCB DEVICE BUSY FLAG            50100019
IECIMPST EQU   X'20'                    UCB POST FLAG                   50400019
IECIMPIM EQU   X'F0'                    ALTERNATE CU PATH MASK          50700019
IECIMPIN EQU   1                        ALTERNATE PATH DISCRIPTOR DISP. 51000019
IECIMBTM EQU   X'0F'                    BYTE ANDING MASK                51300019
         AIF   (&VAR(1) EQ 0).MIN031     'UNI-PROCESSING'               51600019
IECIMSIX DC    H'6'                     OFFSET FROM TCHTAB TO TCHCODE   51900019
.MIN031  ANOP                                                           52200019
IECIMHWD DS    H                        HALFWORD WORKAREA               52500019
IECIMBYT EQU   IECIMHWD                 ONE BYTE WORKAREA               52800019
IECIMSA1 DS    F                        FIRST WORD OF SAVEAREA FOR &REG 53100019
IECIMSA2 DS    F                        SECOND WORD FOR &PARM...        53400019
IECIMHWM DC    X'0000FFFF'              MASK TO ZERO TOP HALF WORD      53700019
         AIF   (&VAR(1) EQ 1).MIN040    'MULTI-PROCESSING'              54000019
IECIMABL DC    X'0000000F'              MASK TO ZERO ALL BUT LOW 4 BITS 54300019
         SPACE 1                                                        54600019
*                                       ALTERNATE MVC INSTRUCTIONS TO   54900019
IECIMVC  DS    0H   |                   MOVE ENTRY TO MINIMAP FOR       55200019
         MVC   IECIMMB1(2,IECIMNWA),0(IECIMW2) PRIMARY ADDRESS          55500019
         MVC   IECIMMB2(2,IECIMNWA),0(IECIMW2) 1ST ALT ADDRESS          55800019
         MVC   IECIMMB3(2,IECIMNWA),0(IECIMW2) 2ND ALT ADDRESS          56100019
         MVC   IECIMMB4(2,IECIMNWA),0(IECIMW2) 3RD ALT ADDRESS          56400019
         SPACE 1                                                        56700019
*                                       ALTERNATE INSTRUCTION OFFSETS - 57000019
IECIMTAB DC    X'00'                    SLACK                           57300019
         DC    X'12'                    OFFSET FOR THIRD ALTERNATE      57600019
         DC    X'0C'                    OFFSET FOR SECOND ALTERNATE     57900019
         DC    X'00'                    SLACK                           58200019
         DC    X'06'                    OFFSET FOR FIRST ALTERNATE      58500019
         DC    X'000000'                SLACK                           58800019
         DC    X'00'                    OFFSET FOR PRIMARY MOVE INSTR.  59100019
.MIN040  DS    0H                       ALIGN AFTER CONSTANTS....       59400019
         SPACE 5                                                        59700019
IECIM010 ST    IECIMREG,IECIMSA1        SAVE UCB INPUT ADDRESS          60000019
         ST    IECIMPRM,IECIMSA2        STORE NIP WORKAREA INPUT ADDR.  60300019
         L     IECIMUCB,IECIMSA1        LOAD INTO WORKING REGISTER      60600019
         L     IECIMNWA,IECIMSA2        LOAD INTO ACTIVE WORK REGISTER. 60900019
         XC    0(8,IECIMNWA),0(IECIMNWA) ZERO MAP ENTRY...              61200019
         TM    IECIMALC(IECIMUCB),IECIMAPS Q.  IS THERE ALTERNATE PATH. 61500019
         BZ    IECIM020                 A.  NO, GO BUILD SINGLE ENTRY.  61800019
*                                                                       62100019
         L     IECIMW1,IECIMCVT         A.  YES, GET CVT ADDRESS...     62400019
         L     IECIMW1,IECIMLCH(IECIMW1) GET POINTER TO LOGICAL CHAN TB 62700019
         SR    IECIMW2,IECIMW2          CLEAR WORKREG -                 63000019
         IC    IECIMW2,IECIMLCI(IECIMUCB) GET LOGICAL CHANNEL INDEX...  63300019
         SLL   IECIMW2,3                IECIMW2 = IECIMW2 * 8           63600019
         LH    IECIMW1,IECIMTCC(IECIMW1,IECIMW2) ADDR OF TEST CHAN CODE 63900019
         N     IECIMW1,IECIMHWM         CLEAR HIGH ORDER HALFWORD...    64200019
         AIF   (&VAR(1) EQ 0).MIN050     'UNI-PROCESSING'               64500019
         SH    IECIMW1,IECIMSIX         BACK-UP TO PREFIX TABLE START.  64800019
*    IECIMW1 POINTS TO TOP OF TEST CHANNEL PREFIX TABLE.                65100019
         TM    IECIMACU(IECIMW1),IECIMPRP  Q.  IS TOP PRIMARY PATH.     65400019
         BO    IECIM015  |              A.  YES, TAKE NORMAL SEQUENCE.  65700019
*                                       A.  NO, REVERSED.               66000019
         MVC   IECIMMB2(2,IECIMNWA),0(IECIMW1) GET ALT. ADDR FROM 1ST.  66300019
         MVC   IECIMMB1(2,IECIMNWA),IECIMNXT(IECIMW1) PRIMARY FROM 2ND. 66600019
         B     IECIM031                 SKIP TO ALT CU CHECK....        66900019
************************                                                67200019
         SPACE 1                                                        67500019
IECIM015 MVC   IECIMMB1(2,IECIMNWA),0(IECIMW1) MOVE PRIME DEV ADDR/CPUA 67800019
         MVC   IECIMMB2(2,IECIMNWA),IECIMNXT(IECIMW1) ALT DEV ADDR/CPUA 68100019
*                                       1ST STAGE MINIMAP BUILT...      68400019
         SPACE 1                                                        68700019
         AGO   .MIN060                 SKIP UNI-PROC CODE               69000019
.MIN050  BCTR  IECIMW1,0                * BACKUP ON HALF WORD TO        69300019
         BCTR  IECIMW1,0                * POINT TO LCHTAB SUMMARY ENTRY 69600019
         SR    IECIMW2,IECIMW2          CLEAR WORKREG, THEN             69900019
         IC    IECIMW2,0(IECIMW1)       PICK UP NUMBER OF PATHS...      70200019
         LR    IECIMW3,IECIMW2          COPY FOR LOOP ITERATION LIMIT   70500019
         STH   IECIMW3,IECIMHWD         AND AND SAVE...                 70800019
         AR    IECIMW2,IECIMW2          DOUBLE TO GET TABLE SPAN        71100019
         LCR   IECIMW2,IECIMW2          NEGATE VALUE...                 71400019
         AR    IECIMW2,IECIMW1          ADD TO GET POINTER TO TOP.      71700019
*   POINTERS NOW SET AT LIMITS OF TEST CHANNEL PREFIX TABLE _           72000019
         SPACE 1                                                        72300019
IECIM030 IC    IECIMW15,IECIMACU(IECIMW2) PICK UP PATH INDICATOR        72600019
         N     IECIMW15,IECIMABL        CLEAR ALL BUT LOW FOUR BITS     72900019
         IC    IECIMW15,IECIMTAB(IECIMW15) PICK UP MVC INSTR. OFFSET    73200019
         EX    0,IECIMVC(IECIMW15)      EXECUTE SAME....                73500019
         SPACE 1                                                        73800019
         LA    IECIMW2,IECIMNXT(IECIMW2) POINT TO NEXT ELEMENT...       74100019
         BCT   IECIMW3,IECIM030         LOOP THROUGH TCH PREFIX TABLE   74400019
*                                       1ST STAGE MINIMAP BUILT         74700019
         LH    IECIMW3,IECIMHWD         PICK UP NUMBER OF PATHS....     75000019
.MIN060  SPACE 1                                                        75300019
IECIM031 LR    IECIMW4,IECIMNWA         COPY NIP WA ADDRESS             75500019
         TM    IECIMACU(IECIMW4),IECIMPIM Q.  IS ALTERNATE PATH C.U.    75700019
         BZ    IECIM040                    A.  NO, GO FOR ALTERNATE CHN 75900019
*                                                                       76200019
         AIF   (&VAR(1) EQ 0).MIN065    'UNI-PROCESSING'                76500019
         MVN   IECIMMB1+1(1,IECIMNWA),IECIMUAD(IECIMUCB) NORMALIZE MAP  76800019
         MVN   IECIMMB2+1(1,IECIMNWA),IECIMUAD(IECIMUCB) ADDRESSES...   77100019
         AGO   .MIN070                                                  77400019
.MIN065  ANOP                                                           77700019
IECIM035 MVN   IECIMMB1+1(1,IECIMW4),IECIMUAD(IECIMUCB) GET UNIT ADDR.  78300019
         LA    IECIMW4,IECIMNXT(IECIMW4) BUMP TO NEXT ENTRY...          78600019
         BCT   IECIMW3,IECIM035         LOOP UNTIL ADDRESSES NORMALIZED 78900019
*                                                                       79200019
.MIN070  B     IECIM050                 SKIP ALT. CHANNEL CODE...       79500019
*************************                                               79800019
         SPACE 1                                                        80100019
         AIF   (&VAR(1) EQ 0).MIN075    'UNI-PROCESSING'                80400019
IECIM040 MVC   IECIMMB1+1(1,IECIMNWA),IECIMUAD(IECIMUCB) NORMALIZE MAP  80700019
         MVC   IECIMMB2+1(1,IECIMNWA),IECIMUAD(IECIMUCB) ADDRESSES....  81000019
         AGO   .MIN080                                                  81300019
.MIN075  ANOP                                                           81600019
IECIM040 MVC   IECIMMB1+1(1,IECIMW4),IECIMUAD(IECIMUCB) GET UNIT ADDR.  81900019
         LA    IECIMW4,IECIMNXT(IECIMW4) BUMP TO NEXT ENTRY...          82200019
         BCT   IECIMW3,IECIM040         LOOP UNTIL ADDRESSES NORMALIZED 82500019
*                                                                       82800019
.MIN080  ANOP                                                           83100019
IECIM050 SR    IECIMRC,IECIMRC          SET RETURN CODE TO ZERO...      83400019
         B     IECIMMXT                 EXIT MACRO CODE....             83700019
*************************                                               84000019
         SPACE 1                                                        84300019
IECIM020 MVC   IECIMMB1(2,IECIMNWA),IECIMCHA(IECIMUCB) P/U DEVICE ADDR. 84600019
         NI    IECIMMB1(IECIMNWA),IECIMBTM  ZERO TOP HALFBYTE OF ADDR.  84900019
*                                       ALTERNATE PATH ENTRIES REMAIN   85200019
*                                       ZERO TO REFLECT LACK OF SAME.   85500019
         LA    IECIMRC,IECIMRC4         SET RETURN TO 4 FOR             85800019
*                                       NO ALTERNATE PATH INDICATION.   86100019
IECIMMXT DS    0H                       EXIT THIS ROUTINE               86400019
         MEXIT                           EXIT MACRO                     86450020
.*                                                                      86500020
.********************************************************************** 86550020
.*                                                                      86600020
.*       ALL IOSGNIP MNOTES ARE TO BE PLACED BELOW...                   86650020
.*                                                                      86700020
.*   1.  .IOSNG1 - AUTHORIZATION VIOLATION                              86750020
.*                                       USED BY ALL FUNCTIONS          86800020
.*                                                                      86850020
.********************************************************************** 86900020
.*                                                                      86950020
.IOSNOGO ANOP                                                           87000020
.IOSNG1  MNOTE 8,'*** ''&AUTH'' MAY NOT INVOKE ''&FUNC'' FUNCTION.'     87050020
         MEXIT                                                          87100020
.*                                                                      87150020
.IOSGEND       ANOP                                                     87300019
         MEND                                                           87600019
