         MACRO                                                          00010041
&NAME    IOSMSSA &WRK1,&WRK2,&UCBR                                      00020041
         LCLC  &A,&B,&N,&U                                              00030041
         AIF   (T'&WRK1 EQ 'O' OR T'&WRK2 EQ 'O' OR T'&UCBR EQ 'O').ERR 00040041
&A       SETC  '&WRK1(1)'                    REMOVE PARENTHESES IF ANY  00050041
&B       SETC  '&WRK2(1)'                                               00060041
&U       SETC  '&UCBR(1)'                                               00070041
         AIF   (T'&NAME EQ 'O').NONAME                                  00080041
&NAME    DS    0H                                                       00090041
.NONAME  ANOP                                                           00100041
&N       SETC  '&SYSNDX'                                                00108041
IECL&N   TM    UCBFL4-UCBOB(&U),X'06'  LOST ATTN? (UCBHOLD+UCBMAT)      00116041
         BZ    IECQ&N              NO - EXIT RC=0                       00124041
         BM    IECS&N              MAYBE - SET FLAG FOR NEXT TIME       00132041
         L     &A,UCBSQC-UCBOB(,&U)  YES - GET WORD WITH UCBFL4         00150041
         LR    &B,&A               COPY IT                              00158041
         O     &B,IECA&N           SET UCBATTN                          00166041
         N     &B,IECB&N           RESET UCBHOLD AND UCBMAT             00174041
         CS    &A,&B,UCBSQC-UCBOB(&U)  SWAP IF WORD NOT ALTERED         00182041
         BNE   IECL&N              ALTERED - START OVER                 00200041
         LA    &A,4                SWAP OK - SET RC=4 FOR LOST ATTN     00208041
         B     IECR&N              EXIT                                 00216041
************** CONSTANTS USED BY MACRO ******************************** 00224041
         DS    0F                                                       00232041
IECA&N   DC    X'00080000'         USED TO SET UCBATTN                  00250041
IECB&N   DC    X'FFF9FFFF'         USED TO RESET UCBHOLD AND UCBMAT     00258041
IECC&N   DC    X'00020000'         USED TO SET UCBMAT                   00266041
************** CONTINUE CODE EXPANSION ******************************** 00274041
IECS&N   L     &A,UCBSQC-UCBOB(,&U)  GET WORD WITH UCBFL4               00282041
         LR    &B,&A               COPY IT                              00300041
         O     &B,IECC&N           SET UCBMAT FOR NEXT TIME             00308041
         CS    &A,&B,UCBSQC-UCBOB(&U)  SWAP WORD IF NOT ALTERED         00316041
         BNE   IECL&N              ALTERED - START OVER                 00324041
IECQ&N   SLR   &A,&A               SWAP OK OR NOT NEEDED - SET RC=0     00332041
IECR&N   DS    0H                  RETURN TO INLINE CODE                00350041
         MEXIT                                                          00358041
.ERR     AIF   (T'&UCBR NE 'O').ER1                                     00366041
         IHBERMAC 4                                                     00374041
.ER1     AIF   (T'&WRK2 NE 'O').ER2                                     00382041
         IHBERMAC 3                                                     00400041
.ER2     AIF   (T'&WRK1 NE 'O').ER3                                     00410041
         IHBERMAC 2                                                     00420041
.ER3     MEND                                                           00430041
