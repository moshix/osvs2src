         MACRO                                                          00050000
&L       BLSCGETF &R,&LV=                                               00100000
.*** START OF SPECIFICATIONS *****************************************/ 00150000
.**                                                                  */ 00200000
.** MODULE NAME= BLSCGETF                                            */ 00250000
.**                                                                  */ 00300000
.** DESCRIPTIVE NAME= IPCS DAS FREE PSUEDO GETMAIN MACRO             */ 00350057
.**                                                                  */ 00400000
.** COPYRIGHT= NONE                                                  */ 00450057
.**                                                                  */ 00500000
.** STATUS= SU57 RELEASE 1, LEVEL 0                                  */ 00550057
.**                                                                  */ 00600000
.** FUNCTION=  BLSCGETF PERFORMS A PSEUDO-GETMAIN FUNCTION FOR THE   */ 00650000
.**            IPCS DATA ACCESS SERVICES FREE MODULE.                */ 00700057
.**                                                                  */ 00750000
.** NOTES= SEE BELOW                                                 */ 00800000
.**                                                                  */ 00850000
.**    REGISTER CONVENTIONS=                                         */ 00900000
.**                                                                  */ 00950000
.**       @00 - REG  0 - INITIALLY CONTAINS THE NUMBER OF BYTES TO   */ 01000000
.**                      ALLOCATE FROM THE DMCB FREE STORAGE AREA.   */ 01050000
.**                      AT EXIT IT WILL CONTAIN THE ADDRESS OF THE  */ 01100000
.**                      NEXT AVAILABLE BYTE IN THE FREE STORAGE     */ 01150000
.**                      AREA.                                       */ 01200000
.**                                                                  */ 01250000
.**       @01 - REG  1 - CONTAINS A POINTER TO THE ADDRESS OF THE    */ 01300000
.**                      DMCB FROM WHICH THE FREE STORAGE IS TO BE   */ 01350000
.**                      OBTAINED.  AT EXIT IT WILL CONTAIN THE      */ 01400000
.**                      ADDRESS OF THE STORAGE WHICH WAS ALLOCATED. */ 01450000
.**                                                                  */ 01500000
.**       @14 - REG 14 - USED TO ADDRESS DMCB DURING STORAGE         */ 01550000
.**                      ALLOCATION.  NOT RESTORED BY MACRO.         */ 01600000
.**                                                                  */ 01650000
.** MODULE TYPE= MACRO                                               */ 01700000
.**                                                                  */ 01750000
.**    PROCESSOR= ASSEMBLER                                          */ 01800000
.**                                                                  */ 01850000
.**    MODULE SIZE= TO BE COMPLETED                                  */ 01900000
.**                                                                  */ 01950000
.** ENTRY POINT= BLSCGETF                                            */ 02000000
.**                                                                  */ 02050000
.**    PURPOSE= SEE FUNCTION                                         */ 02100000
.**                                                                  */ 02150000
.**    LINKAGE=                                                      */ 02200000
.**             R01 LOADED WITH A POINTER TO THE DMCB ADDRESS        */ 02250000
.**                                                                  */ 02300000
.**             R00 LOADED WITH REQUESTED STORAGE SIZE.              */ 02350000
.**                                                                  */ 02400000
.**             THIS MACRO IS INVOKED BY THE PROLOGUE CODE  IN LIEU  */ 02450000
.**             OF GETMAIN FOR THE IPCS DAS FREE MODULE.             */ 02500057
.**                                                                  */ 02550000
.** EXIT NORMAL=                                                     */ 02600000
.**                                                                  */ 02650000
.**             REGISTER 1 CONTAINS THE ADDRESS OF THE AREA WHICH    */ 02700000
.**             WAS ALLOCATED.                                       */ 02750000
.**                                                                  */ 02800000
.** EXIT ERROR=                                                      */ 02850000
.**                                                                  */ 02900000
.**             IF THE DMCB FREE STORAGE AREA CONTAINS INSUFFICIENT  */ 02950000
.**             STORAGE A USER ABEND 999 WILL OCCUR.  THIS SITUATION */ 03000000
.**             SHOULD NEVER OCCUR.                                  */ 03050000
.**                                                                  */ 03100000
.** EXTERNAL REFERENCES=                                             */ 03150000
.**                                                                  */ 03200000
.**    CONTROL BLOCKS=  DATA ACCESS SERVICES CONTROL BLOCK (DMCB)    */ 03250057
.**                                                                  */ 03300000
.** CHANGE ACTIVITY= AS FOLLOWS:                                     */ 03350000
*         THIS MODULE IS WRITTEN FOR @G57LPSR                        */ 03400057
.**                                                                  */ 03450057
.*** END OF SPECIFICATIONS *******************************************/ 03500057
         LCLC  &BPT                                                     03550057
&BPT     SETC 'DMG&SYSNDX'                                              03600057
&L       L     @14,0(,@01)       USE REGISTER 14 TO ADDRESS DMCB        03650057
         LR    @01,@00           LENGTH INTO REG 1 FOR INC BY LOAD ADDR 03700057
         LA    @00,7(@01)        THIS AND NEXT 2 INSTRUCTIONS           03750057
         SRL   @00,3             ROUND LENGTH TO A MULTIPLE OF EIGHT    03800057
         SLL   @00,3             FOR COMPATIBILITY WITH GETMAIN         03850057
         A     @00,DMCBFRE(@14)  COMPUTE NEXT FREE ADDRESS              03900057
         C     @00,DMCBEOB(@14)  IS THERE ENOUGH ROOM?                  03950057
         BL    &BPT              IF SO THEN PROCEED                     04000057
         ABEND 999,DUMP          SHOULD NEVER OCCUR                     04050057
&BPT     L     @01,DMCBFRE(@14)  PICK UP ADDRESS TO USE FOR @DATD       04100057
         ST    @00,DMCBFRE(@14)  STORE ADDRESS OF NEXT FREE AREA        04150057
         MEND                                                           04200057
