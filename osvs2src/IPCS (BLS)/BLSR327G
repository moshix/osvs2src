         TITLE 'BLSR3270--BLSR327G--POST-PROCESS MANUAL INPUT'          00010057
*---------------------------------------------------------------------* 00020057
*                                                                     * 00030057
*BLSR327G--POST-PROCESS MANUAL INPUT                                  * 00040057
*                                                                     * 00050057
*---------------------------------------------------------------------* 00058057
         SPACE                                                          00066057
BLSR327G CLC   WKAADDR+1(3),ESAULAD+1 HAS THE ADDRESS BEEN CHANGED?     00074057
         BE    LG000600           NO                                    00082057
         TM    WKAADDR,WKAADRLL   IS THE OLD ADDRESS ALREADY            00100057
*                                 IN THE STACK, OR RESULT OF A ROLL?    00108057
         BNZ   LG000600           YES                                   00116057
LG000200 CLI   WKASKNUM,11        STACK FULL NOW?                       00124057
         BNE   LG000400           NO                                    00132057
         MVC   WKASTACK(40),WKASTACK+4                                  00150057
         MVC   WKASTACK+41(3),WKAADDR+1                                 00158057
         MVC   WKANOTES(80),WKANOTES+8                                  00166057
         XC    WKANOTES+80(8),WKANOTES+80                               00174057
         MVC   WKASKASI(20),WKASKASI+2                                  00182057
         MVC   WKASKASI+20,WKAASID                                      00200057
         OI    FLAG0,FLAG0RW2     WRITE STACK BACK                      00208057
         ICM   RF,1,WKANBNUM      NUMBER OF ACTIVE NOTE LIST ENTRIES    00216057
         BZ    LG000600                                                 00224057
         BCTR  RF,0               DECREMENT                             00232057
         STC   RF,WKANBNUM        ADDRESS STACK ENTRY OFF               00250057
         OI    FLAG0,FLAG0RW3     WRITE NOTE LIST BACK ALSO             00258057
         B     LG000600           A FULL STACK HAS BEEN UPDATED         00266057
IG000300 OC    0(0,R6),=19AL1(WKALNCTW) *** SUBJ EX, B                  00274057
LG000400 SLR   RF,RF              PREPARE FOR INSERT CHAR               00282057
         IC    RF,WKASKNUM        CURRENT NUMBER OF ACTIVE ENTRIES      00300057
         LA    R0,1(0,RF)         UP 1                                  00308057
         STC   R0,WKASKNUM        SAVE NEW ACTIVE ENTRY NUMBER          00316057
         CLI   WKASKNUM,11        DID THE STACK JUST FILL UP?           00324057
         BNE   LG000500           -NO                                   00332057
         OI    NEWWCC,WCCALARM    YES, BEEP AT HIM                      00350057
LG000500 ALR   RF,RF              DOUBLE TO FORM HWORD INDEX            00358057
         LH    R0,WKAASID         MOVE ASID TO STACK                    00366057
         STH   R0,WKASKASI(RF)    INTO PROPER SLOT                      00374057
         ALR   RF,RF              DOUBLE HWORD INDEX TO GET WORD INDEX  00382057
         L     R0,WKAADDR         MOVE CURRENT ADDRESS                  00400057
         ST    R0,WKASTACK(RF)    INTO PROPER SLOT                      00408057
         OI    FLAG0,FLAG0RW2     RE-WRITE STACK LINE                   00416057
LG000600 CLI   WKASKNUM,11        IS THE ADDRESS STACK FULL?            00424057
         BL    LG000605           -NO                                   00432057
         OI    FLAG2,FLAG2STK     -YES, REMEMBER TO PUT OUT MESSAGE     00450057
LG000605 TM    FLAG1,FLAG1ADR+FLAG1ASI+FLAG1FMT+FLAG1ARE  UPDATE        00458057
*                                 -CONTROL ARRAY?                       00466057
         BZ    LG000800           NO                                    00474057
         TM    FLAG1,FLAG1ADR+FLAG1ASI NEW ADDRESS?                     00482057
         BZ    LG000615           NO                                    00500057
         MVC   WKAADDR(4),ESAULAD NEW ADDRESS                           00508057
         TM    FLAG1,FLAG1RLL     RESULT OF SKIP,ROLL OR STACK?         00516057
         BZ    LG000610           NO                                    00524057
         OI    WKAADDR,WKAADRLL   SUPPRESS IMPLICIT STACK OPERATIONS    00532057
LG000610 MVC   WKAASID(2),ESAUAS2+2 NEW ASID                            00550057
LG000615 MVC   WKAFMT,ESAUDTY     NEW FORMAT                            00558057
         CLI   WKAFMT,C'C'        HANDLE CASE WHEN                      00566057
         BE    LG000620           -NO INITIALIZATION                    00574057
         MVI   WKAFMT,C'X'        -HAS BEEN DONE                        00582057
LG000620 EQU   *                                                        00600057
         IC    RF,WKAAREA         PREPARE TO UPDATE CONTROL ARRAY       00608057
         N     RF,=X'00000007'    KEEP JUST LOW THREE BITS              00616057
         LR    R0,RF              SAVE BYTE INDEX (1-ORG)               00624057
         SLL   RF,2               FORM WORD INDEX 1-ORG                 00632057
         LR    R6,RF              SAVE WORD INDEX (1-ORG)               00650057
         LA    RF,WKAVCTA-4(RF)   ADDRESS PROPER ARRAY                  00658057
         MVC   WKAVCTA-WKAVCTA(4,RF),WKAADDR NEW ADDRESS                00666057
         MVC   WKAVASI-WKAVCTA(2,RF),WKAASID NEW ASID                   00674057
         MVC   WKAVFMT-WKAVCTA(1,RF),WKAFMT NEW FORMAT                  00682057
         NI    WKAVFLAG-WKAVCTA(RF),255-WKAVCTAI OFF INVALID FLAG       00700057
         LR    RF,R0              AREA VALUE BACK IN GOOD INDEX REG     00708057
         IC    RF,WKALINES-1(RF)  NUMBER LINES THIS AREA                00716057
         LTR   RF,RF              ANY LINES ALLOCATED?                  00724057
         BNZ   LG000700           YES                                   00732057
         MVI   NEWWCC,WCCALARM+WCCRSMDT                                 00750057
         B     LG000800           GO BLOW YOUR HORN                     00758057
LG000700 BCTR  RF,0               DOWN 1 FOR EX OF OC                   00766057
         L     R6,WKALNCTV-4(R6)  OFFSET INTO CONTROL VECTOR            00774057
         LA    R6,WKALNCTL(R6)    PLACE TO START WRITING                00782057
         EX    RF,IG000300        MARK LINES TO BE RE-WRITTEN           00800057
LG000800 TM    FLAG0,FLAG0SUS     MUST WE DEPART NOW?                   00810057
         BNO   BLSR327B           NO - PREPARE OUTPUT                   00820057
         B     LA005600           YES, HANDLE SUBCOMMAND                00830057
