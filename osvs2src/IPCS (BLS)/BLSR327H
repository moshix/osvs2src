         TITLE 'BLSR3270--BLSR327H--CLOSED, INDEPENDENT SUBROUTINES'    00050000
*---------------------------------------------------------------------* 00100000
*                                                                     * 00150000
*BLSR327H--REDEFINE ALLOCATION OF WINDOWS A, B, C, AND D              * 00200000
*                                                                     * 00250000
*---------------------------------------------------------------------* 00300000
         SPACE 2                                                        00350000
*---------------------------------------------------------------------* 00400000
*        VALIDATE THE USERS REQUEST AS TO SIZES AND SUM               * 00450000
*---------------------------------------------------------------------* 00500000
         SPACE                                                          00550000
BLSR327H OI    FLAG1,FLAG1LIN     DISPLAY RESULTING ALLOCATION          00600000
         SLR   RF,RF              CLEAR IT                              00650000
         LA    R0,4               NUMBER OF AREAS WE LET HIM DEFINE     00700000
         SLR   R4,R4              CLEAR IT                              00750000
         SLR   R5,R5              DITTO - INDEX CURRENT AREA DEF        00800000
         LA    RE,19              MAX NUMBER LINES HE CAN ALLOCATE      00850000
LH000200 IC    RF,NEWLINES(R5)    HOW MUCH DID HE ASK FOR HERE          00900000
         CR    RF,RE              MORE THAN WE HAVE TO GIVE?            00950000
         BNH   LH000300           NO                                    01000000
         STC   RE,NEWLINES(R5)    YES, FORCE REMAINDER HERE             01050000
         LR    RF,RE              FORCE REMAINDER TO ZERO               01100000
LH000300 SLR   RE,RF              DECREMENT NUMBER LINES AVAIL          01150000
         LA    R5,1(0,R5)         INCREMENT INDEX VALUE                 01200000
         BCT   R0,LH000200        CHECK REST OF HIS REQUEST             01250000
         SPACE 2                                                        01300000
*---------------------------------------------------------------------* 01350000
*        RE NOW HAS UNALLOCATED LINES COUNT, AND ALLOCATION IS VALID. * 01400000
*        SCHEDULE ANY REWRITES REQUIRED                               * 01450000
*---------------------------------------------------------------------* 01500000
         SPACE                                                          01550000
         CLC   WKALINES(4),NEWLINES ANY CHANGES INDICATED?              01600000
         BER   R3                 SAME AS BEFORE                        01650000
         MVC   WKALINES(4),NEWLINES DIFFERENT, UPDATE OLD VALUES        01700000
         SLR   R4,RF              BYTE VALUE INDEX OF UN-EQUAL ALLOC    01750000
         SLR   RF,RF              ASSUME NO NULL LINE ON ROW 6          01800000
         CH    RE,=H'3'           CAN WE PUT A NULL LINE AT POSIT 6?    01850000
         BNH   LH000400           NO                                    01900000
         LA    RF,1               INITIALIZE INDEX FOR FIRST AREA       01950000
         BCTR  RE,0               DECREMENT NUMBER OF NULL LINES        02000000
LH000400 ST    RF,WKALNCTV        INIT FIRST LINE CONTROL VECTOR        02050000
         LA    R0,3               NUMBER OF SLOTS LEFT IN LNCTV         02100000
         SLR   R1,R1              CLEAR SOME REGS - INDEX LNCTV         02150000
         SLR   R6,R6              INDEX LINES ALLOCATION                02200000
         SLR   R7,R7              NEXT AREA'S ALLOCATION                02250000
LH000500 IC    R7,NEWLINES(R6)    PICK UP NEXT ALLOCATION               02300000
         ALR   RF,R7              ADD TO PREVIOUS INDEX                 02350000
         LTR   RE,RE              ANY NULLS LEFT TO INSERT?             02400000
         BZ    LH000600           NO                                    02450000
         BCTR  RE,0               YES, DECREMENT SPARE COUNT            02500000
         LA    RF,1(0,RF)         AND INCREMENT INDEX VALUE             02550000
LH000600 ST    RF,WKALNCTV+4(R1)  SET NEW INDEX                         02600000
         LA    R1,4(0,R1)         INCREMENT WORD INDEX REGISTER         02650000
         LA    R6,1(0,R6)         INCREMENT BYTE INDEX REGISTER         02700000
         BCT   R0,LH000500        GO CHECK NEXT AREA, IF ANY            02750000
         SPACE 2                                                        02800000
*---------------------------------------------------------------------* 02850000
*        LINE CONTROL INDEX UPDATED, NOW MARK CONTROL VECTOR. THE     * 02900000
*        SCREEN MUST BE RE-WRITTEN BELOW THE FIRST CHANGE IN ITS      * 02950000
*        ALLOCATION                                                   * 03000000
*---------------------------------------------------------------------* 03050000
         SPACE                                                          03100000
         XC    NEWLNCTL(19),NEWLNCTL CLEAR CURRENT UPDATE CONTROL       03150000
         LA    R0,4               NUMBER OF AREAS                       03200000
         LA    RF,1               BYTE INDEX VALUE, AND AREA NUMBER     03250000
         SLR   R1,R1              WORD INDEX FOR WKALNCTV               03300000
LH000700 L     R6,WKALNCTV(R1)    FETCH INDEX INTO LNCTL                03350000
         IC    RE,NEWLINES-1(RF)  NUMBER LINES THIS AREA                03400000
         LTR   RE,RE              ANY LINES THIS AREA?                  03450000
         BZ    LH000900           NO                                    03500000
LH000800 STC   RF,NEWLNCTL(R6)    YES, MARK THE UPDATE CONTROL          03550000
         LA    R6,1(0,R6)         INDEX NEXT BYTE IN UPDATE CTL         03600000
         BCT   RE,LH000800        HIT ALL OF THEM                       03650000
LH000900 LA    R1,4(0,R1)         UPDATE WORD INDEX                     03700000
         LA    RF,1(RF)           UPDATE BYTE INDEX AND AREA ID         03750000
         BCT   R0,LH000700        DO ALL AREAS                          03800000
         SPACE 2                                                        03850000
*---------------------------------------------------------------------* 03900000
*        CONTROLS ALL SET, MARK AREAS TO BE RE-WRITTEN                * 03950000
*---------------------------------------------------------------------* 04000000
         SPACE                                                          04050000
         LA    R6,WKALNCTL        POINT TO OLD VALUES                   04100000
         LA    R4,NEWLNCTL        POINT TO NEW VALUES                   04150000
         LA    R7,19              NUMBER OF BYTES TO CHECK              04200000
         LR    R5,R7              FIELDS HAVE EQUAL LENGTH              04250000
         LR    RF,R4              SAVE START ADDR                       04300000
         CLCL  R6,R4              CHECK NEW AGAINST OLD                 04350000
         SLR   R4,RF              OFFSET OF FIRST NEW AREA 0-18         04400000
         MVC   WKALNCTL(19),NEWLNCTL UPDATE THE VALUES BLSR327B USES    04450000
         SPACE 2                                                        04500000
*---------------------------------------------------------------------* 04550000
*        HAVE THE RIGHT AREA TO UPDATE... IS THE OFFSET CORRECT?      * 04600000
*---------------------------------------------------------------------* 04650000
         SPACE                                                          04700000
         IC    R7,0(R6)           SEE WHERE WE STOPPED                  04750000
         SLA   R7,2               FORM WORD INDEX, TEST FOR NULL LINE   04800000
         BZ    LH001000           WE WILL INSERT A NULL HERE            04850000
         L     R7,WKALNCTV-4(R7)  PICK UP INDEX INTO LNCTL              04900000
         CR    R7,R4              STARTING POSITION SAME OR HIGHER?     04950000
         BNL   LH001000           YES                                   05000000
         LA    R6,WKALNCTL(R7)    NO, GET CORRECT RE-WRITE START        05050000
         LR    R4,R7              AND CORRECT OFFSET FROM LNCTL         05100000
LH001000 LA    RE,18              MAX EXECUTE LENGTH                    05150000
         SLR   RE,R4              EX LENGTH TO USE                      05200000
         EX    RE,IG000300        MARK ALL AFTER THIS ONE               05250000
         BR    R3                 GO BACK THE RIGHT WAY                 05300000
