         MACRO                                                          00050000
&LABEL   BLSRDEND &PREFIX                                               00100000
.*--------------------------------------------------------------------* 00150000
.* BLSRDEND--TERMINATE A DATA TYPE PROCESSING TABLE DEFINITION        * 00200000
.*--------------------------------------------------------------------* 00250000
         GBLA  &DTDIM                                                   00300000
         GBLC  &GP,&GD(100),&GN(100),&GS(100),&GV(100)                  00350000
         LCLA  &JD,&JL,&JX                                              00400000
         LCLC  &CD,&CN,&CS,&CV,&CX                                      00450000
&GP.DIM  DC    F'&DTDIM'               DIMENSION OF ARRAY               00500000
*------- END OF TABLE HEADING ----------------------------------------* 00550000
         SPACE                                                          00600000
*------- BEGIN BODY OF TABLE -----------------------------------------* 00650000
&JD      SETA  1                       CURRENT DATA NAME INDEX          00700000
.L01     AIF   (&JD GT &DTDIM).L99     ALL DATA NAME PROCESSED          00750000
&CD      SETC  '&GD(&JD)'              CURRENT DATA NAME                00800000
&JL      SETA  K'&CD                   LENGTH OF DATA NAME              00850000
&CN      SETC  '&GN(&JD)'              NORMALIZED DATA NAME             00900000
&CS      SETC  '&GS(&JD)'              FIND EXIT ENTRY POINT NAME       00950000
&CV      SETC  '&GV(&JD)'              SCAN EXIT ENTRY POINT NAME       01000000
&JX      SETA  &JD                     NORMALIZED SEARCH INDEX          01050000
         AIF   ('&CD' EQ '&CN').L03    SELF-NORMALIZED                  01100000
&JX      SETA  &DTDIM                  NORMALIZED SEARCH INDEX          01150000
&CX      SETC  '&GD(&DTDIM)'           SEARCH DATA NAME                 01200000
.L02     AIF   ('&CN' EQ '&CX').L03    NORMALIZED TYPE FOUND            01250000
&JX      SETA  &JX-1                   NORMALIZED SEARCH INDEX          01300000
&CX      SETC  '&GD(&JX)'              SEARCH DATA NAME                 01350000
         AIF   (&JX GE 1).L02          CONTINUE NORMALIZED TYPE SEARCH  01400000
         MNOTE 8,'NORMALIZED DATA NAME ERROR FOR &CD'                   01450000
&JX      SETA  &JD                     SUBSTITUTE DATA NAME             01500000
.L03     ANOP                                                           01550000
&GP.&JD  DC    FL1'&JL',CL31'&CD',F'&JX',CL8'&CS',CL8'&CV'              01600000
&JD      SETA  &JD+1                   DATA NAME INDEX                  01650000
         AGO   .L01                                                     01700000
.L99     ANOP                                                           01750000
         SPACE                                                          01800000
*------- END OF DATA NAME PROCESSING TABLE ---------------------------* 01850000
         MEND                                                           01900000
