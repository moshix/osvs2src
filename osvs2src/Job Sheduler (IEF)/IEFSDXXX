         TITLE 'IEFSDXXX - SYSOUT WRITER SPANNED DATASETS'              00100017
*                                                                       00110020
*          RELEASE 22 DELETIONS/CHANGES                                 00120020
*          RELEASE 21 DELETIONS/CHANGES                                 00130020
*3200117000,133000-135000                                        A46610 00135021
*          RELEASE 20 DELETIONS/CHANGES                                 00140020
* 433000-512000                                                  MAPMAC 00150020
*2438                                                            A35780 00155020
*                                                                       00160020
*********************************************************************** 00200017
*                                                                     * 00300017
*TITLE-- IEFSDXXX - SYSOUT WRITER SPANNED DATASETS                    * 00400017
*                                                                     * 00500017
*STATUS CHANGE LEVEL 001                                                00600020
*                                                                     * 00700017
*FUNCTION/OPERATION-- THIS MODULE IS USED TO HANDLE VARIABLE SPANNED  * 00800017
*   DATASETS. IF EITHER INPUT OR OUTPUT DATASETS ARE VARIABLE SPANNED * 00900017
*   THIS MODULE WILL BE ENTERED. IN MOST CASES THERE IS NO BUILDING   * 01000017
*   OF THE COMPLETE LOGICAL RECORD FROM THE INPUT SEGMENTS BEFORE     * 01100017
*   THE RECORD IS BROKEN UP INTO SEGMENTS TO OUTPUT. RATHER, THE      * 01200017
*   INPUT SEGMENTS ARE MOVED INTO THE OUTPUT SEGMENTS DYNAMICALLY     * 01300017
*   AND GET AND PUT OF SEGMENTS IS DONE AS NEEDED. THE EXCEPTION TO   * 01400017
*   THIS IS WHEN THE WRITER OUTPUTS MACHINE CONTROL CHARACTERS AND    * 01500017
*   THE INPUT DOES NOT HAVE MACHINE CONTROL CHARACTERS. IN THIS CASE  * 01600017
*   THE WHOLE RECORD MUST BE BUILT BEFORE IT IS PUT OUT BECAUSE THE   * 01700017
*   CONTROL CHARACTER FROM THE FOLLOWING RECORD IS NEEDED TO GENERATE * 01800017
*   THE CONTROL CHARACTER FOR THIS RECORD.                            * 01900017
*   WHEN THE OUTPUT IS IN VARIABLE SPAN FORMAT AS MANY LINES OF       * 02000017
*   OUTPUT WILL BE GENERATED AS IS NEEDED TO PROCESS THE WHOLE        * 02100017
*   RECORD. IN THIS WAY A COMPLETE RECORD CAN BE PRINTED OR PUNCHED   * 02200017
*   NO MATTER HOW LONG IT MAY BE.                                     * 02300017
*                                                                     * 02400017
*ENTRY POINT-- IEFSDXXX - FROM THE IEFSD086, IEFSD087, IEFSD094 AND   * 02500017
*   IEFSD089 MODULES. THE ENTRY FROM EACH OF THESE EXCEPT IEFSD089    * 02600017
*   IS TO SET UP THE CONTROL AREAS THAT CONTAIN INFORMATION NEEDED    * 02700017
*   TO GET AND PUT VARIABLE SPANNED RECORDS AND TO GET THE AREA       * 02800017
*   NEEDED TO BUILD THE WHOLE RECORD IF THAT IS NEEDED (SEE           * 02900017
*   FUNCTION/OPERATION ABOVE). THERE ARE TWO ENTRIES FROM IEFSD089.   * 03000017
*   ONE ENTRY WHICH GOES TO IEFSDXXX TO HANDLE THE SEGMENT JUST       * 03100017
*   OBTAINED BY A GET AND THE OTHER WHICH GOES TO IEFSDXXX TO FIX     * 03200017
*   UP THE AREA JUST OBTAINED BY PUT.                                 * 03300017
*                                                                     * 03400017
*INPUT-- THE TOP BYTE OF REGISTER 15 CONTAINS:                        * 03500017
*   0 WHICH INDICATES A SEGMENT OBTAINED BY GET IS TO BE PROCESSED    * 03600017
*   4 WHICH INDICATES AN AREA JUST OBTAINED BY PUT IS TO BE FIXED     * 03700017
*   8 WHICH INDICATES THE SPANNED CONTROL AREA IS TO BE SET UP FOR    * 03800017
*      MESSAGES OR JOB SEPARATOR.                                     * 03900017
*  12 WHICH INDICATES THE SPANNED CONTROL AREA IS TO BE SET UP FOR    * 04000017
*      A DATASET.                                                     * 04100017
*   REGISTER 7  CONTAINS THE WORK LIST USED BY DSB AND SMB ROUTINE    * 04200017
*   REGISTER 8  CONTAINS THE WORK AREA FOR STANDARD OUTPUT ROUTINE    * 04300017
*   REGISTER 6  CONTAINS THE INPUT DCB OR MESSAGE.                    * 04400017
*                                                                     * 04500017
*OUTPUT-- WHEN ENTRY IS FROM IEFSD086, IEFSD087, OR IEFSD094 THE      * 04600017
*   SAME MODULE IS RETURNED TO WITH THE SPANNING CONTROL AREA SET UP. * 04700017
*   WHEN ENTRY IS FROM IEFSD089 RETURN IS TO THIS MODULE WITH A       * 04800017
*   CODE IN REGISTER 15:                                              * 04900017
*   0 GO TO PUT                                                       * 05000017
*   4 GO TO GET                                                       * 05100017
*                                                                     * 05200017
*EXTERNAL ROUTINES-- NONE                                             * 05300017
*                                                                     * 05400017
*EXIT-- RETURNS TO THE INSTRUCTION FOLLOWING THE ONE WHICH BRANCHED   * 05500017
*   TO THIS ROUTINE.                                                  * 05600017
*                                                                     * 05700017
*TABLES/WORK AREAS-- THE CONTROL AREA FOR SPANNING, ANY AREA GOT TO   * 05800017
*   BUILD THE COMPLETE RECORD IN IF SUCH AREA IS OVER 168 BYTES, THE  * 05900017
*   CONDITIONAL GETMAIN LIST, THE DCB, THE WORKLIST USED BY DSB AND   * 06000017
*   SMB ROUTINE, AND THE WORK AREA FOR STANDARD OUTPUT ROUTINE.       * 06100017
*                                                                     * 06200017
*ATTRIBUTES-- THIS ROUTINE IS REENTRANT AND REFRESHABLE.              * 06300017
*                                                                     * 06400017
*NOTES-- THE CONDITIONAL GETMAIN AND RETURN MACROS ARE USED.          * 06500017
*                                                                     * 06600017
*********************************************************************** 06700017
R0       EQU   0                                                        06800017
R1       EQU   1                                                        06900017
R2       EQU   2                                                        07000017
R3       EQU   3                                                        07100017
R4       EQU   4                                                        07200017
R5       EQU   5                                                        07300017
R6       EQU   6                                                        07400017
R7       EQU   7                                                        07500017
R8       EQU   8                                                        07600017
R9       EQU   9                                                        07700017
R10      EQU   10                                                       07800017
R11      EQU   11                                                       07900017
R12      EQU   12                                                       08000017
R13      EQU   13                                                       08100017
R14      EQU   14                                                       08200017
R15      EQU   15                                                       08300017
IEFSDXXX CSECT                                                          08400017
*THE FOLLOWING AND R5 ARE BUSY                                          08500017
         USING WORKLST,R7                                               08600017
         USING WKSOR,R8                                                 08700017
         USING IHADCB,R6                INPUT DCB OR MESSAGE LOC.       08800017
*INITIALIZATION                                                         08900017
         SAVE  (14,12)                  SAVE REGISTERS                  09000017
         BALR  R9,0                     ESTABLISH BASE                  09100017
         USING *,R9                                                     09200017
         L     R1,WOXXA                 GET ADDR. OF CONTROL AREA       09300017
         ST    R13,4(R1)                PRESERVE OLD SAVE AREA          09400017
         LR    R13,R1                   GET NEW SAVE AREA               09500017
         USING WORKFXXX,R13             GET WORK AREA BASE              09600017
*BRANCH TO THE ROUTINE DESIRED                                          09700017
         SRL   R15,24                   GET WHERE TO BRANCH TO          09800017
         B     *+4(R15)                 BRANCH TO TABLE                 09900017
         B     MAINR                    BRANCH TO INPUT RECORD          10000017
         B     AFPUT                    BRANCH TO PUT DONE              10100017
         B     SUDTA                    BRANCH TO DATASET SET UP        10200017
*SET UP CONSTANTS FOR INPUT NOT DATA SET                                10300017
         XC    SWXXX(WORKEEND-SWXXX),SWXXX  CLEAR WORK AREA             10400017
         LA    R12,168                  GET MAXIMUM INPUT SIZE          10500017
         ST    R12,MAXI1                SAVE IT                         10600017
         B     SUOUC                    BRANCH TO DO OUTPUT CONST.      10700017
*SET UP CONSTANTS FOR DATASET INPUT                                     10800017
SUDTA    XC    SWXXX(WORKEEND-SWXXX),SWXXX  CLEAR WORK AREA             10900017
         OI    SWXXX,DTASW              TURN ON DATASET SWITCH          11000017
         SR    R4,R4                    CLEAR REG 4                     11100017
         TM    DCBRECFM,ASASW+MACSW     IF NOT CONTROL CHAR. THEN       11200017
         BZ    INLTE                    GO TO GET MAX. IN  LENGTH       11300017
         LA    R4,1                     PUT 1 TO SUBTRACT FOR C.C.      11400017
INLTE    LH    R12,DCBBLKSI             GET BLOCK SIZE                  11500017
         TM    DCBRECFM,UNKSW           IF UNKNOWN TYPE THEN            11600017
         BO    SAIMA                    GO SAVE MAX LENGTH       A46610 11700021
         LH    R12,DCBLRECL             GET MAX. RECORD SIZE            11800017
         LTR   R12,R12                  IF NOT OVER 32K THEN            11900017
         BP    INLTD                    BRANCH AS HAVE LENGTH           12000017
         L     R12,MXSIZ                GET MAXIMUM SIZE                12100017
INLTD    TM    DCBRECFM,FIXSW           IF FIXED TYPE THEN              12200017
         BO    SAIMA                    GO SAVE MAX. IN LENGTH          12300017
         LA    R4,4(R4)                 ADD 4 TO SUBTRACT FOR V TYPE    12400017
SAIMA    SR    R12,R4                   GET REAL MAX. IN                12500017
         ST    R12,MAXI1                SAVE IT                         12600017
*SET UP CONSTANTS FOR OUTPUT                                            12700017
         DROP  R6                                                       12800017
         USING IHADCB,R5                ESTABLISH OUTPUT DCB BASE       12900017
SUOUC    SR    R4,R4                    CLEAR REG 4                     13000017
         MVI   SEGIN,0                  INDICATE 0                      13100017
         L     R5,DCBOT                 GET OUTPUT DCB                  13200017
INLTF    LA    R3,LENLO+4               GET LOCATION OF LENGTH          13600017
         TM    DCBRECFM,UNKSW           IF NOT UNKNOWN TYPE THEN        13700017
         BM    INLTG                    GO TO TEST FOR FIXED            13800017
         LH    R1,DCBBLKSI              GET BLOCK SIZE                  13830017
         LTR   R1,R1                    IF A BLOCK SIZE THEN            13860017
         BNZ   INTLI                    GO TO USE IT                    13890017
         LH    R1,DCBBUFL               GET BUFFER LENGTH               13920017
INTLI    LR    R11,R1                   HAVE MIN. LENGTH                13950017
         SR    R11,R4                   GET ACTUAL LENGTH               14000017
         CR    R11,R12                  IF OUTPUT SHORTER THEN          14100017
         BNH   USEOT                    GO SAVE MIN. OUTPUT LENGTH      14200017
         LR    R11,R12                  GET SMALLER LENGTH              14300017
USEOT    STH   R11,DCBLRECL             USE IT AS RECORD SIZE           14400017
         LR    R12,R1                   GET MAXIMUM LENGTH              14500017
         B     SAOMK                    GO SAVE MIN. OUTPUT LENGTH      14600017
INLTG    LH    R12,DCBLRECL             GET MAX. RECORD SIZE            14700017
         LTR   R12,R12                  IF NOT OVER 32K THEN            14800017
         BP    INLTH                    BRANCH AS HAVE LENGTH           14900017
         L     R12,MXSIZ                GET MAXIMUM SIZE                15000017
INLTH    LR    R11,R12                  GET MIN. OUT FOR F TYPE         15100017
         TM    DCBRECFM,FIXSW           IF FIXED TYPE THEN              15200017
         BO    SAOMI                    GO SAVE MIN. OUTPUT LENGTH      15300017
         LA    R4,4(R4)                 ADD 4 TO SUBTRACT FOR V TYPE    15400017
         LA    R11,18                   GET MIN. OUT FOR V NOT SPANNED  15500017
         TM    DCBRECFM,VARSP           IF SPANNED TYPE THEN            15600017
         BO    SAOMA                    GO TO FIX FOR SPANNED           15700017
SAOMI    SR    R11,R4                   GET ACTUAL MIN. OUT LENGTH      15800017
SAOMK    ST    R11,MINO1                SAVE IT                         15900017
SAOMA    SR    R12,R4                   GET REAL MAX. OUT               16000017
         ST    R12,MAXO1                SAV IT                          16100017
         ST    R3,LENLO                 SAVE IT AS WHERE LENGTH SAVED   16200017
*GET SPECIAL WORK AREA IF ASA INPUT AND VARIABLE SPANNED MACH. OUT      16300017
         TM    DCBRECFM,VARSP+MACSW     IF NOT VS MACH. OUTPUT THEN     16400017
         BNO   XXXRT                    GO TO RETURN                    16500017
         TM    DCBRECFM,UNKSW           IF UNKNOWN OUTPUT THEN          16600017
         BO    XXXRT                    GO TO RETURN                    16700017
         TM    SORS1,MACHS              IF MACHINE INPUT THEN           16800017
         BO    XXXRT                    GO TO RETURN                    16900017
         TM    SWXXX,DTASW              IF NOT DATASET INPUT THEN       17000017
         BZ    VALES                    GO TO USE AREA WE HAVE          17100017
         C     R12,MAXI1                COMPARE OUT AND IN MAXIMUM      17200017
         BNH   CMPTH                    IF HAVE SMALLER THEN BRANCH     17300017
         L     R12,MAXI1                GET SMALLER                     17400017
CMPTH    C     R12,THTOK                COMPARE TO 32K IF SMALLER       17500017
         BL    GCORM                    THEN GO TO GET WORK AREA        17600017
         OI    SORS1,OVERT              TURN ON CANT HANDLE IT SW       17700017
         B     VALES                    GO TO SET UP FAKE AREA          17800017
GCORM    C     R12,ONSXA                COMPARE TO 168 IF NOT HIGH      17900017
         BNH   VALES                    USE THE AREA AVAILABLE          18000017
         MVC   WOKRL(GETME-GETMC),GETMC PUT IN GETMAIN LIST             18100017
         ST    R12,WOKRL                PUT LENGTH IN LIST              18200017
         LA    R12,WOKRA                GET WHERE TO SAVE SIZE          18300017
         ST    R12,WOKRA                SAVE IT IN ITSELF               18400017
         LA    R1,WOKRL                 GET ADDRESS OF LIST             18500017
         GETMAIN EC,MF=(E,(1))          GET MAIN CONDITIONALLY          18600017
         LTR   R15,R15                  IS THE CORE GOT                 18700017
         BZ    XXXRT                    THEN GO TO RETURN               18800017
         OI    SORS1,CNTGE              TURN ON CANT GET CORE SW.       18900017
*USE AREA ALREADY SET UP AS WORK AREA                                   19000017
VALES    LA    R11,168                  GET 168                         19100017
         ST    R11,WOKRL                MAKE IT LENGTH                  19200017
         LA    R11,BSKWA                GET ADDRESS OF AREA             19300017
         ST    R11,WOKRA                SAVE IT AS WORK AREA ADD.       19400017
XXXRT    L     R13,4(R13)               GET SAVE AREA                   19500017
         RETURN (14,12)                 RETURN                          19600017
********                                                                19700017
*                                                                       19800017
* THE FOLLOWING IS THE BASIC ROUTINE WHICH HANDLES THE SPANNING         19900017
* FOR PUT. THIS IS BRANCHED TO FROM IEFSD089 AND RETURNS TO THERE.      20000017
*                                                                       20100017
********                                                                20200017
*SAVE SEGMENT AND CHECK IT                                              20300017
MAINR    MVC   INSEG(1),SEGIN           SAVE NEW SEGMENT                20400017
         MVI   SEGIN,0                  CLEAR SEGMENT IND.              20500017
*IS THIS A FIRST SEGMENT                                                20600017
         TM    INSEG,NOTFI              IF NOT FIRST SEGMENT THEN       20700017
         BO    CNTRT                    BRANCH TO DO SUBSEQUENT SEG.    20800017
*IS THERE A RECORD TO BE PUT FROM WORK AREA                             20900017
         TM    SWXXX,WATPT              IF 'TO BE PUT' SWITCH ON THEN   21000017
         BO    MVFWA                    BRANCH TO SET UP TO PUT         21100017
*INITIALIZE FOR RECORD JUST GOT                                         21200017
RECJG    MVC   MAXI2(12),MAXI1          INITIALIZE MAX. + MIN. COUNTS   21300017
*RETURN TO PUT                                                          21400017
RETPU    L     R15,LENLO                GET SEGMENTS LOCATION           21500017
         MVC   WOLCL+2(1),2(R15)        SAVE SEGMENT INDICATION         21600017
         SR    R15,R15                  MAKE R15 ZERO AS RETURN CODE    21700017
RETAC    L     R13,4(R13)               GET WHERE R13 POINTED           21800017
         RETURN (14,12),RC=(15)                                         21900017
*AFTER PUT WHEN THERE IS SPANNING BRANCH BACK TO HERE                   22000017
         USING IHADCB,R5                MAKE OUTPUT BASE FOR DATASET    22100017
*GET LENGTH AND LOCATION OF PUT                                         22200017
AFPUT    L     R1,ATENW+4               GET REG. SAVE AREA              22300017
         LM    R0,R1,20(R1)             GET LENGTH AND LOC.             22400017
*IS THE OUTPUT VARIABLE SPANNED                                         22500017
         TM    DCBRECFM,VARSP           IF OUTPUT IS SPANNED THEN       22600017
         BO    LLOUT                    BRANCH AS R0 HAS LENGTH         22700017
*GET LENGTH AVAILABLE WHEN NOT SPANNED                                  22800017
         L     R0,MAXO1                                                 22900017
*SAVE LENGTH AND LOCATION TO MOVE OUTPUT RECORD INTO                    23000017
LLOUT    ST    R0,SOUTL                                                 23100017
         ST    R1,SOUTL+4                                               23200017
*IS THE OUTPUT VARIABLE                                                 23300017
         TM    DCBRECFM,VARSW           IF OUTPUT ISNT VARIABLE         23400017
         BZ    FLNVA                    BRANCH TO AVOID V FIX           23500017
         TM    DCBRECFM,FIXSW           IF OUTPUT ISNT VARIABLE         23600017
         BO    FLNVA                    BRANCH TO AVOID V FIX           23700017
*FIX HEADER FOR VARIABLE TYPE OUTPUT                                    23800017
         ST    R1,LENLO                 SAVE HEADER ADDR AS LENGTH LOC. 23900017
         XC    0(4,R1),0(R1)            ZERO OUT HEADER                 24000017
         TM    INSEG,NOTFI              IF FIRST SEGMENT THEN           24100017
         BZ    GSGLT                    BRANCH TO NOT CHANGE SEGMENT    24200017
         MVI   2(R1),NOTFI              INDICATE CONTINUING SEG.        24300017
GSGLT    LA    R2,4                     GET SEGMENT LENGTH              24400017
         BAL   R14,MODOD                BRANCH TO FIX OUTPUT COUNT      24500017
         B     FIRSG                    BRANCH TO TEST FOR FIRST SEG.   24600017
*FIX LENGTH FOR NON-VARIABLE OUTPUT                                     24700017
FLNVA    L     R12,LENLO                GET LENGTH LOCATION             24800017
         XC    0(2,R12),0(R12)          MAKE LENGTH 0                   24900017
*IS THIS A FIRST SEGMENT                                                25000017
FIRSG    TM    INSEG,NOTFI              IF NOT FIRST SEG. THEN          25100017
         BO    CNTRT                    BRANCH TO AVOID INITIALIZING    25200017
*INITIALIZE OUTPUT FOR FIRST SEGMENT                                    25300017
         OI    INSEG,NOTFI              FIX INPUT SEG. TO NOT FIRST     25400017
         TM    DCBRECFM,ASASW+MACSW     IF NO CONTROL CHAR. THEN        25500017
         BNM   CNTRT                    BRANCH TO NOT PUT IT IN         25600017
         L     R12,SOUTL+4              GET C.C. LOCATION               25700017
         ST    R12,LOCAT                AND SAVE IT                     25800017
         MVC   0(1,R12),SAVEC           PUT C.C. IN IT                  25900017
         MVC   SAVEC,SAVED              SAVE NEXT C.C. IF ONE EXISTS    26000017
         LA    R2,1                     GET LENGTH OF C.C.              26100017
         BAL   R14,MODOD                BRANCH TO FIX OUTPUT COUNT      26200017
*CONTROL ROUTINE TO DECIDE WHAT TO DO NEXT                              26300017
CNTRT    LM    R3,R4,MAXI2              GET MAX. IN AND OUT LEFT        26400017
         L     R11,INLSF                GET INPUT LENGTH LEFT           26500017
         L     R12,SOUTL                GET OUTPUT LENGTH LEFT          26600017
         LTR   R2,R4                    GET MAX. OUT LENGTH. IF 0 THEN  26700017
         BZ    RECAP                    BRANCH TO NO MORE OUTPUT        26800017
         CR    R2,R3                    IF MAX. IN LENGTH NOT LESS THEN 26900017
         BNH   CMP11                    KEEP THE LOW NUMBER             27000017
         LTR   R2,R3                    GET MAX. IN LENGTH. IF 0 THEN   27100017
         BZ    FIMIN                    BRANCH TO NO MORE INPUT         27200017
CMP11    CR    R2,R11                   IF IN LENGTH NOT LESS THEN      27300017
         BNH   CMP12                    KEEP THE LOW NUMBER             27400017
         LTR   R2,R11                   GET IN LENGTH. IF 0 THEN        27500017
         BZ    LSTES                    BRANCH TO INPUT USED UP         27600017
CMP12    CR    R2,R12                   IF OUT LENGTH NOT LESS THEN     27700017
         BNH   NTDON                    KEEP THE LOW NUMBER. GO TO MOVE 27800017
         LTR   R2,R12                   GET OUT LENGTH. IF 0 THEN       27900017
         BZ    FIXLT                    BRANCH TO OUTPUT USED UP        28000017
NTDON    BAL   R14,MOVCH                BRANCH TO MOVE                  28100017
*FIX OUTPUT LENGTH. REG 2 HAS MODIFYING NUMBER                          28200017
         LM    R3,R4,MAXO2              GET MAX. AND MIN. OUTPUT        28300017
         SR    R3,R2                    SUBTRACT FROM MAX. OUTPUT       28400017
         SR    R4,R2                    SUBTRACT FROM MIN. OUTPUT       28500017
         STM   R3,R4,MAXO2              RESTORE                         28600017
         BAL   R14,MODOD                BRANCH TO FIX OTHER OUT COUNT   28700017
*FIX INPUT LENGTH. REG 2 HAS MODIFYING NUMBER                           28800017
         LM    R3,R4,INLSF              GET IN LENGTH AND LOCATION      28900017
         SR    R3,R2                    SUBTRACT FROM LENGTH            29000017
         AR    R4,R2                    ADD TO LOCATION                 29100017
         STM   R3,R4,INLSF              RESTORE                         29200017
         L     R4,MAXI2                 GET MAX. INPUT                  29300017
         SR    R4,R2                    SUBTRACT FROM MAX. INPUT        29400017
         ST    R4,MAXI2                 RESTORE                         29500017
         B     CNTRT                    BRANCH TO DECIDE WHAT NEXT      29600017
*IS THIS THE LAST OUTPUT SEGMENT                                        29700017
LSTES    TM    INSEG,NOTLA              IF NOT LAST THEN                29800017
         BO    RTGET                    GO TO GET NEXT                  29900017
*IS THERE MORE TO BE MOVED TO GET MINIMUM OUTPUT                        30000017
FIMIN    L     R12,MINO2                GET MIN. OUTPUT                 30100017
         C     R12,MAXO2                IF CAN MOVE IT ALL THEN         30200017
         BL    MOVNO                    BRANCH TO MOVE IT               30300017
         L     R12,MAXO2                GET MAXIMUM OUTPUT              30400017
MOVNO    LTR   R12,R12                  IF NOTHING MORE NEEDED THEN     30500017
         BNP   RECAP                    BRANCH TO RECORD ALL PUT        30600017
*MOVE ENOUGH TO MAKE MINIMUM. (NOTE SPANNING OUTPUT CANT COME HERE)     30700017
         L     R2,SOUTL+4               GET OUTPUT LOCATION             30800017
         MVI   0(R2),C' '               MOVE IN 1 BLANK                 30900017
         ST    R2,INLSF+4               WILL MOVE FROM BLANK            31000017
         LA    R2,1(R2)                 WILL MOVE TOO NEXT              31100017
         ST    R2,SOUTL+4                                               31200017
         LR    R2,R12                   GET AMOUNT TO MOVE              31300017
         S     R2,ONELY                 SUBTRACT 1 ALREADY MOVED        31400017
         BZ    PCTFI                    BRANCH IF ALL MOVED             31500017
         BAL   R14,MOVCH                BRANCH TO FIX OUT DONE          31600017
PCTFI    A     R2,ONELY                 GET ACTUAL NUMBER MOVED         31700017
         BAL   R14,MODCO                BRANCH TO MODIFY COUNTS         31800017
         B     RTGET                    BRANCH AS ARE DONE              31900017
*IS THE WORK AREA RECORD BEING PUT                                      32000017
RECAP    TM    SWXXX,WABPT              IF WORK AREA BEING PUT THEN     32100017
         BO    RSTGT                    BRANCH TO START NEXT RECORD     32200017
*RETURN TO GET                                                          32300017
RTGET    LA    R15,4                    MAKE R15 FOUR AS RETURN CODE    32400017
         B     RETAC                    BRANCH TO ACTUAL RETURN         32500017
*IS THE OUTPUT SPANNED                                                  32600017
FIXLT    TM    DCBRECFM,VARSP           IF NOT SPANNED TYPE             32700017
         BNO   RTGET                    GO TO GET                       32800017
*INDICATE OUTPUT RECORD SEGMENT IS NOT LAST. (ONLY USED IF SPAN OUTPUT) 32900017
         L     R12,LENLO                GET HEADER ADDRESS              33000017
         OI    2(R12),NOTLA                                             33100017
*IS WORK AREA RECORD BEING PUT                                          33200017
         TM    SWXXX,WABPT              IF WORK AREA BEING PUT THEN     33300017
         BO    RETPU                    BRANCH TO PUT                   33400017
*IS WORK AREA TO BE USED TO BUILD RECORD                                33500017
         L     R12,WOKRL                GET LENGTH OF WORK AREA         33600017
         LTR   R12,R12                  IF NONE THEN                    33700017
         BZ    RETPU                    BRANCH TO PUT                   33800017
********                                                                33900017
*                                                                       34000017
* THE FOLLOWING TAKES CARE OF CASES WHERE A WORK AREA IS USED TO BUILD  34100017
* RECORD IN.                                                            34200017
*                                                                       34300017
********                                                                34400017
*SET UP WORK AREA TO CONTINUE BUILDING RECORD FROM INPUT                34500017
         MVC   MAXI3(12),MAXI2          SAVE CURRENT MAX. + MIN. COUNTS 34600017
         LA    R12,LENLO+4              GET LENGTH LOCATION             34700017
         ST    R12,LENLO                SAVE IT                         34800017
         XC    LENLO+4(4),LENLO+4       MAKE LENGTH 0                   34900017
         MVC   SOUTL(4),MAXO2           MAKE MAX. OUT LEFT THE OUT SIZE 35000017
         MVC   SOUTL+4(4),WOKRA         MAKE WORK AREA THE OUT LOC.     35100017
         MVC   WOKRA+6(2),WOLCL         SAVE LENGTH FOR PUT             35200017
         OI    SWXXX,WATPT              TURN ON WORK AREA TO BE PUT SW  35300017
         B     CNTRT                    BRANCH TO DECIDE WHAT TO DO     35400017
*HAVE MOVED INPUT RECORD INTO WORK AREA. PREPARE TO MOVE FROM WORK AREA 35500017
MVFWA    MVC   MAXI2(12),MAXI3          RESTORE OLD MAX. + MIN. COUNTS  35600017
         MVC   INLSW(8),INLSF           SAVE INPUT INFORMATION          35700017
         MVC   INSEW(1),INSEG           SAVE INPUT SEGMENT              35800017
         MVI   INSEG,NOTFI              MAKE INPUT SEGMENT NOT FIRST    35900017
         MVC   INLSF+2(2),LENLO+4       MAKE INPUT LENGTH THE MAX. OUT  36000017
         MVC   INLSF+4(4),WOKRA         MAKE WORK AREA THE IN LOC.      36100017
         MVC   WOLCL(2),WOKRA+6         RESTORE LENGTH FOR PUT          36200017
         NI    SWXXX,255-WATPT          TURN OFF WORK AREA TO BE PUT SW 36300017
         OI    SWXXX,WABPT              TURN ON WORK AREA BEING PUT SW  36400017
         B     RETPU                    BRANCH TO PUT                   36500017
*RESTORE INFORMATION FROM LAST GET TO PREPARE TO MOVE IT                36600017
RSTGT    NI    SWXXX,255-WABPT          TURN OFF WORK AREA BEING PUT SW 36700017
         MVC   INLSF(8),INLSW           RESTORE INPUT INFORMATION       36800017
         MVC   INSEG(1),INSEW           RESTORE INPUT SEGMENT           36900017
         B     RECJG                    GO TO SET UP FOR MOVING INPUT   37000017
********                                                                37100017
*                                                                       37200017
* SUBROUTINES                                                           37300017
*                                                                       37400017
********                                                                37500017
*SUBROUTINE TO MOVE  FROM THE INPUT TO THE OUTPUT. REG 2 HAS MOVE AMT.  37600017
MOVCH    L     R3,INLSF+4               GET LOCATION TO MOVE FROM       37700017
         L     R4,SOUTL+4               GET LOCATION TO MOVE TO         37800017
         LR    R11,R2                   SAVE AMOUNT TO MOVE             37900017
MOVLO    LR    R12,R11                                                  38000017
         BCTR  R12,0                    SAVE MOVING CONSTANT            38100017
         N     R12,TWFIF                WILL MOVE ONLY UP TO 256        38200017
         BZ    MOVST                    IF ONLY 1 GO TO MOVE ONLY 1     38300017
         EX    R12,MOVST                EXECUTE MOVE OF CURRENT AMOUNT  38400017
MOVST    MVC   0(0,R4),0(R3)            MOVE 1                          38500017
         A     R12,ONELY                GET AMOUNT MOVED CORRECTED      38600017
         AR    R3,R12                   ADD TO FROM  LOC.               38700017
         AR    R4,R12                   ADD TO TO LOC.                  38800017
         SR    R11,R12                  SUBTRACT AMOUNT MOVED           38900017
         BNZ   MOVLO                    IF NOT DONE THEN MOVE MORE      39000017
         BR    R14                      RETURN                          39100017
*SUBROUTINE TO FIX OUTPUT COUNT. REG. 2 HAS MODIFYING NUMBER            39200017
MODOD    LM    R3,R4,SOUTL              GET OUTPUT LENGTH AND LOC       39300017
         SR    R3,R2                    SUBTRACT FROM LENGTH            39400017
         AR    R4,R2                    ADD TO LOCATION                 39500017
         STM   R3,R4,SOUTL              RESTORE THEM                    39600017
MODCO    L     R4,LENLO                 GET LOCATION OF LENGTH          39700017
         MVC   WOLCL(2),0(R4)           MOVE TO BOUNDRY                 39800017
         LH    R3,WOLCL                 GET LENGTH                      39900017
         AR    R3,R2                    ADD TO LENGTH                   40000017
         STH   R3,WOLCL                 RESTORE LENGTH                  40100017
         MVC   0(2,R4),WOLCL            MOVE FROM BOUNDRY               40200017
         BR    R14                      RETURN                          40300017
*CONSTANTS                                                              40400017
         DS    0F                                                       40500017
MXSIZ    DC    X'7FFFFFFF'              MAXIMUM NUMBER TO USE FOR SIZE  40600017
THTOK    DC    X'00008000'              32,768 TOO BIG FOR SET SIZE     40700017
TWFIF    DC    X'000000FF'              255                             40800017
FOURY    DC    X'00000004'                                              40900017
TWOLY    DC    X'00000002'                                              41000017
ONELY    DC    X'00000001'                                              41100017
ONSXA    DC    F'168'                                                   41200017
*GETMAIN LIST                                                           41300017
GETMC    GETMAIN EC,LV=168,MF=L                                         41400017
GETME    EQU   *                                                        41500017
         DC    C'ZAPSZAPSZAPSZAPS'                                      41600017
         DC    C'ZAPSZAPSZAPSZAPS'                                      41700017
         DC    C'ZAPSZAPSZAPSZAPS'                                      41800017
         DC    C'ZAPSZAPSZAPSZAPS'                                      41900017
         DC    C'ZAPSZAPSZAPSZAPS'                                      42000017
*EQUATES                                                                42100017
ASASW    EQU   4                        ASA CONTROL CHARACTER           42200017
MACSW    EQU   2                        MACHINE CONTROL CHARACTER       42300017
VARSW    EQU   64                       VARIABLE SWITCH WHEN NOT F      42400017
FIXSW    EQU   128                      FIXED SWITCH WHEN NOT V         42500017
NOTLA    EQU   X'1'                     NOT LAST SEGMENT                42600017
NOTFI    EQU   X'2'                     NOT FIRST SEGMENT               42700017
VARSP    EQU   X'48'                    VARIABLE SPANNED SWITCH         42800017
UNKSW    EQU   VARSW+FIXSW              UNKNOWN SWITCH                  42900017
*CONTROL AREA FOR SPANNING                                              43000017
*DEFINE DCB                                                             43100017
         DCBD  DSORG=PS                                                 43200017
         IEFSDWKT                                                MAPMAC 45200020
         IEFSDSDR                                                MAPMAC 47200020
         IEFSDWKX                                                MAPMAC 49200020
         END                                                            51300017
