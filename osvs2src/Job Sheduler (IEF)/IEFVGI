         TITLE 'IEFVGI - IEFQMNGR'                                      00020000
         IEFQMNGR                                                       00040000
         TITLE 'IEFVGI - IWA'                                           00060000
         IEFCOMWA                                                Y02668 00090002
         IEFVMIWA                                                       00092002
         TITLE 'IEFVGI - AWA'                                           00100000
AWA      DSECT                                                          00120000
         USING AWA,RA                                                   00140000
         IEFVMAWA                                                       00160000
         TITLE 'IEFVGI - CSECT'                                         00180000
IEFVGI   CSECT                                                          00200000
*********************************************************************** 00220000
*                                                                     * 00240000
*        INPUT TO THIS ROUTINE.                                       * 00260000
*                                                                     * 00280000
*              R1 = POINTS TO ENTRY TO BE PLACED IN DICTIONARY.       * 00300000
*              RA = ADDRESS OF AUXILIARY WORK AREA.                   * 00320000
*              RC = ADDRESS OF INTERPRETER WORK AREA.                 * 00340000
*              RD = ADDRESS OF CURRENT REGISTER SAVE AREA.            * 00360000
*              RE = RETURN ADDRESS.                                   * 00380000
*                                                                     * 00400000
*********************************************************************** 00420000
*                                                                       00440000
         STM   14,12,12(13)       SAVE REGISTERS.                       00460000
         SPACE                                                          00480000
         BALR  RB,0               REG 11 IS THE BASE REGISTER           00500000
         USING *,RB                    OF THE INPUT ROUTINE.            00520000
*****************************************************************Y02028 00525002
* MODULE TRACE CODE - FOR TESTING.                               Y02028 00530002
         L     RF,TRACEV           LOAD TRACE RTNE ADDR.         Y02028 00535002
         BALR  RE,RF               ENTER MOD ID IN TRACE RECORD. Y02028 00540002
TRACEV   DC    V(TRACE)            TRACE RTNE ADDR.              Y02028 00545002
         DC    C'VGI '             MOD ID USED BY TRACE.         Y02028 00550002
* TRACE RETURNS HERE.                                            Y02028 00555002
*****************************************************************Y02028 00560002
         B     VGI                 BR AROUND ID.                 Y02668 00570002
         MODID BR=NO               MODULE IDENT.                 Y02668 00580002
*        DC    C'REL4'              FUNCTION ID AND            @Z40MPTH 00590003
*        DC    C'02'   LEV # (RACF)                            @Z40RPSM 00594003
         DC    C'03'   LEV # (MERGE SU4,10,16 CODE TO 037 PTF) @ZA27750 00598003
VGI      DS    0H                                                Y02668 00610002
         USING AWA,RA             AWA ADDRESS                           00660000
         SPACE                                                          00680000
         LA    R9,SREFB           ADDRESS OF INPUT DICTIONARY.          00700000
         SR    R6,R6              SET R6 FOR IC INSTRUCTIONS.           00720000
         SR    R8,R8              R8 = ZERO FOR COMPARES & ZEROEING.    00740000
         LA    R7,7(R9)           POINT AT FIRST ENTRY IN TABLE.        00760000
         TM    SWD,DCTFST         IS THIS THE FIRST DICT ENTRY.         00780000
         BO    GI1                NO. GO TO POINT TO NEXT ENTRY SLOT.   00800000
*                                                                       00820000
*   FIRST DICTIONARY ENTRY.  SET DCTFST, ZERO SREFB TABLE AND ZERO      00840000
*        TSREFB SLOT IN IWA. ALSO, SET TABLE ID = 8.                    00860000
*                                                                       00880000
         OI    SWD,DCTFST         SET BIT INDICATING FIRS DICT ENTRY.   00900000
         XC    0(176,R9),0(R9)    ZERO DICT 1.                          00920000
         MVI   3(R9),GIDCTID      SET TABLE ID.                         00940000
         ST    R8,TSREFB          ZERO TSREFB SLOT IN IWA.              00960000
         B     GI2                GO TO STORE ENTRY.                    00980000
*                                                                       01000000
*   NOT FIRST ENTRY - CHAIN TO END OF DICT.                             01020000
*                                                                       01040000
GI1      DS    0H                                                       01060000
         IC    R6,0(R7)           PICK UP LENGTH OF ENTRY.              01080000
         LA    R7,0(R6,R7)        INCREMENT TO NEXT ENTRY POSITION.     01100000
         CLI   0(R7),X'00'        IS IT THE END OF TABLE.               01120000
         BNE   GI1                NO. GO TO GET NEXT ENTRY POSITION.    01140000
         SPACE                                                          01160000
*                                                                       01180000
*   END OF DICT ENTRIES - COMPUTE OVERFLOW POSSIBILITY.                 01200000
*                                                                       01220000
         IC    R6,0(R1)           GET NEW ENTRY LENGTH.                 01240000
         LA    R5,1(R6,R7)        COMPUTE NEW END ADDR+1 (TO SHOW END). 01260000
         SR    R5,R9              SUBTRACT START ADDRESS                01280000
         CH    R5,GI176           WILL ENTRY OVERFLOW TABLE.            01300000
         BH    GIOVFLW            YES. GO TO HANDLE OVERFLOW.           01320000
*                                                                       01340000
*   MOVE ENTRY TO DICTIONARY. COMPUTE RELATIVE ADDRESS.                 01360000
*                                                                       01380000
GI2      DS    0H                                                       01400000
         IC    R6,0(R1)           GET ENTRY LENGTH.                     01420000
         BCTR  R6,0               DECREMENT FOR EXECUTE INSTRUCTION.    01440000
         EX    R6,GIMVC1          MOVE NEW ENTRY TO TABLE.              01460000
         SR    R7,R9              COMPUTE RELATIVE ADDRESS OF ENTRY.    01480000
         SLL   R7,24              SHIFT REL ADDR TO 1ST BYTE.           01500000
*                                                                       01520000
*   CHECK FOR EXEC PROC STMT - STORE REL ADDRESS AND ZERO TTR.          01540000
*                                                                       01560000
         CLI   1(R1),GIPROC       IS IT AN EXEC PROC ENTRY.             01580000
         BNE   GI3                NO. GO TO CHECK FOR EXEC PGM.         01600000
         SPACE                                                          01620000
         ST    R7,RELPROC         SET PROC REL ADDR & ZERO TTR IN IWA.  01640000
         B     GIEXIT             GO TO EXIT.                           01660000
*                                                                       01680000
*   CHECK FOR EXEC PGM STMT - STORE REL ADDRESS AND ZERO TTR.           01700000
*                                                                       01720000
GI3      CLI   1(R1),GIPGM        IS IT AN EXEC PGM ENTRY.              01740000
         BNE   GI3A                    NO, GO TEST PGM/PROC             01760000
         XC    RELPROC(4),RELPROC       CLEAR LAST PROC POINTER         01780000
         B     GI4                     GO SET LAST PGM REL ADDR         01800000
        SPACE                                                           01820000
GI3A     DS    0H                                                       01840000
         CLI   1(R1),GIPPM        IS IT PGM WITHIN A PROCEDURE.         01860000
         BNE   GIEXIT             NO GO TO EXIT.                        01880000
GI4      DS    0H                                                       01900000
         ST    R7,RELPGM          SET PGM REL ADDR & ZERO TTR IN IWA.   01920000
*        FALL THRU TO EXIT                                              01940000
*                                                                       01960000
*   EXIT.                                                               01980000
*                                                                       02000000
GIEXIT   DS    0H                                                       02020000
         LM    14,12,12(13)       RESTORE REGS                          02040000
         BR    14                 EXIT                                  02060000
         SPACE 3                                                        02080000
*********************************************************************** 02100000
*                                                                     * 02120000
*        HANDLING OF DICTIONARY OVERFLOW.                             * 02140000
*                                                                     * 02160000
*********************************************************************** 02180000
GIOVFLW  DS    0H                                                       02200000
         LR    R5,R1              SAVE R1.                              02220000
*********************************************************************** 02240000
         IEFSAVER SAVEPTR         GET NEW REGIESTER SAVE AREA.          02260000
*********************************************************************** 02280000
         SPACE                                                          02300000
         LA    R4,QPARM           Q-MANAGER PARAMETER AREA              02320000
         USING IOPARAMS,R4             ADDRESSING.                      02340000
         L     R3,QMPCL           SET UP REGS FOR STORE DICT ADD & TTR. 02360000
         L     R2,GIVHQ           INTERFACE MODULE (IEFVHQ) ADDRESS.    02380000
         SPACE                                                          02400000
*                                                                       02420000
*   CHECK FOR FIRST DICT PUT OUT - ASSIGN FIRST TTR.                    02440000
*                                                                       02460000
         CL    R8,TSREFB          IS THIS THE FIRST DICTIONARY.         02480000
         BNE   GIOVFLW1           NO. GO TO CHAIN DICTS.                02500000
         MVI   QMPOP,QMASGN       INDICATE ASSIGN ONLY.                 02520000
         BALR  RE,R2              LINK TO Q-MNGR INTERFACE.             02540000
         SPACE                                                          02560000
         MVC   TSREFB(4),0(R3)    MOVE TTR TO IWA (FIRST DICT TTR SAVE) 02580000
         MVC   0(3,R9),0(R3)      ALSO MOVE TTR TO DICTIONARY.          02600000
*   FALL THRU TO CHAIN TABLES.                                          02620000
GIOVFLW1 DS    0H                                                       02640000
         MVC   4(3,R9),IWANXTSV    MOVE CHAIN SVA TO DICT BLOCK.YM02716 02660002
         SPACE                                                          02680000
*                                                                       02700000
*   CHECK VOR PROC TTR - IF NO TTR ASSIGN DICT TTR TO RELPROC.          02720000
*                                                                       02740000
         LA    RF,RELPROC         POINT TO PROC TTR IN IWA.             02760000
         BAL   RE,GITTRCK         LINK TO CHECK FOR TTR.                02780000
*                                                                       02800000
*   CHECK FOR PGM TTR - IF NO TTR ASSIGN DICT TTR TO RELPGM.            02820000
*                                                                       02840000
         LA    RF,RELPGM          POINT TO PGM TTR IN IWA.              02860000
         BAL   RE,GITTRCK         LINK TO CHECK FOR PGM TTR & MVC       02880000
*                                      DICT TTR TO RELPGM IN IWA.       02900000
*                                                                       02920000
*   WRITE AND ASSIGN DICTIONARY - MOVE CHAIN TTR TO DICT TTR - UPDATE   02940000
*        TNEXT AND CLEAR (TO ZEROES) REST OF DICT 1. POINT R7 AT        02960000
*        BEGINNING DICTIONARY ENTRY.                                    02980000
*                                                                       03000000
         ST    R9,0(R3)           MOVE DICT ADDR TO QMPCA.              03020000
         MVC   4(3,R3),0(R9)      MOVE TTR TO QMPEX.                    03040000
         MVI   DISPLID(R3),DICTRYID  BLOCK ID TO REMOTE LIST     Y02621 03050002
         SPACE                                                          03060000
         BALR  RE,R2              LINK TO QMNGR INTERFACE (IEFVHQ).     03080000
* AFTER BLOCK IS WRITTEN, CLEAR BLOCK ID IN REMOTE LIST TO       Y02621 03084002
* PREVENT INCORRECT IDENTIFICATION OF NEXT BLOCK WRITTEN. SWA    Y02621 03088002
* MGR USES  THIS ID IF NONZERO. IF ZERO, IT WILL USE ID IN BLOCK.Y02621 03092002
         MVI   DISPLID(R3),0       ZERO ID IN REMOTE LIST.       Y02621 03096002
         SPACE                                                          03100000
         MVC   0(3,R9),IWANXTSV    MOVE CHAIN SVA TO NEW BLOCK. YM02716 03120002
         SPACE                                                          03140000
         MVC   IWANXTSV(4),0(R3)   SAVE SVA FOR NEXT DICT BLOCK YM02716 03160002
         SPACE                                                          03180000
         XC    4(172,R9),4(R9)    ZERO REST OF DICT (ID STAYS).         03200000
         SPACE                                                          03220000
         LA    R7,7(R9)           POINT R7 AT 1ST ENTRY SLOT OF THIS    03240000
*                                      DICTIONARY.                      03260000
*********************************************************************** 03280000
         IEFRELSE  SAVEPTR        RELEASE NEW REGISTER SAVE AREA.       03300000
*********************************************************************** 03320000
         LR    R1,R5              RESTORE R1.                           03340000
         B     GI2                TO TO MOVE NEW ENTRY TO DICTIONARY.   03360000
         SPACE                                                          03380000
*                                                                       03400000
*   CHECK AND UPDATE RELPROC AND RELPGM.                                03420000
*                                                                       03440000
GITTRCK  DS    0H                                                       03460000
         CL    R8,0(RF)           IS THERE AN ENTRY FOR RELPROC/RELPGM  03480000
         BC    8,0(RE)            NO. - RETURN -                        03500000
         CLC   1(3,RF),GIZERO3    YES. IS THERE ALREADY A TTR ASSIGNED. 03520000
         BNE   0(RE)              YES. - RETURN -                       03540000
         MVC   1(3,RF),0(R9)      NO MOVE DICT TTR TO RELPROC/RELPGM.   03560000
         BR    RE                 RETURN.                               03580000
         SPACE 2                                                        03600000
GIMVC1   MVC   0(1,R7),0(R1)      MOVE NEW ENTRY TO DICTIONARY.         03620000
         SPACE 2                                                        03640000
*********************************************************************** 03660000
*                                                                     * 03680000
*        CONSTANTS                                                    * 03700000
*                                                                     * 03720000
*********************************************************************** 03740000
GIVHQ    DC    V(IEFVHQ)          Q-MANAGER INTERFACE.                  03760000
GI176    DC    H'176'             LENGTH OF DICTIONARY.                 03780000
GIZERO3  DC    X'000000'          RELPROC/RELPGM TTR CHECK.             03800000
GIPROC   EQU   X'80'              EXEC PROC DICT INDICATOR.             03820000
GIPGM    EQU   X'40'              EXEC PGM DICT INDICATOR.              03840000
GIPPM    EQU   X'20'               PGM WITHIN PROC INDICATOR.           03860000
GIDCTID  EQU   X'08'              DICTIONARY 1 ID.                      03880000
DISPLID  EQU   7                  DISPLACEMENT OF BLOCK ID       Y02621 03884002
DICTRYID EQU   X'25'              BLOCK ID FOR DICTIONARY RECORD Y02621 03888002
*                                                                Y02668 03892002
PATCH    DC    25F'0'              **********PATCH SPACE*********Y02028 03896002
         END                                                            03900000
