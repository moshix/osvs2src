*                                                                     * 00600019
*  TITLE: IEFVIND-EXPAND INTERFACE                                    * 00900019
*                                                                     * 01200019
*  FUNCTION: THE MODULE IS USED AS A PSEUDO ACCESS METHOD. THE ADDRESS* 01500019
*            OF THE ENTRY POINT HAS BEEN PUT INTO THE DCBREAD FIELD OF* 01800019
*            THE BPAM DCB. WHENEVER A READ IS ISSUED IN IEFVHA AND THE* 02100019
*            ADDRESS IS IN THE DCB THIS MODULE IS ENTERED. IT SETS UP * 02400019
*            PARAMETER LISTS AND BRANCHES TO IEZDCODE TO EXPAND THE   * 02700019
*            RECORD. IF ANOTHER QUEUE RECORD IS NEEDED, IT BRANCHES TO* 03000019
*            IEFVHQ TO READ THE NEXT RECORD.                          * 03300019
*                                                                     * 03600019
* ENTRY POINT: IEFVIND                                                * 03900019
*                                                                     * 04200019
* INPUT: RD POINTS TO THE SAVE AREA                                   * 04500019
*        RE RETURN ADDRESS                                            * 04800019
*        RC POINTS TO THE IWA                                         * 05100019
*                                                                     * 05400019
* EXITS: RETURNS ONLY TO THE CALLER.                                  * 05700019
*                                                                     * 06000019
* OUTPUT: ALL REGISTERS ARE RESTORED                                  * 06300019
*                                                                     * 06600019
* INTERNAL EXITS: IEFVHQ-READ ANOTHER RECORD FROM THE QUEUE           * 06900019
*                 IEZDCODE-EXPAND A RECORD                            * 07200019
*                                                                     * 07500019
* WORKAREAS AND TABLES-                                               * 07800019
*     .IEFVMIWA-IWA                                                   * 08100019
*     .IEFQMNGR-QMPA                                                  * 08400019
*     .WORKAREA-WORK AREA AND DIRECTORY                               * 08700019
*     .IEZPARM-PARAMETER LIST                                         * 09000019
*     .DCBD - BPAM DCB                                       M3739O106* 09300019
*                                                                     * 09600019
* REGISTER USEAGE: R4-PARAMETER LIST TO IEZDCODE                      * 09900019
*                  R6-QMPA                                            * 10200019
*                  R7-WORK AREA                                       * 10500019
*                  RB-ADDRESSIBILITY OF MODULE                        * 10800019
*                  RC-IWA                                             * 11100019
*                                                                     * 11400019
* MACROES USED: IEFSAVER                                              * 11700019
*               IEFRELSE                                              * 12000019
*               SAVE                                                  * 12300019
*               RETURN                                                * 12600019
*                                                                     * 12900019
* OPERATION:.REGISTERS ARE SAVED                                      * 13200019
*           .A SAVE AREA IS GOTTEN                                    * 13500019
*           .ADDRESSIBILITY IS ESTABLISHED FOR THE WORK AREA AND THE  * 13800019
*           .QMPA                                                     * 14100019
*           .THE ADDRESS OF THE PARAMETER LIST IS GOTTEN FROM IWAPARM,* 14400019
*            OFFSET X'100' IN THE IWA                                 * 14700019
*           .THE PARAMETER LIST IS COMPLETED IF NECESSARY             * 15000019
*           .IEZDCODE EXPANDS A RECORD                                * 15300019
*           .A RETURN CODE IS TESTED                                  * 15600019
*            A ZERO INDICATES THAT THE RECORD WAS EXPANDED.           * 15900019
*            THE SAVE AREA RELEASED AND CONTROL RETURNED TO IEFVHA    * 16200019
*           IF THE RETURN CODE IS FOUR, THE LAST COMPRESSED           * 16500019
*           RECORD HAS ALREADY BEEN EXPANDED. IEFVHQ READS A NEW RECORD 16800019
*           FROM THE QUEUE AND THE RECORD IS EXPANDED.                * 17100019
*                                                                     * 17400019
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * 17700019
         EJECT                                                          18000019
WORKAREA DSECT                                                          18300019
         USING WORKAREA,R7                                              18600019
WKTTR    DS    CL4                     POINTER TO NEXT RECORD           18900019
WKQMPAPT DS    CL4                     ADDRESS OF QMPA                  19200019
WKRECORD DS    CL168                   COMPRESSED RECORD                19500019
WKPTR1   DS    CL4                     POINTER TO ITSELF                19800019
WKCT     DS    CL1                     NUMBER OF ENTRIES MADE IN        20100019
*                                      THIS SEGMENT OF THE DIRECTORY    20400019
WKPTR2   DS    CL3                     HOLDER OF TTR FROM QUEUE   O106  20700019
*                                      MANAGER                    O106  21000019
WKPROCN1 DS    CL8                     PROCEDURE NAME                   21300019
WKTTR1   DS    CL3                     TTR OF FIRST RECORD              21600019
WKPROCN2 DS    CL8                     PROCEDURE NAME                   21900019
WKTTR2   DS    CL3                     TTR OF FIRST RECORD              22200019
WKPROCN3 DS    CL8                     PROCEDURE NAME                   22500019
WKTTR3   DS    CL3                     TTR OF FIRST RECORD              22800019
WKPROCN4 DS    CL8                     PROCEDURE NAME                   23100019
WKTTR4   DS    CL3                     TTR OF FIRST RECORD              23400019
WKPROCN5 DS    CL8                     PROCEDURE NAME                   23700019
WKTTR5   DS    CL3                     TTR OF FIRST RECORD              24000019
WKPROCN6 DS    CL8                     PROCEDURE NAME                   24300019
WKTTR6   DS    CL3                     TTR OF FIRST RECORD              24600019
WKPROCN7 DS    CL8                     PROCEDURE NAME                   24900019
WKTTR7   DS    CL3                     TTR OF FIRST RECORD              25200019
WKPROCN8 DS    CL8                     PROCEDURE NAME                   25500019
WKTTR8   DS    CL3                     TTR OF FIRST RECORD              25800019
WKPROCN9 DS    CL8                     PROCEDURE NAME                   26100019
WKTTR9   DS    CL3                     TTR OF FIRST RECORD              26400019
WKPROCNA DS    CL8                     PROCEDURE NAME                   26700019
WKTTRA   DS    CL3                     TTR OF FIRST RECORD              27000019
WKPROCNB DS    CL8                     PROCEDURE NAME                   27300019
WKTTRB   DS    CL3                     TTR OF FIRST RECORD              27600019
WKPROCNC DS    CL8                     PROCEDURE NAME                   27900019
WKTTRC   DS    CL3                     TTR OF FIRST RECORD              28200019
WKPROCND DS    CL8                     PROCEDURE NAME                   28500019
WKTTRD   DS    CL3                     TTR OF FIRST RECORD              28800019
WKPROCNE DS    CL8                     PROCEDURE NAME                   29100019
WKTTRE   DS    CL3                     TTR OF FIRST RECORD              29400019
WKPROCNF DS    CL8                     PROCEDURE NAME                   29700019
WKTTRF   DS    CL3                     TTR OF FIRST RECORD              30000019
WKSAVE1  DS    CL1                                               A33705 30100020
         EJECT                                                          30300019
         IEFCOMWA                                                       30600002
         IEFCVRWA                                                       30700002
         EJECT                                                          30900019
         IEFQMNGR                                                       31200019
         EJECT                                                          31400019
QMPADDR  DS    CL4                     ADDRESS OF WORK BUFFER           31500019
QMPATTR  DS    CL3                     TTR OF JOB QUEUE RECORD          31800019
QMPAZER  DS    CL1                     RESERVED FIELD                   32100019
         EJECT                                                          32400019
IEZPARM  DSECT                         PARAMETER AREA                   32700019
PARMADDR DS    CL4                     ADDRESS OF PARAMETER LIST        33000019
PROCNAME DS    CL8                     PROCEDURE NAME                   33300019
DIRECT   DS    CL4                     ADDRESS OF DIRECTORY             33600019
IWAPT    DS    CL4                     ADDRESS OF IWA                   33900019
RTCD     DS    CL4                     RETURN CODE FOR IEFVINB          34200019
         ORG   PARMADDR                                                 34500019
IEZADDR  DS    CL4                     ADDRESS OF WORK AREA        O106 34800019
IEZUNCOM DS    A                       PTR TO UNCOMPRESSED RECORD       35100019
IEZCOMPR DS    A                       PTR TO NEXT COMPRESSED RECORD    35400019
IEZLEN   DS    H                       LENGTH OF UNCOMPRESSED RECORD    35700019
IEZOUT   DS    H                       LENGTH OF AREA FOR OUTPUT RECORD 36000019
IEZCHAR  DS    C                       CHARACTER CODE OF BLANK          36300019
IEZTYPE  DS    B                       FLAG ASSOCIATED WITH RECORD      36600019
IEZRES   DS    H                       WORK AREA                        36900019
*                                      THE IWA                          37200019
         EJECT                                                          37500019
         DCBD  DSORG=(PS)                                               37800019
         EJECT                                                          38100019
IEFVIND  CSECT                                                          38400019
         SAVE (14,12)                                                   38700019
         BALR  RB,0                                                     39000019
         USING *,RB                                                     39300019
**************************************************************** Y02668 39330002
* MODULE TRACE CODE - FOR TESTING                                Y02668 39360002
         L     RF,TRACEV           LOAD TRACE RTNE ADDR.         Y02668 39390002
         BALR  RE,RF               ENTER MOD ID IN TRACE RECORD. Y02668 39420002
TRACEV   DC    V(TRACE)            TRACE RTNE ADDR.              Y02668 39450002
         DC    C'VIND'             MOD ID USED BY TRACE          Y02668 39480002
* TRACE RETURNS HERE.                                            Y02668 39510002
**************************************************************** Y02668 39540002
         B     VIND000                                                  39600019
         SPACE                                                          39900019
         MODID BR=NO                                            Y01886  39950001
         DC    C'SPLT'             FUNCTION ID AND               Y02668 40250002
         DC    C'02'               LEVEL # (NEL DELETION FIX).   Y02668 40550002
         DC    C'MVM'              FUNCTION ID AND               Y02668 40650002
         DC    C'04'         LEVEL # (WA MACRO UPDATES).         Y02668 40750002
         SPACE                                                          41100019
VIND000  DS    0H                                                       41400019
         IEFSAVER                                                       41700019
         L     R7,IWAWKBF              ADDRESS OF WORK AREA        3391 41730019
         L     R6,WKQMPAPT             ADDRESS OF IN-STREAM QMPA   3391 42240019
         SPACE                                                          42600019
         USING IOPARAMS,R6                                              42660019
         L     R2,QMPCL                ADDR OF EXTERNAL PARM LIST  3391 42720019
         SPACE                                                          42900019
*********************************************************************** 43200019
*                                                                     * 43500019
*  IF THIS IS THE FIRST STATEMENT OF THE PROCEDURE, CORE IS GOTTEN FOR* 43800019
*  THE PARAMETER LIST, THE ADDRESS IS SAVED IN THE IEA, AND THE   4399* 44100019
*  PARAMETER LIST IS INITIALIZED.  THE EXTERNAL PARAMETER LIST    4399* 44400019
*  FOR IEFVHQ AND IEFQMRAW ARE INITIALIZED TO POINT TO THE FIRST  4399* 44700019
*  RECORD OF THE PROCEDURE AND THAT 953694 Z2-92 4                4399* 45000019
*                                                                 4399* 45300019
*********************************************************************** 45600019
*                                                                     * 45900019
*********************************************************************** 46200019
         SPACE                                                          46500019
         L     R4,IWAPARM              ADDRESS OF PARAMETER LIST        46800019
         USING IEZPARM,R4                                               47100019
         LA    R4,DISPLZER(R4)         CLEAR HIGH ORDER BYTE       4399 47200019
         NI    SWI,UNBLKPC             TURN OFF SWITCHES FRO BLOCKED    47400019
*                                      PROCLIB                          47700019
         SPACE                                                          48000019
         LTR   R4,R4                   IS THIS THE FIRST STMT OF THE    48070019
*                                      PROCEDURE                   4399 48140019
         BNZ   VIND025                 NO-EXPAND THE STATEMENT     4399 48210001
         LA    R0,VINDPARM             LENGTH OF PARM LIST         4399 48280019
         GETMAIN R,LV=(0)                                               48350019
         O     R1,IWAPARM              RETAIN HIGH ORDER BYTE INFOM4268 48420019
         ST    R1,IWAPARM              ADDR OF PARMLIST           M4268 48490019
         LR    R4,R1                   ADDRESS OF PARM LIST        4399 48560019
         USING IEZPARM,R4                                          4399 48630019
         LA    R3,DISPLFOR(R1)         ADDRESS OF PARM INFORMATION 4399 48700019
         ST    R3,IEZADDR              ADDRESS OF PARM INFORMATION 4399 48770019
         MVC   IEZUNCOM(MVFOUR),PSTMT     ADDRESS OF OUTPUT BUFFER 4399 48840019
         MVI   IEZCHAR,CHAR            CHARACTER TO BE ADDED       4399 48910019
         MVI    IEZTYPE,TYPE           NOT A SPECIAL RECORD        4399 48980019
         LA  R3,INLN                   LENGTH OF OUTPUT BUFFER     4399 49050019
         STH   R3,IEZLEN               LENGTH OF OUTPUT BUFFER     4399 49120019
         MVC   MVFOUR(MVTHREE,R2),WKPTR2 TTR OF FIRST RECORD OF PROC    49190019
         LA    R5,VIND025              ADDR ROUTINE TO EXPAND RECORD    49260001
         SPACE                                                          49500019
VIND010  DS    0H                                                       49800019
*********************************************************************** 50100019
*                                                                     * 50400019
*  READ THE RECORD FROM THE JOB QUEUE AND THEN REINITIALIZE THE       * 50700019
* PARAMETER LIST FOR IEZDCODE                                         * 51000019
*                                                                     * 51300019
*********************************************************************** 51600019
         ST    R6,QPARMP               REPLACE R/I QMPA PTR             51900019
         MVC   DISPLZER(MVFOUR,R2),IWAWKBF ADDRESS OF WORK BUFFER  3391 52200019
        MVI   QMPOP,QMREAD            READ                              52500019
         L     RF,VIEFVHQ              ADDRESS OF Q MANAGER INTERFACE   52800019
         BALR  RE,RF                   Q MANAGER INTERFACE              53100019
         LA    R1,QPARM                ADDRESS OF QMPA (R/I)            53400019
         ST    R1,QPARMP               REPLACE IN-LINE PROC QMPA PTR    53700019
         LA    R3,OUTLEN               LENGTH OF INPUT BUFFER      4361 54000019
         STH   R3,IEZOUT           LENGTH OF INPUT BUFFER         M3667 54300019
         LA    R3,WKRECORD             ADDRESS OF WORK BUFFER           54600019
         ST    R3,IEZCOMPR             ADDRESS OF WORK BUFFER           54900019
         BR    R5                      RETURN TO CALLER            4399 55000019
         SPACE                                                          55200019
*********************************************************************** 55800019
*                                                                     * 56100019
* EXPAND A RECORD                                                     * 56400019
*                                                                     * 56700019
*********************************************************************** 57000019
VIND025  DS    0H                                                  4399 57400019
         L     R1,IEZCOMPR             LOAD THE ABSOLUTE ADDR OF A33705 57410020
*                                      NEXT STMT TO BE EXPANDED  A33705 57420020
         SR    R1,R7                   CALC RELATIVE DISPLMNT    A33705 57430020
*                                      INTO THE WORK BUFFER OF   A33705 57440020
*                                      NEXT STMT TO BE EXPANDED  A33705 57450020
         STC   R1,WKSAVE1              STORE THIS RELATIVE       A33705 57460020
*                                      DISPLACEMENT INTO THE     A33705 57470020
*                                      DIRECTORY. DURING RDR     A33705 57480020
*                                      SUSPENSION IN PCP THIS    A33705 57490020
*                                      DISPLACEMENT IS USED TO   A33705 57500020
*                                      RESTORE THE RDR TO THE POIA33705 57510020
*                                      AT WHICH IT WAS,WHEN IT   A33705 57520020
*                                      WAS SUSPENDED.            A33705 57530020
         SPACE                                                          57600019
         L     R1,IWAPARM              ADDRESS OF PARAMETER LIST        57900019
         L     R8,VIEDCODE             IEZDCODE VCON             Y01029 57950001
         BALR  RE,R8                   BRANCH TO IEZDCODE        Y01029 58500001
         LTR   RF,RF                   WAS RECORD THERE                 58800019
         BNZ   VIND040             A NON-ZERO RETURN CODE         M3738 58840019
*                                  INDICATES THERE ARE NO MORE    M3738 58880019
*                                  RECORDS (CODE 4) OR A PARAM    M3738 58920019
*                                  LIST ERROR (CODE 8). NON-ZERO  M3738 58960019
*                                  IS ASSUMED TO BE CODE 4, SINCE M3738 59000019
*                                  THE ONLY CONDITION UNDER WHICH M3738 59040019
*                                  CODE 8 WILL BE RETURNED IS IN  M3738 59080019
*                                  THE EVENT OF SOME WILD STORE   M3738 59120019
*                                  INTO THE PARAMETER LIST. A     M3738 59160019
*                                  RETURN CODE OF 8 WILL RESULT   M3738 59200019
*                                  IN TRUNCATION OR NON-EXPANSION M3738 59240019
*                                  OF THE INSTREAM PROCEDURE.     M3738 59280019
         SPACE                                                          59400019
VIND030  DS    0H                                                       59700019
*********************************************************************** 60000019
*                                                                     * 60300019
*  THE RECORD WAS EXPANDED.                                           * 60600001
*   RETURN TO PROCESS THE EXPANDED RECORD AS IF IT HAD COME FROM      * 60900019
*   PROCLIB                                                           * 61200019
*                                                                     * 61500019
*********************************************************************** 61800019
         SPACE                                                          62700019
         IEFRELSE                                                       63000019
         SPACE                                                          63300019
         RETURN (14,12)                                                 63600019
         SPACE                                                          63900019
VIND040  DS    0H                                                       64200019
*********************************************************************** 64500019
*                                                                     * 64800019
* CHECK TO SEE IF THIS IS THE END OF THE RECORD.  THE FIRST WORD      * 65100019
* WILL CONTAIN A TTR OF THE NEXT RECORD IF ONE EXISTS                 * 65400019
*                                                                     * 65700019
*********************************************************************** 66000019
         L     R3,WKTTR                TTR OF NEXT RECORD          3391 66300019
         LTR   R3,R3                   END OF PROCEDURE            3391 66600019
         BE    VIND050                 END OF PROCEDURE                 66900019
         SPACE                                                          67200019
*********************************************************************** 67500019
*                                                                     * 67800019
* BUILD THE QMPA AND READ A RECORD                                    * 68100019
*                                                                     * 68400019
*********************************************************************** 68700019
         MVC   WKPTR2(MVTHREE),WKTTR   UPDATE PTR TO REFLECT TTR A33705 68750020
*                                      OF PORTION OF PROCEDURE   A33705 68800020
*                                      BEING EXPANDED            A33705 68850020
         MVC   MVFOUR(MVTHREE,R2),WKTTR  READ A RECORD             3391 68900019
         LA    R5,VIND025              COMPRESS THE RECORD         4399 69100019
         B     VIND010                                                  69300019
         SPACE                                                          69600019
VIND050  DS    0H                                                       69900019
*********************************************************************** 69930019
*                                                                 4399* 69960019
*  END OF FILE HAS BEEN REACHED.  THE ADDRESS OF BPAM IS PUT BACK INTO  69990019
*  THE BPAM DCB, THE ADDRESS OF THE EODAD PLACED IN REGISTER 14, THE  * 70020019
*  PARM LIST FOR IEZDCODE IS FREED AND THE POINTER TO IT IS CLEARED   * 70050019
*  CONTROL IS RETURNED TO IEFVHAS END OF PROCEDURE ROUTINE VIA REG 14   70080019
*                                                                 4399* 70110019
*********************************************************************** 70140019
         L     RA,PDCBP                ADDRESS OF PROC DDB              70200019
         MVC   DCBREAD+DISPLONE-IHADCB(MVTHREE,RA),IWABPAM    M3739O106 70500019
         L     R4,MVFOUR(RD)           ADDR OF SAVE AREA SET       O106 70800019
*                                      REGISTER 14 TO THE EODAD   O106  71100019
*                                      ROUTINE BECAUSE THIS IS THEO106  71400019
*                                      END OF THE IN-LINE PROCEDURE106  71700019
         MVC   DISPL13(MVTHREE,R4),DCBEODAD+DISPLONE-IHADCB(RA)         72000019
         L     R1,IWAPARM              ADDRESS OF PARM LIST        4399 72050019
         LA    R0,VINDPARM             LENGTH OF PARM LIST         4399 72100019
         FREEMAIN R,A=(1),LV=(0)                                        72150019
         XC    IWAPARM+DISPLONE(MVTHREE),IWAPARM+DISPLONE  CLEAR   4399 72200019
*                                      ADDRESS OF PARM LIST        4399 72250019
         B     VIND030                 END OF IN-LINE PROCEDURE         72300019
         SPACE                                                          72600019
*********************************************************************** 72900019
*                                                                     * 73200019
*   CONSTANTS AND EQUATES                                             * 73500019
*                                                                     * 73800019
*********************************************************************** 74100019
VIEFVHQ  DC    V(IEFVHQ)               ADDRESS OF Q MANAGER INTERFACE   74400019
VIEDCODE DC    V(IEZDCODE)             DECOMPRESS RECORD ROUTINE Y01029 74450001
DISPLZER EQU   0                       CLEAR THE HIGH ORDER BIT    4399 74500019
VINDPARM EQU   24                      LENGTH OF PARM LIST         4399 74600019
INSTPROC EQU   X'80'                   INSTREAM PROC BEING PROCESSE4399 74700019
DISPLFOR EQU   4                       DISPLACEMENT                4399 74800019
CHAR     EQU   X'40'                   CHARACTER BEING ADDED       4399 74900019
TYPE     EQU   X'00'                   NOT A SPECIAL RECORD        4399 75000019
INLN     EQU   X'50'                   LENGTH OF OUTPUT BUFFER     4399 75100019
DISPLONE EQU   1                        DISPLACEMENT               4399 75200019
OUTLEN   EQU   168                     LENGTH OF OUTPUT BUFFER     4399 75300019
MVTHREE  EQU   3                       LENGTH OF MOVE                   75900019
UNBLKPC  EQU   X'77'                   TURN OFF THE BLO KED PROCLIB     76500019
*                                       SWITCHS IN SWI IN THE IWA       76800019
MVFOUR   EQU   4                       LENGTH OF MOVE                   77400019
DISPL12  EQU   12                      DISPLACEMENT               O106  77700019
DISPL13  EQU   13                      DISPLACEMENT                     78000019
PATCH    DC    25F'0'              PATCH SPACE                   Y02668 78100002
   END                                                                  78300019
