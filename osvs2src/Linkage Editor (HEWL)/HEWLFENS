         TITLE    'HEWLFENS  ENTER SEGMENT NUMBERS'                     00050021
*********************************************************************** 00100021
*                                                                     * 00150021
*STATUS: CHANGE LEVEL                                                 * 00200021
*                                                                     * 00250021
*FUNCTION/OPERATION:                                                  * 00300021
*    1) ENTER SEGMENT NUMBERS FOR LABEL REFERENCES IN CESD            * 00350021
*    2) DETERMINE NUMBER OF ENTAB LINES REQUIRED PER SEGMENT (OVERLAY)* 00400021
*                                                                     * 00450021
*ENTRY POINTS:                                                        * 00500021
*    HEWLFENS- CONTROL SECTION NAME                                     00550021
*                                                                     * 00600021
*INPUT:                                                               * 00650021
*    1) REG 2 - ADDRESS OF ALL PURPOSE TABLE                          * 00700021
*    2) REG 13- ADDRESS OF CALLER SUPPLIED REGISTER SAVE AREA         * 00750021
*    3) TABLES LISTED BELOW                                           * 00800021
*                                                                     * 00850021
*OUTPUT:                                                              * 00900021
*    1) UPDATED COMPOSITE EXTERNAL SYMBOL DICTIONARY-LABEL REFERENCES * 00950021
*    2) COUNT OF ENTAB LINES PER SEGMENT                              * 01000021
*                                                                     * 01050021
*EXTERNAL ROUTINES:                                                   * 01100021
*    1) HEWLELOG - ERROR DIAGNOSTIC ROUTINE                           * 01150021
*    2) HEWLCAD1 - AN ENTRY POINT IN EITHER HEWLKADA OR HEWLFADA      * 01200021
*        A) HEWLCAD1 IS ENTERED FROM HEWLKENS TO RECORD THE NUMBER OF * 01250021
*           LINES OF ENTAB -   REQUIRED PER SEGMENT-                  * 01300021
*          1)IN THE 'F' LINKAGE EDITOR HEWLCAD1 RECORDS THE ENTAB LINE* 01550021
*            COUNT IN THE SEGMENT LENGTH TABLE AND SETS THE 'E' BIT TO* 01600021
*            1-INDICATING THERE IS AN ENTAB FOR THIS SEGMENT          * 01650021
*        B) HEWLCAD1 IS ENTERED FOR EACH SEGMENT WHICH CONTAINS AN    * 01700021
*           ENTAB                                                     * 01750021
*                                                                     * 01800021
*EXITS:                                                               * 01850021
*   RETURNS TO ADDRESS PASSED IN REGISTER 14                          * 01900021
*                                                                     * 01950021
*TABLES:                                                              * 02000021
*   1) CALLS LIST                                                     * 02050021
*   2) COMPOSITE EXTERNAL SYMBOL DICTIONARY                           * 02100021
*   3) DOWNWARDS CALLS LIST (INTERNAL)                                * 02150021
*   4) SEGMENT TABLE 1                                                * 02200021
*   5) ALL PURPOSE TABLE                                              * 02250021
*                                                                     * 02300021
*ATTRIBUTES:                                                          * 02350021
*                                                                     * 02400021
*********************************************************************** 02450021
HEWLFENS CSECT                                                          02500021
*                                                                     * 02550021
*                                                                     * 02600021
*                                                                       02650021
*                                                                       02700021
*                                                                       02750021
*********************************************************************** 02800021
*                                                                       02850021
*             REGISTERS USED IN HEWLKENS                              * 02900021
*********************************************************************** 02950021
*                                                                       03000021
ERRCODRG EQU   0                        ERROR CODE REGISTER             03050021
WORK4    EQU   0                        WORK                            03100021
WORK1    EQU   1                        WORK                            03150021
ERRSYMRG EQU   1                        ADDRESS OF SYMBOL               03200021
APT      EQU   2                        ALL PURPOSE TABLE ADDRESS       03250021
CALLADD  EQU   3                        ADDRESS OF CALLS LIST           03300021
TEMP2    EQU   3                       TEMPORARY WORK REGISTER          03350021
CURCHNAD EQU   3                        CURRENT CHAIN ADDRESS           03400021
CURINDEX EQU   4                        CURRENT INDEX                   03450021
CURPPTR  EQU   5                        CURRENT P POINTER               03500021
CURRPTR  EQU   6                        CURRENT R POINTER               03550021
CURPSEGN EQU   7                        CURRENT P SEGMENT NUMBER        03600021
CURRSEGN EQU   8                        CURRENT R SEGMENT NUMBER        03650021
CURSEGNO EQU   9                        CURRENT SEGMENT NUMBER          03700021
TEMP1    EQU   9                       TEMPORARY WORK REGISTER          03750021
BASE     EQU   12                       BASE REGISTER                   03800021
ACUMZERO EQU   13                       ACCUMULATO                      03850021
CULTOTCT EQU   14                       CULMULATIVE TOTAL NO. BYTES -2  03900021
RETURN   EQU   14                       RETURN (RETURN FROM CPTH)       03950021
WORK2    EQU   10                       WORK                            04000021
BRANCH   EQU   15                                                       04050021
CULRELCT EQU   15                       CULMULATIVE REL. NO. BYTES      04100021
ERRADDRG EQU   15                       ADDRESS OF ERROR ROUTINE        04150021
WORK3    EQU   15                       WORK                            04200021
WORK5    EQU   15                       WORK   (NOT USED PRESENTLY)     04250021
*********************************************************************   04300021
*              DISPLACEMENTS                                            04350021
*********************************************************************   04400021
DISP     EQU   0                       DISPLACEMENT FOR SAVE AREA       04450021
*********************************************************************** 04500021
*              MASKS                                                    04550021
OVLYBIT  EQU   X'20'                    MASK FOR TESTING FOR OVERLAY    04600021
*                                                                       04650021
XCAL     EQU   X'04'                    XCAL TEST MASK                  04700021
*********************************************************************** 04750021
*********************************************************************** 04800021
*              SAVE CALLERS REGISTERS AND INITIALIZE BASE  REGISTER     04850021
*********************************************************************** 04900021
         STM   14,12,DISP(13)           SAVE CALLERS REGS               04950021
         USING ENS00500+2,BASE                                          05000021
         USING APTDSECT,APT   ESTABLISH ADDRESSABILITY FOR  APT         05050021
ENS00500 BALR  BASE,0                   INITIALIZE BASE                 05100021
         BC    15,MODID+6              BRANCH AROUND MODID              05150021
MODID    DC    C'01MENS'                                                05200021
         ST    13,SAVERG13              SAVE 13 -ADD OF CALLERS  REGS   05250021
*********************************************************************** 05300021
*                                                                       05350021
*********************************************************************** 05400021
*              SCAN CESD  - PUT PROPER  SG NO OF LR IN CESD LR ENTRY    05450021
*********************************************************************** 05500021
ENS0070  L     WORK1,CHESD        CESD ADD                              05550021
         LH    WORK2,ENCDC         CESD CURR CT                         05600021
ENS00750 LA    WORK1,16(WORK1)         INC CESD ADD                     05650021
         MVN   TESTTYPE(1),8(WORK1)     MOVE TYPE TO TEST FOR LR        05700021
         CLI   TESTTYPE,X'03'           IS TYPE = LR                    05750021
         BC    7,ENS00770               NO / GO TEST END OF LOOP        05800021
*********************************************************************** 05850021
*              TYPE = LR                                                05900021
*********************************************************************** 05950021
         LH    WORK3,14(WORK1)         ID                               06000021
         SLL   WORK3,4                 REL ADD                          06050021
         A     WORK3,CHESD        ADD OF CS                             06100021
         MVC   12(1,WORK1),12(WORK3)   PUT CS.SG.NO. IN SG.FD. OF LR    06150021
ENS00770 BCT   WORK2,ENS00750          BRANCH IF LOOP NOT DONE          06200021
*********************************************************************** 06250021
*              TEST FOR OVELAY / NO RETURN TO CALLER /YES CONTINUE PROC 06300021
*********************************************************************** 06350021
         TM    PDSE7,OVLYBIT       IS PROGRAM IN OVERLAY                06400021
         BC    8,ENS115                 NO/ GO TO RETURN TO CALLER      06450021
*********************************************************************** 06500021
*              TEST FOR NO ENTRYS IN CALLS LIST - NO V CONSTANTS-ERR  * 06550021
*********************************************************************** 06600021
         NI    PSEGONE,X'00'           INIT-TO TEST FOR VALID CALL      06650021
*                                      FROM SEGMENT 1                   06700021
         SR    ACUMZERO,ACUMZERO                                        06750021
         CH    ACUMZERO,ENCLC      IS CALL LIST EMPTY                   06800021
         BC    8,ENS110                YES / GO TO PRINT ERROR MESS.    06850021
*********************************************************************** 06900021
*              SCAN CALLS LIST AND INSERT RELAYIVE ADDRESS (CA)       * 06950021
*********************************************************************** 07000021
         L     CALLADD,CLLT        ADDRESS OF CALLS LIST                07050021
         SR    CULTOTCT,CULTOTCT                                        07100021
ENS00800 SR    CULRELCT,CULRELCT                                        07150021
ENS00900 LA    CULRELCT,2(CULRELCT)     INCREMENT CUL RELATIVE CT BY 2  07200021
         CH    ACUMZERO,0(CALLADD,CULRELCT)  IS CALLS LIST ENTRY = 0    07250021
         BC    7,ENS00900                    NO                         07300021
         STH   CULRELCT,0(CALLADD)       T)  INSERT CURRENT CHAIN ADD.  07350021
         LA    CALLADD,0(CALLADD,CULRELCT)                              07400021
         LA    CULTOTCT,0(CULTOTCT,CULRELCT) IN CHAIN / FIND TOTAL CT   07450021
         CH    CULTOTCT,ENCLC      IS SCAN DONE                         07500021
         BC    7,ENS00800              NO                               07550021
*                                                                       07600021
*                                                                       07650021
         NI    PSEGONE,X'00'            INIT. TO TEST VALID CALL FR SG1 07700021
*********************************************************************** 07750021
*              SET DOWNWARD CALLS LIST (ENTRY LIST) TO ZERO + INITALIZE 07800021
*********************************************************************** 07850021
         L     WORK1,RECNT         ADD OF ENTRY LIST                    07900021
         SR    TEMP1,TEMP1                                              07950021
         LH    TEMP2,ENCDC                                              08000021
         SRL   TEMP2,2                 NO. OF FULL WORDS IN LIST        08050021
         LA    TEMP2,1(TEMP2)          IF CESD CT LESS 4-SRL = 0 - T2 0 08100021
ENS011   LA    WORK1,4(WORK1)          INC ADDRESS OF NEXT ENTRY        08150021
         ST    TEMP1,4(WORK1)           SET THIS ENTRY TO 0             08200021
         BCT   TEMP2,ENS011            BRANCH IF LOOP NOT DONE          08250021
         SR    CURSEGNO,CURSEGNO                                        08300021
*********************************************************************** 08350021
*              INCREMENT CURRENT SEGMENT NUMBER/TEST IF ALL SEGMENTS    08400021
*              PROCESSED/YES RETURN TO CALLER / NO CONTINUE             08450021
*********************************************************************** 08500021
ENS015   LA    CURSEGNO,1(CURSEGNO)                                     08550021
         L     CURCHNAD,CLLT       ADD OF CALLS LIST                    08600021
         SR    ACUMZERO,ACUMZERO                                        08650021
         CH    CURSEGNO,CSNO       HAVE ALL SEGMENTS BEEN PROCESSED     08700021
         BC    2,ENS110                YES                              08750021
*********************************************************************** 08800021
*              INITIALIZE TO FIND NEXT P IN CALLS LIST                  08850021
*********************************************************************** 08900021
ENS017   SR    CURINDEX,CURINDEX                                        08950021
         SR    CURPSEGN,CURPSEGN                                        09000021
         LA    CURINDEX,2(CURINDEX)          INDEX TO P PTR             09050021
         LH    CURPPTR,0(CURCHNAD,CURINDEX)  CURRENT P PTR              09100021
         LR    WORK1,CURPPTR                 CURRENT P PTR              09150021
         SLL   WORK1,4                       MULT BY 16                 09200021
         A     WORK1,CHESD         POINTS TO CESD LINE  ID=P            09250021
         IC    CURPSEGN,12(WORK1)       CURRENT SEGMENT NO OF P         09300021
*********************************************************************** 09350021
*              DOES THE CONTROL SECTION WITH ITS ESDID = THIS P LIE IN  09400021
*              THE CURRENT SEGMENT/ NO GO TO TEST END OF CALLS LIST     09450021
*********************************************************************** 09500021
*********************************************************************** 09550021
*              TEST FOR A CONTROL SECTION IN SEGMENT 1                  09600021
*********************************************************************** 09650021
         LA    WORK1,1                  INIT WORK1 TO 1                 09700021
         CLR   WORK1,CURPSEGN           IS THIS P IN SEGMENT 1          09750021
         BC    7,ENS018                 NO / CONTINUE                   09800021
         OI    PSEGONE,X'80'            SET IND A CALL FROM SEG 1 SEEN  09850021
ENS018   CLR   CURSEGNO,CURPSEGN        DOES CURR.P SEG.NO.= CURR.SEG.  09900021
         BC    7,ENS1200                NO                              09950021
*********************************************************************** 10000021
*              INCREMENT TO NEXT CALLS LIST ENTRY / TEST IF ALL RS FOR  10050021
*              THIS P HAVE BEEN PROCESSED                               10100021
*********************************************************************** 10150021
ENS020   LA    CURINDEX,2(CURINDEX)     INCREMENT TO NEXT R OR CHAIN    10200021
         CH    CURINDEX,0(CURCHNAD)     IS SCAN FOR THIS P PTR DONE     10250021
         BC    8,ENS1200                YES                             10300021
*********************************************************************** 10350021
*              USING R POINTER REFERENCE CESD TO DETERMINE WHAT ITS     10400021
*              TYPE IS / ER,PR NO GOOD                                  10450021
*********************************************************************** 10500021
         LH    CURRPTR,0(CURCHNAD,CURINDEX)  PICK UP CURRENT R PTR      10550021
         LR    WORK1,CURRPTR                 CURRENT R PTR              10600021
         SLL   WORK1,4                       MULT BY 16                 10650021
         A     WORK1,CHESD         POINTS TO ENTRY IN CESD R=ID         10700021
*********************************************************************** 10750021
         MVN   TESTTYPE,8(WORK1)        MOVE LOW 4 BITS FOR TEST        10800021
         CLI   TESTTYPE,X'08'           IS TYPE = SD WITH RLDS          10850021
         BC    2,ENS020                 HIGH - GO TO NEXT R OR CA       10900021
         BC    8,ENS030                 YES - PROCESS SD                10950021
         CLI   TESTTYPE,X'05'           IS TYPE = CM                    11000021
         BC    2,ENS020                 HIGH - GO TO NEXT R OR CA       11050021
         BC    8,ENS030                 YES - PROCESS CM                11100021
         TM    TESTTYPE,X'03'           IS TYPE = SD OR LR              11150021
         BC    4,ENS020                 NO - GO TO PROCESS NEXT R OR CA 11200021
*                                       TYPES SD,SD W/RLD,LR,CM OK      11250021
*********************************************************************** 11300021
*              FIND SEGMENT NUMBER OF THIS R POINTER - USE CESD         11350021
*********************************************************************** 11400021
ENS030   SR    CURRSEGN,CURRSEGN                                        11450021
         IC    CURRSEGN,12(WORK1)       PICK UP SEGMENT NO OF R = SR    11500021
*********************************************************************** 11550021
*              TEST FOR UPWARD CALL - YES - NO ENTAB LINES REQUIRED     11600021
*********************************************************************** 11650021
         CLR   CURRSEGN,CURPSEGN        DOES  SR = SP                   11700021
         BC    8,ENS020                 YES UPWARD CALL / NO LINES      11750021
         LR    WORK1,CURRSEGN           PASS SR TO COMMON PATH          11800021
         LR    WORK2,CURPSEGN           PASS SP TO COMMON PATH          11850021
         BAL   RETURN,ENS1500                                           11900021
         CLR   WORK2,CURRSEGN           DOES  SR = SC                   11950021
         BC    8,ENS020                 YES  (UPWARD CALL) / NO LINES   12000021
*********************************************************************** 12050021
*              TEST FOR DOWNWARD CALL - YES - MAY REQUIRE ENTAB         12100021
*********************************************************************** 12150021
         CLR   WORK2,CURPSEGN           DOES  SP = SC                   12200021
         BC    8,ENS080                 YES  (DOWNWARD CALL)            12250021
*********************************************************************** 12300021
*              DETERMINE  REGION NUMBERS FOR SEGMENT NUMBERS P AND R    12350021
*********************************************************************** 12400021
         SR    WORK1,WORK1                                              12450021
         SR    WORK2,WORK2                                              12500021
         SR    WORK4,WORK4                                              12550021
ENS040   IC    WORK4,LSTS(WORK1)   HI SEG OF THIS REG                   12600021
         LA    WORK1,1(WORK1)                REG NO.FOR SP IN WORK1     12650021
         CR    CURPSEGN,WORK4          DOES SP LIE IN THIS REG          12700021
         BC    2,ENS040                NO                               12750021
ENS050   IC    WORK4,LSTS(WORK2)   HI SEG OF THIS REG                   12800021
         CR    CURRSEGN,WORK4  DOES SR LIE IN THIS REG                  12850021
         LA    WORK2,1(WORK2)                REG NO.FOR SR IN WORK2     12900021
         BC    2,ENS050                NO                               12950021
*********************************************************************** 13000021
*              DO BOTH SEGMENTS LIE IN THE SAME REGION / YES MAY B ERR  13050021
*********************************************************************** 13100021
         CLR   WORK1,WORK2              DOES  RSP = RSR                 13150021
         BC    7,ENS080                 NO / CALL TO DIFFERENT REGION   13200021
*********************************************************************** 13250021
*              CONVERT SEGMENT NUMBER FROM WHICH THIS EXCLUSIVE CALL  * 13300021
*              HAS BEEN MADE FROM BINARY TO DECIMAL FOR PRINTING MESS.* 13350021
*********************************************************************** 13400021
         LR    WORK3,CURPSEGN           FROM SEGMENT                    13450021
         CVD   WORK3,CVDAREA            CONVERT TO DECIMAL              13500021
         UNPK  CVDAREA+0(5),CVDAREA+6(3)  UNPACK TO SUPPLY ZONE         13550021
         CLI   CVDAREA,C'0'            IS FIRST DIGIT ZERO              13600021
         BC    7,ENS055                NO                               13650021
         MVI   CVDAREA,C' '            YES--MOVE IN BLANK               13700021
         CLI   CVDAREA+1,C'0'          IS SECOND DIGIT ZERO             13750021
         BC    7,ENS055                NO                               13800021
         MVI   CVDAREA+1,C' '          YES--MOVE IN BLANK               13850021
ENS055   MVI   CVDAREA+3,C' '                                           13900021
         MVC   CVDAREA+4(4),CVDAREA+3  BLANK REMAINING FIVE CHARACTERS  13950021
*                                       PRINTING 8 CHARACTERS           14000021
*********************************************************************** 14050021
*              USING DOWNWARD CALLS LIST DETERMINE IF THERE IS AN ENTAB 14100021
*              IN A SEGMENT COMMON TO CURRENT(SP) TO SYMBOL WITH ESDID  14150021
*              = TO R /YES-IS VALID XCAL / NO- IS INVALID XCALL         14200021
*********************************************************************** 14250021
         L     WORK3,RECNT         ENTRY LIST ADDRESS                   14300021
         IC    WORK4,4(WORK3,CURRPTR)   GET ENTRY AT THIS R = SPCR      14350021
         LTR   WORK2,WORK4              DOES SEG.NO = 0/ PUT SPCR WORK2 14400021
         BC    8,ENS070                 YES/NO PREVIOUS CALLS TO THIS R 14450021
         LR    WORK1,CURPSEGN           PASS SP TO CPTH                 14500021
         BAL   RETURN,ENS1500           GO TO COMMON PATH  ANS =  SC    14550021
         CLR   WORK2,WORK4              DOES SC = SPCR                  14600021
         BC    7,ENS070                 NO /NO CALL TO THIS R FROM A    14650021
*                                       SEGMENT IN ROOT PATH OF SP      14700021
*********************************************************************** 14750021
*              TEST IF XCALL OPTION SPECIFIED - YES -EXECUTE            14800021
*********************************************************************** 14850021
         LH    ERRCODRG,ERRPARM1        ASSUME XCALL ON/NO ERROR-WARN   14900021
         TM    APT0,XCAL           IS XCAL OPTION = ON                  14950021
         BC    1,ENS060                 XCALL ON/ GO PRINT WARNING      15000021
         LH    ERRCODRG,ERRPARM2        XCALL OFF /EXECUTE ON LET ONLY  15050021
*                                       SEVERITY 3                      15100021
*********************************************************************** 15150021
*              GO TO ERROR DIAGNOSTIC ROUTINE TO PRINT MESSAGE          15200021
*********************************************************************** 15250021
ENS060   ST    ACUMZERO,SAVECT          SAVE ENTAB COUNT                15300021
         LR    ACUMZERO,CURRPTR         ID OF R - TO SYMBOL             15350021
         SLL   ACUMZERO,4               RELATIVE ADD OF SYMBOL          15400021
         A     ACUMZERO,CHESD      TRUE ADDRESS OF SYMBOL               15450021
         L     ERRADDRG,ERDIG      ERROR ADD                            15500021
         LA    ERRSYMRG,CVDAREA        ADDRESS OF SEGMENT NUMBER        15550021
         BALR  RETURN,ERRADDRG                                          15600021
         L     ACUMZERO,SAVECT          RESTORE COUNT OF ENTAB LINES    15650021
         BC    15,ENS020                                                15700021
*********************************************************************** 15750021
*              AN INVALID CALL HAS BEEN DETECTED  SET UP TO PRINT MESS. 15800021
*********************************************************************** 15850021
ENS070   LH    ERRCODRG,ERRPARM3        INVALID XCALL / EXECUTE ON LET  15900021
         BC    15,ENS060                                                15950021
*********************************************************************** 16000021
*              TEST IF A PREVIOUS CALL HAS BEEN MADE TO THIS SYMBOL     16050021
*              FROM A SEGMENT COMMON TO THE CURRENT SEGMENT             16100021
*********************************************************************** 16150021
ENS080   L     WORK3,RECNT         ADD OF DOWNWARD CALLS LIST           16200021
         SR    WORK2,WORK2                                              16250021
         IC    WORK2,4(WORK3,CURRPTR)   GET SEG NO OF PREVIOUS CALL     16300021
         LTR   WORK4,WORK2              WAS THERE A PREVIOUS CALL       16350021
         BC    8,ENS090                 NO                              16400021
         LR    WORK1,CURPSEGN           PASS SP TO CPTH                 16450021
         BAL   RETURN,ENS1500           GO TO COMMON PATH/ANS IS = SC   16500021
         CLR   WORK2,WORK4              DOES SC = SPCR                  16550021
         BC    8,ENS020                 YES /AN ENTAB LINE EXISTS IN    16600021
*                                       THE ROOT PATH OF SP FOR R       16650021
*********************************************************************** 16700021
*              PUT THE CURRENT SEGMENT NUMBER IN THE DOWNWARDS CALL     16750021
*              LIST AT THE R TH ENTRY-- R IS THE ESDID OF THE CALLED    16800021
*              SYMBOL                                                   16850021
*********************************************************************** 16900021
ENS090   L     WORK3,RECNT                                              16950021
         STC   CURPSEGN,4(WORK3,CURRPTR)                                17000021
         LA    ACUMZERO,1(ACUMZERO)                                     17050021
*********************************************************************** 17100021
*              TEST IF A CALL HAS BEEN MADE FROM SEG 1 - YES-TO VALID   17150021
*              SYMBOL / YES - SET INDICATOR IN PSEGONE -BIT 2           17200021
*********************************************************************** 17250021
         TM    PSEGONE,X'80'            IS THIS CALL FROM SEGMENT 1     17300021
         BC    8,ENS020                 NO /                            17350021
         OI    PSEGONE,X'40'            YES / AN A VALID CALL WAS  MADE 17400021
         BC    15,ENS020                                                17450021
*********************************************************************** 17500021
*              TEST IF A VALID CALL HAS BEEN FROM SEGMENT 1 TO A SYMBOL 17550021
*              NOT IN SEGMENT 1                                         17600021
*********************************************************************** 17650021
ENS110   LH    ERRCODRG,ERRPARM4                                        17700021
         TM    PSEGONE,X'40'                                            17750021
         BC    1,ENS115                 YES/ DO NOT PRINT ERROR MESS    17800021
         L     ERRADDRG,ERDIG      ADD OF ERROR DIAGNOSTIC ROUTINE      17850021
         BALR  RETURN,ERRADDRG          GO TO ERROR DIAGNOSTIC ROUTINE  17900021
*********************************************************************** 17950021
*              RESTORE REGISTERS AND RETURN TO CALLER                   18000021
*********************************************************************** 18050021
ENS115   L     13,SAVERG13              RESTORE ADDRESS OF CALLERS AREA 18100021
         LM    14,12,DISP(13)           RESTORE CALLERS REGISTERS       18150021
         BCR   15,14                    RETURN TO CALLER                18200021
*********************************************************************** 18250021
*              SET INDICATOR FOR PROCESSING A CONTROL SECTION IN SG1TO0 18300021
*********************************************************************** 18350021
ENS1200  NI    PSEGONE,X'7F'            SET P IN SEGMENT 1 TO 0         18400021
*********************************************************************** 18450021
*              TEST FOR END OF PROCESSING CURRENT SEGMENT               18500021
*********************************************************************** 18550021
         AH    CURCHNAD,0(CURCHNAD)     ADD.CURRENT CHAIN ADDRESS TO    18600021
*                                       ADD.OF PREVIOUS CHAIN ADDRESS   18650021
ENS010   LH    WORK1,0(CURCHNAD)        CURRENT CHAIN                   18700021
         LTR   WORK1,WORK1              IS SCAN FOR CURRENT SEGMENT DON 18750021
         BC    2,ENS017                NO  / LOOK AT NEXT P             18800021
*********************************************************************** 18850021
*              TEST IF THERE ANY ENTAB LINES FOR THIS SEGMENT/ YES GO   18900021
*              TO HEWLCAD1 TO RECORD COUNT.                             18950021
*********************************************************************** 19000021
         LTR   ACUMZERO,ACUMZERO        ANY ENTAB LINES FOR THIS SEG    19050021
         BC    8,ENS015                 NO  GO TO PROCESS NXT SEGMENT   19100021
         LM    RETURN,BRANCH,ENS1300    SET UP RETURN & BRANCH REGS     19150021
         BCR   15,BRANCH                GO TO PCEB / ENTER ENTAB INCESD 19200021
*********************************************************************** 19250021
*********************************************************************** 19300021
*              CPTH -  COMMON PATH SUBROUTINE                         * 19350021
*                                                                     * 19400021
*                                                                     * 19450021
*********************************************************************** 19500021
*                                                                     * 19550021
*                   REQUIRED REGISTERS  & PARAMETERS                  * 19600021
*                        BASE                                         * 19650021
*                        WORK1     SEG A  (NOT SAVED)                 * 19700021
*                        WORK2     SEG B  (NOT SAVED)  ANS PUT HERE   * 19750021
*                        WORK3     WORK REGISTER (NOT SAVED)          * 19800021
*                        RETURN    RETURN ADDRESS                     * 19850021
*                        APT       ALL PURPOSE TABLE                  * 19900021
*********************************************************************** 19950021
*********************************************************************** 20000021
ENS1500  L     WORK3,SGT1          ADDRESS OF SEGTAB1                   20050021
ENS1600  CLR   WORK2,WORK1              DOES  SEG B = SEG A             20100021
         BCR   8,RETURN                 YES - ANS IN WORK 2             20150021
         BC    2,ENS1700                NO  -SEG B GTR SEG A            20200021
         XR    WORK2,WORK1              NO  -SEG B LOW SEG A            20250021
         XR    WORK1,WORK2              EXCHANGE PARAMETERS             20300021
         XR    WORK2,WORK1                                              20350021
ENS1700  IC    WORK2,0(WORK3,WORK2)     GET PREVIOUS SEGMENT NUMBER     20400021
         LTR   WORK2,WORK2              IS PREVIOUS SEGMENT NUMBER = 0  20450021
         BC    2,ENS1600                NO                              20500021
         LA    WORK2,1                  SET ANS = 1                     20550021
         BCR   15,RETURN                                                20600021
*********************************************************************** 20650021
*********************************************************************** 20700021
ENS1300  DC    A(ENS015)                RETURN ADDRESS                  20750021
         DC    V(HEWLCAD1)                                              20800021
SIX      DC    H'6'                     CONSTANT OF 6                   20850021
*                                                                       20900021
TESTTYPE DC    X'00'                    USED FOR TESTING LOW 4 BITS OF  20950021
*                                       ESD TYPE                        21000021
PSEGONE  DC    X'00'                    USED TO INDICATE AVALID CALL    21050021
*                                       FROM SEGMENT 1                  21100021
*                                       BIT 1- INIT= 0  1= CALL FROMSG1 21150021
*                                       BIT 2- INIT= 0  1= CALL FROMSG1 21200021
*                                        TO A VALID SYMBOL NOT IN SEG 1 21250021
CVDAREA  DS    2D          '            PRINT IMAGE FOR XCALL MESSAGE   21300021
SAVECT   DC    F'0'                     SAVE  ENTAB COUNT WHEN GOING TO 21350021
*                                       ERROR DIAGNOSTIC ROUTINE        21400021
SAVERG13 DS    F                        ADDRESS OF CALLERS REG SAVEAREA 21450021
*********************************************************************** 21500021
*              ERROR PARAMETERS                                       * 21550021
*********************************************************************** 21600021
MESS1    EQU   X'3110'                                                  21650021
MESS2    EQU   X'3211'                                                  21700021
MESS3    EQU   X'3212'                                                  21750021
MESS4    EQU   X'020F'                                                  21800021
ERRPARM1 DC    AL2(MESS1)               WARNING XCLUSIVE CALL-XCALL ON  21850021
ERRPARM2 DC    AL2(MESS2)               ERROR XCLUSIVE  CALL -XCALL OFF 21900021
ERRPARM3 DC    AL2(MESS3)               ERROR INVALID XCLUSIVE CALL     21950021
ERRPARM4 DC    AL2(MESS4)               NO CALLS FROM SEG 1 TO VALID    22000021
*                                                                       22050021
*                                                                       22100021
*                                                                       22150021
*********************************************************************** 22200021
         HEWAPT                                                         22250021
*********************************************************************** 22300021
         END                                                            22350021
