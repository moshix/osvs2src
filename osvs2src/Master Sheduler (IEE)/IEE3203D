         TITLE 'IEE3203D - VARY KEYWORD SCAN ROUTINE'             M4957 00200003
IEE3203D CSECT                                                          00400003
* (C) BEFORE PARENSCN   OPERAND COMPARE LENGTHS                @ZA07514 00450003
* (C) KEYTAB   ADDED LEFT PAREN TO COMPARE VALUES              @ZA07514 00500003
* (C) TABSCAN,TEST FOR ZERO OPERAND LENGTH                     @ZA27029 00550003
*********************************************************************** 00600003
*                                                                       00800003
* MODULE NAME       = IEE3203D                                          01000003
*    CSECT NAME     = IEE3203D                                          01200003
*                                                                       01400003
* LOAD MODULE NAME  = IGC0003D                                          01600003
*                                                                       01800003
* DESCRIPTIVE NAME  = VARY KEYWORD SCAN MODULE                          02000003
*                                                                       02200003
* COPYRIGHT         = MISSING                                           02400003
*                                                                       02600003
* STATUS            = OS/VS2 RELEASE 3                                  02800003
*                                                                       03000003
* FUNCTION          = THIS MODULE SCANS THE VARY PRIMARY KEYWORDS       03200003
*                     NET, CH, CPU, STOR, PATH, CN AND GIVES CONTROL TO 03400003
*                     THE ATCAM PROCESSOR FOR NET OR TO IEE0803D FOR    03600003
*                     THE OTHER KEYWORDS.  IF THERE IS NO MATCH ON THE  03800003
*                     PRIMARY KEYWORDS THEN THIS ROUTINE SCANS FOR THE  04000003
*                     SECONDARY KEYWORDS IN THE COMMAND FIELD AND THEN  04200003
*                     COMPARES THIS KEYWORD TO EACH ENTRY IN A TABLE    04400003
*                     OF VALID VARY KEYWORDS.  SUCH KEYWORDS ARE:       04600003
*                        ONLINE        HARDCPY                          05200003
*                        OFFLINE       MSTCONS                          05400003
*                        CONSOLE       ONTP/OFFTP (TCAM)                05600003
*                                                                       06400003
* OPERATION         = IEE3203D FIRST SCANS THE VARY COMMAND OPERAND FOR 06600003
*                     THE PRIMARY KEYWORD WHICH IS DEFINED AS THE WORD  06800003
*                     FOLLOWING THE VERB 'VARY'.                        07000003
*                     THE PRIMARY KEYWORDS THAT ARE SUPPORTED ARE:      07200003
*                        V NET   FOR ATCAM                              07400003
*                        V PATH  FOR MP                                 07600003
*                        V CH    FOR MP                                 07800003
*                        V CPU   FOR MP                                 08000003
*                        V STOR  FOR MP                                 08200003
*                        V CN    FOR SUBSYSTEM                          08300003
*                     IF NET IS FOUND THEN THIS ROUTINE WILL ROUTE      08400003
*                     TO THE ATCAM PROCESSOR TO SERVICE THE COMMAND.    08600003
*                     IF THE OTHERS ARE FOUND, THEN THIS ROUTINE WILL   08800003
*                     CHANGE THE VERB CODE IN THE XAN FOR THEM AS       09000003
*                     FOLLOWS:                                          09200003
*                        V PATH = X'50'                                 09400003
*                        V CH   = X'58'                                 09600003
*                        V CPU  = X'54'                                 09800003
*                        V STOR = X'5C'                                 10000003
*                        V CN   = X'78'                                 10100003
*                     AND CONTROL IS PASSED TO IEE0803D TO CREATE A     10200003
*                     CSCB FOR THEM SO THAT THEY CAN EVENTUALLY BE      10400003
*                     ATTACHED.                                         10600003
*                     IF NONE OF THE ABOVE, THEN THIS ROUTINE SCANS FOR 10800003
*                     THE SECONDARY KEYWORD WHICH IS DEFINED AS THE     11000003
*                     WORD FOLLOWING THE FIRST COMMA NOT EMBEDDED IN    11200003
*                     PARENS.  R9 IS USED TO INDEX THROUGH THE OPERAND  11400003
*                     TO FIND THE ',KEYWORD'.  COMMANDS OF THE FOLLOW-  11600003
*                     ING FORMS ARE ACCEPTABLE:                         11800003
*                        V ,SECONDARY KEYWORD                           11900003
*                        V UNIT,SECONDARY KEYWORD                       12000003
*                        V UNIT,(MULTIPLE UNITS),SECONDARY KEYWORD      12200003
*                        V (MULTIPLE UNITS),SECONDARY KEYWORD           12400003
*                        V (MULTIPLE UNITS),UNIT,SECONDARY KEYWORD      12600003
*                     (ANY NUMBER OF BALANCED INNER/OUTER PARENS MAY BE 12800003
*                     SPECIFIED)                                        13000003
*                     WHEN THE COMMA PRECEDING THE KEYWORD IS FOUND, A  13200003
*                     POINTER TO THE START OF THE KEYWORD IS SAVED AND  13400003
*                     THE SCAN CONTINUES UNTIL A COMMA, BLANK OR THE    13600003
*                     END OF BUFFER IS FOUND.  WHEN THE COMPLETE        13800003
*                     KEYWORD IS FOUND, IT IS COMPARED TO EACH ENTRY    14000003
*                     IN THE VALID KEYWORD TABLE UNTIL A MATCH OR THE   14200003
*                     END OF THE TABLE IS FOUND. EACH ENTRY CONSISTS    14400003
*                     OF AN EIGHT (8) BYTE KEYWORD FOLLOWED BY THE      14600003
*                     ADDRESS OF THE RESPECTIVE PROCESSOR.              14800003
*                     THE TABLE SCAN IS DONE BY ESTABLISHING ADDRESS-   15000003
*                     ABILITY TO THE TABLE AND THEN INCREMENTING THE    15200003
*                     BASE REGISTER BY THE LENGTH OF AN ENTRY AS EACH   15400003
*                     NON-MATCH IS FOUND.  THIS METHOD INSURES FUTURE   15600003
*                     FLEXIBILITY BECAUSE NO MATTER HOW MANY NEW        15800003
*                     KEYWORDS ARE ADDED TO THE TABLE, THE LABEL        16000003
*                     'KEYTAB', WITH AN INDEX WILL ALWAYS POINT TO THE  16200003
*                     CURRENT KEYWORD AND ITS EXITING POINT.  WHEN A    16400003
*                     MATCH IS FOUND, EXIT IS MADE BY BRANCHING TO THE  16600003
*                     MODULE INDICATED IN THE TABLE.  FOR AN ERROR,     16800003
*                     EXIT IS MADE TO IEE0503D.  WHEN AN 'OFFLINE' OR   16880003
*                     'ONLINE' KEYWORD IS FOUND WITH A ',S', THIS       16960003
*                     INDICATES A SS/1 VARY REQUEST.  CONTROL IS THEN   17040003
*                     PASSED TO IEE9403D.                               17200003
*                                                                       17320003
* NOTES                                                                 17400003
*    DEPENDENCIES   = KEYWORDS SUCH AS ONLINE/OFFLINE/CONSOLE/HARDCPY/  17600003
*                     MSTCONS/ONTP/OFFTP ARE STANDARD.                  17800003
*                     THE NET (ATCAM), AND CH, CPU, STOR, PATH, AND CN  18000003
*                     PRIMARY KEYWORDS ARE CONSIDERED STANDARD ALSO.    18200003
*    CHARACTER/CODE DEPENDENCIES                                        18400003
*                   = THIS MODULE CAN BE ASSEMBLED WITH ANY NEW         18600003
*                     CHARACTER/CODE SET.                               18800003
*                                                                       19000003
* RESTRICTIONS      = ON ENTRY, REG2 PTR TO THE PARAM LIST (XSA)        19200003
* REGISTER CONVENTIONS                                                  19400003
*                   = REGISTERS ARE DEFINED AT LABEL: DEFREG            19600003
*    PATCH LABEL    = N/A                                               19800003
*                                                                       20000003
* MODULE TYPE       = CSECT                                             20200003
*    PROCESSOR      = ASSEMBLER                                         20400003
*    MODULE SIZE    = VS/2 R3  = X'3A6'                        @ZM30466 20600003
*    ATTRIBUTES     = REENTRANT, REUSABLE, REFRESHABLE, PAGED LPA,      20800003
*                     ZERO PSW PROTECT KEY, SUPERVISOR MODE             21000003
*                                                                       21200003
* ENTRY POINTS      = IEE3203D FROM COMMAND ROUTER RTN IEE0403D         21400003
*   PURPOSE         = VARY COMMAND ROUTER                               21600003
*   LINKAGE         = BRANCH                                            21800003
*   INPUT           = XSA PTR IN REG 2                                  22000003
*                     THE XSA CONTAINS:                                 22200003
*                        PTR TO PARMLIST IN XAR                         22400003
*                        PTR TO LIST AND VERB CODE IN XAL               22600003
*                        VERB = VARY IN XAV                             22800003
*                        UCM ID IN XAU                                  23000003
*   REGISTERS SAVED = NONE                                              23200003
*   REGISTER USAGE  = R2  = XSA BASE                                    23400003
*                     R4  = COUNTER FOR KEYWORD CHARACTER               23600003
*                     R6  = PTR TO FIRST CHARACTER OF KEYWORD           23800003
*                     R7  = END OF BUFFER REG                           24000003
*                     R9  = PTR TO CURRENT CHARACTER OF OPERAND FIELD   24200003
*                     R11 = BASE FOR VALID KEYWORD TABLE                24400003
*                     R12 = ADDRESSABILITY                              24600003
*   REGISTERS RESTORED                                                  24800003
*                   = NONE                                              25000003
*                                                                       25200003
* EXITS - NORMAL    =  IN THIS SYSTEM IEE3203D EXITS TO:                25400003
*   CONDITIONS           IEE0803D FOR KEYWORDS - ONLINE, CONSOLE        25600003
*                                                AND OFFLINE            25800003
*                        IEE4703D FOR KEYWORDS - HARDCPY                26000003
*                        ISTCFF3D FOR KEYWORDS(PRIMARY) - NET           26200003
*                        IEE0803D FOR KEYWORDS(PRIMARY) - CH, CPU,      26400003
*                                     PATH, STOR, AND CN                26600003
*                        IEE4303D FOR KEYWORDS - MSTCONS                26800003
*                        IED1303D FOR KEYWORDS - ONTP, OFFTP            26900003
*                        IEE9403D FOR KEYWORDS - ONLINE AND OFFLINE     27000003
*                                      WITH SS/1 PARAMETER              27100003
*   OUTPUT          = SAME AS INPUT TO THIS MODULE                      27200003
*   RETURN CODES    = NONE                                              27400003
*                                                                       27600003
* EXITS - ERROR     = EXIT TO MESSAGE MODULE IEE0503D                   27800003
*   CONDITIONS      = WHEN AN ERROR IS DETECTED                         28000003
*   OUTPUT          = REG 2 PTS TO THE XSA WHICH CONTAINS:              28200003
*                     ERROR MSG CODE IN THE XAE                         28400003
*                     VERB IN THE XAV                                   28600003
*                     UCM ID IN THE XAU                                 28800003
*   RETURN CODES    = NONE                                              29000003
*                                                                       29200003
* EXTERNAL REFERENCES                                                   29400003
*   ROUTINES        = ISTCFF3D  FOR NET IN ATCAM                        29600003
*                   = IEE0803D  FOR CH, CPU, STOR, PATH, AND CN         29800003
*                     IEE0803D  FOR OFFLINE/ONLINE/CONSOLE              30000003
*                     IEE4703D  FOR HARDCPY                             30200003
*                     IEE4303D  FOR MSTCONS                             30400003
*                     IED1303D  FOR ONTP/OFFTP                          30600003
*                     IEE0503D  FOR ERROR MESSAGES                      30800003
*                     IEE9403D  FOR OFFLINE/ONLINE WITH ',S'            30900003
*   DATA AREAS      = SEE EXITS - OUTPUT DATA                           31000003
*                                                                       31200003
* CONTROL BLOCKS    = EXTENDED SAVE AREA (XSA)                          31400003
*                        FOR FORMAT SEE - INPUT DATA AND                31600003
*                        OUTPUT DATA SECTIONS                           31800003
*                                                                       32000003
* TABLES            = NONE                                              32200003
*                                                                       32400003
* MACROS            = NONE                                              32600003
*                                                                       32800003
* MAPPING MACROS    = IEEXSA                                            33000003
*    SERIALIZATION = NONE REQUIRED IN THIS MODULE                       33200003
*                                                                       33400003
* CHANGE LEVEL      = Y02669, Y02674, Z30LPTJ, Z309PTJ,        @ZM30466 33600003
*                     ZM30466,ZA07514,ZA27029                  @ZA27029 33700003
*                                                                       33800003
* CHANGE ACTIVITY  = N/A                                                34000003
*                                                                       34200003
* MESSAGES          = 'IEE307I VARY DELIMITER ERROR'                    34400003
*                        WHEN THERE IS:                                 34410003
*                         A) NO COMMA AFTER PARENTHESES                 34420003
*                         B) UNBALANCED PARENTHESES                     34430003
*                         C) NO COMMA BEFORE KEYWORD                    34440003
*                         D) A KEYWORD FOUND IN A SET OF PARENTHESIS    34445003
*                         E) A SET OF PARENTHESIS THAT DOES NOT START   34446003
*                            IN THE FIRST POSITION FOR SECONDARY KYWDS  34447003
*                         F) MULTIPLE COMMA BEFORE KEYWORD     @ZA27029 34448003
*                     'IEE308I VARY TERM LENGTH ERROR'                  34450003
*                        WHEN COMMAND CONTAINS AT LEAST ONE BLANK       34460003
*                     'IEE309I VARY UNIDENTIFIABLE KEYWORD'             34470003
*                        WHEN KEYWORD DOES NOT MATCH ANY TABLE ENTRY    34480003
*                     'IEE311I VARY PARAMETER MISSING'                  34490003
*                        WHEN THERE IS:                                 34500003
*                         A) NO POINTER TO THE COMMAND PARAMETER LIST   34600003
*                         B) NO UNIT SPECIFIED BEFORE CERTAIN           34680003
*                            SECONDARY KEYWORDS                         34720003
*                     'IEE312I VARY PARAMETERS CONFLICT'                34760003
*                        WHEN A VARY CONSOLE COMMAND IS SPECIFIED WITH  34840003
*                        A SS/1 PARAMETER                               34920003
*                     'IEE908I VARY CMD LENGTH EXCEEDS MAX.'            36200003
*                        WHEN A VARY ONLINE/OFFLINE/CONSOLE COMMAND'S   36400003
*                        PARAMETER LIST IS GREATER THAN 124 CHARACTERS, 36480003
*                        OR WHEN A VARY ONLINE/OFFLINE WITH ',S'        36560003
*                        COMMAND'S PARAMETER LIST IS GREATER THAN 126   36640003
*                        CHARACTERS                                     36720003
*                                                                       36950003
* ABEND CODES       = NONE                                              37000003
*                                                                       37200003
* PACKAGING         = IEE3203D WILL BE PART OF LOAD MODULE              37400003
*                        = IGC0003D                                     37600003
*                     THIS LOAD MODULE ALIASES ARE                      37800003
*                        = NONE                                         38000003
*                     THIS LOAD MODULE ENTRY POINTS ARE                 38200003
*                        = IGC0003D                                     38400003
*                     MODULE WILL RESIDE IN: LPALIB                     38600003
*                                                                       38800003
* SYSGEN            = SUPPLIED BY SECTIONS:                             39000003
*                        LOAD MODULE NAME                               39200003
*                        PACKAGING                                      39400003
*                                                                       39600003
* SYSTEM LIBRARIES  = NONE                                              39800003
*                                                                       40000003
*********************************************************************** 40200003
          EJECT                                                         40400003
*  REGISTER ASSIGNMENTS                                                 40600003
*                                                                       40800003
DEFREG   EQU   *                                                 Y02669 41000003
R1       EQU   1                                                        41200003
R2       EQU   2              XSA BASE                                  41400003
R3       EQU   3                                                        41600003
R4       EQU   4              COUNTER FOR KEYWORD CHARC.                41800003
R5       EQU   5                                                        42000003
R6       EQU   6              PTR TO FIRST CHAR OF KEYWORD              42200003
R7       EQU   7                   POINTER TO END OF BUFFER             42400003
R8       EQU   8                   WORK REG                             42600003
R9       EQU   9                   PTR TO CURRENT CHAR OF OPERAND FIELD 42800003
R10      EQU   10                                                       43000003
R11      EQU   11                  BASE FOR VALID KEYWORD TABLE         43200003
R12      EQU   12                  PGM BASE                             43400003
R13      EQU   13                                                       43600003
R14      EQU   14                                                       43800003
R15      EQU   15                                                       44000003
*    EQUATES                                                            44200003
C0       EQU   0                                                        44400003
C1       EQU   1                                                        44600003
C2       EQU   2                                                        44800003
C3       EQU   3                                                        45000003
C4       EQU   4                                                        45200003
C5       EQU   5                                               @ZA07514 45250003
C6       EQU   6                                                 Y02669 45400003
C7       EQU   7                                                 Y01029 45600003
C8       EQU   8                                                        45800003
D124     EQU   124                                                      46000003
D126     EQU   126                                             @Z30LPTJ 46100003
ERRX39   EQU   X'39'                                             Y02669 46200003
LENG     EQU   12           LENGTH OF KEYWORD-MODULE CODE ENTRY  Y01029 46400003
COMMA    EQU   C','                COMMA CHAR                           46600003
LFTPAREN EQU   C'('                LEFT PAREN CHAR                      46800003
RGTPAREN EQU   C')'                RIGHT PAREN CHAR                     47000003
BLANK    EQU   C' '                BLANK CHAR                           47200003
PARENSW  EQU   3                   SWITCH FOR BALANCED PARENS           47400003
ERR7     EQU   07                  ERROR CODE   DELIM ERROR             47600003
ERR8     EQU   8                   ERROR CODE   TERM LENGTH    @Z309PTJ 47650003
ERR9     EQU   9                   ERROR CODE   UNIDENT KEYWORD         47700003
ERR11    EQU   11                  ERROR CODE   PARAM MISSING           47800003
ERR12    EQU   12                  ERROR CODE   PARAMETER CONFLICT      47900003
CHCODE   EQU   X'58'                                             Y02669 48200003
CPUCODE  EQU   X'54'                                             Y02669 48400003
STORCODE EQU   X'5C'                                             Y02669 48600003
PATHCODE EQU   X'50'                                             Y02669 48800003
CNCODE   EQU   X'78'                                           @Z309PTJ 49000003
         EJECT                                                          49200003
*********************************************************************** 49400003
* FUNCTIONS OF THIS ROUTINE-                                          * 49600003
*     1. ESTABLISHES PROGRAM AND XSA ADDRESSABILITY                   * 49800003
*     2. INITIALIZES A BALANCED PAREN SWITCH AND A CHARACTER COUNTER  * 50000003
*     3. CHECKS XAL FOR 0. (IF 0, BRANCHES TO ERROR ROUTINE.)         * 50200003
*     4. COMPUTES THE END OF BUFFER                                   * 50400003
*********************************************************************** 50600003
         BALR  R12,C0              ESTABLISH PGM ADDRESSABILITY         50800003
         USING *,R12                                                    51000003
         MODID BR=YES                                            Y01886 51200003
         USING XSA,R2              BASE FOR EXTENDED SAVE AREA          51400003
         L     R9,XAL              REG 9 POINTS TO OPERAND FIELD OF    *51600003
                                   VARY COMMAND                         51800003
         LA    R9,C0(R9)           ZERO OUT HIGH ORDER BYTE             52000003
         LA    R5,ERR11            SET ERR CODE FOR MSG 'PARAM MISSING' 52100003
         LTR   R9,R9               IS PARAMETER PTR 0 (NO KEYWORD)      52200003
         BZ    ERREXIT             YES,PTR IS 0, GO TO ERROR ROUTINE    52600003
         L     R7,XAR              LOAD POINTER TO THE BUFFER THAT      52800003
         LA    R7,C0(R7)           CONTAINS THE VARY COMMAND            53000003
         AH    R7,C0(R7)           COMPUTE END OF BUFFER                53200003
         CLC   C0(C4,R9),CNET        IS IT 'NET'FOR VTAM       @ZA07514 53600003
         BE    VTAMEXT               YES, EXIT TO VTAM MOD.      Y02674 53800003
         CLC   C0(C3,R9),CCH        IS IT CHANNEL FOR MP       @ZA07514 54000003
         BE    CHANNEL               YES,CHANGE CODE AND EXIT    Y02669 54200003
         CLC   C0(C4,R9),CCPU        IS IT CPU FOR MP          @ZA07514 54400003
         BE    CPU                   YES, CHANGE CODE AND EXIT   Y02669 54600003
         CLC   C0(C5,R9),CSTOR       IS IT STOR FOR MP         @ZA07514 54800003
         BE    STORAGE               YES, CHANGE CODE AND EXIT   Y02669 55000003
         CLC   C0(C5,R9),CPATH       IS IT PATH FOR MP         @ZA07514 55200003
         BE    PATH                  YES, CHANGE CODE AND EXIT   Y02669 55400003
         CLC   C0(C3,R9),CCN         IS IT CN FOR SUBSYSTEM?   @ZA07514 55460003
         BE    CN                    YES, CHANGE CODE AND EXIT @Z309PTJ 55530003
* NO PRIMARY KEYWORD, SET UP AND CHECK THE SECONDARY KEYWORDS    Y02669 55600003
*                                                                Y02669 55800003
         SR    PARENSW,PARENSW     INITIALIZE PAREN SWITCH TO 0         56000003
         SR    R4,R4               INITIALIZE CHARACTER COUNTER TO 0    56200003
         EJECT                                                          56400003
*********************************************************************** 56600003
* THE COMMAND OPERAND IS SCANNED FOR A LEFT PAREN OR A COMMA.  IF A   * 56700003
* COMMA IS FOUND IN THE FIRST POSITION, A CHECK IS MADE TO SEE IF THE * 56800003
* KEYWORD AFTER THE COMMA IS VALID FOR THIS CASE.  IF NOT, AN ERROR   * 56860003
* EXIT IS MADE TO ISSUE AN IEE311I MESSAGE TO THIS EFFECT.  IF A LEFT * 56920003
* PARENTHESIS IS FOUND, THE OPERAND IS SCANNED FOR BALANCED PARENS    * 57000003
* AND THEN THE KEYWORD.  IF THERE IS NO LEFT PAREN, AND A COMMA IS    * 57200003
* FOUND IN THE SCAN, THE KEYWORD SHOULD FOLLOW.                       * 57400003
*********************************************************************** 57600003
         CLI   C0(R9),COMMA        IS FIRST CHAR. A COMMA?     @Z309PTJ 57620003
         BNE   PARENSCN            NO, START SCANNING          @Z309PTJ 57640003
         CLC   C1(C7,R9),ONL       IS IT 'ONLINE' PLUS BLANK?  @ZM30466 57740003
         BE    ERREXIT             YES, TAKE ERROR EXIT        @ZM30466 57780003
         CLC   C1(C7,R9),ONLCOMA   IS IT 'ONLINE' PLUS COMMA?  @ZM30466 57781003
         BE    ERREXIT             YES, TAKE ERROR EXIT        @ZM30466 57782003
         CLC   C1(C8,R9),OFL       IS IT 'OFFLINE' PLUS BLANK? @ZM30466 57783003
         BE    ERREXIT             YES, TAKE ERROR EXIT        @ZM30466 57784003
         CLC   C1(C8,R9),OFLCOMA   IS IT 'OFFLINE' PLUS COMMA? @ZM30466 57785003
         BE    ERREXIT             YES, TAKE ERROR EXIT        @ZM30466 57786003
         CLC   C1(C8,R9),CONS      IS IT 'CONSOLE' PLUS BLANK? @ZM30466 57787003
         BE    ERREXIT             YES, TAKE ERROR EXIT        @ZM30466 57788003
         CLC   C1(C8,R9),CONSCOMA  IS IT 'CONSOLE' PLUS COMMA? @ZM30466 57789003
         BE    ERREXIT             YES, TAKE ERROR EXIT        @ZM30466 57790003
         CLC   C1(C8,R9),MSTC      IS IT 'MSTCONS' PLUS BLANK? @ZM30466 57791003
         BE    ERREXIT             YES, TAKE ERROR EXIT        @ZM30466 57792003
         CLC   C1(C8,R9),MSTCCOMA  IS IT 'MSTCONS' PLUS COMMA? @ZM30466 57794003
         BE    ERREXIT             YES, TAKE ERROR EXIT        @ZM30466 57796003
PARENSCN EQU   *                                                        57800003
         SR    R6,R6               CLEAR 'BIT TEST' REGISTER   @Z30LPTJ 57830003
         CLI   C0(R9),LFTPAREN     IS FIRST CHAR. A '('?       @Z30LPTJ 57860003
         BNE   SCANCHAR            NO, AVOID INDICATOR SETTING @Z30LPTJ 57890003
         LA    R6,C1(R6)           YES, SET INDICATOR          @Z30LPTJ 57920003
SCANCHAR EQU   *                                               @Z30LPTJ 57950003
         CLI   C0(R9),LFTPAREN     IS CURRENT CHAR. A '('?     @Z30LPTJ 58000003
         BE    INCSWTCH            YES,GO TO CHECK FOR BAL PARENS       58200003
         CLI   C0(R9),COMMA        IS CURRENT CHAR A COMMA              58400003
         BE    SAVEPTR             YES, GO TO SCAN KEYWORD              58600003
         CLI   C0(R9),BLANK        IS CURRENT CHAR. A BLANK?   @Z309PTJ 58660003
         BE    TERMERR             YES, TAKE ERROR EXIT        @Z309PTJ 58730003
         CLI   C0(R9),RGTPAREN     A RIGHT PARENTHESIS EXISTS? @Z30LPTJ 58750003
         BE    DELIMERR            YES, TAKE ERROR EXIT        @Z30LPTJ 58770003
         LA    R9,C1(R9)           INCREMENT PTR TO NEXT CHAR           58800003
         CR    R9,R7               HAS END OF BUFFER BEEN REACHED       59000003
         BE    DELIMERR            YES,GO TO ERR RTN, COMMA MISSING     59200003
         B     SCANCHAR            GO BACK TO CHECK NEXT CHAR. @Z30LPTJ 59400003
*********************************************************************** 59600003
* THIS ROUTINE SCANS THE PARAMETER LIST FOR BALANCED PARENS. WHEN A   * 59800003
* LEFT PAREN IS FOUND, PARENSW IS INCREMENTED BY 1, FOR A RIGHT PAREN * 60000003
* IT IS DECREMENTED BY 1. WHEN PARENSW IS 0, BALANCED PARENS HAVE BEEN* 60200003
* FOUND, AND A COMMA AND THE KEYWORD SHOULD FOLLOW.                   * 60400003
*********************************************************************** 60600003
INCSWTCH LA    PARENSW,C1(PARENSW) INCREMENT BY 1 FOR LEFT PAREN        60800003
         CLI   C1(R9),BLANK        IS NEXT CHARACTER A BLANK?  @Z309PTJ 60860003
         BE    TERMERR             YES, TAKE ERROR EXIT        @Z309PTJ 60930003
CHARSCAN LA    R9,C1(R9)           INCREMENT PTR TO NEXT CHAR           61000003
         CLI   C0(R9),LFTPAREN     IS CURRENT CHAR A LEFT PAREN         61200003
         BE    INCSWTCH            YES,CHAR IS'(',SET SWTCH,CHECK AGAIN 61400003
         CLI   C0(R9),BLANK        IS CURRENT CHAR. A BLANK?   @Z309PTJ 61460003
         BE    TERMERR             YES, TAKE ERROR EXIT        @Z309PTJ 61530003
         CLI   C0(R9),RGTPAREN     IS CURRENT CHAR A RIGHT PAREN        61600003
         BNE   CHKSWTCH            NO,CHECK PAREN SWITCH FOR 0          61800003
         BCTR  PARENSW,C0          DECREMENT BY 1 FOR RIGHT PAREN       62000003
CHKSWTCH LTR   PARENSW,PARENSW     IS SWITCH 0 (BALANCED PARENS)        62200003
         BZ    KEYSCAN             YES,GO TO SCAN KEYWORD AFTER PARENS  62400003
         CLI   C0(R9),COMMA        IS THE CHARACTER A COMMA?   @Z30LPTJ 62420003
         BNE   CHKBUFF             NO, CHECK FOR END OF BUFFER @Z30LPTJ 62440003
         CLC   C1(C6,R9),ONL       IS IT AN 'ONLINE' KEYWORD?  @Z30LPTJ 62460003
         BE    DELIMERR            YES, TAKE ERROR EXIT        @Z30LPTJ 62480003
         CLC   C1(C7,R9),OFL       IS IT AN 'OFFLINE' KEYWORD? @Z30LPTJ 62500003
         BE    DELIMERR            YES, TAKE ERROR EXIT        @Z30LPTJ 62520003
         CLC   C1(C7,R9),CONS      IS IT A 'CONSOLE' KEYWORD?  @Z30LPTJ 62540003
         BE    DELIMERR            YES, TAKE ERROR EXIT        @Z30LPTJ 62560003
CHKBUFF  EQU   *                                               @Z30LPTJ 62580003
         CR    R9,R7               NO, CHECK FOR END OF BUFFER REACHED  62600003
         BNE   CHARSCAN            NO,GO BACK TO CHECK NEXT CHAR        62800003
DELIMERR LA    R5,ERR7             DELIM ERR UNBAL PARENS OR NO COMMA   63000003
         B     ERREXIT             GO TO ERROR RTN TO SET UP XCTL       63200003
TERMERR  EQU   *                                               @Z309PTJ 63250003
         LA    R5,ERR8             SET TERM LENGTH ERROR CODE  @Z309PTJ 63300003
         B     ERREXIT             GO TO ERROR EXIT ROUTINE    @Z309PTJ 63350003
         EJECT                                                          63400003
*********************************************************************** 63600003
* THIS ROUTINE CHECKS FOR A COMMA PRECEDING A KEYWORD AND IF THE COMMA* 63800003
* IS PRESENT, SCANS THE KEYWORD. A POINTER TO THE FIRST CHAR OF THE   * 64000003
* KEYWORD IS SAVED. (A KEYWORD IS DEFINED AS THE FIRST WORD FOLLOWING * 64200003
* A COMMA NOT EMBEDDED IN PARENS.) THE KEYWORD IS COMPLETE WHEN A     * 64400003
* COMMA, A BLANK, OR END OF BUFFER IS FOUND.                          * 64600003
*********************************************************************** 64700003
KEYSCAN  EQU   *                                                        64800003
         LTR   R6,R6               IS FIRST CHAR. A '('?       @Z30LPTJ 64860003
         BZ    DELIMERR            NO, TAKE ERROR EXIT         @Z30LPTJ 64920003
         LA    R9,C1(R9)           INCREMENT PTR TO NEXT CHAR           65000003
         CLI   C0(R9),COMMA        IS CURRENT CHAR A COMMA              65200003
         BNE   DELIMERR            NO.COMMA MUST PRECEDE KEYWORD        65400003
SAVEPTR  LA    R6,C1(R9)           SAVE PTR TO 1ST CHAR OF KEYWORD      65600003
SCAN1    LA    R9,C1(R9)           INCREMENT PTR TO CURR CHAR OF KEYWD  65800003
         CLI   C0(R9),COMMA        IS CURRENT CHAR A COMMA              66000003
         BE    TABSCAN             YES,COMMA MEANS END OF KEYWD FOUND   66200003
         CLI   C0(R9),BLANK        NO,CHECK CURRENT CHAR FOR BLANK      66400003
         BE    TABSCAN             YES,BLANK MEANS END OF KEYWD FOUND   66600003
         CR    R9,R7               NO,CHECK FOR END OF BUFFER           66800003
         BE    TABSCAN             GO TO SCAN TABLE                     67000003
         LA    R4,C1(R4)           INCREMENT KEYWORD CHAR COUNTER       67200003
         B     SCAN1               GO BACK TO CHECK NEXT CHAR OF KEYWD  67400003
         EJECT                                                          67600003
*********************************************************************** 67800003
* THIS ROUTINE COMPARES THE KEYWORD WITH EACH ENTRY IN A TABLE OF     * 68000003
* VALID VARY KEYWORDS UNTIL A MATCH, OR THE END OF THE TABLE IS FOUND.* 68200003
* WHEN A MATCH IS FOUND, THE ADDRESS FOLLOWING THE KEYWORD IN   Y01029* 68400003
* THE TABLE IS USED TO EXIT BY.                                 Y01029* 68600003
*********************************************************************** 68800003
TABSCAN  EQU   *                                                        69000003
         LA    R11,KEYTAB          PUT ADDRESS OF KEYWORD TABLE IN A    69200003
         USING KEYTAB,R11          REG AND ESTABLISH ADDRESSABILITY     69400003
         LA    R8,TABEND           PUT ADDRESS OF END OF TABLE IN A REG 69600003
         LTR   R4,R4               VALID OPERAND LENGTH        @ZA27029 69650003
         BZ    DELIMERR            NO, SET ERROR CODE          @ZA27029 69700003
         BCTR  R4,C0               DECREMENT CHAR COUNTER FOR EX INSTR  69800003
         LA    R5,ERR9             PRESET CODE FOR UNIDENT KEYWORD MSG  69900003
SCAN2    EQU   *                                                        70000003
         CLR   R11,R8              HAS END OF TABLE BEEN REACHED        70200003
         BH    ERREXIT             YES, THE VERB IS INVALID             70600003
         EX    R4,DOCLC            INSERT KEYWORD LENGTH IN CLC INSTR   70800003
         BE    XMOD                IF KEYWDS MATCH,GOTO SET UP EXIT MOD 71000003
INCPTR   LA    R11,LENG(R11)       IF NOT, INCREMENT PTR TO NEXT KEYWD  71200003
         B     SCAN2         GO TO CHECK NEXT ENTRY                     71400003
XMOD     LA    R3,C1(R11)          COMPUTE A POINTER TO THE FIRST       71600003
         AR    R3,R4               CHARACTER AFTER THE KEYWORD          71800003
         CLI   C7(R11),BLANK           IF THE EIGHTH CHARACTER   Y01029 72000003
*                      OF THE TABLE ENTRY IS NOT A BLANK, THE    Y01029 72200003
*                      KEYWORD IS EIGHT CHARACTERS.  IT IS A     Y01029 72400003
*                      MATCH AND EXIT SHOULD BE MADE TO THE      Y01029 72600003
*                      MODULE INDICATED IN THAT TABLE ENTRY      Y01029 72800003
         BNE   EXIT                    BRANCH TO APPROPRIATE RT  Y01029 73000003
         CLI   C0(R3),BLANK        IS THE CHARACTER BLANK               73200003
         BNE   INCPTR              IF NOT, A FALSE MATCH HAS BEEN FOUND 73400003
*                                  EG. KEYWORD 'ABC' WOULD MATCH TABLE  73600003
*                                  ENTRY 'ABCD' BECAUSE ONLY THE FIRST  73800003
*                                  3 CHARACTERS WERE COMPARED.          74000003
         DROP  R11                                             @Z30LPTJ 74050003
         EJECT                                                          74100003
*                                                                       74400003
*  THIS CODE WAS ADDED FOR THE ONLINE/OFFLINE/CONSOLE KEYWORDS   Y02669 74600003
*  BECAUSE SINCE THEY ARE ATTACHED TASKS, A CSCB IS CREATED FOR  Y02669 74800003
*  THEM WHOSE COMMAND BUFFER LENGTH IS ONLY 124 BYTES.           Y02669 75000003
*  IF THE COMMAND WAS ISSUED FROM A CONSOLE WHOSE CHARACTER SPAN Y02669 75200003
*  IS GREATED THAN 124 CHARACTERS, THE COMMAND WILL BE TRUNCATED Y02669 75400003
*  AND SUBSEQUENTLY, VARY PROCESSORS WILL THINK IT IS INVALID.   Y02669 75600003
*  TO PREVENT THIS EARLY, A SCAN IS DONE OF THE PARM LIST OF THE Y02669 75800003
*  BUFFER AND IF IT EXCEEDS 124 CHARACTERS, MSG 'IEE908I VARY    Y02669 76000003
*  CMD LENGTH EXCEEDS MAX.' WILL BE ISSUED IMMEDIATELY AND THE   Y02669 76200003
*  COMMAND WILL NOT BE PROCESSED.                                Y02669 76400003
*                                                                Y02669 76600003
EXIT     EQU   *                                                 Y01029 76700003
         LR    R3,R11     SAVE REG11 PTR SINCE IT WILL BE USED   Y02669 76800003
         CLC   C0(C6,R11),ONL     IS IT AN ONLINE COMMAND        Y02669 77000003
         BE    BUFFCHK            YES, GO CHECK BUFFER LENGTH    Y02669 77200003
         CLC   C0(C7,R11),OFL     IS IT AN OFFLINE COMMAND       Y02669 77400003
         BE    BUFFCHK            YES, GO CHECK BUFFER LENGTH    Y02669 77600003
         CLC   C0(C7,R11),CONS    IS IT A CONSOLE COMMAND        Y02669 77800003
         BE    BUFFCHK            YES, GO CHECK BUFFER LENGTH    Y02669 78000003
*  IT IS NOT THE ABOVE, THEREFORE CAN EXIT NORMALLY              Y02669 78200003
*                                                                Y02669 78400003
EXITOK   EQU   *                                                 Y02669 78600003
         LR    R11,R3             RESTORE REG11                  Y01029 78800003
         L     R15,C8(R11)             GET ADDRESS OF EXIT RT    Y01029 79000003
         BR    R15                     EXIT TO APPROPRIATE RT    Y01029 79200003
BUFFCHK  EQU   *                                                 Y02669 79600003
         L     R9,XAL             PTR TO LIST POSITION OF PARAM  Y02669 79800003
         LA    R9,C0(R9)                                         Y02669 80000003
         LA    R10,C1             INCREMENTER                    Y02669 80200003
         LR    R11,R7             OBTAIN END OF BUFFER POINTER          80400003
         BCTR  R11,C0             MINUS ONE                      Y02669 81000003
EOBLOOP  EQU   *                                                 Y02669 81200003
         CLI   C0(R9),BLANK  IS THE NEXT CHAR AN ENDING BLANK    Y02669 81400003
         BE    EXITCHK            YES GO COMPUT LENTH OF CMD     Y02669 81600003
         CLC   C0(C3,R9),SS1      NEXT 3 CHAR INDICATE A SS/1  @Z30LPTJ*81660003
                                   REQUEST?                    @Z30LPTJ 81680003
         BE    SS1YES             YES, BRANCH                  @Z30LPTJ 81700003
         CLC   C0(C3,R9),SS1COMMA NEXT 3 CHAR INDICATE A SS/1  @Z30LPTJ*81720003
                                   REQUEST?                    @Z30LPTJ 81740003
         BE    SS1YES             YES, BRANCH                  @Z30LPTJ 81760003
         BXLE  R9,R10,EOBLOOP     NO GO CHECK NEXT CHARACTER     Y02669 81800003
*                                                                Y02669 82000003
EXITCHK  EQU   *                                                 Y02669 82200003
         L     R10,XAL            POINTER TO BEGINNING           Y02669 82400003
         LA    R10,C0(R10)                                       Y02669 82600003
         SR    R9,R10             SIZE OF CMD IN REG9            Y02669 82800003
         LA    R10,D124           MAX SIZE                       Y02669 83000003
         CR    R9,R10             IS IT LONGER OR NOT?           Y02669 83200003
         BNH   EXITOK             NO, IT IS SAFE, GO PROCESS CMD Y02669 83600003
SIZEERR  EQU   *                                               @Z30LPTJ 83900003
         LA    R5,ERRX39          ERROR CODE                     Y02669 84200003
         EJECT                                                          84800003
*********************************************************************** 85000003
* EXIT ROUTINE-                                                       * 85200003
* THE CORRECT ERROR CODE IS PUT IN XAE IN THE XSA AND CONTROL   Y01029* 85400003
* IS PASSED TO IEE0503D                                         Y01029* 85600003
*********************************************************************** 85800003
ERREXIT  EQU   *                                                        86000003
         STC   R5,XAE              PUT ERROR CODE IN XAE                86200003
         L     R15,VCON05              GET ADDRESS OF IEE0503D   Y01029 86400003
         BR    R15                     BRANCH TO IEE0503D        Y01029 86600003
*                                                                       86800003
SS1YES   EQU   *                                               @Z30LPTJ 86810003
         CLC   C0(C7,R3),CONS     IS IT CONSOLE KEYWORD?       @Z30LPTJ 86814003
         BE    SS1ERROR           YES, ERROR                   @Z30LPTJ 86818003
         LA    R9,C2(R9)          INCREMENT COUNTER FOR SS/1   @Z30LPTJ 86822003
SS1LOOP  EQU   *                                               @Z30LPTJ 86826003
         CLI   C0(R9),BLANK       AN ENDING BLANK CHARACTER?   @Z30LPTJ 86830003
         BE    ENDLOOP            YES, COMPUTE COMMAND LENGTH  @Z30LPTJ 86834003
         BXLE  R9,R10,SS1LOOP     NO, CHECK NEXT CHARACTER     @Z30LPTJ 86838003
ENDLOOP  EQU   *                                               @Z30LPTJ 86842003
         L     R10,XAL            POINTER TO BEGINNING OF      @Z30LPTJ*86846003
                                   BUFFER                      @Z30LPTJ 86850003
         LA    R10,C0(R10)        CLEAR HIGH ORDER BYTE        @Z30LPTJ 86854003
         SR    R9,R10             COMPUTE SIZE OF BUFFER       @Z30LPTJ 86858003
         LA    R10,D126           MAXIMUM SIZE                 @Z30LPTJ 86862003
         CR    R9,R10             IS IT < OR = MAXIMUM SIZE?   @Z30LPTJ 86866003
         BH    SIZEERR            NO, ERROR EXIT               @Z30LPTJ 86870003
         L     R15,VCON94         GET SS/1 PREPROCESSOR MODULE @Z30LPTJ 86874003
         BR    R15                GO TO SS/1 SYSTEM            @Z30LPTJ 86878003
SS1ERROR EQU   *                                               @Z30LPTJ 86882003
         LA    R5,ERR12           INSERT ERROR CODE            @Z30LPTJ 86886003
         B     ERREXIT                                         @Z30LPTJ 86890003
*                                                                       86900003
VTAMEXT  EQU   *                                                 Y02674 87000003
         L     R15,VVTAM          GET VCON VTAM MOD              Y02674 87200003
         BR    R15                EXIT TO IT                     Y02674 87400003
CHANNEL  EQU   *                                                 Y02669 87600003
         MVI   XAN,CHCODE         CHANGE THE COMMAND CODE        Y02669 87800003
         B     EXIT08             GO TO 0803D EXIT               Y02669 88000003
CPU      EQU   *                                                 Y02669 88200003
         MVI   XAN,CPUCODE        CHANGE THE COMMAND CODE        Y02669 88400003
         B     EXIT08             GO TO 0803D EXIT               Y02669 88600003
STORAGE  EQU   *                                                 Y02669 88800003
         MVI   XAN,STORCODE       CHANGE THE COMMAND CODE        Y02669 89000003
         B     EXIT08             GO TO 0803D EXIT               Y02669 89200003
PATH     EQU   *                                                 Y02669 89400003
         MVI   XAN,PATHCODE       CHANGE THE COMMAND CODE        Y02669 89600003
         B     EXIT08                GO TO 0803D EXIT          @Z309PTJ 89650003
CN       EQU   *                                               @Z309PTJ 89700003
         MVI   XAN,CNCODE            CHANGE THE COMMAND CODE   @Z309PTJ 89750003
EXIT08   EQU   *                                                 Y02669 89800003
         L     R15,VCON08         GET ADDRESS OF 0803D           Y02669 90000003
         BR    R15                EXIT TO IEE0803D               Y02669 90200003
*                                                                Y02669 90400003
         EJECT                                                          90600003
DOCLC    CLC   C0(C0,R11),C0(R6)   DOES CURRENT TABLE ENTRY EQU KEYWD   90800003
         DS    0F                  ALIGN ON FULL WORD BOUNDARY          91000003
VCON05   DC    V(IEE0503D)         ERROR MSG MODULE              Y01029 91200003
VVTAM    DC    V(ISTCFF3D)                                       Y02674 91400003
VCON08   DC    V(IEE0803D)                                       Y02669 91600003
VCON94   DC    V(IEE9403D)         SS/1 PREPROCESSOR MODULE    @Z30LPTJ 91700003
ONLCOMA  DC    CL7'ONLINE,'                                    @ZM30466 91800003
OFLCOMA  DC    CL8'OFFLINE,'                                   @ZM30466 91900003
CONSCOMA DC    CL8'CONSOLE,'                                   @ZM30466 92000003
MSTCCOMA DC    CL8'MSTCONS,'                                   @ZM30466 92100003
         DS    0F                  ALIGN ON FULL WORD BOUNDARY          92200003
*********************************************************************** 92400003
KEYTAB   EQU   *                   START OF VALID KEYWORD TABLE         92600003
*                                  ****KEYWORD FOLLOWED BY ITS****      92800003
*                                  ****    MODULE ADDR    ****   Y01029 93000003
*                                                              @ZM30466 93020003
*             ***********************************************  @ZM30466 93040003
*             *  ADDITIONAL TABLE ENTRIES MAY BE INSERTED   *  @ZM30466 93060003
*             *  INTO THIS TABLE AS LONG AS THEY CONFORM    *  @ZM30466 93080003
*             *  TO THE PRESENT TABLE STRUCTURE.            *  @ZM30466 93100003
*             ***********************************************  @ZM30466 93120003
*                                                              @ZM30466 93140003
ONL      DC    CL8'ONLINE'                                     @ZM30466 93200003
         DC    V(IEE0803D)                 ..                    Y02669 93600003
OFL      DC    CL8'OFFLINE'                ..                  @ZM30466 93800003
         DC    V(IEE0803D)                 ..                    Y02669 94000003
CONS     DC    CL8'CONSOLE'                ..                  @ZM30466 94200003
         DC    V(IEE0803D)                 ..                    Y02669 94400003
HARDC    DC    CL8'HARDCPY'                ..                  @ZM30466 94600003
         DC    V(IEE4703D)                 ..                    Y01029 94800003
MSTC     DC    CL8'MSTCONS'                ..                  @ZM30466 95000003
         DC    V(IEE4303D)                 ..                    Y01029 95200003
ONT      DC    CL8'ONTP'                   ..                  @ZM30466 95400003
         DC    V(IED1303D)                 ..                    Y01029 95600003
OFFT     DC    CL8'OFFTP'                  ..                  @ZM30466 95800003
         DC    V(IED1303D)                 ..                    Y01029 96000003
TABEND   EQU   *-LENG                                                   96200003
*                                                                       96400003
CNET     DC    C'NET,'                                         @ZA07514 96600003
CCH      DC    C'CH('                                          @ZA07514 96800003
CCPU     DC    C'CPU('                                         @ZA07514 96850003
CSTOR    DC    C'STOR('                                        @ZA07514 96900003
CPATH    DC    C'PATH('                                        @ZA07514 96950003
CCN      DC    C'CN('                                          @ZA07514 97000003
SS1      DC    C',S '                                          @Z30LPTJ 97480003
SS1COMMA DC    C',S,'                                          @Z30LPTJ 97510003
PATCH    DC    CL50'*** IEE3202D PATCH AREA ***'               @Z30LPTJ 97540003
*********************************************************************** 97600003
         EJECT                                                          97700003
SETXSA   DSECT                                                          97800003
         IEEXSA                                                         98000003
         END                                                            98200003
