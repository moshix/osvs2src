          MACRO                                                         00050002
&NAME     IEEBUFSC                                                      00060002
.*******************************************************************    00100002
.*                                                                      00150002
.*     ROUTINE NAME        IEEBUFSC                                     00200002
.*                                                                      00250002
.*     FUNCTION            TO SCAN A BUFFER FOR A GIVEN KEYWORD OF      00300002
.*                         A GIVEN LENGTH AND TO RETURN A POINTER       00350002
.*                         TO THE KEYWORD VALUE AND THE LENGTH OF       00400002
.*                         THE VALUE                                    00450002
.*                                                                      00500002
.*     ENTRY POINT         IEEBUFSC                                     00550002
.*                                                                      00600002
.*     ATTRIBUTES          REFRESHABLE                                  00650002
.*                                                                      00700002
.*     OPERATION           1. SET UP PTR TO BUFFER -1 AND PTR           00750002
.*                            TO KEYWORD LENGTH                         00800002
.*                         2. IF BUFFER PTR = SPECIFIED KEYWORD         00850002
.*                            GO TO FOUND ROUTINE (3).                  00900002
.*                            A. IF NOT, INCREMENT BUFFER PTR           00950002
.*                               BY ONE.                                01000002
.*                            B. IF PTR IS AT END OF BUFFER SET         01050002
.*                               UP NOT FOUND RETURN CODE (8) IN        01100002
.*                               R15 AND EXIT.                          01150002
.*                            C. IF PTR IS AT A COMMA, GO BACK TO       01200002
.*                               OPERATION 2.                           01250002
.*                            D. IF PTR IS AT A BLANK SET UP NOT        01300002
.*                               FOUND RETURN CODE (8) IN R15 AND       01350002
.*                               EXIT.                                  01400002
.*                            E. OTHERWISE GO TO OPERATION 2.A.         01450002
.*                         3. AT FOUND ROUTINE SET UP R14 TO POINT      01500002
.*                            TO KEYWORD VALUE                          01550002
.*                         4. SCAN KEYWORD VALUE TO THE END OF          01600002
.*                            THE BUFFER, A COMMA OR A BLANK            01650002
.*                         5. CALCULATE LENGTH OF KEYWORD VALUE.        01700002
.*                         6. IF LENGTH IS < OR = ZERO, SET UP          01750002
.*                            AN INVALID KEYWORD VALUE LENGTH           01800002
.*                            RETURN CODE (4) IN R15 AND EXIT.          01850002
.*                         7. OTHERWISE, SET GOOD RETURN CODE           01900002
.*                            OF ZERO IN R15 AND EXIT.                  01950002
.*                                                                      02000002
.*     EXTERNAL REFS       NONE                                         02050002
.*                                                                      02100002
.*     SVCS                NONE                                         02150002
.*                                                                      02200002
.*     INPUT               R1 - PTR TO BEGINNING OF BUFFER              02250002
.*                         R0 - PTR TO LAST BYTE OF BUFFER + 1          02300002
.*                         R15- PTR TO KEYWORD WITH THE LENGTH          02350002
.*                              OF THE KEYWORD IN THE HIGH ORDER        02400002
.*                              BYTE.                                   02450002
.*                              EG. KEYWORD FORMAT 'DSNAME='            02500002
.*                                  R15 WOULD POINT TO THE 'D'          02550002
.*                                  WITH A 6 IN THE HIGH ORDER          02600002
.*                                  BYTE.                               02650002
.*                                                                      02700002
.*     OUTPUT              R14  PTR TO FIRST BYTE OF KEYWORD            02750002
.*                              VALUE IN BUFFER OR WORKAREA             02800002
.*                         R1   LENGTH OF KEYWORD VALUE                 02850002
.*                         R15  RETURN CODE OF ZERO.                    02900002
.*                                                                      02950002
.*     ERROR OUTPUT        R15  CONTAINS A FOUR IF KEYWORD              03000002
.*                              HAD AN INVALID LENGTH                   03050002
.*                         R15  CONTAINS AN EIGHT IF THE KEYWORD        03100002
.*                              WAS NOT FOUND IN THE BUFFER             03150002
.*                                                                      03200002
.*     EXIT                TO NEXT SEQUENTIAL INSTRUCTION AFTER         03250002
.*                         MACRO CALL                                   03300002
.*                                                                      03350002
.*     NOTES               LISTS OF OPERANDS IN PARENTHESES ARE         03360002
.*                         NOT PROCESSED; THIS MACRO IS INTENDED        03370002
.*                         TO PROCESS ONLY A SINGLE VALUE  FOR          03380002
.*                         A GIVEN KEYWORD                              03390002
.*******************************************************************    03400002
          BCTR  1,0                DEC. ADDRESS TO BEG. OF BUFFER       03500002
          SR    14,14              CLEAR LENGTH REGISTER                03550002
          SLDL  14,8               KEYWORD LENGTH TO LOW ORDER          03600002
          LTR   14,14              IS LENGTH ZERO?                      03650002
          BZ    NG&SYSNDX          YES, GO SET BAD R.C.                 03700002
          SRL   15,8               ADDRESS BACK TO LOW ORDER 3 BYTES    03750002
AB&SYSNDX EQU   *                                                       03800002
          EX    14,CO&SYSNDX       KEYWORD FOUND IN BUFFER?             03850002
          BE    FD&SYSNDX          YES, GO TO FOUND ROUTINE             03900002
CD&SYSNDX EQU   *                                                       03950002
          LA    1,1(1)             INCREMENT BUFFER POINTER             04000002
          CR    1,0                IS END OF BUFFER REACHED?            04050002
          BE    NF&SYSNDX          YES, KEYWORD NOT PRESENT             04100002
          CLI   0(1),MA&SYSNDX     IS IT A COMMA?                       04150002
          BE    AB&SYSNDX          YES,CHECK FOR KEYWORD AGAIN          04200002
          CLI   0(1),LK&SYSNDX     IS IT A BLANK?                       04250002
          BE    NF&SYSNDX          YES, GO SET BAD R.C.                 04300002
          B     CD&SYSNDX          NO, RETURN TO SEARCH                 04350002
FD&SYSNDX EQU   *                                                       04400002
          AR    1,14               ADD LENGTH TO KEYWORD PTR.           04450002
          LA    14,2(1)            SET PTR TO KEYWORD VALUE             04500002
SC&SYSNDX EQU   *                                                       04550002
          LA    1,1(1)             INCREMENT PTR TO = SIGN              04600002
          CR    1,0                IS END OF BUFFER REACHED?            04650002
          BE    LG&SYSNDX          YES,GO CALCULATE VALUE LENGTH        04700002
          CLI   0(1),MA&SYSNDX     NO, IS IT A COMMA?                   04750002
          BE    LG&SYSNDX          YES, CALCULATE VALUE LENGTH          04800002
          CLI   0(1),LK&SYSNDX     NO, IS IT A BLANK?                   04850002
          BE    LG&SYSNDX          YES, CALCULATE VALUE LENGTH          04900002
          B     SC&SYSNDX          NO RETURN TO SCAN                    04950002
LG&SYSNDX EQU   *                                                       05000002
          SR    1,14               SUBT. BEG. OF KEYWORD FROM END       05050002
          BC    12,NG&SYSNDX       IF < OR = ZERO, NOT FOUND            05100002
          SR    15,15              CLEAR RETURN CODE REG.               05150002
          B     OT&SYSNDX          LENGTH SET UP IN REG ZERO            05200002
NF&SYSNDX EQU   *                                                       05250002
          LA    15,8               SET NOT FOUND RETURN CODE            05300002
          B     OT&SYSNDX          RETURN TO END                        05350002
NG&SYSNDX EQU   *                                                       05400002
          LA    15,4               SET BAD LENGTH CODE                  05450002
          B     OT&SYSNDX          RETURN TO END                        05500002
CO&SYSNDX CLC   1(0,1),0(15)       INPUT KEYWORD = BUFFER WORD?         05550002
MA&SYSNDX EQU   C','               COMMA FOR COMPARES                   05600002
LK&SYSNDX EQU   C' '               BLANK FOR COMPARES                   05650002
OT&SYSNDX EQU   *                                                       05700002
          MEND                                                          05750002
