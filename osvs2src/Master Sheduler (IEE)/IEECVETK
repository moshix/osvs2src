IEECVETK CSECT                                                          00050002
         TITLE 'IEECVETK IGC5K07B TIMER-INTERPRETER'                    00150002
*********************************************************************** 00200002
*                                                                       00250002
*  STATUS    CHANGE LEVEL 1 VS2 RELEASE 2                               00300023
*                                                                       00310023
*   APARS FIXED ZA02835                                                 00320023
*                                                                       00350002
*  FUNCTION    THE PURPOSE OF THIS ROUTINE IS TO RESOLVE THE DIFFERENT  00400002
*            TIME INTERVALS AT WHICH ROLL MAY HAVE BEEN CHOSEN FOR THE  00450002
*            DIFFERENT DISPLAY CONSOLES.  THIS ROUTINE THUS GAINS       00500002
*            CONTROL FROM THE OPTIONS ROUTINE TO DETERMINE THE PROPER   00550002
*            TIME INTERVAL.  THIS ROUTINE ALSO GAINS CONTROL WHENEVER   00600002
*            THE TIMER HAS ELAPSED, TO FIND OUT AND INDICATE TO THE     00650002
*            PROCESSOR WHICH, IF ANY, OF THE DISPLAYS ARE READY TO BE   00700002
*            ROLLED.                                                    00750002
*                                                                       00800002
*  ENTRY POINT    CONTROL IS PASSED ONLY THROUGH THE CSECT LABEL,       00850002
*               IEECVETK.  THE FIRST EXECUTABLE INSTRUCTION IS LABELED  00900002
*               START.                                                  00950002
*                                                                       01000002
*  INPUT    THE VALUES IN TWO HALF-WORDS IN EACH DCM.                   01050002
*                                                                       01100002
*  OUTPUT    THE ABOVE VALUES, PLUS AN ADDITIONAL ONE IN THE BASE DCM.  01150002
*                                                                       01200002
* EXTERNAL REFERENCES NONE                                              01250002
*                                                                       01300002
*  EXITS - NORMAL  IEECVETH OR P OR R DEVICE-DEPENDENT IO               01350002
*                  IEECVET1 PROCESSOR ROUTINE                           01400002
*                  IEECVET2 DISPLAY 1                                   01450002
*                  IEECVETD MESSAGE 1                                   01500002
*                  IEECVETE MESSAGE 2                                   01550002
*                  IEECVETG OPEN-CLOSE                                  01600002
*                  THE PARTICULAR EXIT THIS ROUTINE MAKES, GENERALLY    01650002
*                  DEPENDS UPON BITS PREVIOUSLY SET BY OTHER ROUTINES.  01700002
*       - ERROR    NONE                                                 01750002
*                                                                       01800002
* TABLES/WORK AREAS DISPLAY CONTROL MODULE (CONTAINED IN DSECT)         01850002
*                                                                       01900002
* ATTRIBUTES   REFRESHABLE, PRIVILEGED, TYPE 4 SVC                      01950002
*                                                                       02000002
* NOTES        THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A      02050002
*              PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL       02100002
*              CHARACTER SET.                                           02150002
*                                                                       02200002
*********************************************************************** 02250002
         EJECT                                                          02300002
*********************************************************************** 02350002
*        REGISTER EQUATES                                             * 02400002
*********************************************************************** 02450002
XFBRANCH EQU   15                  BRANCH EXIT ADDRESS                  02500002
XERTRN   EQU   14                  BALR RETURN ADDRESS                  02550002
XFQUOTEN EQU   15                  ODD WORK REG FOR DIVIDEND            02600002
XEREMAIN EQU   14                  EVEN WORK REG FOR DIVIDEND           02650002
XDBDCM   EQU   13                  BASE DCM BASE REGISTER               02700002
XCPBASE  EQU   12                  PROGRAM BASE REGISTER                02750002
XBTDCM   EQU   11                  PAGEABLE DCM BASE REGISTER           02800002
XARDCM   EQU   10                  RESIDENT DCM BASE REGISTER           02850002
X9UCM    EQU   9                   UCM BASE REGISTER                    02900002
X8UCME   EQU   8                   UCM ENTRY BASE                       02950002
X7UCMLST EQU   7                   LAST UCM ENTRY FOR BXLE              03000002
X6UCMESZ EQU   6                   UCM ENTRY LENGTH FOR BXLE            03050002
X5ZERO   EQU   0                   FOR SETTING FIELDS TO ZERO           03100002
X4WORK   EQU   4                   TEMPORARY WORK REGISTER              03150002
X3LGCD   EQU   3                   LATEST G.C.D                         03200002
X2NGCD   EQU   2                   NEW G.C.D                            03250002
X1PARM   EQU   1                   CXSA ADDRESS                         03300002
         EJECT                                                          03550002
*********************************************************************** 03600002
*        PROGRAM INITIALIZATION                                       * 03650002
*********************************************************************** 03700002
         BALR  XCPBASE,N0          LOAD PROGRAM BASE REGISTER           03750002
         USING *,XCPBASE           ESTABLISH PROG ADDRESSABILITY        03800002
         USING UCMLIST,X8UCME      ESTABLISH UCMENTRY ADDRESSABILITY    03850002
         USING CXSA,X1PARM         ESTABLISH CXSA ADDRESSABILITY        03900002
         USING UCM,X9UCM           ESTABLISH UCM ADDRESSABILITY         03950002
         USING DCMTSRT,XARDCM      ESTABLISH R DCM ADDRESSABILITY       04000002
         USING DCMSTRT,XBTDCM      ESTABLISH DCM ADDRESS                04050002
         B     BEGIN               BYPASS PATCH AREA        JE @ZA02835 04060023
ICATCH   DC    CL8'IEECVETK'       MODULE NAME              JE @ZA02835 04070023
         DC    CL8'&SYSDATE'       DATE LAST ASSEMBLY       JE @ZA02835 04080023
         DC    XL2'5056'           JULIAN DATE LAST CHANGE  JE @ZA02835 04090023
         DC    S(*)                                         JE @ZA02835 04092023
         DC    CL52'ZAPS'          PATCH AREA               JE @ZA02835 04094023
BEGIN    DS    0H                                           JE @ZA02835 04096023
         L     X8UCME,CSAUCM       UCM ENTRY BASE                       04100002
         L     X9UCM,CSACTLM       ADDRESS UCM BASE                     04110002
         L     XARDCM,UCMXB        RESIDENT DCM BASE                    04150002
         L     XBTDCM,DCMADTRN     PAGEABLE DCM BASE                    04200002
         L     XDBDCM,DCMSUBAD     ADDRESS IEECSUB                      04250002
         USING SUB,XDBDCM          TEMPORARY BASE FOR SUB CTL BLOCK     04300002
         L     XDBDCM,SUBPBASE     ADDRESS BASE DCM                     04350002
         USING DCMSTMR,XDBDCM      ESTABLISH BASE DCM ADDRESSABILITY    04400002
         MVC   DCMTRACE(DCMTRLEN),DCMTRAC2 SHIFT OLD TRACE ENTRIES      04500002
         MVI   DCMTREN1,ID1        PUT CSECT ID INTO                    04550002
         MVI   DCMTREN2,ID2        NEW TRACE ENTRY                      04600002
         L     XFBRANCH,CSAXC      ADDRESS FREELOCK SUBROUTINE          04610002
         BALR  XERTRN,XFBRANCH     RELEASE LOCKS                        04630002
         SR    X5ZERO,X5ZERO       REG OF ZEROS                         04650002
         LH    X2NGCD,DCMIVGCD     NEW GCD EQUALS OLD GCD               04700002
         TM    DCMTIMES,DCMOPTTI   ENTRY FROM OPTIONS ROUTINE           04850002
         BZ    ROUND               NO, DO NOT COMPUTE NEW GCD           04900002
         OI    DCMIND,DCMENTOP     IND THAT ENTRY FROM OPTIONS RTN      04950002
         TM    DCMTIMES,DCMOCTTI   IS DEVICE TO BE CLOSED               05000002
         BZ    NEW                 NO  BRANCH                           05050002
         MVI   DCMDEL,CHARY        CANCEL ROLL MODE                     05100002
         MVI   CSACODE,CSACLOSE    RESET CLOSE CODE                     05150002
NEW      SR    X3LGCD,X3LGCD       ZERO OUT LGCD REGISTER               05200002
         CLI   DCMDEL,CHARR        IS MODE NOW R OR RD                  05250002
         BE    IN                  YES, GO MOVE IN INTERVAL             05300002
         ST    X5ZERO,DCMINTVL     ZERO OUT COUNTERS                    05350002
         B     ROUND               BRANCH                               05400002
IN       MVC   DCMINTVL(N2),DCMRTME     MOVE IN INTVL OF THIS DCM       05450002
         EJECT                                                          05500002
*********************************************************************** 05550002
*        LOOP FOR SEARCHING OF DCMS                                   * 05600002
*********************************************************************** 05650002
ROUND    L     X8UCME,UCMVEA       ADDRESS FIRST UCM ENTRY              05700002
         LM    X6UCMESZ,X7UCMLST,UCMVEZ INCREMENT AND LIMIT BXLE REGS   05710002
         ST    XARDCM,DCMSVDCM     SAVE CURRENT DCM POINTER             05750002
LOOP1    TM    UCMATR,UCMUF        IS DEVICE ACTIVE                     05850002
         BZ    BX1                 NO, GET NEXT                         05900002
         TM    UCMDISP,UCMDISPC    IS THIS A GRAPHICS CONSOLE           05950002
         BZ    BX1                 NO GET NEXT ONE                      06000002
         L     XARDCM,UCMXB        GET RDCM ADDRESS                     06050023
         L     XBTDCM,DCMADTRN     GET TDCM ADDRESS         JE @ZA02835 06100023
         CLI   DCMDEL,CHARR        CONSOLE IN ROLL MODE     JE @ZA02835 06110023
         BNE   BX1                 NO, DON'T BOTHER WITH IT JE @ZA02835 06120023
         CH    X5ZERO,DCMINTVL    IS INTERVAL ZERO                      06150002
         BE    BX1                 YES, GET NEXT                        06200002
         TM    DCMIND,DCMSWCH      FIRST PASS THRU BXLE LOOP            06250002
         BO    SET                 NO, IGNORE NEXT TEST                 06300002
         TM    DCMIND,DCMENTOP     ENTRY FROM OPTIONS ROUTINE           06350002
         BZ    SET                 NO, BRANCH                           06400002
         B     MATH                YES, BRANCH                          06450002
BX1      BXLE  X8UCME,X6UCMESZ,LOOP1   GO GET NEXT ONE                  06500002
         L     XARDCM,DCMSVDCM     RESTORE CURRENT R DCM POINTER        06550002
         L     XBTDCM,DCMADTRN     RESTORE CURRENT T DCM POINTER        06600002
         TM    DCMIND,DCMSWCH      FIRST PASS THRU BXLE LOOP            06650002
         BO    NOTOP               NO, IGNORE NEXT TEST                 06700002
         TM    DCMIND,DCMENTOP     ENTRY FROM OPTIONS ROUTINE           06750002
         BZ    NOTOP               NO, BRANCH                           06800002
         OI    DCMIND,DCMSWCH      SET ON FIRST PASS FLAG               06850002
         EJECT                                                          06900002
*********************************************************************** 06950002
*        PROCESSING AFTER FIRST EXIT FROM LOOP                        * 07000002
*********************************************************************** 07050002
OPTIONS  LR    X2NGCD,X3LGCD       SAVE NEW GCD                         07100002
         TM    DCMTIMES,DCMTIMER   TIMER ELAPSED BIT ON                 07150002
         BO    ROUND               YES, BRANCH                          07200002
         TM    DCMIND,DCMTMRST     STIMER RUNNING BIT ON                07250002
         BZ    FULL                NO, LOOK FOR FULL SCREENS            07300002
         L     X4WORK,UCMPXA       POINT TO COMM TASK TCB     MH Y02137 07350002
         L     X4WORK,N120(X4WORK) GET TQE POINTER                      07550002
         LTR   X4WORK,X4WORK       VALID ADDRESS              MH Y02137 07560002
         BZ    FULL                 NO, RESTART TIMER         MH Y02137 07570002
         TM    N0(X4WORK),ONQUE    IS ELEMENT ON QUEUE                  07600002
         BO    FULL                NO, STIMER NOT ACTIVE                07650002
         STH   X2NGCD,DCMNEXTT     STORE GCD FOR NEXT STIMER            07700002
         OI    DCMINTI,DCMTMRCH    INDICATE INTERVAL CHANGE             07750002
         B     EXIT                DEPART                               07800002
FULL     STH   X5ZERO,DCMIVGCD     SET GCD TO ZERO                      07850002
         B     ROUND               SEE IF ANY SCREEN IS FULL            07900002
*                                                                       07950002
*        OPTIONS ENTRY PROCESSING                                       08000002
MATH     LH    XFQUOTEN,DCMINTVL   EXAMINE THIS DCMS INTERVAL           08050002
         LTR   X3LGCD,X3LGCD       IS THIS THE FIRST SO FAR             08100002
         BNZ   COMP                NO, COMPARE LAST 2 FOUND             08150002
         ST    X8UCME,DCMUAD       THIS IS ONLY ONE SO FAR              08200002
         LR    X3LGCD,XFQUOTEN     ITS INTERVAL IS THE LATEST GCD       08250002
         B     BX1                 LOOK FOR ANOTHER                     08300002
COMP     CR    X3LGCD,XFQUOTEN     LATEST EQUAL THIS INTERVAL           08350002
         BE    BX1                 YES, NO CHANGE POSSIBLE              08400002
         BL    DIV                 LATEST IS SMALLER, READY TO GO       08450002
         ST    X8UCME,DCMUAD       SMALLEST INTERVAL SO FAR             08500002
         LR    XEREMAIN,XFQUOTEN   START REG SWAP                       08510002
DIVAGAIN LR    XFQUOTEN,X3LGCD     LARGER NUMBER IS DIVIDEND            08550002
         LR    X3LGCD,XEREMAIN     SMALLER IS DIVISOR                   08600002
DIV      SR    XEREMAIN,XEREMAIN   CLEAR EVEN REGISTER                  08650002
         DR    XEREMAIN,X3LGCD     DIVIDE TO GET REMAINDER              08700002
         LTR   XEREMAIN,XEREMAIN   ANY REMAINDER                        08750002
         BZ    BX1                 NO, LAST REMAINDER IS LGCD           08800002
         B     DIVAGAIN            YES, SEE IF IT IS THE LAST           08850002
         EJECT                                                          09500002
*********************************************************************** 09550002
*        PROCESSING AFTER SECOND EXIT FROM LOOP                       * 09600002
*********************************************************************** 09650002
NOTOP    TM    DCMINTI,DCMTMRCH    HAS INTERVAL CHANGED                 09700002
         BZ    STME                NO, BRANCH                           09750002
         LH    X2NGCD,DCMNEXTT     PICK UP NEW TIME                     09800002
         NI    DCMINTI,N255-DCMTMRCH    SET OFF INDICATOR               09850002
STME     STH   X2NGCD,DCMIVGCD     STORE NEW GCD                        09900002
         MH    X2NGCD,HWC          CONVERT TO UNITS OF 0.01 SECONDS     09950002
         ST    X2NGCD,DCMSTMR      STORE FOR STIMER MACRO               10000002
         TM    DCMINTI,DCM1FULL    ARE ANY SCREENS FULL                 10050002
         BZ    EXIT                NO, DEPART                           10100002
         TM    DCMINTI,DCMDYRL     HAS AN STIMER BIT BEEN SET           10150002
         BO    EXIT                YES, DEPART                          10200002
         OI    DCMIND,DCMTMRST     SET ON STIMER RUNNING BIT            10250002
         STIMER  REAL,DCMSTART,BINTVL=DCMSTMR                           10350002
         B     EXIT                DEPART                               10400002
         SPACE 2                                                        10450002
*                                                                       10500002
*        PROCESSING SECOND SCANNING OF LOOP                             10550002
*                                                                       10600002
SET      TM    DCMR2FLG,DCMRXSFL   IS SCREEN FULL                       10650002
         BO    UPD                 YES, UPDATE COUNTER                  10700002
         CLI   DCMTOPDS,N0         IS DISPLAY ON SCREEN                 10750002
         BE    ZAP                 NO, ZERO OUT COUNTER                 10800002
         TM    DCMR2FLG,DCMRXUNV   IS UNVIEWABLE MSG DISPLAYED          10850002
         BO    UPD                 YES, UPDATE COUNTER                  10900002
ZAP      STH   X5ZERO,DCMTMCTR     ZERO OUT TIME COUNTER                10950002
         B     BX1                 RETURN TO LOOP                       11000002
UPD      OI    DCMINTI,DCM1FULL    SET ON ONE FULL SCREEN BIT           11050002
         LH    X4WORK,DCMIVGCD     GET CURRENT GCD                      11100002
         AH    X4WORK,DCMTMCTR     ADD IT TO TIME COUNTER               11150002
         CH    X4WORK,DCMINTVL     DOES TOTAL EQUAL INTERVAL            11200002
         BNL   GO                  YES, TRY TO POST                     11250002
         STH   X4WORK,DCMTMCTR     STORE UPDATED COUNTER VALUE          11300002
         B     BX1                 RETURN TO LOOP                       11350002
GO       TM    DCMINTI,DCMDYRL     STIMER BIT ON IN ANY DCM YET         11400002
         BO    ZER                 YES, CONTINUE                        11450002
         OI    DCMINTI,DCMDYRL     SET ON ONE READY TO ROLL             11500002
         OI    DCMR2FLG,DCMRXTMR   SET STIMER BIT ON IN DCM             11550002
ZER      OI    DCMR2FLG,DCMRXRLL   TURN ROLL BIT ON                     11600002
         STH   X5ZERO,DCMTMCTR     ZERO OUT TIME COUNTER                11700002
         C     XARDCM,DCMSVDCM     IS THIS DCM IN CONTROL               11750002
         BE    BX1                 YES, RETURN TO LOOP                  11800002
         TM    DCMR2FLG,DCMRXDEL   IS DELETION PENDING                  11850002
         BO    BX1                 YES, RETURN TO LOOP                  11900002
         TM    UCMSTS,UCMBF        BUSY BIT ON                          11950002
         BO    BX1                 YES, RETURN TO LOOP                  12000002
         CLI   UCMECB,X80          IS DEVICE WAITING                    12050002
         BE    POST                YES, POST IT                         12100002
         CLI   UCMECB,X00          IS DEVICE READY                      12150002
         BNE   BX1                 NO, RETURN TO LOOP                   12200002
POST     POST  UCMECB,255          POST UCME WITH CODE 255              12250002
         B     BX1                 RETURN TO LOOP                       12450002
         EJECT                                                          12500002
*********************************************************************** 14350002
*        DETERMINE WHERE TO GO AND TRANSFER CONTROL VIA BR 15         * 14400002
*********************************************************************** 14450002
EXIT     NI    DCMINTI,N255-DCM1FULL-DCMDYRL SET OFF FLAGS              14500002
         NI    DCMIND,N255-DCMSWCH-DCMENTOP  SET OFF FLAGS              14550002
         L     XFBRANCH,DCMNPROC   LOAD THE ADDRESS OF PROC 1           14560002
         TM    DCMTIMES,DCMOPTTI   ENTRY FROM OPTIONS ROUTINE           14600002
         BZ    XCTL                NO, EXIT TO IEECVET1                 14650002
         TM    DCMTIMES,DCMOCTTI   IS DEVICE TO BE CLOSED               14700002
         BO    SHUT                YES, ARRANGE FOR IT                  14750002
         TM    DCMOPTST,DCMOPRLL   IS ROLL MODE IN EFFECT               14800002
         BO    LABEL               YES, BRANCH                          14850002
         TM    DCMR2FLG,DCMRXSFL   IS SCREEN FULL                       14900002
         BZ    LABEL               NO, BRANCH                           14950002
         NI    DCMIOCM2,N255-DCMBLWRR   NOT WRITE RIGHT HALF WARNING    15000002
         OI    DCMCMSG1,DCMMSGWT   TURN ON MESSAGE WAITING BIT          15050002
LABEL    TM    DCMTIMES,DCMOTTMM   MSG MOD BIT SET                      15100002
         BZ    IO                  NO, EXIT TO I/O ROUTINE              15150002
         TM    DCMCMSG2,DCMASYRT+DCMASYCD    ASY ERROR CONDITION        15200002
         BM    MSG2                GO TO MSG MOD TWO                    15250002
         L     XFBRANCH,DCMNMSG1   LOAD MESSAGE 1 ADDRESS               15300002
         B     XCTL                EXIT TO IEECVETD                     15350002
MSG2     L     XFBRANCH,DCMNMSG2   LOAD MESSAGE 2 ADDRESS               15400002
         B     XCTL                EXIT TO IEECVETE                     15450002
SHUT     L     XFBRANCH,DCMNOPCL   LOAD OPEN/CLOSE ADDRESS              15500002
         B     XCTL                EXIT TO IEECVETG                     15550002
IO       L     XFBRANCH,DCMIORTN   LOAD IO ROUTINE ADDRESS              15600002
*                                  FOR EXIT TO IEECVET(H,P,R, OR U)     15610002
XCTL     NI    DCMTIMES,N255-DCMTIMER-DCMOPTTI-DCMOTTMM-DCMOCTTI-DCMRMT*15650002
               TI                  SET OFF ALL TIMER FLAGS              15700002
         NI    DCMR2FLG,N255-DCMRXTIM   SET OFF TIMER ELAPSED FLAG      15750002
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS                 15800002
         BR    XFBRANCH            EXIT ADDRESS ALREADY SET             15850002
         EJECT                                                          15950002
*********************************************************************** 16000002
*        PROGRAM EQUATES AND CONSTANTS                                * 16050002
*********************************************************************** 16100002
ID1      EQU   C'E'                1ST CHARACTER OF CSECT ID            16110002
ID2      EQU   C'K'                2ND CHARACTER OF CSECT ID            16120002
N0       EQU   0                   NUMBER                               16150002
N1       EQU   1                   NUMBER                               16200002
N2       EQU   2                   NUMBER                               16250002
N3       EQU   3                   NUMBER                               16300002
N4       EQU   4                   NUMBER                               16350002
N16      EQU   16                  CVT POINTER ADDRESS           S21003 16450002
N120     EQU   120                 DISPLACEMENT OF TCBTME        S21003 16500002
N255     EQU   255                 NUMBER                               16550002
X00      EQU   X'00'               NUMBER                               16600002
X80      EQU   X'80'               NUMBER                               16650002
BLANK    EQU   C' '                NUMBER                               16700002
CHARA    EQU   C'A'                NUMBER                               16750002
CHARR    EQU   C'R'                NUMBER                               16800002
CHARY    EQU   C'Y'                NUMBER                               16850002
HF0      EQU   X'F0'               NUMBER                               16900002
ONQUE    EQU   X'80'               TIMER ELEMENT NOT ON QUEUE    S21003 16950002
         SPACE 4                                                        17000002
HWC      DC    H'100'              VALUE                                17150002
*********************************************************************** 17550002
*        FLOW CHART STATEMENTS                                        * 17600002
*********************************************************************** 17650002
*/*IEECVETK: CHART */                                                   17700002
*/*%A: E (,%B) ENTRY POINT */                                           17750002
*/*%B: P (,%C) PROGRAM INITIALIZATION */                                17800002
*/*%C: D (YES,FINISH,NO,%D) ENTRY FROM ROLL ROUTINE */                  17850002
*/*%D: D (YES,%E,NO,ROUND) ENTRY FROM OPTIONS ROUTINE */                17900002
*/*%E: D (YES,%F,NO,NEW) IS DEVICE TO BE CLOSED */                      17950002
*/*%F: P (,NEW) CANCEL ROLL MODE */                                     18000002
*/*NEW: D (YES,IN,NO,%G) IS MODE NOW R OR RD */                         18050002
*/*%G: P (,ROUND) ZERO OUT COUNTERS */                                  18100002
*/*IN: P (,ROUND) MOVE IN INTERVAL OF THIS DCM */                       18150002
*/*ROUND: P (,LOOP1) INIT FOR SCAN */                                   18200002
*/*LOOP1: D (YES,%H,NO,BX1) IS DEVICE ACTIVE AND GRAPHIC */             18250002
*/*%H: D (YES,BX1,NO,%I) IS INTERVAL ZERO */                            18300002
*/*%I: D (YES,%J,NO,SET) FIRST PASS THRU LOOP */                        18350002
*/*%J: D (YES,MATH,NO,SET) ENTRY FROM OPTIONS ROUTINE */                18400002
*/*BX1: D (MORE,LOOP1,DONE,%K) EXECUTE BXLE */                          18450002
*/*%K: D (YES,%L,NO,NOTOP) FIRST PASS THRU LOOP */                      18500002
*/*%L: D (YES,OPTIONS,NO,NOTOP) ENTRY FROM OPTIONS ROUTINE */           18550002
*/*OPTIONS: D (YES,ROUND,NO,%M) TIMER ELAPSED FLAG ON */                18600002
*/*%M: D (YES,%N,NO,FULL) TIMER RUNNING FLAG ON */                      18650002
*/*%N: P (,EXIT) SAVE GCD FOR NEXT TIMER */                             18700002
*/*FULL: P (,ROUND) SET GCD TO ZERO */                                  18750002
*/*MATH: D (YES,%O,NO,COMP) THIS VALUE GT INTERVAL */                   18800002
*/*%O: P (,COMP) LOAD GREATER INTERVAL */                               18850002
*/*COMP: D (YES,BX1,NO,%P) LATEST GCD EQ INTERVAL */                    18900002
*/*%P: D (YES,%Q,NO,FIG) IS LATEST GCD ZERO */                          18950002
*/*%Q: P (,BX1) MOVE IN THIS INTERVAL */                                19000002
*/*FIG: P (,BX1) COMPUTE AND SAVE NEW GCD */                            19050002
*/*NOTOP: D (YES,%R,NO,STME) HAS INTERVAL CHANGED */                    19100002
*/*%R: P (,STME) PICK UP NEW TIME */                                    19150002
*/*STME: D (YES,%S,NO,EXIT) ARE ANY SCREENS FULL */                     19200002
*/*%S: D (YES,EXIT,NO,%T) HAS STIMER FLAG BEEN SET */                   19250002
*/*%T: P (,EXIT) ISSUE TIMER MACRO */                                   19300002
*/*SET: D (YES,UPD,NO,%U) IS SCREEN FULL */                             19350002
*/*%U: D (YES,%V,NO,ZAP) IS DISPLAY ON SCREEN */                        19400002
*/*%V: D (YES,UPD,NO,ZAP) IS UNVIEW. MSG. DISP. */                      19450002
*/*ZAP: P (,BX1) ZERO OUT TIME COUNTER */                               19500002
*/*UPD: D (YES,GO,NO,BX1) DOES TOTAL EQ INTERVAL */                     19550002
*/*GO: D (YES,%W,NO,NOTDCM) IS THIS DCM IN CORE */                      19600002
*/*%W: P (,NOTDCM) IND THAT DCM IS IN CORE */                           19650002
*/*NOTDCM: D (YES,NOTCORE,NO,%X) STIMER ON IN ANY DCM YET */            19700002
*/*%X: P (,NOTCORE) SET ON DCM TIMER FLAGS */                           19750002
*/*NOTCORE: D (YES,%Y,NO,BX1) ATTEMPT TO POST */                        19800002
*/*%Y: D (YES,POST,NO,BX1) IS POST POSSIBLE */                          19850002
*/*POST: P (,BX1) POST CONSOLE FOR ROLL */                              19900002
*/*FINISH: P (,ZIP) COMPUTE AND MOVE WQE COUNT TO DCM */                19950002
*/*ZIP: D (YES,IO,NO,%Z) WARNING MESSAGE REQUIRED */                    20000002
*/*%Z: P (,XCTL) SET OFF FULL SCREEN FLAG */                            20050002
*/*EXIT: D (YES,%AA,NO,XCTL) ENTRY FROM OPTIONS ROUTINE */              20100002
*/*%AA: D (YES,SHUT,NO,%BB) IS DEVICE TO BE CLOSED */                   20150002
*/*%BB: D (YES,LABEL,NO,%CC) IS ROLL MODE IN EFFECT */                  20200002
*/*%CC: D (YES,%DD,NO,LABEL) IS SCREEN FULL */                          20250002
*/*%DD: P (,LABEL) SET ON MESSAGE WAITING FLAG */                       20300002
*/*LABEL: D (YES,%EE,NO,IO) MSG MODULE FLAG SET */                      20350002
*/*%EE: D (YES,MSG2,NO,%FF) ASY. ERROR CONDITION */                     20400002
*/*%FF: P (,XCTL) INIT FOR MSG ONE EXIT */                              20450002
*/*MSG2: P (,XCTL) INIT FOR MSG TWO EXIT */                             20500002
*/*SHUT: P (,XCTL) INIT FOR OPEN/CLOSE EXIT */                          20550002
*/*IO: P (,XCTL) INIT FOR I/O MODULE EXIT */                            20600002
*/*XCTL: R XCTL TO PROPER DIDOCS MODULE */                              20650002
*/*IEECVETK: END */                                                     20700002
         EJECT                                                          20750002
*********************************************************************** 20800002
*        PROGRAM DSECTS                                               * 20850002
*********************************************************************** 20900002
         IHACTM  CXSA                                                   21000002
         EJECT                                                          21050002
         IHAPSA                                                         21060000
         EJECT                                                          21100000
         CVT    DSECT=YES                                               21102000
         EJECT                                                          21110000
         IEECUCM  FORMAT=NEW                                            21150002
         EJECT                                                          21160002
         IEERDCM                                                        21200002
         EJECT                                                          21300002
         IEETDCM                                                        21350002
         EJECT                                                          21450002
SUB      DSECT                                                          21500002
         IEECSUB  Y,Y,N,N,Y,Y                                           21550002
         END                                                            21600002
