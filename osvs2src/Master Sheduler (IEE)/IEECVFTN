IEECVFTN CSECT                                                          00050002
*D 069500-079500,101000,177500                                MB YM0664 00060002
*/*IEECVFTN: CHART */                                                   00100002
*    THIS ROUTINE WAS CREATED FOR STATUS DISPLAY SUPPORT, LINE ITEM     00150002
*    21002, OS RELEASE 21.                                              00200002
*/*HEADER                                                               00250002
*/*     CHART 6N  IEECVFTN - IGC6N07B - K D,H; K D,U; K D,F CONTROL     00300002
*/*COMMANDS FOR OUT-OF-LINE DISPLAYS */                                 00350002
         TITLE 'IEECVFTN STATUS DISPLAY INTERFACE 3'                    00400002
*********************************************************************** 00450002
*                                                                     * 00500002
* STATUS                                                              * 00550002
*    CHANGE LEVEL=0                                                   * 00600002
*                                                                     * 00650002
* FUNCTION                                                            * 00700002
*    TO HANDLE CONTROL (K) COMMANDS AFFECTING OUT-OF-LINE STATUS      * 00750002
*    DISPLAYS.   (K D,H K D,F K D,U)                                  * 00800002
*                                                                     * 00850002
* ENTRY POINT                                                         * 00900002
*    IEECVFTN (CSECT)                                                 * 00950002
*    IGC6N07B (LOAD MODULE NAME)                                      * 01000002
*                                                                     * 01050002
* INPUT                                                               * 01100002
*    REG 1 POINTS TO CXSA WHICH POINTS TO DCM  WHICH CONTAINS INDI-   * 01150002
*    CATORS.                                                          * 01200002
*    K COMMAND PARAMETER LIST POINTED TO IN THE DCM                   * 01250002
*    DCMADOPN POINTS TO OPERAND(S) IN PARAMETER LIST                  * 01300002
*                                                                     * 01350002
* OUTPUT                                                              * 01400002
*    ALTERATION OF THE CONTROL LINES OF OUT OF LINE DISPLAYS.         * 01450002
*    BIT SETTINGS IN DCM AND SACB FOR OTHER ROUTINES TO ANALYZE       * 01500002
*                                                                     * 01550002
* EXTERNAL REFERENCES                                                 * 01600002
*    SVC 34                                                           * 01650002
*    XCTL                                                             * 01700002
*                                                                     * 01750002
* EXITS,NORMAL                                                        * 01800002
*    I/O RTN    (DEVICE-DEPENDENT) TO OUTPUT A NEW CONTROL LINE       * 01850002
*                                                                     * 01900002
* EXITS,ERROR                                                         * 01950002
*    TO PROCCESOR 1 UPON INCORRECT COMMAND INPUT                      * 02000002
*                                                                     * 02050002
* ATTRIBUTES                                                          * 02100002
*    REFRESHABLE, PRIVILEGED, TYPE 4 SVC                              * 02150002
*                                                                     * 02200002
* TABLES/WORK AREAS                                                   * 02250002
*    UCM, RDCM, DCM, CXSA, WQE, SACB                                  * 02300002
*                                                                     * 02350002
*********************************************************************** 02400002
         EJECT                                                          02450002
*                                                                       02500002
*********************************************************************** 02550002
*    REGISTERS                                                          02600002
*                                                                       02650002
X1PARM   EQU   1                                                        02660002
XERTRN   EQU   14                                                       02662002
XFBRANCH EQU   15                                                       02670002
XF       EQU   15                  WORK REG                             02700002
X14      EQU   14                  RETURN REG                           02750002
RETURN   EQU   14                  RETURN REG                           02800002
XD       EQU   13                  WORK REG                             02850002
SACBPT   EQU   13                  SACB BASE REG                        02900002
XC       EQU   12                  THIS RTN'S BASE REG                  02950002
XB       EQU   11                  CXSA BASE REG                        03000002
XA       EQU   10                  UCM BASE REG                         03050002
X9       EQU   9                   TRANSIENT DCM BASE REG               03100002
XRDCM    EQU   8                   RESIDENT DCM BASE REG                03150002
LIST     EQU   7                   UCMLIST BASE REG                     03200002
X6       EQU   6                   WORK REG                             03250002
PARMREG  EQU   1                   PARAMETER LIST BASE REG              03300002
FLDADR   EQU   15                  MASKING FIELD POINTER                03350002
MAJWQER  EQU   4                   MAJOR WQE BASE REG                   03400002
X5       EQU   5                   WORK REG                             03450002
X4       EQU   4                   WORK REG                             03500002
X3       EQU   3                   WORK REG                             03550002
X2       EQU   2                   WORK REG                             03600002
X1       EQU   1                   WORK REG                             03650002
X0       EQU   0                   WORK REG                             03700002
R3       EQU   3                   WORK REG                             03750002
R4       EQU   4                   WORK REG                             03800002
R5       EQU   5                   WORK REG                             03850002
R6       EQU   6                   WORK REG                             03900002
********************************************************************    03950002
         EJECT                                                          04000002
********************************************************************    04050002
*     EQUATES                                                           04100002
********************************************************************    04150002
*                                                                       04200002
ID1      EQU   C'F'                1ST CHARACTER OF CSECT ID            04210002
ID2      EQU   C'N'                2ND CHARACTER OF CSECT ID            04220002
BLANK    EQU   X'40'                                                    04250002
********************************************************************    04300002
*              FIELD INCREMENTS                                         04350002
N0       EQU   0                                                        04400002
N1       EQU   1                                                        04450002
N2       EQU   2                                                        04500002
N3       EQU   3                                                        04550002
N4       EQU   4                                                        04600002
N5       EQU   5                                                        04650002
N6       EQU   6                                                        04700002
N7       EQU   7                                                        04750002
N8       EQU   8                                                        04800002
N9       EQU   9                                                        04850002
ND       EQU   13                  FIELD LENGTH                         04900002
N34      EQU   34                                                       04950002
N64      EQU   64                                                       05000002
N255     EQU   255                                                      05050002
CF0      EQU   X'F0'                                                    05100002
F0       EQU   X'F0'                                                    05150002
*********************************************************************** 05200002
         EJECT                                                          05250002
*/* E IEECVFTN */                                                       05300002
*                                                                       05350002
*    ESTABLISH ADDRESSABILITY                                           05400002
*                                                                       05450002
*/*%BASES: P (,%ANALYZ) ESTABLISH ADDRESSABILITY */                     05500002
         BALR  XC,N0               ESTABLISH ROUTINE'S                  05550002
         USING *,XC                BASE REGISTER                        05600002
         LR    XB,X1               ESTABLISH CXSA                       05650002
         USING CXSA,XB             ADDRESSABILITY                       05700002
         L     XA,CSACTLM          ESTABLISH UCM BASE                   05800002
         USING UCM,XA              ADDRESSABILITY                       05850002
         L     LIST,CSAUCM         LOAD UCM ENTRY ADDRESS               05900002
         USING UCMLIST,LIST        ESTABLISH UCME  ADDRESSABILITY       05950002
         L     XRDCM,UCMXB         GET POINTER TO RESIDENT DCM          06000002
         USING DCMTSRT,XRDCM          ESTABLISH RDCM BASE REG           06050002
         L     X9,N0(XRDCM)        LOAD TRANSIENT DCM PT                06100002
         USING DCMSTRT,X9         ESTABLISH TDCM BASE REG               06150002
         USING DCMACB,SACBPT       SACB BASE REG                        06200002
         USING KPARM,PARMREG       ESTABLISH PARAMETER BASE REG         06250002
         MVC   DCMTRACE(DCMTRLEN),DCMTRAC2 SHIFT OLD TRACE ENTRIES      06260002
         MVI   DCMTREN1,ID1        PUT CSECT ID INTO                    06270002
         MVI   DCMTREN2,ID2        NEW TRACE ENTRY                      06280002
         L     XFBRANCH,CSAXB      ADDRESS GETLOCK SUBROUTINE           06290002
         LR    X1PARM,XB           PASS CXSA ADDRESS IN REG 1           06292002
         BALR  XERTRN,XFBRANCH     OBTAIN LOCKS                         06294002
*                                                                       06300002
*********************************************************************** 06350002
         EJECT                                                          06400002
*********************************************************************** 06450002
*                                                                       06500002
*                                                                       06550002
*/*%ANALYZ: P (,%P01) GET POINTER TO OPERANDS */                        06600002
*    ANALYZE COMMAND IN PARAMETER LIST                                  06650002
         L     PARMREG,DCMADOPN    LOAD POINTER TO OPERAND(S)           06700002
         SH    PARMREG,FOUR        DECREMENT POINTER BY 4               06750002
*/*%P01: S (,%P03) SACBINIT: FIND SACB TO MATCH AREA ID IN PARAM LIST   06800002
*/**/                                                                   06850002
         BAL   X14,SACBINIT        GO FIND SACB TO MATCH AREA ID        06900002
*/*%P03: P (,%COMP) BASE CONTROL LINE DSECT */                          07100002
         USING DCONTROL,X5         BASE CONTROL LINE DSECT              07150002
*                                                                       07200002
*/*%COMP: D (NO,%COMP2,YES,KDFRTN) IS COMMAND K D,F */                  07250002
         CLC   KOPN(N4),KDF        NO--IS COMMAND K D,F                 07300002
         BE    KDFRTN              YES-GO SET FORWARD FRAME BITS        07350002
*/*%COMP2: D (NO,%COMP3,YES,BLDLINE) IS COMMAND K D,H */                07400002
         CLC   KOPN(N4),KDH        NO--IS COMMAND K D,H                 07450002
         BE    BLDLINE              YES, UPDATE CONTROL LINE  MB Y02958 07500002
*/*%COMP3: D (NO,ERROR,YES,BLDLINE) IS COMMAND K D,U */                 07550002
         CLC   KOPN(N4),KDU        NO--IS COMMAND K D,U                 07600002
         BE    BLDLINE              YES, UPDATE CONTROL LINE  MB Y02958 07650002
*    IF NONE OF ABOVE, IT IS AN ERROR THAT SHOULD NEVER HAPPEN.         07700002
*    GO BACK TO PROCESSOR 1 SO THAT IT CAN CONTINUE PROCESSING          07750002
*    WITHOUT CAUSING POSSIBLE SYSTEM FAILURE.                           07800002
         SPACE 2                                                        07850002
*/*ERROR: P (,XCTL) GET PROCESSOR1 ADDRESS */                 MB Y02958 07900002
ERROR    EQU   *                                                        07950002
         L     XFBRANCH,DCMNPROC   LOAD PROC 1 ADDRESS                  08000002
         B     XCTL                EXIT TO IEECVET1                     08050002
*                                                                       08100002
*********************************************************************** 08150002
         EJECT                                                          09350002
*/*KDFRTN: P (,FRAMEOFF) TURN OFF FRAME FULL BIT IN SACB */             09400002
KDFRTN   EQU   *                                                        09450002
*    ON A K D,F COMMAND THE FRAME FULL BITS IN THE MAJOR WQE FOR THE    09500002
*    DISPLAY AND IN THE SACB ARE TURNED OFF.                            09550002
*    THE OUT OF LINE DISPLAY PENDING BIT IS TURNED ON.                  09600002
*    THE SETTING OF THESE BITS CAUSES CONTROL TO LATER BE PASSED TO THE 09650002
*    STATUS DISPLAY INTERFACE 2 MODULE TO OUTPUT THE NEXT FRAME.        09700002
*    THE FRAME NUMBER IS THEN UPDATED AND THE CONTROL LINE IS REWRITTEN 09750002
         TM    DCMAFLG1,DCMAFULL   IS FRAME FULL BIT ON IN SACB         09800002
         BZ    FRAMEOFF            NO--IF ZERO                          09850002
         NI    DCMAFLG1,N255-DCMAFULL TURN OFF FRAME FULL BIT IN SACB   09900002
*                                  BIT                                  09950002
*/*FRAMEOFF: S (,%FRM01) MASKRTN: SET BIT IN WQE OFF */                 10000002
FRAMEOFF EQU   *                                                        10050002
         BAL   X14,MASKRTN         GO SET THE BIT IN WQE OFF            10100002
*/*%FRM01: P (,%FRM02) TURN ON OUT-OF-LINE DISP. PENDING BIT */         10200002
         OI    UCMSDS5,UCMSDS5C    TURN ON OUT-OF-LINE DISPLAY PENDING  10250002
*/*%FRM02: P (,BLDLINE) UPDATE FRAME NUMBER IN SACB */                  10300002
         IC    X2,DCMAFR+N1         LOAD FIRST DIGIT                    10350002
         SRDL  X2,N4               SHIFT OUT LOW NIBBLE                 10400002
         SR    X2,X2               CLEAR REG                            10450002
         SLDL  X2,N4               RESTORE LOW NIBBLE IN REG 2          10500002
         LA    X2,N1(X2)           BUMP NUMBER                          10550002
         C     X2,TEN              IS NUMBER = TO 10                    10600002
         BNL   NEXTBYTE            YES                                  10650002
         STC   X2,DCMAFR+N1        RESTORE NUMBER IN SACB               10700002
         OI    DCMAFR+N1,F0       RESTORE HIGH EBCDIC NIBBLE            10750002
         B     NORESET                                                  10800002
NEXTBYTE EQU   *                                                        10850002
         IC    X2,DCMAFR           LOAD SECOND DIGIT                    10900002
         SRDL  X2,N4               SHIFT OUT LOW NIBBLE                 10950002
         SR    X2,X2               CLEAR REG                            11000002
         SLDL  X2,N4               RESTORE LOW NIBBLE IN REG 2          11050002
         LA    X2,N1(X2)           BUMP NUMBER                          11100002
         C     X2,TEN              IS NUMBER = TO 10                    11150002
         BNL   ZEROIT              REACHED 99 VALUE                     11200002
         STC   X2,DCMAFR           RESTORE DIGIT                        11250002
         OI    DCMAFR,F0           RESTORE HIGH NIBBLE                  11300002
         B     RESETLOW                                                 11350002
ZEROIT   MVI   DCMAFR,F0           SET SECOND DIGIT TO ZERO             11400002
RESETLOW MVI   DCMAFR+N1,F0          SET FIRST DIGIT TO ZERO            11450002
NORESET  EQU   *                                                        11500002
         B     BLDLINE             GO BUILD LINE                        11550002
*                                                                       11600002
*********************************************************************** 11650002
         EJECT                                                          11700002
*/* E BLANKET */                                                        11750002
BLANKET  EQU   *                                                        11800002
*/*%BLNK01: P (,%BLNK02) BLANK ONE LINE                                 11850002
*/**/                                                                   11900002
* INPUT: REG X3 CONTAINS LENGTH TO BE BLANKED.                          11950002
* INPUT: REG X5 POINTS TO LINE OF DCM TO BE BLANKED.                    11960002
         MVI   N0(X5),BLANK        MOVE A BLANK CHAR                    12000002
         SH    X3,TWO              DECREMENT BY 2 (FOR MVI+EX)          12050002
         EX    X3,BLANKWRT         SPREAD BLANKS THROUGHOUT LINE        12100002
*/*%BLNK02: R RETURN TO CALLER */                                       12150002
         BR    XF                                                       12200002
SACBINIT EQU   *                                                        12250002
*/* E SACBINIT */                                                       12300002
*    SET UP SACB POINTERS                                               12350002
*/*%ACB1: P (,IDLOOP) LOAD SACB POINTER FROM RDCM */                    12400002
         L     SACBPT,DCMADSDS         LOAD SACB POINTER FROM RDCM      12450002
IDLOOP   EQU   *                                                        12500002
*/*IDLOOP: D (NO,ERROR,YES,%ACB2) VALID ADDRESS */                      12550002
         LTR   SACBPT,SACBPT       IS IT A VALID ADDR                   12600002
         BZ    ERROR               NO--GO BACK TO PROCESSOR 1           12650002
*/*%ACB2: D (YES,FOUNDID,NO,%ACB3) AREA ID'S MATCH */                   12700002
         CLC   KAID(N1),DCMAID    ARE AREA ID'S EQUAL                   12750002
         BE    FOUNDID            YES-                                  12800002
*/*%ACB3: P (,IDLOOP) LOAD NEXT SACB POINTER */                         12850002
         L     XD,N0(XD)          LOAD POINTER TO NEXT SACB             12900002
         B     IDLOOP                                                   12950002
FOUNDID  EQU   *                                                        13000002
*/*FOUNDID: R RETURN TO CALLER */                                       13050002
         BR    RETURN              RETURN TO CALLER                     13100002
*********************************************************************** 13150002
         EJECT                                                          13200002
BLDLINE  EQU   *                                                        13620002
*     THE FOLLOWING CODE BUILDS THE NEW CONTROL LINE IN THE DCM         13630002
*                                                                       13640002
*/*BLDLINE: P SET UP BYTE COUNT, LINE NO. FOR I/O */          MH  M2517 13642002
         MVC   DCMBYTCT(N2),DCMCORLN INDICATE WRITE 1 LINE    MH  M2517 13644002
         MVC   DCMLINEN(N1),DCMATOP INDIC. WRITE TOP LINE     MH  M2517 13646002
*/*%TESTSD: D (YES,%USESIB,NO,USEINSTR) CONSOLE USE = STATUS DISPLAY?   13648002
*/**/                                                                   13648402
         TM    UCMDISP,UCMDISPG    Q. STATUS DISPLAY CONSOLE  MF  M2057 13648802
         BNO   USEINSTR            NO, USE INSTRUCTION LINE   MF  M2057 13649202
* WRITE CONTROL LINE FROM SCREEN IMAGE BUFFER                 MF  M2057 13649602
*/*%USESIB: P (,IOPARM) INDICATE WRITE PARTIAL FROM SCREEN IMAGE BUFFER 13649702
*/**/                                                                   13649802
         SR    X5,X5               CLEAR REG                  MF  M2057 13649902
         IC    X5,DCMATOP          GET CONTROL LINE NUMBER    MF  M2057 13666602
         BCTR  X5,N0               SUBTRACT ONE               MF  M2057 13676602
         MH    X5,DCMCORLN         MULTIPLY BY LINE LENGTH    MF  M2057 13678602
         A     X5,DCMASCRN         ADD BEGINNING OF SCREEN    MF  M2057 13680602
*                                       IMAGE BUFFER          MF  M2057 13682602
         OI    DCMIOCM1,DCMWRPAR   INDICATE WRITE PARTIAL     MF  M2057 13683002
         B     MVCNTRL             GO SETUP CONTROL LINE      MH  M2517 13683102
USEINSTR EQU   *                                              MF  M2057 13683202
* WRITE CONTROL LINE FROM INSTRUCTION LINE                    MF  M2057 13688802
*/*USEINSTR: P INDICATE WRITE INFO DISPLAY FROM                         13690802
*/* INSTRUCTION LINE */                                                 13692802
         L     X5,DCMAINS          GET INSTRUCTION LINE ADDR  MF  M2057 13693202
         OI    DCMIOCM2,DCMWINFD   INDICATE WRITE INFO DISPLAYMF  M2057 13693602
*/*%P02: S (,%P03) BLANKET: BLANK LINE OF DCM FOR CONTROL LINE */       13694002
         LH    X3,DCMLGNTH         GET LENGTH OF A LINE       MF  M2057 13694102
         BAL   XF,BLANKET          GO BLANK LINE              MF  M2057 13694202
*/*%P03: P (,%BLD01) BASE CONTROL LINE DSECT */                         13694302
         USING DCONTROL,X5         BASE CONTROL LINE DSECT    MF  M2057 13708202
*/*%BLD01: D (YES,IMAGE,NO,%BLD02) TITLE LINE IN SCREEN IMAGE BUFFER */ 13950002
         TM    DCMADFLG,DCMACSIB       DYNAMIC DISPLAY CONTROL LINE     14000002
*              IN SRCEEN IMAGE BUFFER                                   14050002
         BO    IMAGE               YES -                                14100002
*/*%BLD02: P (,MVCNTRL) MOVE TITLE FROM WQE BLOCK */                    14150002
         SPACE 2                                                        14200002
         L     X4,DCMAMJWQ         LOAD CONSOLE OUTPUT Q PT             14250002
         L     MAJWQER,N0(X4)    LOAD MAJOR WQE ADDR                    14300002
         USING WMJM,MAJWQER         ESTABLISH MAJOR WQE BASE R          14350002
         LH    X3,WMJMTXTL          GET LENGTH OF TITLE                 14400002
         SH    X3,FIVE              DECREMENT BY 5                      14450002
         EX    X3,MVTITLE           MOVE TITLE TO LINE                  14500002
         B     DROPIT                                                   14550002
MVTITLE  MVC   TITLE(N0),WMJMTXT            CONTROL LINE TITLE MOVE     14600002
DROPIT   EQU   *                                                        14650002
         DROP  MAJWQER                                                  14700002
*/*MVCNTRL: P (,%BLD03) MOVE IN FIRST SET OF ASTERS AND FRAME NO. */    14750002
MVCNTRL  EQU   *                                                        14800002
         MVC   FRAME+N1(N5),CFRAME       MOVE FRAME WORD                14850002
         TM    DCMAFLG1,DCMADEND   Q. LAST FRAME ON SCREEN              14900002
         BO    LAST                YES- GO MOVE IN LAST                 14950002
         CLI   DCMAFR,CF0           Q. IS FIRST DIGIT OF FRAME          15000002
*                                     NUMBER ZERO                       15050002
         BE    ONEDIGIT            YES- BRANCH TO MOVE ONE DIGIT        15100002
         MVC   FRAME+N7(N2),DCMAFR  MOVE FRAME NUMBER INTO LINE         15150002
         B     BLD             GO BUILD LINE                            15200002
ONEDIGIT EQU   *                                                        15250002
         MVC   FRAME+N8(N1),DCMAFR+N1 MOVE ONLY ONE FRAME DIGIT IN      15300002
         B     BLD             GO BUILD LINE                            15350002
LAST     EQU   *                                                        15400002
         MVC   FRAME+N7(N4),CLAST                                       15450002
BLD      EQU   *                                                        15500002
         MVI   CNTRL2+N2,ASTERISK        PUT ASTERISK IN CONTROL 2      15550002
         MVI   CNTRL3+N2,ASTERISK        PUT ASTERISK IN CONTROL 2      15600002
*/*%BLD03: D (YES,CNTRLDD,NO,%BLD04) DYNAMIC DISPLAY */                 15650002
         TM    DCMADFLG,DCMADD    Q. DYNAMIC DISPLAY                    15700002
         BO    CNTRLDD             BR TO BUILD DYNAMIC LINE             15750002
*/*%BLD04: P (,MVAID) MOVE STATIC DISP. OPTIONS F AND E */              15800002
*    OPTIONS FOR STATIC DISPLAY ARE   *F   *E                           15850002
         MVI   CNTRL2+N3,FORWARD  PUT F OPT.   IN CONTROL 2             15900002
         MVI   CNTRL3+N3,ERASE    PUT E OPT.   IN CONTROL 3             15950002
         B     MVAID               BR TO MOVE IN AREA ID                16000002
CNTRLDD  EQU   *                                                        16050002
*/*CNTRLDD: D (YES,MVHOLD,NO,%BLD05) DYN. DISP. IN HOLD MODE */         16100002
         TM    DCMADFLG,DCMAHOLD   Q. DYNAMIC DISPLAY IN HOLD MODE      16150002
         BO    MVHOLD              BR TO MOVE HOLD OPTIONS              16200002
*    OPTIONS FOR DISPLAY IN UPDATE MODE ARE   *PT   *H                  16250002
*/*%BLD05: P (,MVAID) MOVE UPDATE OPTIONS PT AND H INTO LINE */         16300002
         MVI   CNTRL2+N3,PT1      PUT P OPT.   IN CONTROL 2             16350002
         MVI   CNTRL2+N4,PT2        PUT M OPT.   IN CONTROL 2           16400002
         MVI   CNTRL3+N3,HOLD     PUT H OPT.   IN CONTROL 2             16450002
         B     MVAID               BR TO MOVE IN AREA ID                16500002
*/*MVHOLD: P (,MVAID) MOVE HOLD MODE OPTIONS F AND U INTO LINE */       16550002
MVHOLD   EQU   *                                                        16600002
*    OPTIONS DYNAMIC SISPLAY IN HOLD MODE ARE   *F    *U                16650002
         MVI   CNTRL2+N3,FORWARD  PUT F OPT. IN CONTROL 2               16700002
         MVI   CNTRL2+N4,BLANK    MOVE IN BLANK               MH  M2517 16710002
         MVI   CNTRL3+N3,UPDATE   PUT U OPT.   IN CONTROL 2             16750002
MVAID    EQU   *                                                        16800002
*/*MVAID: P (,IODISP) CONVERT CONS. ID AND MOVE IT AND AREA ID INTO     16850002
*/*LINE */                                                              16900002
*   MOVE AREA ID IN -- CCA                                              16950002
*    THIS CODE WILL CONVERT HEX ID TO EBCDIC                            17000002
         SR    X4,X4               CLEAR REG                            17050002
         IC    X4,UCMID           GET CONSOLE ID (IN HEX)               17100002
         CVD   X4,DCMPACK         CONVERT TO PACKED DECIMAL             17150002
         UNPK  DCMPACK(N3),DCMPACK+N6(N2) UNPACK NUMBER                 17200002
         OI    DCMPACK+N2,CF0    FIX SIGN                               17250002
         CLI   DCMPACK+N1,CF0    IS FIRST DIGIT = TO ZERO               17300002
         BNE   MOVE               NO--                                  17350002
         MVI   DCMPACK+N1,BLANK  REPLACE ZERO WITH A BLANK              17400002
MOVE     EQU   *                                                        17450002
         MVC   AREAID+N1(N2),DCMPACK+N1 MOVE CC NO. TO CONTROL LINE     17500002
         MVC   AREAID+N3(N1),DCMAID MOVE AREA ID TO CONTROL LINE        17550002
         EJECT                                                          17600002
*********************************************************************** 17650002
IODISP   EQU   *                                                        17700002
*/*IODISP: P (,XCTL) INDICATE WRITE DISPLAY */                          17750002
         L     XFBRANCH,DCMIORTN   LOAD IO ROUTINE ADDRESS              17850002
*                                  FOR EXIT TO IEECVET(H,P,R, OR U)     17860002
XCTL     EQU   *                                                        18050002
*/*XCTL: R RETURN */                                                    18100002
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS                 18150002
         BR    XFBRANCH            EXIT ADDRESS ALREADY SET             18200002
         SPACE 2                                                        18350002
*                                                                       18400002
*    THE FOLLOWING CODE MOVES CONTROL LINE TITLE FROM SCREEN IMAGE      18450002
IMAGE    EQU   *                                                        18500002
*/*IMAGE: P (,MVCNTRL) MOVE TITLE FROM SCREEN IMAGE BUFFER LINE */      18550002
         SR    X3,X3               CLEAR REG                            18600002
         IC    X3,DCMATOP          PICK UP TOP LINE NO.                 18650002
         BCTR  X3,N0               DECREMENT BY ONE                     18700002
         MH    X3,DCMCORLN       GET DISPLACE MENT INTO MSG AREA S21003 18750002
         A     X3,DCMASCRN         ADD MSG AREA START ADDR              18800002
         MVC   TITLE(L'TITLE),N0(X3) MOVE TITLE                         18850002
         B     MVCNTRL             CONTINUE                             18900002
*********************************************************************** 18950002
         EJECT                                                          19000002
*    THIS ROUTINE WILL TURN THE MAJOR WQE BIT OFF WHICH                 19050002
*    CORRESPONDS TO THE BIT NUMBER OF THE CONSOLE ID.                   19100002
*    TURNING THE BIT OFF WILL TELL OTHER ROUTINES TO DEQUEUE            19150002
*    THE WQE FROM THIS CONSOLE'S WQE OUTPUT QUEUE.                      19200002
*/* E MASKRTN */                                                        19250002
MASKRTN  EQU   *                                                        19300002
         L     X4,DCMAMJWQ         LOAD CONSOLE OUTPUT Q PT             19350002
*/*%MSK01: P (,%MSK02) LOAD OUTPUT QUEUE PT */                          19400002
         L     MAJWQER,N0(X4)    LOAD MAJOR WQE ADDR                    19450002
*/*%MSK02: P (,%MSK03) LOAD MAJOR WQE POINTER AND BASE IT */            19500002
         LA    MAJWQER,N0(MAJWQER) CLEAR HIGH BYTE                      19550002
         USING WMJM,MAJWQER        ESTABLISH MAJOR WQE BASE R           19600002
*/*%MSK03: P (,%MSK04) GET ADDR OF CONSOLE'S FRAME FULL DBL WRD */      19650002
         LA    FLDADR,WMJMCONS      GET CONSOLE'S  FRAME FULL BIT       19700002
         DROP  MAJWQER                                                  19750002
         SR    R4,R4              CLEAR REG                             19800002
         SR    R6,R6              CLEAR REG                             19850002
         IC    R6,UCMID           LOAD CONSOLE ID                       19900002
*/*%MSK04: P (,%MSK05) LOAD CONSOLE ID; LOAD 64 MAX. BIT NO. */         19950002
         LA    R3,N64             PRIME REG WITH MAXIMUM BIT NO.        20000002
*/*%MSK05: P (,%MSK06) SHIFT DOUBLE REG BY DIFFERENCE BET. MAX. BIT NO. 20050002
*/*AND CONS. ID */                                                      20100002
         LA    R5,N1              REG 5 = 1                             20150002
         SR    R3,R6              SUBTRACT ID BIT FROM MAXIMUM BIT NO.  20200002
*    NOTE: THIS DYNAMIC SHIFT INSTRUCTION SHIFTS REGS 4 AND 5 TO THE    20250002
*          LEFT BY THE NUMBER CONTAINED IN REG 3.                       20300002
         SLDL  R4,N0(R3)          SHIFT BIT BY REMAINDER IN REG 3       20350002
*/*%MSK06: D (YES,OK1,NO,NEXTREG) IS BIT IN FIRST 4 BYTES */            20400002
         N     R4,N0(FLDADR)      TEST FOR BIT IN FIELD 1               20450002
         BNZ   OK1                BRANCH IF FOUND TO BE ON              20500002
         N     R5,N4(FLDADR)      TEST FOR BIT IN FIELD 2               20550002
         BNZ   NEXTREG            BRANCH IF FOUND TO BE ON              20600002
NOTON    EQU   *                                                        20650002
         BR    RETURN              THE BIT IS NOT ON                    20700002
OK1      EQU   *                                                        20750002
*/*OK1: P (,%MSK07) TURN BIT OFF IN FIRST WORD */                       20800002
         ST    R4,DCMDSAV          STORE MASK                           20850002
         XC    N0(N4,FLDADR),DCMDSAV EXCLUSIVE OR BIT OFF               20900002
*/*%MSK07: R RETURN TO CALLER */                                        20950002
         BR    RETURN                                                   21000002
NEXTREG  EQU   *                                                        21050002
*/*NEXTREG: P (,%MSK07) TURN BIT OFF IN SECOND WORD */                  21100002
         ST    R5,DCMDSAV          STORE MASK                           21150002
         XC    N4(N4,FLDADR),DCMDSAV EXCLUSIVE OR BIT OFF               21200002
         BR    RETURN                                                   21250002
*                                                                       21300002
*********************************************************************** 21350002
         EJECT                                                          21400002
********************************************************************    21450002
*   EXECUTED INSTRUCTIONS                                               21500002
*                                                                       21550002
BLANKWRT MVC   N1(N0,X5),N0(X5) EXEC. FOR BLANKING WRITE AREA           21600002
*                                                                       21650002
********************************************************************    21700002
*       CONSTANTS                                                       21750002
********************************************************************    21800002
TEN      DC    F'10'               CONSTANT                             21850002
TWO      DC    H'2'                CONSTANT                             21900002
FOUR     DC    H'4'                CONSTANT                             21950002
FIVE     DC    H'5'                CONSTANT                             22000002
CLAST    DC    C'LAST'             LAST                                 22150002
KDF      DC    C'D,F '             CONSTANT FOR K D,F                   22200002
KDH      DC    C'D,H '             CONSTANT FOR K D,H                   22250002
KDU      DC    C'D,U '             CONSTANT FOR K D,U                   22300002
CFRAME   DC    C'FRAME'                                                 22450002
********************************************************************    22650002
         EJECT                                                          22700002
********************************************************************    22750002
*        DSECTS                                                         22800002
*    ALL DSECTS FOLLOW THIS POINT                                       22850002
*                                                                       22900002
DCONTROL DSECT                                                          22950002
* CONTROL LINE OF AN OUT OF LINE DISPLAY                                23000002
         DS    0XL74                                                    23050002
TITLE    DS    XL37                TITLE                                23100002
FRAME    DS    XL11                FRAME NUMBER                         23150002
CNTRL1   DS    XL7                 CONTROL CHARACTER 1                 X23200002
                                        (RESERVED FOR BACKWARD         X23250002
                                        FRAMING)                        23300002
ASTERISK EQU   C'*'                INDICATES DETECTABLE FIELD           23350002
CNTRL2   DS    XL8                 CONTROL CHARACTER 2                  23400002
FORWARD  EQU   C'F'                     FRAME FORWARD                   23450002
PT1      EQU   C'P'                     STOP                            23500002
PT2      EQU   C'T'                          TRACK                      23550002
CNTRL3   DS    XL7                 CONTROL CHARACTER 3                  23600002
ERASE    EQU   C'E'                     ERASE DISPLAY                   23650002
HOLD     EQU   C'H'                     HOLD DYNAMIC DISPLAY            23700002
UPDATE   EQU   C'U'                     UPDATE DYNAMIC DISPLAY          23750002
AREAID   DS    XL4                 AREA ID - CCA                        23800002
         EJECT                                                          23850002
         IEERDCM                                                        23900002
         EJECT                                                          24000002
         IHACTM  CXSA                                                   24250002
         EJECT                                                          24300002
         IEECUCM FORMAT=NEW                                             24400002
         EJECT                                                          24450002
         IEETDCM                                                        24500002
         EJECT                                                          24600002
KPARM    DSECT                                                          24650002
         IEECVMAP KPARAM                                                24700002
         EJECT                                                          24750002
         IHAWQE  DSECT=YES                                              24850002
*********************************************************************** 24900002
*/*IEECVFTN: END */                                                     24950002
         END                                                            25000002
