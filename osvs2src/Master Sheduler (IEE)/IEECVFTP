IEECVFTP CSECT                                                          00100000
*A 118600,125100                                              MB YM2416 00200000
*    THIS ROUTINE WAS CREATED FOR STATUS DISPLAY SUPPORT, LINE ITEM     00400000
*    21002, OS RELEASE 21.                                              00500000
         TITLE 'IEECVFTP - K E,D AND PT A COMMANDS'                     00900000
*********************************************************************** 01000000
*                                                                     * 01100000
* STATUS                                                              * 01200000
*    CHANGE LEVEL=0                                                   * 01300000
*                                                                     * 01400000
* APARS FIXED --- ZA14992                                             * 01430000
*                                                                     * 01460000
* FUNCTION                                                            * 01500000
*    TO HANDLE CONTROL (K) COMMANDS AFFECTING OUT-OF-LINE STATUS      * 01600000
*    DISPLAYS. ( K E,D AND PM A.)                                     * 01700000
*    TO INDICATE IN THE SSCT THAT ALL LINES BELOW A DISPLAY HAVE      * 01800000
*    BEEN APPROPRIATELY BLANKED.                                      * 01900000
*                                                                     * 02000000
* ENTRY POINT                                                         * 02100000
*    IEECVFTP (CSECT NAME)                                            * 02200000
*                                                                     * 02300000
* INPUT                                                               * 02400000
*    REG 1 POINTS TO CXSA WHICH POINTS TO DCM  WHICH CONTAINS INDI-   * 02500000
*    CATORS.                                                          * 02600000
*    K COMMAND PARAMETER LIST POINTED TO IN THE DCM                   * 02700000
*    DCMADOPN POINTS TO OPERAND(S) IN PARAMETER LIST                  * 02800000
*                                                                     * 02900000
* OUTPUT                                                              * 03000000
*    BIT SETTINGS IN DCM AND SACB FOR OTHER ROUTINES TO ANALYZE AND   * 03100000
*    BLANKED DISPLAY OR WRITE UNVIEABLE MESSAGES TO SCREEN            * 03200000
*                                                                     * 03300000
* EXTERNAL REFERENCES                                                 * 03400000
*                                                                     * 03500000
* EXITS,NORMAL                                                        * 03600000
*    I/O RTN    (DEVICE-DEPENDENT) TO ERASE A STATUS DISPLAY          * 03700000
*    OR BLANK OUT MESSAGE AREAS BETWEEN  OR BELOW STATUS DISPLAYS.    * 03800000
*                                                                     * 03900000
* EXITS,ERROR                                                         * 04000000
*    TO PROCCESOR 1 UPON INCORRECT COMMAND INPUT                      * 04100000
*                                                                     * 04200000
* ATTRIBUTES                                                          * 04300000
*    REFRESHABLE, PRIVILEGED, TYPE 4 SVC                              * 04400000
*                                                                     * 04500000
* TABLES/WORK AREAS                                                   * 04600000
*    UCM, DCM,CXSA, WQE, SACB                                         * 04700000
*                                                                     * 04800000
*                                                                     * 04900000
*********************************************************************** 05000000
         EJECT                                                          05100000
*********************************************************************** 05200000
*    REGISTERS                                                          05300000
*                                                                       05400000
********************************************************************    05500000
X1PARM   EQU   1                                                        05600000
XERTRN   EQU   14                                                       05700000
XFBRANCH EQU   15                                                       05800000
XF       EQU   15                  WORK REG                             05900000
X14      EQU   14                  RETURN REG                           06000000
RETURN   EQU   14                  RETURN REG                           06100000
XD       EQU   13                  WORK REG                             06200000
SACBPT   EQU   13                  SACB BASE REG                        06300000
XC       EQU   12                  THIS RTN'S BASE REG                  06400000
XB       EQU   11                  CXSA BASE REG                        06500000
XA       EQU   10                  UCM BASE REG                         06600000
X9       EQU   9                   TRANSIENT DCM BASE REG               06700000
XRDCM    EQU   8                   RESIDENT DCM BASE REG                06800000
LIST     EQU   7                   UCMLIST BASE REG                     06900000
X6       EQU   6                   WORK REG                             07000000
PARMREG  EQU   2                   PARAMETER LIST BASE REG              07100000
WQEBASE  EQU   4                   WQE BASE REG                         07200000
OUTQREG  EQU   1                   POINTER TO CONSOLE OUTPUT           X07300000
                                        QUEUE ENTRY                     07400000
X5       EQU   5                   WORK REG                             07500000
X4       EQU   4                   WORK REG                             07600000
X3       EQU   3                   WORK REG                             07700000
X1       EQU   1                   WORK REG                             07800000
X0       EQU   0                   WORK REG                             07900000
         EJECT                                                          08000000
********************************************************************    08100000
*     EQUATES                                                           08200000
********************************************************************    08300000
ID1      EQU   C'F'                1ST CHARACTER OF CSECT ID            08400000
ID2      EQU   C'P'                2ND CHARACTER OF CSECT ID            08500000
HEXTWO   EQU   X'02'               NO LONGER NEEDED BIT IN SUB Q        08600000
VALIDOFF EQU   255-1               VALID ENTRY BIT MASK-TO TURN OFF     08700000
TOPCHAIN EQU   X'10'               USE TOP MINOR ON CHAIN               08800000
E2       EQU   X'E2'                                                    08900000
ZERO     EQU   X'00'                                                    09000000
BLANK    EQU   X'40'                                                    09100000
N0       EQU   0                                                        09200000
N1       EQU   1                                                        09300000
N2       EQU   2                                                        09400000
N3       EQU   3                                                        09500000
N4       EQU   4                                                        09600000
N8       EQU   8                                                        09700000
NA       EQU   10                                                       09800000
NB       EQU   11                  FIELD INCREMENT                      09900000
NC       EQU   12                  FIELD INCREMENT                      10000000
ND       EQU   13                  FIELD INCREMENT                      10100000
N255     EQU   255                                                      10200000
NOMORE   EQU   8                                                        10300000
NOTTOPSW EQU   X'01'               NOT TOP DISPLAY SWITCH FLAG BIT      10400000
ONLYONE  EQU   X'02'               WORK ON 1 DISPLAY AREA ONLY          10500000
         EJECT                                                          10600000
********************************************************************    10700000
*                                                                       10800000
*    ESTABLISH ADDRESSABILITY                                           10900000
*                                                                       11000000
********************************************************************    11100000
         SPACE 2                                                        11300000
         BALR  XC,N0               ESTABLISH ROUTINE'S                  11500000
         USING *,XC                BASE REGISTER                        11600000
         B     BEGIN               BRANCH AROUND PATCH AREA             11700000
ICATCH   DC    CL8'IEECVFTP'      EYECATCHER AND FIELD MAINTENANCE      11800000
         DC    CL8'&SYSDATE',CL56' ' DATE AND PATCH AREA       Y02958   11900000
BEGIN    EQU   *                   END OF PATCH AREA                    12000000
         LR    XB,X1               ESTABLISH CXSA                       12100000
         USING CXSA,XB             ADDRESSABILITY                       12200000
         L     XA,CSACTLM          ESTABLISH UCM ADDRESS                12300000
         USING UCM,XA              ADDRESSABILITY                       12400000
         L     LIST,CSAUCM         LOAD UCM ENTRY ADDRESS               12500000
         USING UCMLIST,LIST        ESTABLISH UCM ENTRY  ADDRESSABILITY  12600000
         L     XRDCM,UCMXB            GET POINTER TO RESIDENT DCM       12700000
         USING DCMTSRT,XRDCM          ESTABLISH RDCM BASE REG           12800000
         L     X9,DCMADTRN        LOAD TRANSIENT DCM PT                 12900000
         USING DCMSTRT,X9           ESTABLISH TDCM BASE REG             13000000
         USING WMNM,WQEBASE        BASE WQE'S                           13100000
         USING DCMACB,SACBPT       SACB BASE REG                        13200000
         USING KPARM,PARMREG       ESTABLISH PARAMETER BASE REG         13300000
         MVC   DCMTRACE(DCMTRLEN),DCMTRAC2 SHIFT OLD TRACE ENTRIES      13400000
         MVI   DCMTREN1,ID1        PUT CSECT ID INTO                    13500000
         MVI   DCMTREN2,ID2        NEW TRACE ENTRY                      13600000
         L     XFBRANCH,CSAXB      ADDRESS GETLOCK SUBROUTINE           13700000
         LR    X1PARM,XB           PASS CXSA ADDRESS IN REG 1           13800000
         BALR  XERTRN,XFBRANCH     OBTAIN LOCKS                         13900000
*                                                                       14000000
         EJECT                                                          14100000
*********************************************************************** 14200000
*    TEST IF BLANKING IS IN PROCESS                                     14300000
*                                                                       14400000
********************************************************************    14500000
         TM    DCMCOM2,DCMCMIN5    Q. RETURN  FROM I/O COMPLETE         14700000
         BO    BLANKRTN            YES-MORE BLANKING WORK TO DO         14800000
         SR    X0,X0               CLEAR REG                            14900000
         ST    X0,DCMDSAV+NC       INIT. SWITCH FIELDS TO 0             15000000
*                                                                       15100000
         TM    DCMCOM2,DCMCBLNK    N0--IS BLANKING FUNCTION REQUIRED    15300000
         BO    START               YES-GO CHECK WHAT'S TO BE BLANKED    15400000
********************************************************************    15500000
*                                                                       15600000
*    ANALYZE COMMAND                                                    15700000
         SPACE 2                                                        15900000
         L     PARMREG,DCMADOPN    LOAD PT TO OPERAND(S)                16000000
         SH    PARMREG,FOUR        DECREMENT POINTER BY FOUR            16100000
SACBINIT EQU   *                                                        16200000
         L     SACBPT,DCMADSDS         LOAD SACB POINTER FROM RDCM      16400000
         MVC   DCMDSAV(N4),DCMWTINT INIT. PT TO INSTRUCTION LINE        16500000
IDLOOP   EQU   *                                                        16700000
         LTR   SACBPT,SACBPT       IS IT A VALID ADDR                   16800000
         BZ    ERROR               NO--GO BACK TO PROCESSOR 1           16900000
         CLC   KAID(N1),DCMAID    ARE AREA ID'S EQUAL                   17100000
         BE    FOUNDID            YES-                                  17200000
         TM    DCMAFLG1,DCMADISP   IS A DISPLAY IN AREA                 17400000
         BNO   SKIPSAVE            NO - DONT SAVE THIS PT               17500000
         SR    X3,X3               CLEAR REG                            17700000
         IC    X3,DCMATOP          GET TOP LINE NO.                     17800000
         BCTR  X3,N0               DECREMENT BY 1                       17900000
         MH    X3,DCMCORLN         MULTIPLY BY LENGTH OF LINE           18000000
         A     X3,DCMASCRN         ADD START OF SCREEN IMAGE BUFF. ADDR 18100000
         ST    X3,DCMDSAV      SAVE ADDR OF SCREEN IMAGE BUFF. TOP LINE 18200000
         MVC   DCMDSAV+ND(N1),DCMAID SAVE NEXT LOWER AREA ID FOR LATER  18300000
SKIPSAVE EQU   *                                                        18400000
         L     XD,N0(XD)          LOAD POINTER TO NEXT SACB             18600000
         B     IDLOOP                                                   18700000
*                                                                       18800000
FOUNDID  EQU   *                                                        19000000
         MVC   DCMLINEN(N1),DCMATOP SAVE TOP LINE NO. FOR I/O RTN       19100000
*                                                                       19200000
********************************************************************    19300000
*                                                                       19400000
         TM    KFLGS,KPT           IS PT BIT ON                         19600000
         BO    PMARTN              YES-                                 19700000
         CLC   KOPN(N4),KED        IS COMMAND K E,D                     19900000
         BE    KEDRTN              YES-GO ERASE DISPLAY                 20000000
*                                                                       20100000
********************************************************************    20200000
*                                                                       20300000
*    IF NONE OF ABOVE, IT IS AN ERROR THAT SHOULD NEVER HAPPEN.         20400000
*    GO BACK TO PROCESSOR 1 SO THAT IT CAN CONTINUE PROCESSING          20500000
*    WITHOUT CAUSING POSSIBLE SYSTEM FAILURE.                           20600000
ERROR    EQU   *                                                        20800000
         L     XFBRANCH,DCMNPROC   LOAD PROC 1 ADDRESS                  20900000
         B     XCTL            EXIT TO IEECVET1                         21000000
*                                                                       21100000
PMARTN   EQU   *                                                        21300000
*    PM A LOGIC FREES THE DYNAMIC DISPLAY AREA BY PROCESSING            21400000
*    AS FOR K E,D,                                                      21500000
*    EXCEPT TO TURN SCREEN FULL BIT OFF IN THE DCM.                     21600000
         NI    DCMR2FLG,N255-DCMRXSFL  TURN OFF SCREEN FULL BIT         21700000
         TM    DCMAFLG1,DCMADISP   Q.DISPLAY IN AREA                    21800000
         BO    KEDRTN              YES,GO ERASE DISPLAY                 21900000
*     IF THERE IS NO DISPLAY IN THE AREA, THE PM COMMAND                22000000
*     MUST HAVE BEEN ISSUED BEFORE THE FIRST DISPLAY WAS BUILT          22100000
         XC    DCMAROW(DCMCLRLN),DCMAROW     CLEAN UP SACB ONLY         22200000
         B     ERROR               GO TO PROCESSOR 1                    22300000
         EJECT                                                          22400000
KEDRTN   EQU   *                                                        22500000
*    ON A K E,D COMMAND--THE MAJOR AND MINOR WQE'S ARE FREED FROM THIS  22600000
*    CONSOLE'S OUTPUT QUEUE.                                            22700000
*    THE MODULE THEN XCTL'S TO  THE BLANKING RTN TO ERASE THE           22800000
*    DISPLAY FROM THE SCREEN IF IT IS NOT THE TOP DISPLAY.              22900000
*    IF IT IS THE TOP DISPLAY, THIS MODULE XCTL'S TO THE I/O RTN        23000000
*    TO WRITE OUT ANY MESSAGES THAT ARE UNDER THE DISPLAY, AND/ OR      23100000
*    TO BLANK THE DISPLAY LINES VISIBLE ON THE SCREEN.                  23200000
*                                                                       23300000
         L     OUTQREG,DCMAMJWQ    GET POINTER TO OUTPUT Q ENTRY        23500000
         USING CQE,OUTQREG         CONSOLE Q ADDRESSABILITY  Y02958     23600000
         TM    DCMAFLG2,DCMAMJFR   IS MAJOR FREED                       23700000
         BO    ERASING             YES - SKIP FREEING                   23800000
         TM    DCMAFLG1,DCMADEND   IS END OF DISPLAY ON SCREEN          24000000
         BO    FREEMAJ            YES, MINORS ALREADY FREED             24100000
         BAL   X14,FINDNEXT        GET POINTER TO NEXT MINOR            24400000
         LTR   WQEBASE,WQEBASE     ANY MORE MINORS                      24500000
         BZ    FREEMAJ             NO, GO FREE MAJOR                    24600000
         SPACE                                                          24700000
*    DECREMENT USE COUNTS OF ALL MINORS NOT OUTPUT                      24800000
FREEMIN  EQU   *                                                        24900000
         SR    X5,X5               CLEAR REG                            25000000
         IC    X5,WMNMUC1          GET USE COUNT FROM MINOR             25100000
         BCTR  X5,N0               DECREMENT USE COUNT BY 1             25200000
         STC   X5,WMNMUC1          STORE NEW USE COUNT                  25300000
         L     WQEBASE,WMNMNX1-N1  GET ADDRESS OF NEXT MINOR            25400000
         LA    WQEBASE,N0(WQEBASE) CLEAR HIGH ORDER BYTE                25500000
         LTR   WQEBASE,WQEBASE     ANY MORE MINORS ON Q                 25600000
         BNZ   FREEMIN             YES, BRANCH TO CONTINUE             X25700000
                                        DECREMENTING USE COUNTS         25800000
         SPACE                                                          25900000
FREEMAJ  EQU   *                                                        26200000
         OI    CQEFLAG,CQEAVAIL  SET NO LONGER NEEDED BIT IN    Y02958 X26300000
                                        SUBQ ELEMENT FLAG BYTE          26400000
         NI    CQEFLAG,N255-CQEENTR  TURN VALID ENTRY BIT OFF   Y02958  26500000
         OI    UCMSTS,UCMTB        SET DEQUEUE BIT ON IN UCME           26600000
********************************************************************    26700000
         EJECT                                                          26800000
ERASING  EQU   *                                                        26900000
         NI    DCMCOM3,N255-DCMCMIN7    TURN OFF INT 7 BLANK FLAG       27100000
         SR    X3,X3               CLEAR REG                            27200000
         IC    X3,DCMLINEN         LOAD LINE NO.                        27300000
*    REG 3 NOW HAS TOP ROW LINE NUMBER                                  27400000
         BCTR  X3,N0               SUBTR. 1 FOR 1-BASE TABLE            27500000
         STC   X3,DCMDSAV+NB       SAVE TOP LINE NO.                    27600000
         A     X3,DCMADSEC         ADD START OF SSCT ADDR TO ROW NUMBER 27700000
         LR    X6,X3               SAVE SSCT ADDRESS FOR NEXT LOOP      27800000
         LH    X5,DCMALN           PICK UP SACB LENGTH                  27900000
         LR    X4,X5               SAVE IT FOR LATER                    28000000
         STH   X4,DCMDSAV+N8       SAVE TOTAL LINE COUNT FOR BLANKING   28100000
         CLC   DCMTOPDS(N1),DCMAID IS THIS TOP DISPLAY                  28300000
         BE    LOOP             YES - DON'T SET FLAG BIT ON             28400000
         MVI   DCMDSAV+NC,NOTTOPSW SET SWITCH FLAG BIT ON               28600000
*                                    IF NOT TOP DISPLAY                 28700000
LOOP     EQU   *                                                        28900000
         TM    N0(X3),DCMSECST     Q. END OF SSCT OR BLANKED LINE       29000000
         BO    CHECKIT1            YES-                                 29100000
         TM    DCMDSAV+NC,NOTTOPSW THIS TOP DISPLAY                     29300000
         BNO   NOTTP               YES - ZERO OUT SCCT CONTROL BYTE     29400000
         MVI   N0(X3),DCMSECBL     TURN ON BLANKED BIT INTO SSCT BYTE   29500000
         B     BMP                 SKIP AROUND                          29600000
NOTTP    EQU   *                                                        29700000
         MVI   N0(X3),ZERO         NO-TURN OFF DISPLAY BITS IN SSCT     29800000
BMP      EQU   *                                                        29900000
         LA    X3,N1(X3)           BUMP SSCT POINTER                    30000000
         BCT   X5,LOOP         DECREMENT LENGTH COUNTER                 30200000
CHECKIT1 EQU   *                                                        30300000
         XC    DCMAROW(DCMCLRLN),DCMAROW CLEAR AREA WITH ZEROS          30500000
         SPACE 2                                                        30600000
*    ALL SSCT INDICATORS FOR THIS DISPLAY HAVE BEEN RESET,              30700000
*    PRIME ALL TABLE POINTERS FOR MESSAGE COUNTING RTN.                 30800000
         SR    X3,X3               CLEAR REG                            30900000
         IC    X3,DCMDSAV+NB       LOAD TOP LINE NO. OF DISP.           31000000
         LR    XF,X3               LOAD DISPLACEMENT NUMBER             31100000
         MH    XF,DCMCORLN         GET START ADDR IN MSG AREA           31200000
         A     XF,DCMASCRN         ADD START OF MSG AREA ADDR           31300000
         MH    X3,DCMSCTCN         DOUBLE THE NUMBER                    31400000
         A     X3,DCMAMTAB         ADD ADDR OF SCT TABLE                31500000
         TM    DCMDSAV+NC,NOTTOPSW THIS TOP DISPLAY                     31700000
         BO    NOTTOP             NO - DON'T CHANGE AREA ID OR WTBUF    31800000
* NOTE: IF NOT THE TOP DISPLAY, IT WILL BE BLANKED 3 LINES AT A TIME    31900000
*                                                                       32000000
********************************************************************    32100000
         EJECT                                                          32200000
         L     SACBPT,DCMDSAV      RESTORE PT TO PREVIOUS BLOCK         32400000
         CL    SACBPT,DCMWTINT      DOES A PREVIOUS BLOCK EXIST         32600000
         BE    SWTEST              NO -                                 32700000
         MVC   DCMTOPDS(N1),DCMDSAV+ND MOVEAREA ID BELOW THIS ONE       32900000
         B     NOSTDISP                                                 33000000
SWTEST   EQU   *                                                        33200000
         MVI   DCMTOPDS,ZERO       REINIT BYTE TO SAY NO DISPLAYS       33300000
* NOTE: IF NO DISPLAY EXISTS, THE ADDRESS OF THE INSTRUCTION LINE IS PT 33400000
NOSTDISP EQU   *                                                        33500000
         ST    SACBPT,DCMWTBUF     UPDATE TO REFLECT NEW STATUS ADDR    33700000
* NOTE: REG 6 CONTAINS ADDRESS OF TOP SSCT LINE OF DISPLAY              33900000
* NOTE2: REGS 0 AND 5 CONTAIN ZEROS                                     34000000
LOOP2    EQU   *                                                        34200000
         TM    N0(X3),DCMMSGIN    Q. MSG IN LINE                        34300000
         BO    OUTX               YES COUNT                             34400000
         AH    X0,ONE              ADD 1 TO BLANK LINE COUNT            34500000
         B     OUTBLK              ADD TO PTS                           34600000
OUTX     EQU   *                                                        34700000
         LA    X5,N1(X5)          BUMP REAL-MSG COUNTER                 34900000
         AH    XF,DCMCORLN         BUMP MSG AREA POINTER                35000000
OUTBLK   EQU   *                                                        35200000
         LA    X3,N2(X3)          BUMP SCT POINTER                      35300000
         LA    X6,N1(X6)           BUMP SSCT POINTER                    35400000
         TM    N0(X3),DCMMSGST    Q. END OF SCT TABLE                   35600000
         BO    OUT                STOP COUNT                            35700000
         BCT   X4,LOOP2            DECREMENT LINE COUNTER               35900000
         TM    DCMDSAV+NC,ONLYONE  WORK ON ONE AREA ONLY                36100000
         BO    OUT                 YES - DONT SEARCH MSGS FURTHER       36200000
         TM    N0(X6),E2     DISPLAY LINE                               36400000
         BNZ   OUT                STOP COUNTING                         36500000
         MVI   N0(X6),ZERO         CLEAN SSCT BYTE                      36600000
         LH    X4,ONE              PRIME REG                            36700000
         B     LOOP2               NO - CONT. TO ADD TO COUNTER         36800000
OUT      EQU   *                                                        37000000
         LR    X4,X0               PRIME BLANK LINE COUNTER             37100000
         AR    X0,X5               TOTAL LINE NUMBER FOR I/O AND SAVE   37200000
         LTR   X4,X4               ANY BLANKING TO DO                   37300000
         BZ    OUT2              NO - DO INLINE MSGS ONLY               37400000
         LR    X5,XF               LOAD ADDR OF MSG AREA SAVED ABOVE    37500000
         LR    X6,X3               SET UP NEW SCT POINTER               37600000
         SR    X6,X4               DECREMENT SCT POINTER TO FIRST LINE  37700000
         SR    X6,X4               DECREMENT AGAIN FOR SECOND BYTE      37800000
         LR    X3,X4               PRIME LOOP CONTROL REG               37900000
LOOPBLNK EQU   *                                                        38000000
         LH    X4,ONE              PRIME LINE COUNTER WITH ONE          38100000
         BAL   XF,BLANKET         BLANK AREA                            38300000
         NI    N0(X6),N255-DCMMSGJK TURN OFF JUNK LINE BIT              38400000
         LA    X6,N2(X6)           BUMP SCT POINTER                     38500000
         AH    X5,DCMCORLN         BUMP MESSAGE AREA POINTER            38600000
         BCT   X3,LOOPBLNK       DECREMENT COUNT AND LOOP IF NOT ZERO   38700000
OUT2     EQU   *                                                        38800000
         TM    DCMDSAV+NC,NOTTOPSW Q. WORKING ON TOP DISPLAY  MF        39000000
         BO    BYT                 NOT TOP DISPLAY - BRANCH   MF        39100000
*                                       AROUND RESETTING      MF        39200000
*                                       WARNING LINE          MF        39300000
         TM    N0(X6),DCMMSGIN     WAS A MESSAGE FOLLOWING IN MSG AREA  39500000
         BO    BYT                 YES - DON'T RESET WARNING LINE       39600000
         TM    DCMR2FLG,DCMRXUNV   Q. IS UNVIEW. MSG ON SCREEN          39800000
         BNO   BYT            NO - SKIP                                 39900000
         TM    DCMR2FLG,DCMRXSFL   YES - IS SCREEN FULL BIT ON          40100000
         BO    BYT            YES - SKIP                                40200000
         OI    DCMIOCM2,DCMBLWRR   BLANK RIT HALF OF WRN. LINE BIT ON   40400000
         OI    DCMIOCM1,DCMWRWRN   TURN ON WRITE WRN. LINE              40500000
BYT      EQU   *                                                        40700000
         MH    X0,DCMCORLN        MULT. COUNT BY NO. OF BYTES/LINE      40800000
         STH   X0,DCMBYTCT        SAVE FOR IO  RTN                      40900000
OUT3     EQU   *                                                        41100000
         OI    DCMIOCM1,DCMWRPAR   INDICATE WRITE PARTIAL MSG AREA      41200000
         B     IOEXIT             GO WRITE MSGS AND/OR BLANK LINES      41300000
*                                                                       41400000
********************************************************************    41500000
         EJECT                                                          41600000
********************************************************************    41700000
COMPREG  EQU   *                                                        41800000
         MVC   DCMLINEN(N1),DCMDSAV+NA MOVE TOP LINE NO.                42000000
         CH    X4,THREE            Q. MORE THAN 3 LINES TO BLANK        42100000
         BH    SAVECNT             YES-                                 42200000
         LTR   X4,X4               ANY LINES TO BLANK                   42400000
         BZ    ERROR                YES - END PROCESS                   42500000
         XC    DCMDSAV+N8(N2),DCMDSAV+N8  SET COUNTER TO NO LINES       42600000
CONTCNT  EQU   *                                                        42700000
         LR    X3,X4               SAVE NO. OF LINES                    42900000
         MH    X4,DCMCORLN         MULT. LINES BY LENGTH                43000000
         STH   X4,DCMBYTCT         SAVE BYTE COUNT FOR I/O RTN          43100000
         LH    X4,ONE              DO FIRST LINE OF BLANKS              43200000
         L     X5,DCMAINS          SET POINTER TO INSTRUCTION LINE      43300000
         BAL   XF,BLANKET          GO BLANK WRITE AREA                  43400000
         LR    X4,X3               RESTORE LINE COUNT                   43500000
         BCTR  X4,N0           DECREMENT FOR THE ONE JUST DONE          43600000
         LTR   X4,X4               ANY MORE TO BLANK                    43700000
         BNP   IODISP2             NO - STOP PROCESS OF BLANKING        43800000
         L     X5,DCMAENTR         LOAD ENTRY AREA                      43900000
         BAL   XF,BLANKET          GO BLANK WRITE AREA                  44000000
         B     IODISP2             GO TO I/O RTN TO WRITE OUT LINES     44100000
BLANKET  EQU   *                                                        44300000
         MH    X4,DCMLGNTH         MULT. LINE CNT BY LINE LENGTH        44500000
         MVI   N0(X5),BLANK        MOVE A BLANK CHAR                    44600000
         SH    X4,TWO              DECREMENT BY 2                       44700000
         EX    X4,BLANKWRT         SPREAD BLANKS THROUGHOUT LINE        44800000
         BR    XF                                                       45000000
SAVECNT  EQU   *                                                        45100000
         OI    DCMCOM2,DCMCMIN5    INDICATE RETURN TO THIS RTN          45300000
         SH    X4,THREE           DECREMENT LINE COUNT BY 3             45400000
         STH   X4,DCMDSAV+N8       SAVE LINE COUNT STILL TO BE DONE     45500000
         LH    X4,THREE            PRIME LINE COUNT                     45600000
         SR    X3,X3               CLEAR REG                            45700000
         IC    X3,DCMDSAV+NA       LOAD LINE NO.                        45800000
         AR    X3,X4               BUMP LINE NO. COUNT BY 3             45900000
         STC   X3,DCMDSAV+NA       SAVE FOR LATER                       46000000
         B     CONTCNT             GO BLANK LINES                       46100000
*                                                                       46200000
********************************************************************    46300000
         EJECT                                                          46400000
* NOTE: IF NOT TOP DISPLAY K E,D'D, THEN JUST BLANK IT                  46500000
*     IF THERE ARE NO MESSAGES IN THE MESSAGE AREA UNDER THE            46600000
*     DISPLAY, BLANKING WILL BE DONE USING THE MESSAGE AREA AND         46700000
*     A PARTIAL WRITE.  ELSE, LINES WILL BE BLANKED USING THE INSTR-    46800000
*     UCTION LINE AND ENTRY AREA, DOING I/O 3 LINES AT A TIME.          46900000
NOTTOP   EQU   *                                                        47000000
         OI    DCMDSAV+NC,ONLYONE  TURN ON ONE AREA SWITCH              47200000
         TM    N0(X3),DCMMSGIN    Q. MSG IN LINE                        47400000
         BNO   LOOP2              NO- GO BLANK FROM MSG AREA            47500000
         MVC   DCMDSAV+NA(N1),DCMLINEN SAVE TOP LINE NUMBER             47700000
         XC    DCMDSAV(N4),DCMDSAV SET WORD INDICATING BLANK ONLY       47800000
BLANKRTN EQU   *                                                        47900000
         NI    DCMCOM2,N255-DCMCMIN5 TURN BIT OFF                       48200000
         SR    X4,X4               CLEAR REG                            48300000
         LH    X4,DCMDSAV+N8       PICK UP LINES STILL TO BLANK         48400000
         B     COMPREG                                                  48500000
*                                                                       48600000
*                                                                       48700000
********************************************************************    48800000
         EJECT                                                          48900000
FINDNEXT EQU   *                                                        49100000
*FIND NEXT MINOR TO BE OUTPUT SO THAT FREEING WILL START THERE          49200000
         TM    CQEFLAG,CQEATTOP  SHOULD MINOR SEARCH BEGIN       Y02958X49400000
                                        AT TOP OF CHAIN                 49500000
         BO    GETFIRST            YES, BRANCH TO GET FIRST MINOR       49600000
         L     WQEBASE,DCMAMIN     GET MINOR POINTER SAVED              49700000
         TM    DCMAFLG2,DCMALMIN   Q. LAST MINOR OUTPUT SAVED           49800000
         BO    GETNEXT             YES, BRANCH TO GET NEXT MINOR        49900000
         LTR   WQEBASE,WQEBASE     Q. ZERO POINTER SAVED                50000000
         BZ    MINPTR0             YES, BRANCH TO HANDLE                50100000
         BR    X14                 RETURN WITH POINTER SAVED TO        X50200000
                                        NEXT MINOR                      50300000
         DROP  WQEBASE                                                  50400000
         USING WMJM,WQEBASE         BASE WQE                            50500000
         SPACE                                                          50600000
MINPTR0  EQU   *                                                        50700000
         L     WQEBASE,CQEWQE GET POINTER TO MAJOR WQE         Y02958   50800000
         TM    WMJMMLW,WMJMMLWH    Q. NULL MINOR ON QUEUE               50900000
         BNO   GETFIRST            NO, GET POINTER TO FIRST            X51000000
                                        MINOR                           51100000
         SR    WQEBASE,WQEBASE     INDICATE NO MINOR WAITING TO        X51200000
                                        BE OUTPUT                       51300000
         BR    X14                 RETURN                               51400000
         SPACE                                                          51500000
GETFIRST EQU   *                                                        51600000
         L     WQEBASE,CQEWQE GET POINTER TO MAJOR               Y02958 51700000
         L     WQEBASE,WMJMMIN     GET POINTER TO FIRST MINOR           51800000
         NI    CQEFLAG,N255-CQEATTOP TURN OFF TOP OF CHAIN       Y02958X51900000
                                        FLAG                            52000000
         NI    DCMAFLG2,N255-DCMALMIN TURN OFF LAST MINOR FLAG @ZA14992 52030000
         XC    DCMAMIN(X4),DCMAMIN  ZERO MINOR PTR IN SACB     @ZA14992 52060000
         BR    X14                 RETURN                               52100000
         SPACE                                                          52200000
GETNEXT  EQU   *                                                        52300000
* FIRST CHECK TO SEE IF LAST MINOR OUTPUT IS STILL ON QUEUE             52400000
         L     WQEBASE,CQEWQE GET POINTER TO MAJOR             Y02958   52500000
         L     WQEBASE,WMJMMIN     GET POINTER TO FIRST MINOR           52600000
LOOKNEXT EQU   *                                                        52700000
         LA    WQEBASE,N0(WQEBASE) CLEAR HIGH ORDER BYTE                52800000
         LTR   WQEBASE,WQEBASE     END OF MINOR QUEUE                   52900000
         BNZ   CHKEQ               NO, BRANCH TO SEE IF EQUAL TO       X53000000
                                        LAST OUTPUT                     53100000
** 1 LINE OF CODE MOVED FM HERE TO 4 INST AFTER LABEL GETFIRST @ZA14992 53200000
         B     GETFIRST            GET POINTER TO FIRST MINOR ON       X53300000
                                        QUEUE SINCE LAST MINOR         X53400000
                                        OUTPUT IS NO LONGER ON Q        53500000
CHKEQ    EQU   *                                                        53600000
         C     WQEBASE,DCMAMIN     MINOR POINTER EQUAL TO              X53700000
                                        POINTER TO LAST MINOR          X53800000
                                        OUTPUT                          53900000
         BE    LASTONQ             YES, LAST OUTPUT IS ON QUEUE         54000000
         L     WQEBASE,WMNMNX1-N1  NO, LOOK AT                          54100000
         B     LOOKNEXT                 NEXT MINOR ON QUEUE             54200000
* LAST MINOR OUTPUT IS STILL ON QUEUE - CONTINUE FROM THERE             54300000
LASTONQ  EQU   *                                                        54400000
         L     WQEBASE,WMNMNX1-N1  GET CHAIN FIELD OF LAST             X54500000
                                        MINOR OUTPUT                    54600000
         LA    WQEBASE,N0(WQEBASE) CLEAR HIGH ORDER BYTE                54700000
         LTR   WQEBASE,WQEBASE     Q. ANY MORE MINORS                   54800000
         BCR   NOMORE,X14          NO, RETURN                           54900000
         NI    DCMAFLG2,N255-DCMALMIN TURN OFF LAST MINOR FLAG          55000000
         BR    X14                 RETURN                               55100000
         EJECT                                                          55300000
********************************************************************    55400000
START    EQU   *                                                        55500000
         L     XFBRANCH,DCMNINT7   LOAD INTERFACE 7 ADDRESS             55700000
         B     XCTL                EXIT TO IEECVFTT                     55800000
*                                                                       55900000
IODISP2  EQU   *                                                        56100000
         OI    DCMIOCM2,DCMWINFD   INDICATE WRITE DISPLAY               56200000
IOEXIT   EQU   *                                                        56400000
         L     XFBRANCH,DCMIORTN   LOAD IO ROUTINE ADDRESS              56500000
*                                  FOR EXIT TO IEECVET(H,P,R, OR U)     56600000
*                                                                       56800000
XCTL     EQU   *                                                        56900000
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS                 57000000
         BR    XFBRANCH            EXIT ADDRESS ALREADY SET             57100000
*                                                                       57200000
********************************************************************    57300000
*                                                                       57400000
BLANKWRT MVC   N1(N0,X5),N0(X5) EXEC. FOR BLANKING WRITE AREA           57500000
*                                                                       57600000
********************************************************************    57700000
ONE      DC    H'1'                CONSTANT                             57800000
TWO      DC    H'2'                CONSTANT                             57900000
THREE    DC    H'3'                CONSTANT                             58000000
FOUR     DC    H'4'                CONSTANT                             58100000
KED      DC    C'E,D '             CONSTANT FOR K E,D                   58200000
*                                                                       58300000
********************************************************************    58400000
         EJECT                                                          58500000
********************************************************************    58600000
*                                                                       58700000
*    ALL DSECTS FOLLOW THIS POINT                                       58800000
*                                                                       58900000
********************************************************************    59000000
         IEETDCM                                                        59100000
         EJECT                                                          59200000
         IHACTM  CXSA                                                   59300000
         EJECT                                                          59400000
         IHACTM  CQE                                                    59500000
         EJECT                                                          59600000
         IEECUCM FORMAT=NEW                                             59700000
         EJECT                                                          59800000
         IEERDCM                                                        59900000
KPARM    DSECT                                                          60000000
         IEECVMAP KPARAM                                                60100000
         EJECT                                                          60200000
         IHAWQE  DSECT=YES                                              60300000
*********************************************************************** 60400000
         END                                                            60600000
