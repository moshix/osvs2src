IEECVFTT CSECT                                                          00300000
*C081000,084500,114000                                        MH Y01565 00600000
*D107000,107500,137000-142000                                 MH Y01565 00900000
* C 080500-081000,084000-084500,093500-094000                 MB YM6838 01200000
*    THIS ROUTINE WAS CREATED FOR STATUS DISPLAY SUPPORT, LINE ITEM     01500000
*    21002, OS RELEASE 21.                                              01800000
         TITLE 'IEECVFTT - STATUS DISPLAY BLANKING FUNCTION'            02100000
*********************************************************************** 02400000
*                                                                     * 02700000
* STATUS                                                              * 03000000
*    CHANGE LEVEL=0                                                   * 03300000
*                                                                     * 03600000
* APARS FIXED ZA10060                                                 * 03900000
*                                                                     * 04200000
* FUNCTION                                                            * 04500000
*    TO OVERLAY WITH BLANKS ANY MESSAGE THAT IS BELOW  OR             * 04800000
*    BETWEEN  A   DISPLAY.                                            * 05100000
*    TO INDICATE IN THE SSCT THAT ALL LINES BELOW A DISPLAY HAVE      * 05400000
*    BEEN APPROPRIATELY BLANKED.                                      * 05700000
*                                                                     * 06000000
* ENTRY POINT                                                         * 06300000
*    IEECVFTT (CSECT NAME)                                            * 06600000
*                                                                     * 06900000
* INPUT                                                               * 07200000
*    REG 1 POINTS TO CXSA WHICH POINTS TO DCM  WHICH CONTAINS INDI-   * 07500000
*    CATORS.                                                          * 07800000
*    K COMMAND PARAMETER LIST POINTED TO IN THE DCM                   * 08100000
*    DCMADOPN POINTS TO OPERAND(S) IN PARAMETER LIST                  * 08400000
*                                                                     * 08700000
* OUTPUT                                                              * 09000000
*    BIT SETTINGS IN DCM AND SACB FOR OTHER ROUTINES TO ANALYZE       * 09300000
*    BLANKED SCREEN AREAS BELOW OR BETWEEN DISPLAYS                   * 09600000
*                                                                     * 09900000
* EXTERNAL REFERENCES                                                 * 10200000
*    SVC 34                                                           * 10500000
*                                                                     * 10800000
* EXITS,NORMAL                                                        * 11100000
*    I/O RTN    (DEVICE-DEPENDENT)                                    * 11400000
*    TO BLANK OUT MESSAGE AREAS BETWEEN  OR BELOW STATUS DISPLAYS.    * 11700000
*                                                                     * 12000000
* EXITS,ERROR                                                         * 12300000
*    TO PROCCESOR 1 UPON INCORRECT COMMAND INPUT                      * 12600000
*                                                                     * 12900000
* ATTRIBUTES                                                          * 13200000
*    REFRESHABLE, PRIVILEGED, TYPE 4 SVC                              * 13500000
*                                                                     * 13800000
* TABLES/WORK AREAS                                                   * 14100000
*    UCM, DCM,CXSA, WQE, SACB                                         * 14400000
*                                                                     * 14700000
*                                                                     * 15000000
*********************************************************************** 15300000
         EJECT                                                          15600000
*/*IEECVFTT: CHART */                                                   15900000
*/* E IEECVFTT */                                                       16200000
*/*	BASES: P (,TSTBIT1) ESTABLISH ADDESSABILITY */                      16500000
*/*TSTBIT1: D (YES,BLANKRTN,NO,TSTBIT2) RETURN FROM I/O COMPLETE */     16800000
*/*TSTBIT2: D (YES,START,NO,ERROR) BLANKING FUNCTION REQUESTED */       17100000
*/*ERROR: P (,XCTL) MOVE IN PROCESSOR 1 NAME */                         17400000
*/*START: P (,VIEWABLE) TURN BLANKING FUNCTION BIT OFF */               17700000
*/*VIEWABLE: P (,DISPLAY) FIND TOP DISPLAY'S FIRST LINE */              18000000
*/*DISPLAY: P (,DISPLAY2) FIND FIRST LINE BELOW TOP DISPLAY */          18300000
*/*DISPLAY2: P (,BLANKIT2) FIND ZERO SSCT BYTE */                       18600000
*/* BLANKIT2: D (YES,BLANKIO,NO,SKIPBLNK) LINE ALREADY BLANKED */       18900000
*/*SKIPBLNK: D (YES,BLANKIO,NO,TSTMSGIN) END OF SSCT */                 19200000
*/*TSTMSGIN: D (YES,BLNKONLY,NO,	BLNK1) IS A MSG UNDERNEATH */          19500000
*/*	BLNK1: P (,BLNKONLY) BUMP BLANK LINE COUNTER */                     19800000
*/*BLNKONLY: P (,	SKP01) SET BLANK INDIC ON IN SSCT */                  20100000
*/*	SKP01: D (YES,BLANKIO,NO,BLANKIT2) DISPLAY LINE */                  20400000
*/*BLANKIO: P (,COMPREG) SAVE TABLE POINTERS */                         20700000
*/*COMPREG: D (YES,SAVECNT,NO,	CMP1) MORE THAN 3 LINES TO BLANK */      21000000
*/*	CMP1: D (YES,CONTCNT,NO,FINISHED) ANY LINES TO BLANK */             21300000
*/*CONTCNT: P (,IODISP2) BLANK ENTRY AREA AND INSTR. LINE */            21600000
*/* E BLANKET */                                                        21900000
*/*	B1: P (,	B2) BLANK A LINE */                                        22200000
*/*	B2: R RETURN TO CALLER */                                           22500000
*/*SAVECNT: P (,CONTCNT) SAVE LINE COUNT MINUS 3 FOR NEXT ROUND */      22800000
*/*BLANKRTN: P (,COMPREG) TURN DCMCMIN5 OFF; GET LINE COUNT TO BLANK    23100000
*/**/                                                                   23400000
*/*IODISP2: P (,IOEXIT) INDICATE TO I/O TO WRITE DISPLAY */             23700000
*/*IOEXIT: P (,XCTL) MOVE IN I/O NAME */                                24000000
*/*XCTL: R RETURN */                                                    24300000
*/*IEECVFTT: END */                                                     24600000
        EJECT                                                           24900000
*********************************************************************** 25200000
*    REGISTERS                                                          25500000
*                                                                       25800000
********************************************************************    26100000
X1PARM   EQU   1                                                        26400000
XERTRN   EQU   14                                                       26700000
XFBRANCH EQU   15                                                       27000000
XF       EQU   15                  WORK REG                             27300000
X14      EQU   14                  RETURN REG                           27600000
RETURN   EQU   14                  RETURN REG                           27900000
XD       EQU   13                  WORK REG                             28200000
SACBPT   EQU   13                  SACB BASE REG                        28500000
XC       EQU   12                  THIS RTN'S BASE REG                  28800000
XB       EQU   11                  CXSA BASE REG                        29100000
XA       EQU   10                  UCM BASE REG                         29400000
X9       EQU   9                   TRANSIENT DCM BASE REG               29700000
XRDCM    EQU   8                   RESIDENT DCM BASE REG                30000000
LIST     EQU   7                   UCMLIST BASE REG                     30300000
X6       EQU   6                   WORK REG                             30600000
X5       EQU   5                   WORK REG                             30900000
X4       EQU   4                   WORK REG                             31200000
X3       EQU   3                   WORK REG                             31500000
X2       EQU   2                   WORK REG                             31800000
X1       EQU   1                   WORK REG                             32100000
X0       EQU   0                   WORK REG                             32400000
         EJECT                                                          32700000
********************************************************************    33000000
*     EQUATES                                                           33300000
********************************************************************    33600000
ID1      EQU   C'F'                1ST CHARACTER OF CSECT ID            33900000
ID2      EQU   C'T'                2ND CHARACTER OF CSECT ID            34200000
E2       EQU   X'E2'               NUMBER                               34500000
ZERO     EQU   X'00'               NUMBER                               34800000
C0       EQU   X'C0'               NUMBER                               35100000
BLANK    EQU   X'40'               NUMBER                               35400000
N0       EQU   0                   NUMBER                               35700000
N1       EQU   1                   NUMBER                               36000000
N2       EQU   2                   NUMBER                               36300000
N3       EQU   3                   NUMBER                               36600000
N4       EQU   4                   NUMBER                               36900000
NC       EQU   12                  NUMBER                               37200000
NE       EQU   14                  NUMBER                               37500000
N255     EQU   255                 NUMBER                               37800000
EIGHT    EQU   X'80'               CONTROL LINE OF DISPLAY              38100000
         EJECT                                                          38400000
********************************************************************    38700000
*                                                                       39000000
*    ESTABLISH ADDRESSABILITY                                           39300000
*                                                                       39600000
********************************************************************    39900000
         SPACE 2                                                        40200000
         BALR  XC,N0               ESTABLISH ROUTINE'S                  40500000
         USING *,XC                BASE REGISTER                        40800000
         B     BEGIN               BRANCH AROUND PATCH AREA             41100000
ICATCH   DC    CL8'IEECVFTT'      EYECATCHER AND FIELD MAINTENANCE      41400000
         DC    CL8'&SYSDATE',CL56' ' DATE AND PATCH AREA       Y02958   41700000
BEGIN    EQU   *                   END OF PATCH AREA                    42000000
         LR    XB,X1               ESTABLISH CXSA                       42300000
         USING CXSA,XB             ADDRESSABILITY                       42600000
         L     XA,CSACTLM          ESTABLISH UCM ADDRESS                42900000
         USING UCM,XA              ADDRESSIBILITY                       43200000
         L     LIST,CSAUCM         LOAD UCM ENTRY ADDRESS               43500000
         USING UCMLIST,LIST        ESTABLISH UCM ENTRY  ADDRESSABILITY  43800000
         L     XRDCM,UCMXB         GET POINTER TO RESIDENT DCM          44100000
         USING DCMTSRT,XRDCM          ESTABLISH R DCM ADDRESSABILITY    44400000
         L     X9,DCMADTRN         LOAD TRANSIENT DCM PT                44700000
         USING DCMSTRT,X9         ESTABLISH TDCM BASE REG               45000000
         MVC   DCMTRACE(DCMTRLEN),DCMTRAC2 SHIFT OLD TRACE ENTRIES      45300000
         MVI   DCMTREN1,ID1        PUT CSECT ID INTO                    45600000
         MVI   DCMTREN2,ID2        NEW TRACE ENTRY                      45900000
         L     XFBRANCH,CSAXC      ADDRESS FREELOCK SUBROUTINE          46200000
         LR    X1PARM,XB           PASS CXSA ADDRESS IN REG 1           46500000
         BALR  XERTRN,XFBRANCH     RELEASE LOCKS                        46800000
*********************************************************************** 47100000
*    TEST IF BLANKING IS IN PROCESS                                     47400000
********************************************************************    47700000
         TM    DCMCOM3,DCMCMIN7    Q. RETURN  FROM I/O COMPLETE         48000000
         BO    BLANKRTN            YES-MORE BLANKING WORK TO DO         48300000
         TM    DCMCOM2,DCMCBLNK    N0--IS BLANKING FUNCTION REQUIRED    48600000
         BO    START               YES-GO CHECK WHAT'S TO BE BLANKED    48900000
********************************************************************    49200000
*    IF NONE OF ABOVE, IT IS AN ERROR THAT SHOULD NEVER HAPPEN.         49500000
*    GO BACK TO PROCESSOR 1 SO THAT IT CAN CONTINUE PROCESSING          49800000
*    WITHOUT CAUSING POSSIBLE SYSTEM FAILURE.                           50100000
********************************************************************    50400000
ERROR    EQU   *                                                        50700000
         L     XFBRANCH,DCMNPROC       LOAD PROC 1 ADDRESS              51000000
         B     XCTL                    EXIT TO IEECVET1                 51300000
START    EQU   *                                                        51600000
         NI    DCMCOM2,N255-DCMCBLNK TURN OFF BLANKING FUNCTION BIT     51900000
         SR    X0,X0               CLEAR REG                            52200000
         L     X3,DCMADSEC         LOAD SSCT ADDR                       52500000
         L     X4,DCMAMTAB         LOAD SCT POINTER                     52800000
********************************************************************    53100000
*    THE FOLLOWING CODE SKIPS VIEWABLE MSGS                             53400000
********************************************************************    53700000
VIEWABLE EQU   *                                                        54000000
         TM    N0(X3),EIGHT       DOES THE LINE CONTAIN A DISPLAY       54300000
         BO    DISPLAY             YES-                                 54600000
         C     X4,DCMADDRL         END OF SCREEN              MB YM6838 54900000
         BH    ERROR               YES, NOTHING TO DO         MB YM6838 55200000
         LA    X3,N1(X3)           NO--BUMP SSCT POINTER                55500000
         LA    X4,N2(X4)           BUMP SCT POINTER                     55800000
         B     VIEWABLE                                                 56100000
********************************************************************    56400000
*    THE FOLLOWING CODE SKIPS THE FIRST DISPLAY                         56700000
********************************************************************    57000000
DISPCNT  EQU   *                                                        57300000
         C     X4,DCMADDRL         END OF SCREEN              MB YM6838 57600000
         BH    ERROR               YES, NOTHING TO DO         MB YM6838 57900000
DISPLAY  EQU   *                                                        58200000
         LA    X3,N1(X3)          BUMP SSCT TABLE POINTER               58500000
         LA    X4,N2(X4)           BUMP SCT POINTER                     58800000
         EJECT                                                          59100000
DISPLAY2 EQU   *                                                        59400000
         CLI   N0(X3),ZERO        Q. IS THERE A DISPLAY IN THIS LINE    59700000
*                                 OR IS THE LINE ALREADY BLANKED        60000000
         BNE   DISPCNT             YES-                                 60300000
BLANKIT1 EQU   *                                                        60600000
         SR    X0,X0               CLEAR REG                            60900000
         LR    X2,X3               PRIME BLANKING STOP ADDR REG         61200000
BLANKIT2 EQU   *                                                        61500000
         TM    N0(X3),DCMSECBL     WAS THIS LINE ALREADY BLANKED        61800000
         BO    BLANKIO            YES-DON'T COUNT IT                    62100000
         C     X4,DCMADDRL         END OF SCREEN              MB YM6838 62400000
         BNH   TSTMSGIN            NO, SEE IF MSG UNDERNEATH  MB YM6838 62700000
         LR    X3,X2               RESET TO SSCT STOP BLANKING ADDR     63000000
         B     BLANKIO2            YES -                                63300000
TSTMSGIN EQU   *                                                        63600000
         TM    N0(X4),DCMMSGIN     IS A MSG LINE UNDERNEATH             63900000
         BNO   BLNKONLY           NO - STOP BLANK LINE COUNT            64200000
         AH    X0,ONE              BUMP COUNTER                         64500000
         LA    X2,N1(X3)           BUMP BLANKING STOP ADDR              64800000
BLNKONLY EQU   *                                                        65100000
         OI    N0(X3),DCMSECBL     SET BLANK LINE INDIC. ON IN SSCT     65400000
SKIPBLNK EQU   *                                                        65700000
         LA    X3,N1(X3)           BUMP SSCT POINTER                    66000000
         LA    X4,N2(X4)           BUMP SCT POINTER                     66300000
         TM    N0(X3),E2           DOES THIS LINE HAVE A DISPLAY IN IT  66600000
         BZ    BLANKIT2            NO--INCLUDE IT IN THE BLANKING COUNT 66900000
         LR    X3,X2               RESET SSCT PT TO STOP BLANK ADDR     67200000
         BCTR  X3,N0               DECR. BY ONE FOR FALLING THRU        67500000
*                                  NEXT INSTRUCTION                     67800000
BLANKIO  EQU   *                                                        68100000
         LA    X3,N1(X3)           BUMP PT BY ONE FOR ALGORITHM FOLLOWS 68400000
BLANKIO2 EQU   *                                                        68700000
         S     X3,DCMADSEC         ISOLATE SSCT LINE NO.                69000000
         LA    X3,N1(X3)           BUMP BY 1 FOR 0 BASE                 69300000
         SR    X3,X0               SUBTR. COUNT TO BLANK                69600000
         STC   X3,DCMDSAV+NE       SAVE FOR LATER              @ZA10060 69900000
         LR    X4,X0               LOAD BLANK LINE COUNT                70200000
COMPREG  EQU   *                                                        70500000
         MVC   DCMLINEN(N1),DCMDSAV+NE MOVE TOP LINE NO.       @ZA10060 70800000
         CH    X4,THREE            Q. MORE THAN 3 LINES TO BLANK        71100000
         BH    SAVECNT             YES-                                 71400000
         LTR   X4,X4               ANY LINES TO BLANK                   71700000
         BNP   START           NO--THEN JOB IS FINISHED       MH Y01565 72000000
         XC    DCMDSAV+NC(N2),DCMDSAV+NC  SET CNTR TO NO LINES @ZA10060 72300000
CONTCNT  EQU   *                                                        72600000
         LR    X3,X4               SAVE NO. OF LINES                    72900000
         MH    X4,DCMCORLN         MULT. LINES BY LENGTH                73200000
         STH   X4,DCMBYTCT         SAVE BYTE COUNT FOR I/O RTN          73500000
         LH    X4,ONE              DO FIRST LINE OF BLANKS              73800000
         L     X5,DCMAINS          SET POINTER TO INSTRUCTION LINE      74100000
         BAL   XF,BLANKET          GO BLANK WRITE AREA                  74400000
         LR    X4,X3               RESTORE LINE COUNT                   74700000
         BCTR  X4,N0               DECREMENT FOR THE ONE JUST DONE      75000000
         LTR   X4,X4               ANY MORE TO BLANK                    75300000
         BNP   IODISP2             NO - STOP PROCESS OF BLANKING        75600000
         L     X5,DCMAENTR         LOAD ENTRY AREA                      75900000
         BAL   XF,BLANKET          GO BLANK WRITE AREA                  76200000
         B     IODISP2             GO TO I/O RTN TO WRITE OUT LINES     76500000
BLANKET  EQU   *                                                        76800000
         MH    X4,DCMLGNTH         MULT. LINE CNT BY LINE LENGTH        77100000
         MVI   N0(X5),BLANK        MOVE A BLANK CHAR                    77400000
         SH    X4,TWO              DECREMENT BY 2                       77700000
         EX    X4,BLANKWRT         SPREAD BLANKS THROUGHOUT LINE        78000000
         BR    XF                                                       78300000
SAVECNT  EQU   *                                                        78600000
         OI    DCMCOM3,DCMCMIN7    INDICATE RETURN TO THIS RTN          78900000
         SH    X4,THREE            DECREMENT LINE COUNT BY 3            79200000
         STH   X4,DCMDSAV+NC       SAVE LINE CNT TO BE DONE    @ZA10060 79500000
         LH    X4,THREE            PRIME LINE COUNT                     79800000
         SR    X3,X3               CLEAR REG                            80100000
         IC    X3,DCMDSAV+NE       LOAD LINE NO.               @ZA10060 80400000
         AR    X3,X4               BUMP LINE NO. COUNT BY 3             80700000
         STC   X3,DCMDSAV+NE       SAVE FOR LATER              @ZA10060 81000000
         B     CONTCNT             GO BLANK LINES                       81300000
BLANKRTN EQU   *                                                        81600000
         NI    DCMCOM3,N255-DCMCMIN7 TURN BIT OFF                       81900000
         SR    X4,X4               CLEAR REG                            82200000
         LH    X4,DCMDSAV+NC       PICK UP LINES TO BLANK      @ZA10060 82500000
         B     COMPREG                                                  82800000
IODISP2  EQU   *                                                        83100000
         OI    DCMIOCM2,DCMWINFD   INDICATE WRITE DISPLAY               83400000
IOEXIT   EQU   *                                                        83700000
         L     XFBRANCH,DCMIORTN   LOAD IO ROUTINE ADDRESS              84000000
*                                  FOR EXIT TO IEECVET(H,P,R, OR U)     84300000
XCTL     EQU   *                                                        84600000
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS                 84900000
         BR    XFBRANCH            EXIT ADDRESS ALREADY SET             85200000
BLANKWRT MVC   N1(N0,X5),N0(X5) EXEC. FOR BLANKING WRITE AREA           85500000
********************************************************************    85800000
ONE      DC    H'1'                CONSTANT                             86100000
TWO      DC    H'2'                                                     86400000
THREE    DC    H'3'                                                     86700000
FOUR     DC    H'4'                                                     87000000
********************************************************************    87300000
         EJECT                                                          87600000
********************************************************************    87900000
*                                                                       88200000
*    ALL DSECTS FOLLOW THIS POINT                                       88500000
*                                                                       88800000
********************************************************************    89100000
         IHACTM  CXSA                                                   89400000
         EJECT                                                          89700000
         IEECUCM  FORMAT=NEW                                            90000000
         EJECT                                                          90300000
         IEERDCM                                                        90600000
         EJECT                                                          90900000
         IEETDCM                                                        91200000
*********************************************************************** 91500000
         END                                                            91800000
