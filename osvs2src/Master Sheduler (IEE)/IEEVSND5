IEEVSND5 CSECT                                                          00050020
         TITLE  'IEEVSND5 - OPERATOR SEND COMMAND I/O ROUTINE'          00100020
*/*IEEVSND5:  CHART */                                                  00150020
*/*HEADER                                                               00200020
*/*IEEVSND5 - OPERATOR SEND COMMAND I/O ROUTINE */                      00250020
*/*           E  IEEVSND5 */                                            00350020
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * 00400020
*                                                                     * 00450020
*    STATUS: CHANGE LEVEL 000                                         * 00500020
*                                                                     * 00700020
*    FUNCTION:                                                        * 00750020
*            THIS ROUTINE PERFORMS     I/O OPERATIONS FOR THE SEND    * 00800020
*            COMMAND.  FUNCTIONS PROVIDED ARE:                        * 00850020
*                                       READ                          * 00900020
*                   CLOSE               WRITE 'DI'                    * 00950020
*                                       WRITE 'DAF'                   * 01000020
*                                                                     * 01050020
*            NECESSARY INFORMATION FOR THE OPERATIONS DESIRED IS      * 01100020
*            SPECIFIED IN THE PARAMETER LIST POINTED TO BY REGISTER1: * 01150020
*                                                                     * 01200020
*                   ---------------------------                       * 01250020
*                   - FLAGS(2) KEY(1) FDBK(1) -                       * 01300020
*                   ---------------------------                       * 01350020
*                   -   RBA FOR READ/WRITE    -                       * 01400020
*                   -    RBA FOR FEEDBACK     -                       * 01450020
*                   ---------------------------                       * 01500020
*                   -      AREA ADDRESS       -                       * 01550020
*                   ---------------------------                       * 01600020
*                   -   DDNAME  (8 BYTES)     -                       * 01650020
*                   -                         -                       * 01700020
*                   ---------------------------                       * 01750020
*                   -      LIMIT COUNT        -                       * 01800020
*                   ---------------------------                       * 01850020
*                   -      DCB ADDRESS        -                       * 01900020
*                   ---------------------------                       * 01950020
*                   -     JFCB ADDRESS        -                       * 01952020
*                   ---------------------------                       * 01960020
*                                                                    *  02000020
*                                                                    *  02050020
*    ENTRY POINT: IEEVSND5                                           *  02100020
*                                                                    *  02150020
*                                                                    *  02200020
*    INPUT:  REGISTER 1 - ADDRESS OF PARAMETER LIST                  *  02250020
*            REGISTER 14- RETURN ADDRESS                             *  02300020
*            REGISTER 15- ENTRY POINT ADDRESS                        *  02350020
*                                                                    *  02400020
*                                                                    *  02450020
*    OUTPUT:                                                         *  02500020
*            REGISTER 1 - ADDRESS OF PARAMETER LIST                  *  02550020
*            REGISTER15 - RETURN CODE                                *  02600020
*                                                                    *  02650020
*    EXTERNAL REFERENCES:                                            *  02700020
*             GETMAIN, FREEMAIN,                                     *  02750020
*            READ, WRITE, CHECK, CLOSE,                              *  02800020
*                                                                    *  02950020
*                                                                    *  03000020
*    EXITS, NORMAL: CONTROL RETURNS TO CALLER                        *  03050020
*                                                                    *  03100020
*    EXITS, ERROR:                                                   *  03150020
*            THIS ROUTINE IS COVERED BY THE STAI SET UP IN IEEVGPSD. *  03160020
*            STAI EXIT ROUTINE RETURNS TO IEEVGPSD.                  *  03170020
*                                                                    *  03200020
*    TABLES/WORK AREAS:                                              *  03250020
*            CVT - TO GET PTR TO UCB LOOKUP TABLE                    *  03300020
*            DCB - DATA CONTROL BLOCK - FOR BROADCAST DATA SET       *  03350020
*            DECB - DATA EVENT CONTROL BLOCK                         *  03400020
*            ECB - EVENT CONTROL BLOCK - TO TEST EXCEPTION CODE      *  03450020
*                    BIT SETTINGS AFTER I/O OPERATION                *  03500020
*            JFCB - JOB FILE CONTROL BLOCK                           *  03550020
*            TCB - TASK CONTROL BLOCK - TO GET ADDRESS OF TIOT       *  03600020
*            TIOT - ENTRY FOR BROADCAST DATA SET IN MASTER'S TIOT    *  03650020
*            UCB - UNIT CONTROL BLOCK - ADDR IS PUT INTO TIOT ENTRY  *  03700020
*                                                                    *  03750020
*                                                                    *  03800020
*    ATTRIBUTES: REENTRANT                                           *  03850020
*                                                                     * 03900020
*    CHARACTER DEPENDENCY CODE:  NONE.                                * 03902020
*                                                                     * 03910020
*    NOTES:                                                           * 03950020
*         1. STANDARD LINKAGE CONVENTIONS USED.                       * 04000020
*         2. THIS ROUTINE IS CALLED BY OPERATOR SEND -                * 04050020
*                                       IEEVSND2, IEEVSND3, IEEVSND8  * 04100020
*                                                                     * 04200020
*                                                                     * 04250020
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * **  04300020
         EJECT                                                          04350020
*********************************************************************** 04400020
*                                                                     * 04450020
*                   R E G I S T E R    E Q U A T E S                  * 04500020
*                                                                     * 04550020
*********************************************************************** 04600020
*                                                                     * 04650020
R0       EQU   0                   REGISTER USED IN GETMAIN, FREEMAIN   04700020
R1       EQU   1                   WORK REGISTER                        04750020
R2       EQU   2                   WORK REGISTER                        04800020
R3       EQU   3                   ADDRESS OF 129 BYTE READ/WRITE AREA  04850020
R4       EQU   4                   ADDRESS OF RBA (RELATIVE BLOCK       04900020
*                                     ADDRESS) OF DESIRED BLOCK         04950020
R5       EQU   5                   WORK REGISTER                        05000020
R6       EQU   6                   RESERVED FOR MAINTENANCE             05050020
R7       EQU   7                   ADDRESS OF PARAMETER LIST            05100020
R8       EQU   8                   WORK REGISTER                        05150020
R9       EQU   9                   BASE REG FOR VARIOUS CONTROL BLOCKS  05200020
R10      EQU   10                  BASE REGISTER FOR DSECT FOR DCB      05250020
R11      EQU   11                  BASE REGISTER FOR CSECT              05300020
R12      EQU   12                  BASE REGISTER FOR DATA AREA DSECT    05350020
R13      EQU   13                  ADDRESS OF THIS ROUTINE'S SAVE AREA  05400020
R14      EQU   14                  ADDRESS OF CALLING ROUTINE RETURN    05450020
R15      EQU   15                  RETURN CODE                        * 05500020
         EJECT                                                          05550020
*                                                                     * 05600020
*                                                                     * 05650020
*/*           P  SAVE REGISTER CONTENTS */                              05700020
         SAVE  (14,12)             SAVE ALL REGISTERS                   05750020
         LR    R7,R1               SAVE ADDRESS OF PARMLIST IN REG7     05800020
         LR    R11,R15             R11 = ADDRESS IEEVSND5 CSECT         05850020
*/*           P  ESTABLISH ADDRESSIBILITY FOR CSECT */                  05900020
         USING IEEVSND5,R11        SET ADDRESSABILITY FOR CSECT         05950020
         MODID  BRANCH=YES                                       Y01886 06000001
         LA    R5,DATAEND-DATABEG  R5 = LENGTH OF DATA AREA FOR GETMAIN 06250020
         SLL   R5,LENG8            SHIFT GETMAIN LENGTH                 06300020
         LA    R4,SP252            REG4 = SUBPOOL INDICATOR = 252       06350020
         SRDL  R4,LENG8            GET SUBPOOL IN HI ORDER BYTE         06400020
*                                  OF REG5.   LENGTH IN LO ORDER        06450020
         LR    R0,R5               SET R0 FOR GETMAIN                   06500020
         GETMAIN  R,LV=(0)         GET CORE FOR WORK AREA               06550020
         LR    R12,R1              MOVE ADDR OF GOTTEN CORE TO REG 12   06600020
         USING DATABEG,R12         SET ADDRESSABILITY FOR DATA AREA     06650020
         ST    R13,SAVER+OFFCALLR  SAVE ADDRESS OF CALLER'S SAVE AREA   06700020
         LR    R5,R13              GET ADDRESS CALLERS SAVE AREA        06750020
         LA    R13,SAVER           R13 = ADDR THIS RTN'S SAVE AREA      06800020
         ST    R13,OFFCALLD(R5)    SAVE ADDRESS OF THIS RTN'S SAVE AREA 06850020
         MVC   DECB1(LISTSL),DEC01 MOVE LISTS FOR MACRO INSTRUCTIONS    06900020
*                                  INTO GOTTEN CORE (DSECT)             06950020
         USING IHADCB,R10          ESTABLISH ADDRESSABILITY FOR IHADCB  07000020
         USING SDIOPARM,R7         SET ADDRESSABILITY FOR PARMLIST DSEC 07050020
         EJECT                                                          10000020
TESTREAD DS    0H                  READ BY BLOCK ID                     10050020
*/*TESTREAD:  D  (YES,,NO,TESTWRDI) IS READ REQUESTED */                10100020
         TM    PARM1,P1READ        IS READ REQUESTED                    10150020
         BZ    TESTWRDI            NO, BRANCH                           10200020
*/*           P  GET DCB PTR FROM PARM LIST */                          10250020
         L     R10,PARMDCB         R10 = ADDRESS OF DCB FROM PARM       10300020
         L     R3,PARMIOAD         SET AREA ADDRESS FROM PARM LIST      10350020
         L     R4,PARMRBA          SET BLOCK ID (RBA) FROM PARM LIST    10400020
*/*           I  READ REQ'D RECORD */                                   10450020
         READ  DECB1,DIF,(R10),(R3),'S',0,(R4),MF=E  READ A RECORD      10500020
         CHECK DECB1               CHECK THE READ                       10550020
         MVC   PARMFDBK(L'PARMFDBK),RECPOS(R4)   PUT FEEDBACK OF RECORD 10600020
*                                  POSITION ON TRACK INTO PARM LIST     10650020
*/*           P  (,ENDIO) RETURN CODE= 0 SUCCESSFUL PROCESSING */       10700020
         XC    RETCODE,RETCODE     SET RETCODE = 0 FOR OPERATION DONE   10750020
         B     ENDIO               END OF PROCESSING                    10800020
         EJECT                                                          10850020
TESTWRDI DS    0H                  WRITE BY BLOCK ID                    10900020
*/*TESTWRDI:  D  (YES,,NO,TESTWRDA) IS WRITE 'DI' REQUESTED */          10950020
         TM    PARM2,P2WRDI        IS WRITE 'DI' REQUESTED              11000020
         BZ    TESTWRDA            NO, BRANCH                           11050020
*/*           P  GET DCB PTR FROM PARM LIST */                          11100020
         L     R10,PARMDCB         R10 = ADDRESS OF DCB FROM PARM       11150020
         L     R3,PARMIOAD         SET AREA ADDRESS FROM PARMLIST       11200020
         L     R4,PARMRBA          SET BLOCK ID (RBA) FROM PARMLIST     11250020
         MVC   KEY(L'PARMKEY),PARMKEY   SET KEY FROM PARMLIST           11300020
*/*           D  (NO,,YES,NOTDEL) IS WRITE A DELETION */                11350020
         TM    PARM2,P2DEL         TEST IF THIS WRITE IS A DELETION     11400020
         BNO   NOTDEL              NO, BRANCH AROUND FIRST BYTE SETTING 11450020
*/*           P  SET 1ST BYTE DATA = RECORD POSITION ON TRACK */        11500020
         MVC   0(L'PARMFDBK,R3),PARMFDBK  PUT POSITION OF RECORD ON     11550020
*                                  TRACK INTO FIRST BYTE OF DATA RECORD 11600020
*                                  TO SHOW 'AVAILABLE' TO BDAM          11650020
NOTDEL   DS    0H                  THIS WRITE IS NOT FOR A DELETION     11700020
*/*NOTDEL:    I  WRITE REQ'D RECORD */                                  11750020
         WRITE DECB2,DI,(R10),(R3),'S',KEY,(R4),MF=E  WRITE A RECORD    11800020
*                                                     BY BLOCK ID (RBA) 11850020
         CHECK DECB2               CHECK WRITE                          11900020
*/*           P  (,ENDIO) RETURN CODE= 0 SUCCESSFUL PROCESSING */       11950020
         XC    RETCODE,RETCODE     SET RETCODE = 0 FOR OPERATION DONE   12000020
         B     ENDIO               END OF PROCESSING                    12050020
         SPACE 4                                                        12100020
TESTWRDA DS    0H                  WRITE 'DA'                           12150020
*/*TESTWRDA:  D  (YES,,NO,TESTCLOS) IS WRITE 'DA' REQUESTED */          12200020
         TM    PARM2,P2WRDAF       IS WRITE 'DA' REQUESTED              12250020
         BZ    TESTCLOS            NO, BRANCH                           12300020
*/*           P  GET DCB PTR FROM PARM LIST */                          12350020
         L     R10,PARMDCB         R10 = ADDRESS OF DCB FROM PARM       12400020
         LA    R5,SYNWRDA          R5 = ADDR SPECIAL SYNAD RTN          12450020
         ST    R5,WORK4            CHANGE SYNAD ROUTINE ADDRESS IN DCB  12500020
         MVC   DCBSYNAD+NEXT(RBAL),WORK4+NEXT  PUT SYNAD ADDR IN DCB    12510020
*/*           P  SET LIMIT OF RECORDS TO BE SEARCHED TO FIND AVAILABLE  12550020
*/*SPACE */                                                             12600020
         MVC   DCBLIMCT+NEXT(HALF),PARMLMCT+HALF  SET LIMIT COUNT IN    12650020
*                                  DCB FOR EXTENDED SEARCH              12700020
*                                  TO NO. RECORDS IN DAT SET FROM REC 1 12750020
         L     R3,PARMIOAD         SET AREA ADDR FROM PARMLIST          12800020
         L     R4,PARMRBA          SET BLOCK ID FROM PARMLIST           12850020
         MVC   KEY(L'PARMKEY),PARMKEY   SET KEY FROM PARMLIST           12900020
*/*           I  WRITE REQ'D RECORD */                                  12950020
         WRITE DECB3,DAF,(R10),(R3),'S',KEY,(R4),MF=E WRITE RECORD      13000020
         CHECK DECB3               CHECK THE WRITE                      13050020
*                                  R4 = ADDRESS OF FEEDBACK RBA.        13100020
*                                  PUT FEEDBACK RBA INTO PARM LIST      13110020
         MVC   PARMRBA+NEXT(RBAL),ZER(R4)                               13150020
         LA    R5,SYNAD1                                                13200020
         ST    R5,WORK4            CHANGE SYNAD ROUTINE ADDRESS IN DCB  13250020
         MVC   DCBSYNAD+NEXT(RBAL),WORK4+NEXT PUT SYNAD ADDR IN DCB     13260020
*/*           P  (,ENDIO) RETURN CODE= 0 SUCCESSFUL PROCESSING */       13300020
         XC    RETCODE,RETCODE     SET RETCODE = 0 FOR OPERATION DONE   13350020
         B     ENDIO               END OF PROCESSING                    13400020
         EJECT                                                          13450020
TESTCLOS DS    0H                  CLOSE                                13500020
*/*TESTCLOS:  D  (YES,,NO,ENDIO) IS CLOSE REQUESTED */                  13550020
         TM    PARM2,P2CLOSE       IS CLOSE REQUESTED                   13600020
         BZ    ENDIO               NO, BRANCH                           13650020
*                                  CLOSE SYS1.BRODCAST DATA SET         13700020
*/*           P  GET DCB PTR FROM PARM LIST */                          13750020
         L     R10,PARMDCB         R10 = ADDRESS OF DCB FROM PARM       13800020
*/*           I  CLOSE BROADCAST DATA SET */                            13850020
         CLOSE  ((R10)),MF=(E,CLOSEL)  CLOSE DATA SET                   13900020
*/*           P  (,ENDIO) RETURN CODE= 0 SUCCESSFUL PROCESSING */       13950020
         XC    RETCODE,RETCODE     SET RETCODE = 0 FOR OPERATION DONE   14000020
FREEDCB  DS    0H                  FREE CORE GOTTEN FOR DCB             14050020
         LA    R5,DCBEND-IHADCB    R5 = LENGTH TO BE FREED              14100020
*/*FREEDCB:   P  FREEMAIN CORE FOR DCB */                               14150020
         LA    R2,SP0              SET SUBPOOL INDICATOR FOR 0          14200020
         FREEMAIN  R,LV=(R5),A=(R10),SP=(R2) FREE CORE FOR DCB          14250020
*                                                                       14300020
*     CHECK IF JFCB CORE WAS GOTTEN VIA GETMAIN AND MUST BE FREED.      14350020
*                                                                       14400020
*/*           D   (YES,,NO,NOJFCB) WAS CORE GOTTEN FOR JFCB */          14450020
         TM    PARM2,P2JFCB        MUST JFCB CORE BE FREED?             14500020
         BZ    NOJFCB              NO, BRANCH AROUND FREEMAIN           14550020
*/*           P   FREE CORE FOR JFCB */                                 14600020
         L     R5,PARMJFCB         R5 = ADDRESS OF JFCB FROM PARM LIST  14650020
         LA    R8,JFCBLNG          R8 = JFCB LENGTH FOR FREEMAIN        14700020
         FREEMAIN  R,LV=(R8),A=(R5),SP=0    FREE CORE FOR JFCB          14750020
NOJFCB   DS    0H                  JFCB CORE HAS BEEN FREED             14800020
*/*NOJFCB:    P  (,ENDIO) DO CLEANUP PROCESSING */                      14850020
         B     ENDIO               END OF PROCESSING                    14900020
         EJECT                                                          22700020
*/*ENDIO:     P  RELOAD CALLER REGISTERS */                             22750020
ENDIO    DS    0H                  EXIT CODE                            22800020
         L     R13,SAVER+OFFCALLR  RELOAD R13 WITH CALLER SAVE AREA ADD 22850020
         SR    R14,R14             CLEAR REG 14                         22900020
         IC    R14,RETCODE         SAVE RETURN CODE IN REG 14           22950020
*                                  BEFORE ISSUING FREEMAIN              23000020
         LA    R8,DATAEND-DATABEG  SET R8 FOR FREEMAIN FOR WORK AREA    23050020
*        R12 HAS ADDRESS OF WORK AREA GOTTEN VIA GETMAIN                23100020
*/*           P  FREEMAIN CORE FOR WORK DSECT */                        23150020
         LA    R6,SP252            LOAD SUBPOOL NUMBER                  23170020
         FREEMAIN  R,LV=(R8),A=(R12),SP=(R6)   FREE CORE FOR WORK AREA  23200020
         LM    R0,R12,OFF0(R13)    RELOAD REGS ZERO THRU 12             23250020
*/*           P  PUT RETURN CODE IN REG 15 */                           23300020
         LR    R15,R14             PUT RETURN CODE INTO REG 15          23350020
         L     R14,OFF14(R13)      RELOAD REG 14 FROM SAVEAREA          23400020
*/*           R  RETURN TO CALLER */                                    23450020
         BR    R14                 RETURN TO CALLER                     23500020
         SPACE 4                                                        23550020
*                                                                     * 24000020
*/*SYNWRDA:   P  SET PTR TO SYNAD RTN */                                24050020
SYNWRDA  DS    0H                  SYNAD ROUTINE FOR WRITE 'DAF' TYPE   24100020
         LA    R5,SYNAD1           R5 = SYNAD ROUTINE ADDR              24150020
         ST    R5,WORK4            CHANGE SYNAD ROUTINE ADDRESS IN DCB  24160020
         MVC   DCBSYNAD+NEXT(RBAL),WORK4+NEXT PUT SYNAD ADDR IN DCB     24170020
         LR    R5,R0               REG0 = ADDRESS OF DECB               24250020
*/*           D  (YES,SYNAD1,NO,) IS ECB = 'SPACE NOT FOUND' */         24300020
         TM    NEXT(R5),ECBSPAC    TEST ECB FOR 'SPACE NOT FOUND'       24350020
         BNO   SYNAD1              IF OFF, BRANCH                       24400020
*/*           P  (,ENDIO) SET RETURN CODE FOR 'DATA SET FULL' */        24450020
         OI    RETCODE,DSFULL      SET RETURN FOR DATA SET FULL         24500020
         B     ENDIO               END OF PROCESSING                    24550020
         SPACE 4                                                        24600020
*/*SYNAD1:    D  (YES,,NO,SYNA) I/O ERROR? */                           25000020
SYNAD1   DS    0H                  SYNAD ROUTINE                        25050020
         LR    R5,R0               REG0 = ADDRESS OF DECB               25100020
         TM    NEXT(R5),ECBIOERR   TEST ECB FOR IO ERROR                25150020
         BNO   SYNA                NO, BRANCH TO SET RETURN CODE        25200020
*/*           P  (,ENDIO) SET RETURN CODE FOR IO ERROR */               25250020
         OI    RETCODE,IOERR       SET RETURN CODE TO IO ERROR          25300020
         B     ENDIO               END OF PROCESSING                    25350020
*                                                                     * 25400020
*/*SYNA:      P  (,ENDIO) SET RETURN CODE TO 'SEND ERROR' */            25450020
SYNA     DS    0H                  PROBLEM IN SEND PROCESSOR            25500020
         OI    RETCODE,SDERR       SET RETURN CODE FOR 'SEND ERROR'     25550020
         B     ENDIO               END OF PROCESSING                    25600020
*                                                                     * 25650020
         SPACE 2                                                        25950020
*/*BADGETM:   P  RELOAD REGISTER CONTENTS */                            26000020
BADGETM  DS    0H                  UNSUCCESSFUL GETMAIN                 26050020
         LM    R14,R12,OFF14(R13)  RELOAD REGS                          26100020
*/*           R  RETURN TO CALLER */                                    26150020
         LA    R15,NOCORE          SET RETURN CODE                      26200020
         BR    R14                 RETURN TO CALLER                     26250020
         EJECT                                                          26300020
JFCBLNG  EQU   176                 LENGTH OF JFCB                       26550020
SP252    EQU   252                 INDICATOR FOR SUBPOOL 252            26600020
SP0      EQU   0                   INDICATOR OF SUBPOOL 0               26650020
R1RBA    DC    X'000000'           RELATIVE BLOCK ADDR OF FIRST RECORD  27500020
         EJECT                                                          27550020
ZER      EQU   0                   USED FOR OFFSET OF ZERO              27700020
NEXT     EQU   1                   USED AS INCREMENT VALUE              27850020
HALF     EQU   2                   USED TO GET TO HALF WORD BDY         27900020
RBAL     EQU   3                   RELATIVE BLOCK ADDRESS LENGTH        28000020
OFFCALLR EQU   4                   OFFSET IN MY SAVE AREA TO ADDR OF    28100020
*                                  CALLER'S SAVE AREA                   28150020
RECPOS   EQU   7                   POSITION OF RECORD BYTE IN           28250020
*                                  2-WORD FEEDBACK AREA AFTER READ      28300020
OFFCALLD EQU   8                   OFFSET IN SAVE AREA TO CALLED ADDR   28350020
LENG8    EQU   8                   LENGTH OF 8 BYTES FOR REG SHIFT      28400020
OFF0     EQU   20                  OFFSET TO REG0 IN SAVE AREA          28450020
OFF14    EQU   12                  OFFSET TO REG14 IN SAVE AREA         28500020
         EJECT                                                          28600020
*********************************************************************** 28650020
*                                                                     * 28700020
*  THE FOLLOWING ARE EXCEPTION CODE BIT SETTINGS IN ECB+1 WHICH ARE     28750020
*  TESTED IN THE SYNAD ROUTINES.                                        28800020
*                                                                     * 28850020
*********************************************************************** 28900020
*                                                                     * 28950020
ECBRECL  EQU   X'40'               RECORD LENGTH CHECK                  29000020
ECBSPAC  EQU   X'20'               SPACE NOT FOUND TO WRITE REC         29050020
ECBIOERR EQU   X'08'               UNCORRECTABLE I/O ERROR              29100020
         SPACE 2                                                        29150020
*********************************************************************** 29200020
*                                                                     * 29250020
*        THE FOLLOWING ARE USED TO SET THE RETURN CODE FOR CALLER     * 29300020
*                                                                     * 29350020
*********************************************************************** 29400020
SDERR    EQU   X'80'               ERROR IN 'SEND' PROCESSOR            29450020
NOCORE   EQU   X'40'               NOT ENOUGH CORE FOR IO RTN           29500020
IOERR    EQU   X'08'               I/O ERROR - SYNAD ENTERED            29600020
NOTMNT   EQU   X'04'               BROADCAST VOLUME NOT MOUNTED         29650020
OPENERR  EQU   X'02'               ERROR OCCURRED DURING OPEN           29700020
DSFULL   EQU   X'01'               DATA SET FULL  ON   WRITE 'DA'       29750020
*                                                                     * 29800020
*                                                                     * 29850020
*                                                                     * 29900020
         EJECT                                                          29950020
*                                                                     * 30000020
*        THE FOLLOWING ARE LIST FORMS OF MACROS WHICH ARE MOVED TO    * 30050020
*        GOTTEN CORE AT EXECUTION TIME.                               * 30100020
*        THE ORDER OF THE MACROS MUST, MUST NOT BE CHANGED            * 30150020
*                                                                     * 30200020
         READ  DEC01,DIF,,,,,,MF=L  READ BY BLOCK ID (RBA) LIST         30250020
         WRITE DEC02,DI,,,,,,MF=L  WRITE BY BLOCK ID (REL. BLOCK ADDR)  30300020
         WRITE DEC03,DAF,,,,,,MF=L WRITE TO ADD A RECORD W/FEEDBACK     30350020
         CLOSE (0),MF=L            CLOSE LIST FORM                      30450020
         GETMAIN  EC,MF=L          GETMAIN LIST FORM                    30500020
LISTSEND EQU   *                   END OF LISTS FOR MACRO INSTRUCTIONS  30650020
*                                  TO BE USED TO MOVE INTO DSECT        30700020
LISTSL   EQU   LISTSEND-DEC01      LENGTH OF LISTS FOR MACRO INSTRUCT.  30750020
         EJECT                                                          33800020
*********************************************************************** 33850020
*                                                                     * 33900020
*        DSECT FOR PARAMETER LIST FROM CALLER                         * 33950020
*                                                                     * 34000020
*********************************************************************** 34050020
*                                                                     * 34100020
SDIOPARM DSECT                     DSECT FOR PARM LIST FROM IEEVSEND    34150020
*                                                                       34200020
PARM1    DS    1C                  FLAG1 FOR OPERATIONS TO BE PERFORMED 34250020
P1OPEN   EQU   X'80'               OPEN - INITIAL FORMATTING IF FIRST   34300020
*                                       TIME DATA SET HAS BEEN USED     34350020
P1READ   EQU   X'40'               READ SPECIFIED RECORD                34400020
P1OPENIN EQU   X'20'               OPEN DATA SET FOR INPUT              34450020
*                                                                     * 34500020
*                                                                     * 34550020
PARM2    DS    1C                  FLAG2 FOR OPERATIONS TO BE PERFORMED 34600020
P2WRDI   EQU   X'80'               WRITE SPECIFIED RECORD BY BLOCK ID   34650020
P2WRDAF  EQU   X'40'               WRITE SPECIFIED RECORD IN NEXT       34700020
*                                       AVAILABLE SPACE WITH FEEDBACK   34750020
P2CLOSE  EQU   X'20'               CLOSE                                34800020
P2DEL    EQU   X'10'               DELETION OF THIS RECORD.             34850020
*                                  PUT PARMFDBK INTO FIRST BYTE OF DATA 34900020
P2JFCB   EQU   X'08'               FREE CORE FOR JFCB                   34950020
PARMKEY  DS    C                   KEY FOR READ/WRITE INSTRUCTION       35000020
PARMFDBK DS    C                   FEEDBACK FROM READ - POSITION        35050020
*                                  OF RECORD ON TRACK.  PUT INTO        35100020
*                                  FIRST BYTE OF DATA FOR DELETE        35150020
PARMRBA  DS    F                   ADDRESS OF RBA FOR READ/WRITE        35200020
PARMIOAD DS    F                   ADDRESS OF BUFFER FOR IO INSTRUCTION 35250020
PARMDDNM DS    CL8                 DDNAME FOR BROADCAST DATA SET        35300020
*        THE FOLLOWING FIELDS ARE NEVER CLEARED FROM PARM LIST          35350020
PARMLMCT DS    F                   LIMIT COUNT FOR EXTENDED SEARCH      35400020
PARMDCB  DS    F                   ADDRESS OF DCB                       35450020
PARMJFCB DS    F                   ADDRESS OF JFCB                      35500020
         EJECT                                                          35550020
*********************************************************************** 35600020
*                                                                     * 35650020
DATABEG  DSECT                     DSECT FOR    WORK DATA AREA          35700020
         READ  DECB1,DIF,,,,,,MF=L  READ BY BLOCK ID (RBA) LIST         35750020
         WRITE DECB2,DI,,,,,,MF=L  WRITE BY BLOCK ID (REL. BLOCK ADDR)  35800020
         WRITE DECB3,DAF,,,,,,MF=L WRITE TO ADD A RECORD W/FEEDBACK     35850020
CLOSEL   CLOSE (0),MF=L            CLOSE LIST FORM                      35950020
GETML    GETMAIN  EC,MF=L          GETMAIN LIST FORM                    36000020
         EJECT                                                          36150020
SAVER    DS    18F                 REGISTER SAVE AREA                   36200020
WORK4    DS    F                   FULL WORD WORK AREA                  36250020
KEY      DS    C                   KEY FOR READ / WRITE OPERATIONS      36550020
RETCODE  DS    C                   RETURN CODE TO BE PLACE IN REG15     37850020
         SPACE 4                                                        37860020
DATAEND  DS    0H                  END OF WORK DATA AREA                37900020
*                                                                     * 37950020
*********************************************************************** 38000020
         EJECT                                                          38200020
         DCBD  DSORG=DA,DEVD=DA                                         38750020
DCBEND   EQU   *                   END OF DCB                           38800020
DCBLNG   EQU   DCBEND-IHADCB       LENGTH OF DCB TO USE IN GETMAIN      38850020
*                                                                     * 38900020
*/*IEEVSND5:  END END OF IEEVSND5 */                                    38950020
         END                                                            39000020
