         MACRO                                                          00060002
&NAME    RECORD &TYPE=,&RCVRY=,&DATAADR=,&PARMADR=,&LENGTH=,           C00070002
               &BUFFER=YES,&POSTING=NO,&HEADER=NO,&JOBNAME=NO,&TIME=NO,C00100041
               &ERRORID=NO                                    @G17EP5W  00110041
* MACCOMP-SC1CM                                                         00112041
* MACSTAT-@G17EP5W                                                      00114041
* MACMEAN-INVOKE RECORDING ROUTINE FOR WTO,SYS1.LOGREC RECORDS OR       00116041
*         EMERGENCY REQUESTS                                            00118041
         LCLA  &CTR1,&CTR2,&CTR3                              @G17EP5W  00120041
         LCLB  &B(12)                                                   00200041
&NAME    DS    0H                                                       00250002
&B(1)    SETB  ('&TYPE' EQ 'TERM')         SYSTEM TERMINATION CALL      00300002
&B(2)    SETB  ('&TYPE' EQ 'LOGREC')       WRITE TO SYS1.LOGREC         00350002
&B(3)    SETB  ('&TYPE' EQ 'WTO')          WRITE TO OPERATOR            00400002
&B(4)    SETB  ('&RCVRY' EQ 'ESTAE')       ESTABLISH RECOVERY VIA ESTAE 00450002
&B(5)    SETB  ('&RCVRY' EQ 'SETFRR')      ESTABLISH RECVRY VIA SETFRR  00500002
&B(6)    SETB  ('&BUFFER' EQ 'YES')        DATA MUST BE BUFFERED        00550002
&B(7)    SETB  ('&BUFFER' EQ 'NO')         DATA MUST NOT BE BUFFERED    00600002
&B(8)    SETB  ('&HEADER' EQ 'YES')        BUILD AN AR149 HEADER        00650002
&B(9)    SETB  ('&JOBNAME' EQ 'YES')       INSERT JOBNAME AFTER HEADER  00700002
&B(10)   SETB  ('&TIME' EQ 'YES')          RETURN TIME PLACED IN RECORD 00750002
&B(11)   SETB  ('&POSTING' EQ 'YES')       POST CALLER WHEN BUFFER FREE 00800002
&B(12)   SETB  ('&ERRORID' EQ 'YES')       INCLUDE ERRORID IN RECORD    00820041
&CTR1    SETA  128*&B(1)+64*&B(2)+32*&B(3)+&B(12)              @G17EP5W 00850041
&CTR2    SETA  128*&B(4)+64*&B(5)+32*&B(6)+16*&B(7)+8*&B(8)+4*&B(9)+2*&C00900002
               B(10)+&B(11)                                             00950002
&CTR3    SETA  &B(8)+&B(9)+&B(10)+&B(11)+&B(12)                @G17EP5W 01000041
         AIF   (&CTR1 LE 1).ERR1                               @G17EP5W 01010041
         AIF   ('&HEADER' NE 'YES' AND '&HEADER' NE 'NO').ERR10         01060002
         AIF   ('&TIME' NE 'YES' AND '&TIME' NE 'NO').ERR11             01070002
         AIF   ('&JOBNAME' NE 'YES' AND '&JOBNAME' NE 'NO').ERR12       01080002
         AIF   ('&POSTING' NE 'YES' AND '&POSTING' NE 'NO').ERR13       01090002
         AIF   ('&BUFFER' NE 'YES' AND '&BUFFER' NE 'NO' AND '&BUFFER' N01092002
               NE 'OPT').ERR14                                          01092402
         AIF   ('&ERRORID' NE 'YES' AND '&ERRORID' NE 'NO').ERR15       01093241
         AIF   ('&TYPE' EQ 'WTO' AND '&HEADER' EQ 'YES').ERR9           01094002
         AIF   ('&TYPE' EQ 'TERM').TERM                                 01100002
         AIF   ('&RCVRY' NE 'ESTAE' AND '&RCVRY' NE 'SETFRR').ERR2      01150002
         AIF   ('&DATAADR' NE '' AND '&PARMADR' NE '').ERR4             01200002
         AIF   ('&DATAADR' EQ '' AND '&PARMADR' EQ '').ERR3             01250002
         AIF   (&CTR3 NE 0 AND '&PARMADR' EQ '').ERR5                   01300002
         AIF   ('&JOBNAME' EQ 'YES' AND '&HEADER' EQ 'NO').ERR6         01350002
         AIF   ('&HEADER' EQ 'YES' AND '&BUFFER' NE 'YES').ERR7         01400002
         AIF   ('&LENGTH' EQ '' AND ('&TYPE' NE 'WTO' OR '&BUFFER' NE  N01450002
               'NO')).ERR8                                              01500002
         AIF   ('&ERRORID' EQ 'YES' AND '&TYPE' NE 'LOGREC').ERR16      01520041
         AIF   ('&LENGTH' EQ '').LNGR0                                  01550002
         AIF   ('&LENGTH' EQ '(0)').LNGR0                               01600002
         AIF   ('&LENGTH'(1,1) EQ '(').LNGRN                            01650002
         CNOP  0,4                                                      01660002
         B     *+8                        BRANCH AROUND CONSTANTS       01700002
         DC    AL1(&CTR1)                 BIT SETTING FOR TYPE          01750002
         DC    AL1(&CTR2)                 BIT SETTING FOR OPTIONS       01800002
         DC    AL2(&LENGTH)               LENGTH OF DATA SUPPLIED       01850002
         L     0,*-4                      REGISTER 0=OPTIONS AND LNGTH  01900002
         AGO   .LOADR1                                                  01950002
.LNGRN   LR    0,&LENGTH                  LENGTH IN REG0                02000002
.LNGR0   CNOP  0,4                        BRANCH AROUND CONSTANTS       02050002
         B     *+6                        BRANCH AROUND CONSTANTS       02060002
         DC    AL1(&CTR1)                 BIT SETTING FOR TYPE          02100002
         DC    AL1(&CTR2)                 BIT SETTING FOR OPTIONS       02150002
         ICM   0,12,*-2                    TYPE AND OPTS IN REG0        02200002
.LOADR1  AIF   ('&PARMADR' NE '').PARM                                  02250002
         IHBINNRA &DATAADR                LOAD REG1 WITH ADDR OF DATA   02300002
         LCR   1,1                        TWO'S COMPLIMENT FOR DATAADR  02310002
         AGO   .LNKAGE                                                  02350002
.TERM    LA    0,&CTR1.(0,0)              BIT SETTING FOR TYPE=TERM     02352002
         SLL   0,24(0)                    IN HIGH ORDER BYTE OF REG0    02354002
         AGO   .LNKAGE                                                  02356002
.PARM    IHBINNRA &PARMADR                REG1 WITH ADDR OF PARMLIST    02360002
.LNKAGE  L     15,FLCCVT-PSA(0,0)         ADDRESS OF CVT                02400002
         L     15,CVTRECRQ-CVTMAP(,15)    ADDRESS OF RECORDING FACILITY 02450002
         BALR  14,15                      BRANCH TO RECORDING FACILITY  02500002
         AGO   .END                                                     02550002
.ERR1    MNOTE 12,'TYPE OPERAND REQD - NOT SPECIFIED'                   02600002
         MEXIT                                                          02610002
.ERR2    MNOTE 12,'RCVRY OPERAND REQD - NOT SPECIFIED'                  02620002
         MEXIT                                                          02630002
.ERR3    MNOTE 12,'DATAADR OR PARMADR OPERAND REQD - NOT SPECIFIED'     02640002
         MEXIT                                                          02642002
.ERR4    MNOTE 12,'DATAADR AND PARMADR OPERANDS BOTH SPECIFIED'         02644002
         MEXIT                                                          02646002
.ERR5    MNOTE 12,'PARMADR REQD WITH HEADER, JOBNAME, TIME, POSTING, ORC02648041
                ERRORID'                                                02648241
         MEXIT                                                          02648402
.ERR6    MNOTE 12,'HEADER=YES REQUIRED IF JOBNAME=YES'                  02648802
         MEXIT                                                          02649202
.ERR7    MNOTE 12,'BUFFER=YES REQUIRED IF HEADER=YES'                   02649602
         MEXIT                                                          02649702
.ERR8    MNOTE 12,'LENGTH OPERAND REQD - NOT SPECIFIED'                 02649802
         MEXIT                                                          02649902
.ERR9    MNOTE  12,'HEADER=YES INVALID WITH TYPE=WTO'                   02699902
         MEXIT                                                          02749902
.ERR10   IHBERMAC  54,,&HEADER                                          02799902
         MEXIT                                                          02849902
.ERR11   IHBERMAC 54,,&TIME                                             02899902
         MEXIT                                                          02949902
.ERR12   IHBERMAC 54,,&JOBNAME                                          02999902
         MEXIT                                                          03049902
.ERR13   IHBERMAC 54,,&POSTING                                          03059902
         MEXIT                                                          03069902
.ERR14   IHBERMAC 54,,&BUFFER                                           03079902
         MEXIT                                                          03089902
.ERR15   IHBERMAC 54,,&ERRORID                                          03091941
         MEXIT                                                          03093941
.ERR16   MNOTE 12,'TYPE=LOGREC REQUIRED IF ERRORID=YES'                 03095941
         MEXIT                                                          03097941
.END     MEND                                                           03099902
