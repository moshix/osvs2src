CC60     TITLE  'IGFC60 - 2860 CHANNEL ANALYSIS ROUTINE'                00150002
IGFC60   CSECT                                                          00200002
******************************************************************      00250002
*                                                                       00300002
*FUNCTION                                                               00350002
*     THIS MODULE PERFORMS THE CHANNEL DEPENDENT ANALYSIS FOR THE 2860  00400002
*     CHANNEL. IT RECEIVES CONTROL FROM THE ERPIB FILL SUBROUTINE       00450002
*     (IGFCCHFE) OF THE CCH CENTRAL MODULE.                             00500002
*                                                                       00550002
*     USING THE INFORMATION CONTAINED WITHIN THE LOGOUT , THE MODULE    00560002
*     DETERMINES THE CORRECT SETTTNGS FOR THE VALIDITY BITS, SOURCE OF  00570002
*     ERRORS INDICATORS, TERMINATION CODES, AND SEQUENCE CODES OF THE   00580002
*     WORK ERPIB. HAVING COMPLETED THE WORK ERPIB THE MODULE RETURNS    00590002
*     TO THE ERPIB FILL SUBROUTINE.                                     00592002
*                                                                       00600002
* STATUS  OS/VS2 REL3.7 SU851                                           00610045
*                                                                       00616045
*     APARS FIXED =                                            @ZA18585 00622045
*          OZ18585, OZ32238,                                            00628045
*                                                                       00640051
*ENTRY POINTS                                                           00650002
*   IGFC60 -  FROM THE CENTRAL IGFCCHFE SUBROUTINE.                     00700002
*                                                                       00750002
*INPUT                                                                  00800002
*   1. CHANNEL LOG                                                      00850002
*   2. GPR6     -IOS UNIT ADDRESS                                       00900002
*   3. GPR8     -ADDRESS OF THE PCCA                                    00950002
*   4. GPR14    -RETURN ADDRESS                                         00960002
*   5. GPR15    -BASE ADDRESS FOR THIS MODULE                           00970002
*   6. CSW                                                              00980002
*   7. CCW AT COMMAND ADDRESS MINUS 8                                   00990002
*                                                                       01000002
*OUTPUT                                                                 01050002
*   1. APPROPRIATE BITS IN ERPIB                                        01100002
*   2. LOGOUT LENGTH AND ADDRESS TO PCCA                                01150002
*                                                                       01200002
*EXTERNAL REFERENCES                                                    01250002
*   CHANNEL LOG                                                         01300002
*   PCCA                                                                01310002
*   CCW                                                                 01320002
*   CSW                                                                 01330002
*                                                                       01350002
*EXIT,NORMAL                                                            01400002
*   RETURN IS VIA LINK REGISTER 14 TO A POINT IN THE                    01450002
*   ERPIB FILL SUBROUTINE(IGFCCHFE)                                     01500002
*                                                                       01550002
*                                                                       01600002
*EXIT,ERROR                                                             01650002
*   NONE                                                                01700002
*                                                                       01750002
*TABLES/WORK AREA                                                       01800002
*   PCCAWERP (WORK ERPIB)                                               01850002
*   C60PATCH MODULE PATCH AREA                                 @ZA32238 01900045
*                                                                       01950002
*ATTRIBUTES                                                             02000002
*   IGFC60 IS DISABLED AND PARALLEL REENTRANT FOR IOS                   02050002
*                                                                       02100002
*NOTES                                                                  02150002
*                                                                       02200002
*   ERPIB ERROR RECOVERY PROCEDURE INTERFACE BYTES                      02250002
*                                                                       02300002
*   BYTE 0 - ZERO                                                       02350002
*   BYTE 1,2,3 - UCB ADDRESS POINTER                                    02400002
*   BYTE 4 - PROGRAM FLAG BITS                                          02450002
*        BIT 0 - SIO INST STORED A CSW                                  02500002
*        BIT 1 - I/O INTERRUPT STORED A CSW                             02550002
*        BIT 2 - TIO INST STORED A CSW                                  02600002
*        BIT 3 - HIO INST STORED A CSW                                  02650002
*        BIT 4 - NOT USED                                               02700002
*        BIT 5 - SENSE DATA STORED                                      02750002
*        BIT 6 - VALID COUNT APPEARS IN CSW                             02800002
*        BIT 7 - UNCONDITIONAL NO RETRY                                 02850002
*   BYTE 5 - PROBABLE SOURCE OF ERROR INDICATOR BITS                    02900002
*        BIT 0 - CPU ERROR                                              02950002
*        BIT 1 - CHANNEL ERROR                                          03000002
*        BIT 2 - STORAGE CONTROL UNIT ERROR                             03050002
*        BIT 3 - STORAGE ERROR                                          03100002
*        BIT 4 - CONTROL UNIT ERROR                                     03150002
*        BIT 5,6,7 - NOT USED                                           03200002
*   BYTE 6 - VALIDITY INDICATOR BITS                                    03250002
*        BIT 0 - INTERFACE ADDRESS                                      03300002
*        BIT 1,2 - NOT USED  MUST BE ZERO                               03350002
*        BIT 3 - RETRY CODE                                             03400002
*        BIT 4 - DEVICE STATUS                                          03450002
*        BIT 5 - CHANNEL PROGRAM ADDRESS AND KEY                        03500002
*        BIT 6 - CHANNEL ADDRESS                                        03550002
*        BIT 7 - DEVICE ADDRESS                                         03600002
*   BYTE 7 - TERMINATION/RETRY CODE BITS                                03650002
*        BIT 0,1 - TERMINATION CODE BITS                                03700002
*        BIT 2,3 - NOT USED                                             03750002
*        BIT 4 - DISCONNECT-IN                                          03800002
*        BIT 5,6,7 - RETRY BITS                                         03850002
*   CHARACTER CODE DEPENDENCIES                                         03900002
*        THIS ROUTINE IS CHARACTER CODE INDEPENDENT                     03950002
*                                                                       04000002
* CHANGE AREA          APPROX                                           04007051
*    TYPE    APAR     SEQUENCE                                 @ZA18585 04014051
*     A      OZ18585  006000                                            04021051
*     A      OZ18585  040000                                            04028051
*     A      OZ18585  048400                                            04035051
*     A      OZ18585  235988                                            04042051
*     C      @G51BPLC 277700                                            04043045
*     DA     OZ32238  084700,085620                                     04044045
*     DC     OZ32238  325500,326700                                     04044545
*                                                                       04045045
*                                                                       04046045
******************************************************************      04050002
         SPACE 2                                                        04100002
******************************************************************      04150002
*****    REGISTER DEFINITION                                            04200002
*                                                                       04250002
XLATEREG EQU   1              REG USED IN TRANSLATION OF CSW VIRT. @ CF 04260002
UAREG    EQU   6              UNIT ADDRESS                              04300002
WKREG1   EQU   10             MAIN WORKING REGISTER                     04400002
WKREG2   EQU   5              WORKING REGISTER                          04450002
WKREG3   EQU   4              SPACE WORK REGISTER                    CF 04500002
LINKRG1  EQU   15             LINK REGISTER(ON ENTRY, BASE @ FOR RTN)CF 04600002
LINKRG2  EQU   14             SECONDARY LINK REGISTER                   04650002
WKERPIB  EQU   9              BASEREG FOR WORK ERPIB                 CF 04800002
CCAREG   EQU   8              BASEREG FOR THE PCCA                      04830002
RTNADRG  EQU   3              BASE REG FOR 2860 ROUTINE              CF 04840002
REG0     EQU   0                       ASID RTN FROM XLATE RTN @ZA18585 04843051
TESTWK   EQU   7                       WORK REGISTER           @ZA18585 04846051
         SPACE 2                                                        04850002
*****************************************************************       04900002
*                                                                       04950002
*****    FIXED CORE LOCATIONS                                           05000002
*                                                                       05050002
*****************************************************************       05100002
CSWLOC   EQU   64                       CSW LOCATION                    05150002
RECLNTH  EQU   102                    LENGTH OF  RECORD ENTRY           05200002
LOGLOC   EQU   304                      LOCATION OF LOG OUT AREA        05250002
LOGWD1A  EQU   304                      1ST HALF OF LOG WORD 1          05300002
LOGWD1B  EQU   308                      2ND HALF OF LOG WORD 1          05350002
LOGWD2A  EQU   312                      1ST HALF OF LOG WORD 2          05400002
LOGWD2B  EQU   316 .                    2ND HALF OF LOG WORD 2          05450002
LOGWD3A  EQU   320 .                    1ST HALF OF LOG WORD 3          05500002
LOGWD3B  EQU   324 .                    2ND HALF OF LOG WORD 3          05550002
         SPACE 2                                                        05600002
*****                                                                   05650002
*        CSW STATUS INDICATORS                                          05700002
*                                                                       05750002
CCCBIT   EQU   X'04'                   CHANNEL CONTROL CHECK            05800002
ICCBIT   EQU   X'02'                   INTERFACE CONTROL CHECK          05850002
         SPACE 1                                                        05900002
*                                                                       05950002
*                                                                       06000002
*                                                                       06050002
*        GENERAL EQUATES                                                06100002
*                                                                       06150002
CCHOPIN  EQU   X'80' .             TO TEST FOR OPERATION IN             06200002
CCHPIT   EQU   X'02' .             POLLING INTERRUPT BIT                06250002
CCHSIOLH EQU   X'02' .             SIO LATCH                            06300002
CCHSTGCK EQU   X'04' .             STORAGE CHECK BIT                    06350002
CCHSETUP EQU   X'10' .                 SET UP BIT IN LOG                06400002
         SPACE                                                          06450002
ZERO     EQU   0 .                 CONSTANT FOR DISPLACEMENT            06500002
ONE      EQU   1 .                 CONSTANT FOR DISPLACEMENT            06550002
TWO      EQU   2 .                 CONSTANT FOR DISPLACEMENT            06600002
THREE    EQU   3 .                 CONSTANT FOR DISPLACEMENT            06650002
FOUR     EQU   4                   CONSTANT FOR DISPLACEMENT            06700002
FIVE     EQU   5 .                 CONSTANT FOR DISPLACEMENT            06750002
SIX      EQU   6 .                 CONSTANT FOR DISPLACEMENT            06800002
SEVEN    EQU   7                   CONSTANT FOR DISPLACEMENT            06850002
EIGHT    EQU   8                   CONSTANT FOR DISPLACEMENT            06900002
CCH3     EQU   3 .                 CONSTANT FOR DISPLACEMENT            06950002
CCH4     EQU   4 .                 CONSTANT FOR DISPLACEMENT            07000002
CCH7     EQU   7 .                 CONSTANT FOR DISPLACEMENT            07050002
CCH23    EQU   23 .                CONSTANT FOR DISPLACEMENT            07100002
HEX00    EQU   X'00'               CONSTANT FOR COUNT                CF 07110002
HEX01    EQU   X'01'               CONSTANT FOR MASK                    07150002
HEX02    EQU   X'02'               CONSTANT FOR COUNT                CF 07160002
HEX08    EQU   X'08'               CONSTANT FOR MASK                    07200002
HEX0E    EQU   X'0E' .             SEQUENCE 2,3,4 TRIGGERS              07250002
HEX0F    EQU   X'0F'               CONSTANT FOR MASK                    07300002
HEX10    EQU   X'10' .             CONSTANT FOR MASK                    07350002
HEX20    EQU   X'20'               CONSTANT FOR MASK                    07400002
HEX40    EQU   X'40'               CONSTANT FOR MASK                    07410002
HEXE0    EQU   X'E0' .             MODE TRIGGERS 9,10,11                07450002
ALLFS    EQU   X'FF' .             CONSTANT FOR MASK                    07500002
BTECNT   EQU   X'03'               BYTE COUNT FOR PARITY TEST           07510002
*                                                                       07550002
         EJECT                                                          07600002
*****    2860 DEPENDENT ERPIB FILL                                      07650002
*        THIS ROUTINE IS CALLED BY IGFCCHFE TO ANALYZE THE 2860         07700002
*        CHANNEL LOG AND SET THE APPROPRIATE BITS IN THE ERPIB.         07750002
         SPACE 2                                                        07800002
         USING *,RTNADRG                                             CF 08350002
         USING PCCA,CCAREG         BASE FOR PCCA DSECT               CF 08400002
         USING IGFERPIB,WKERPIB    BASE FOR WRK ERPIB DSECT             08450002
         SPACE 2                                                        08470045
         OI    PCCACHF1,PCCACF17  SET FOOTPRINT (2860 RTN ENTERED)   CF 08490045
CCH02860 LR    RTNADRG,LINKRG1     GET BASE ADDR IN BASE REG         CF 08510045
         MODID                                                 @ZA32238 08530045
         LA    WKERPIB,PCCAWERP   GET ADDRESS OF WORK ERPIB IN PCCA  CF 08570002
         LA    WKREG2,SIX          CONTROL NUMBER OF WORDS TO CHECK  CF 08580002
         LA    WKREG1,LOGLOC      GET BEGINNING OF CHAN LOGOUT AREA  CF 08600002
CCH029A  CLC   ZERO(CCH4,WKREG1),ALLONES DID CHAN FAIL TO LOG        CF 08700002
         BE    SETSYSTR            YES,BRANCH TO SET TERM               08750002
         LA    WKREG1,CCH4(WKREG1) POINT TO NEXT LOG WORD            CF 08800002
         BCT   WKREG2,CCH029A      GO CONTINUE TEST                     08850002
*                                                                       08900002
         IC    WKREG2,LOGWD3B+THREE    GET UNIT ADDR PARITY BIT         08950002
         SRL   WKREG2,SEVEN             RIGHT ADJUST BIT                09000002
         STC   WKREG2,PCCALGP1          SET UP PARITY TEST PARAMETER CF 09050002
         MVI   PCCACHPB,HEX01      NO. OF BYTES TO BE TESTED IN PCCA CF 09060002
         LA    WKREG1,LOGWD2A+3    GET LOC OF UNIT ADDRESS IN LOG    CF 09080002
         BAL   LINKRG1,LOGPAR      TEST UNIT ADDRESS PARITY             09200002
         B     CCH109 .              GOOD PARITY=VALID UNIT ADDRESS     09300002
         OI    CCHSW2,SYSTERM      TURN ON I/O RESTART SWITCH        CF 09320002
         B     CCH109A                  INVALID UNIT ADDRESS            09350002
         SPACE 1                                                        09400002
*****************************************************************       09450002
*        THIS ROUTINE CHECKS TO INSURE THAT THE CORRECT UNIT ADDRESS  I 09500002
*        IS SET VALID. IF IOS UNIT ADDRESS IS NOT EQUAL TO  UNIT        09550002
*        ADDRESS IN LOG, IOS UNIT ADDRESS IS INVALID                    09600002
         SPACE 2                                                        09650002
CCH109   LA    WKREG2,LOGWD2A+THREE .   GET UA ADDR IN LOG AREA.      I 09900002
UAVALSET TM    RECBYTE,CCHTIOB .        IS THIS AN ERROR ON A TIO     I 09950002
         BO    CCHTSTUA .            BRANCH TO TEST IF UNIT ADDRESS OK  10000002
CCHDAVAL OI    IGVALIDB,CCHDAV .        SET UNIT ADDRESS VALID BIT      10050002
         B     CCH109A                  TO RETURN TO NORMAL FLOW        10100002
         SPACE 1                                                        10150002
*                                      DOES THE UNIT ADDRESS FROM       10200002
*                                      IOS EQUAL THE UNIT ADDRESS       10250002
TSTLOGUA CLI   ZERO(WKREG2),ZERO .     IN THE LOG                       10300002
         SPACE 1                                                        10350002
CCHTSTUA EX    UAREG,TSTLOGUA .         SEE IF HAVE CORRECT UA          10400002
         BE    CCHDAVAL .               YES,GO SET DEVICE ADDRESS VALID 10450002
         SPACE 2                                                        10500002
CCH109A  SR    WKREG2,WKREG2       CLEAR REG                            11200002
         IC    WKREG2,LOGWD3B+CCH3 .    GET PARITY BIT FOR COMMAND REG  11250002
         SRL   WKREG2,TWO .             ISOLATE PARITY BIT TO LOW     I 11300002
         STC   WKREG2,PCCALGP1      STORE FOR PARITY CHK SUBROUTINE  CF 11350002
         MVI   PCCACHPB,HEX01       NO. BYTES TO BE TESTED IN PCCA   CF 11360002
         LA    WKREG1,LOGWD2A+1     ADDR OF BYTE TO BE TESTED        CF 11380002
         BAL   LINKRG1,LOGPAR .         GO CHECK PARITY               I 11400002
         OI    CCHSW1,CCHCMDRG .        SET COMMAND REG VALID BIT       11650002
         TM    CSWLOC+FIVE,ICCBIT  .   WAS THE ERROR AN ICC           I 11700002
         BO    CCH110 .                 YES, BRANCH                     11750002
         OI    IGTERMSQ,COMPFES .       NO, SET FES TERMINATION CODE    11800002
         TM    LOGWD3B,CCHOPIN .   IS OP-IN UP                        I 11850002
         BZ    CKCMDADP            NO,SKIP SETTING VALID BIT.         I 11900002
         OI    IGVALIDB,CCHUSV .        SET VALID UNIT STATUS BIT       11950002
CKCMDADP IC    WKREG2,LOGWD3B+TWO .    GET PARITY BITS FOR CMD ADDR   I 12000002
         SRL   WKREG2,FOUR              ISOLATE 3 PARITY BITS TO LOW    12050002
         STC   WKREG2,PCCALGP1          STORE IN PARAMETER FOR SUBRTNCF 12100002
         MVI   PCCACHPB,BTECNT     NO. BYTES TO BE TESTED IN PCCA    CF 12110002
         LA    WKREG1,LOGWD1A+1    ADDRESS OF 1ST BYTE TO BE TESTED  CF 12130002
         BAL   LINKRG1,LOGPAR .         GO TO CHECK PARITY              12150002
         B     CCH11A              GOOD PARITY                          12200002
         B     CCH112 .           BAD PARITY, SKIP NEXT TEST            12350002
*                                          LOG COMMAND COPMARE TO       12400002
CCH11A   CLC   LOGWD1A+ONE(THREE),CSWLOC+ONE  CSW COMMAND ADDRESS       12450002
         BL    CCH112 .                 COMMAND ADDR INVALID, SKIP SET  12500002
         OI    IGVALIDB,CCHCMDV .       SET COMMAND ADDRESS VALID BIT   12550002
         SPACE 2                                                        12600002
CCH112   TM    RECBYTE,CCHHIOB+CCHTIOB .     HIO OR TIO IN CSW          13400002
         BNZ   CCH128 .                 YES,BRANCH                      13450002
CCH112G  TM    LOGWD3A+ONE,HEX10 .      SET UP ON IN LOG                13500002
         BO    CCH113 .                 YES, GO TO SET RETRY CODE       13550002
         TM    CCHSW1,CCHCMDRG .        IS COMMAND REG PARITY VALID     13600002
         BZ    CCH113 .                 NO, GO SET RETRY CODE           13650002
         TM    LOGWD3B,CCHOPIN .        IS OP-IN UP                     13700002
         BO    CCH112H .                YES,GO SEE IF TIO               13750002
         NI    IGTERMSQ,ALLFS-COMPFES . SET TERM CODE TO 00             13800002
CCH112H  DS    0H                                                       13850002
         TM    RECBYTE,CCHTIOB .        IS THIS ENTRY CAUSED BY TIO     13900002
         BO    CCH114 .                 YES, CHECK SEQ TRIGERS          13950002
         TM    LOGWD3A,HEX08 .          SEQ TWO TRIGER ON               14000002
         BO    CCH115 .                 YES,GO CHK DATA TRANSFER        14050002
         TM    LOGWD3B+ONE,CCHSTGCK .  IS THIS A STORAGE ERROR          14100002
         BO    CCH112D .           YES,BRANCH                           14150002
CCH112A  OI    IGBLAME,CCHCHNL .  INTERNAL CHANNEL ERROR                14200002
CCH112B  OI    IGTERMSQ,RTCODE4 .       SET RETRY CODE TO 01            14250002
         TM    LOGWD3B,CCHSIOLH .       IS SIO LATCH ON                 14300002
         BZ    CCHRETRN .               NO,RETURN TO COMMON POINT       14350002
CCH112C  OI    IGVALIDB,CCHUSV .        SET VALID UNIT STATUS BIT       14400002
         B     CCHRETRN .               RETURN TO COMMON POINT          14450002
*                                                                       14500002
         SPACE 1                                                        14550002
CCH112D  TM    IGVALIDB,CCHCMDV .  IS C.A. VALID                        14600002
         BZ    CCH112A .           NO,BRANCH,SET CHANNEL ERROR          14650002
*                                                                       14700002
         OI    IGBLAME,CCHSTG .   STORAGE IS SOURCE OF ERROR            14750002
         B     CCH112B .           GO CHECK SIO LATCH                   14800002
         SPACE 1                                                        14850002
*                                                                       14900002
         SPACE 2                                                        14950002
CCH110   OI    IGTERMSQ,COMPSEL .       SET TERM CODE FOR SEL RESET     15600002
         TM    LOGWD3B+ONE,HEX40   Q. DISCONNECT-IN UP               CF 15610002
         BZ    CCH110E              NO. GO SET CMD ADDRESS VALID     CF 15620002
CCH110D  OI    IGTERMSQ,CCHDI       TURN ON DISCONNECT-IN IN ERPIB   CF 15630002
CCH110E  OI    IGVALIDB,CCHCMDV .       SET COMMAND ADDR VALID BIT      15650002
         TM    LOGWD3A+ONE,CCHPIT .    IS POLLING INTERUPT BIT SET      15700002
         BZ    CCH110A             NO,BRANCH                            15750002
         OI    IGBLAME,CCHINTFC . INTERFACE IS SOURCE OF ERROR          15800002
         OI    IGTERMSQ,RTCODE3 .  SET RETRY CODE TO 011                15850002
         B     CCHRETRN .               GO TO COMMON RETURN POINT       15900002
         SPACE 1                                                        15950002
CCH110A  TM    RECBYTE,CCHHIOB+CCHTIOB .     HIO OR TIO IN CSW          16000002
         BNZ   CCH123 .                      YES,BRANCH                 16050002
CCH110B  TM    LOGWD3A+ONE,CCHSETUP .   SET UP BIT ON IN LOG            16100002
         BO    CCH116 .                 YES, GO TO SET RETRY CODE       16150002
         TM    CCHSW1,CCHCMDRG .        HAS COMMAND REG VALID PARITY    16200002
         BZ    CCH116 .                 NO,GO SET RETRY CODE=100        16250002
         TM    LOGWD3A,HEX20 .          COMMAND CHAINING                16300002
         BO    CCH110C .                YES,BRANCH                      16350002
         TM    LOGWD3B,CCHSIOLH .       IS SIO LATCH ON                 16400002
         BO    CCH110C .                YES,BRANCH                      16450002
         TM    LOGWD3A,HEX08 .          SEQ TWO TRIGER ON               16500002
         BO    CCH115 .                 YES, CHECK FOR DATA TRANSFER    16550002
CCH110C  DS    0H .                                                     16600002
         OI    IGBLAME,CCHINTFC . INTERFACE IS SOURCE OF ERROR          16650002
         OI    IGTERMSQ,RTCODE1 .       NO, SET RETRY CODE TO 001       16700002
         B     CCHRETRN .               GO TO COMMON RETURN POINT       16750002
*                                                                       16800002
         SPACE 2                                                        16850002
CCH117   TM    CSWLOC+FIVE,ICCBIT       WAS ERROR AN ICC                17150002
         BO    CCH117A                  YES,DONOT SET COUNT VALID       17200002
         OI    IGPRGFLG,CCHCNTB         SET COUNT VALID                 17250002
CCH117A  B     CCHRETRN                 GO TO COMMON RETURN POINT       17300002
*                                                                       17350002
CCH116   OI    IGTERMSQ,RTCODE4 .       SET RETRY CODE TO 100           17400002
         OI    IGBLAME,CCHINTFC . INTERFACE IS SOURCE OF ERROR          17450002
         B     CCHRETRN .               GO TO COMMON RETURN POINT       17500002
*                                                                       17550002
         SPACE 2                                                        17600002
CCH113   OI    IGTERMSQ,RTCODE4 .       SET RETRY CODE TO 100           18150002
         TM    IGVALIDB,CCHDAV .   IS UNIT ADDRESS VALID                18200002
         BO    CCH113A .           YES,GO TEST STORAGE CHECK            18250002
         TM    LOGWD3B,CCHSIOLH .  IS SIO LATCH ON                      18300002
         BZ    CCH113B .           NO,BRANCH.INTERNAL CHAN ERROR        18350002
*****************************************************************       18400002
*        CPU WAS SOURCE OF ERROR                                        18450002
         OI    IGBLAME,CCHCPU .   CPU WAS SOURCE OF ERROR               18500002
         B     CCH113C .                                                18550002
*****************************************************************       18600002
         SPACE 1                                                        18650002
*                                                                       18700002
*        SEE IF CHANNEL ERROR OR STORAGE ERROR                          18750002
*                                                                       18800002
CCH113A  TM    LOGWD3B+ONE,CCHSTGCK .  IS THIS A STORAGE ERROR          18850002
         BZ    CCH113B .           NO,BRANCH. CHANNEL ERROR             18900002
         OI    IGBLAME,CCHSTG .   SOURCE OF ERROR IS STORAGE            18950002
         B     CCH113C .            BRANCH TO STORAGE ERROR             19000002
         SPACE 1                                                        19050002
CCH113B  OI    IGBLAME,CCHCHNL .  CHANNEL IS SOURCE OF ERROR            19100002
CCH113C  TM    LOGWD3B,CCHSIOLH .  IS SIO LATCH ON                      19150002
         BZ    CCHRETRN                 GO TO COMMON RETURN POINT       19200002
         NI    IGVALIDB,ALLFS-CCHCMDV    COMMAND ADDR VALID BIT OFF     19250002
*                                      GO TO SET COUNT VALID BIT        19300002
         B     CCHRETRN                 GO TO COMMON RETURN POINT       19350002
*                                                                       19400002
         SPACE 2                                                        19450002
CCH114   TM    LOGWD3A,HEX0E .          ANY SEQ TRIGERS (2-3-4)         19900002
         BC    FIVE,CCH115 .            YES, CHECK FOR DATA TRANSFER    19950002
         TM    LOGWD3A,HEX20 .     COMMAND CHAINING BIT ON IN LOG       20000002
         BZ    CCH114B .           NO,GO SET CODE                       20050002
         TM    LOGWD3B,CCHOPIN .   IS OP-IN UP                          20100002
         BO    CCH115 .            YES,CHECK FOR DATA TRANSFER          20150002
         OI    IGBLAME,CCHCHNL .  CHANNEL IS SOURCE OF ERROR            20200002
         OI    IGTERMSQ,RTCODE1 .  SET RETRY CODE TO 001                20250002
         B     CCHRETRN .          GO TO COMMON RETURN POINT            20300002
         SPACE 1                                                        20350002
CCH114B  OI    IGTERMSQ,RTCODE4 .  SET RETRY CODE TO 100                20400002
         OI    IGBLAME,CCHCHNL .       INTERNAL CHANNEL ERROR           20450002
         B     CCH117 .            GO TO COUNT VALID AND RETURN         20500002
         SPACE 2                                                        20650002
CCH115   TM    LOGWD3B,CCHOPIN .   IS OP-IN UP                          21300002
         BO    CCH115B .           NO,BRANCH                            21350002
         TM    CSWLOC+FIVE,CCCBIT .    IS THIS ICC ONLY                 21400002
         BO    CCH115C .           NO,BRANCH.                           21450002
CCH115A  OI    IGBLAME,CCHINTFC . ERROR DUE TO INTERFACE                21500002
         B     CCH118                   BRANCH TO SET RETRY CODE        21550002
         SPACE 1                                                        21600002
CCH115B  TM    CSWLOC+FIVE,ICCBIT .    WAS THIS ICC                     21650002
         BO    CCH115A .           YES,BRANCH. INTERFACE ERROR          21700002
         TM    LOGWD3B+ONE,CCHSTGCK .  IS THIS STORAGE CHECK            21750002
         BO    CCH115D .           YES,BRANCH                           21800002
CCH115C  OI    IGBLAME,CCHCHNL .  INTERNAL CHANNEL ERROR                21850002
         B     CCH115E .            SKIP STORAGE ERROR SET              21900002
         SPACE 1                                                        21950002
CCH115D  OI    IGBLAME,CCHSTG .   SOURCE OF ERROR IS STORAGE            22000002
CCH115E  TM    IGVALIDB,CCHCMDV .  IS THE COMMAND VALID BIT SET IN LOG  22050002
         BZ    CCH118 .                 NO, GO TO SET RETRY CODE        22100002
         MVC   PCCALGP1(1),LOGWD3B+TWO  MOVE PARITY BITS FOR SUBROUT CF 22150002
         MVI   PCCACHPB,HEX02           NO. BYTES TO BE CKED IN PCCA CF 22160002
         LA    WKREG1,LOGWD1B+2         ADR OF BYTES TO BE TESTED    CF 22170002
         BAL   LINKRG1,LOGPAR .         GO TO CHECK PARITY              22200002
         B     CCH119                   GOOD PARITY                  CF 22350002
         B     CCH118 .                 BAD PARITY, SKIP COMPARE     CF 22400002
CCH119   NI    LOGWD3A+TWO,HEX0F        CLEAR UNWANTED BITS             22450002
         MVI   PCCALGP1,HEX00           SET UP PARITY BYTE           CF 22452002
         MVI   PCCACHPB,HEX01           NO. BYTES TO BE CHECKED      CF 22460002
         LA    WKREG1,LOGWD3A+TWO       ADDRESS OF BYTE TO CHECKED   CF 22470002
         BAL   LINKRG1,LOGPAR .        GO TO CHECK PARITY ON BYTE COUNT 22500002
         B     CCH120 .                BRANCH, COUNT VALID           CF 22700002
CCH118   OI    IGTERMSQ,RTCODE5 .       SET RETRY TO 101                22750002
         B     CCHRETRN .               COMMON RETURN PR IN SUBROUT.    22800002
*                                                                       22850002
         SPACE 2                                                        22900002
CCH120   L     XLATEREG,CSWLOC          GET CCW ADDRESS              CF 23450002
         LA    XLATEREG,ZERO(XLATEREG)  CLEAR HIGH ORDER BYTE        CF 23500002
         SH    XLATEREG,CCHLENGH        REDUCE ADDR BY EIGHT         CF 23550002
         BM    CCH118                   THE ADDR IS NEGATIVE(INVALID)CF 23560002
         ST    LINKRG2,PCCACHW2         SAVE THE FILL ERPIB RETURN @ CF 23570002
         L     WKREG1,CVTPTR            GET POINTER TO THE CVT       CF 23580002
         USING CVTMAP,WKREG1                                            23590002
         L     LINKRG1,CVTPTRV          GET THE ADDRESS OF XLATE RTN CF 23592002
         DROP  WKREG1                                                   23594002
         BALR  LINKRG2,LINKRG1          GO TO THE TRANSLATE ROUTINE  CF 23596002
         L     LINKRG2,PCCACHW2         RESTORE FE RETURN PT @       CF 23598002
         LTR   LINKRG1,LINKRG1          REAL ADDRESS TRANSLATED ?    CF 23598402
         BNZ   CCH118                   NO. ADDRESS INVALID          CF 23598802
         L     TESTWK,PCCAPSAV         GET ADDR. OF PSA(V)     @ZA18585 23599251
         L     TESTWK,PSAAOLD-PSA(TESTWK) GET ADDR. OF PSAAOLD @ZA18585 23599351
         LH    TESTWK,ASCBASID-ASCB(TESTWK) GET ASID NUMBER    @ZA18585 23599451
         CR    TESTWK,REG0             COMPARE ASID            @ZA18585 23599651
         BNZ   CCH118                  NO - ASID'S NOT EQUAL   @ZA18585 23599751
         LR    WKREG2,XLATEREG          GET THE VIRTUAL ADDRESS IN RGCF 23599851
         LH    WKREG3,SIX(WKREG2) .     LOAD COUNT FROM THE CCW         23600002
         N     WKREG3,CCHMASK1 .        AND OUT UNWANTED BITS           23650002
         IC    WKREG2,THREE(WKREG2) .  GET LOW ORDER BYTE OF DATA ADDR  23700002
         N     WKREG2,CCHMASK2 .        AND OUT BUT LOW ORDER THREE BIT 23750002
         CH    WKREG3,CSWLOC+SIX .      CCW COUNT TO CSW COUNT          23800002
         BL    CCH122 .                 LOW,CONTINUE VALIDITY           23850002
CCH120A  AR    WKREG3,WKREG2 .          ADD THE LOW BITS TO COUNT       23900002
         STH   WKREG3,PCCACHW2          STORE UPDATED COUNT FOR TESTCF  23950002
         CLC   PCCACHW2(TWO),LOGWD1B+TWO COUNT EQUAL LOG COUNT      CF  24000002
         BNE   CCH121 .                 NO, GO TO SET RETRY CODE        24050002
         IC    WKREG3,LOGWD3A+TWO       GET BYTE COUNT REG              24100002
         N     WKREG3,CCHMASK2 .        ZERO UNWANTED BITS              24150002
         CR    WKREG2,WKREG3 .          EQUAL                           24200002
         BNE   CCH121 .                 NO, GO TO SET CONDITION CODE    24250002
         OI    IGTERMSQ,RTCODE2 .       SET RETRY CODE TO 010           24300002
         B     CCH117 .                 GO TO SET COUNT VALID BIT       24350002
*                                                                       24400002
CCH121   OI    IGTERMSQ,RTCODE3 .       SET RETRY CODE TO 011           24450002
         B     CCH117 .                 GO TO SET COUNT VALID BIT       24500002
*                                                                       24550002
CCH122   LH    WKREG1,CSWLOC+SIX .      GET CSW BYTE COUNT              24600002
         N     WKREG1,CCHMASK1 .        AND OUT UNWANTED BITS           24650002
         SR    WKREG1,WKREG2 .          SUBTRACT DAB FROM COUNT         24700002
         CR    WKREG1,WKREG3 .          CSW COUNT TO CCW COUNT          24750002
         BH    CCH118 .                 HIGH,BR TR INDETERMINATE        24800002
         B     CCH120A .                GO CHECK FOR DATA TRANSFER      24850002
*                                                                       24900002
         SPACE 2                                                        24950002
CCH123   TM    LOGWD3A,HEX20+HEX0E .   CMD CHAIN OR SEQ TRIGGERS        25400002
         BNZ   CCH110B .               YES,BRANCH                       25450002
         TM    RECBYTE,CCHHIOB .        WAS ERROR ON A HIO              25500002
         BO    CCH127 .                 YES,BRANCH                      25550002
         TM    LOGWD3A+ONE,CCHSETUP .   IS SET UP ON                    25600002
*                                      NO,GO SET CODE                   25650002
         BZ    CCH116 .                AND ERROR SOURCE                 25700002
*                                                                       25750002
CCH125   TM    LOGWD3B,HEX10 .          SELECT OUT                      25800002
*                                       YES,GO SET CODE                 25850002
         BO    CCH116 .                 AND ERROR SOURCE                25900002
CCH126   OI    IGBLAME,CCHCHNL .       SET CHAN AS ERROR SOURCE         25950002
         OI    IGTERMSQ,RTCODE4 .       SET RETRY CODE TO 100           26000002
         B     CCHRETRN .               GO TO COMMON RETURN POINT       26050002
*                                                                       26100002
CCH127   TM    LOGWD3A+ONE,CCHSETUP .   IS SET UP ON                    26150002
         BZ    CCH126 .                 NO,BRANCH                       26200002
         B     CCH125                  GO SET CODE AND ERROR SOURCE     26250002
*                                                                       26300002
         SPACE 2                                                        26350002
CCH128   TM    LOGWD3A,HEX20+HEX0E .    COMMAND CHAIN OR SEQ TRIGGERS   26750002
         BNZ   CCH112G .                YES,BRANCH                      26800002
         TM    RECBYTE,CCHTIOB .        WAS CAUSE OF ERROR A TIO        26850002
         BO    CCH114B .                YES,BRANCH                      26900002
         TM    IGVALIDB,CCHDAV .        IS UNIT ADDRESS VALID           26950002
         BO    CCH129 .                 YES,BRANCH                      27000002
         OI    IGBLAME,CCHCPU .        SET CPU AS SOURCE OF ERROR       27050002
         B     CCH130 .                 BRANCH TO CPU ERROR             27100002
*                                                                       27150002
CCH129   OI    IGBLAME,CCHCHNL .       SET CHAN AS SOURCE OF ERROR      27200002
CCH130   OI    IGTERMSQ,RTCODE4 .       SET RETRY CODE TO 100           27250002
         B     CCHRETRN .              GO TO COMMON RETURN POINT        27300002
         SPACE 2                                                        27350002
SETSYSTR OI    CCHSW1,CCHNOLST     SET RECORD ONLY SWITCH               27700002
         OI    CCHSW2,SYSTERM+NOLOG    SET SYSTEM TERM PLUS NOLOG       27750002
CCHRETRN MVC   PCCALOGL,LOGLNTH    PUT LOGOUT LENGTH IN PCCA   @G51BPLC 27770051
         LA    WKREG2,LOGLOC       GET THE ADDRESS LOGOUT            CF 27850002
         ST    WKREG2,PCCALOGA     IN THE PCCA                       CF 27900002
         BR    LINKRG2             RETURN TO CCH CENTRAL             CF 28000002
*                                                                       28200002
         EJECT                                                          28250002
*****    PARITY TEST SUBROUTINE                                         28300002
*                                                                       28350002
*        THIS SUBROUTINE IS ENTERED BY THE 2860 DEPENDENT CHANNEL       28400002
*        ANALYSIS MAINLINE VIA LINKRG1.  ITS PURPOSE TO DETERMINE       28450002
*        IF THE PARITY IN ANY GIVEN PORTION OF THE LOG IS GOOD.  THIS   28500002
*        IS DETERMINED BY CALCULATING THE PARITY ON A PARTICULAR BYTE   28550002
*        AND COMPARING THE RESULT TO THE STORED PARITY BIT, ALSO IN     28600002
*        THE LOG.                                                       28650002
         SPACE 2                                                        29300002
LOGPAR   SR    WKREG2,WKREG2 .          ZERO REGISTER                CF 29900002
         IC    WKREG2,PCCACHPB .    GET COUNT OF NUMBER OF BYTES     CF 29950002
CCH202   MVI   PCCALGP2,HEX01 .         CLEAR PARITY BYTE            CF 30000002
         SR    WKREG3,WKREG3 .          CLEAR REGISTER                  30050002
         IC    WKREG3,ZERO(WKREG1) .       GET BYTE TO BE CHECKED       30100002
CCH201   LTR   WKREG3,WKREG3 .          TEST REGISTER FOR ZERO          30150002
         BZ    CCH200 .                 END, COMPARE PARITY BIT         30200002
         EX    WKREG3,CCHXOR .          CALCULATE PARITY                30250002
         SRL   WKREG3,ONE               GET NEXT BIT                    30300002
         B     CCH201 .                 LOOP                            30350002
*                                                                       30400002
         SPACE 2                                                        30450002
CCH200   NI    PCCALGP2,HEX01 .         AND OUT UNWANTED BITS FOR CLICF 30950002
         IC    WKREG3,PCCALGP1 .  GET PARITY BITS                    CF 31000002
         BCTR  WKREG2,ZERO              REDUCE COUNT OF NUMBER OF BYTES 31050002
         SRL   WKREG3,ZERO(WKREG2) .    ISOLATE PARITY BIT TO BE CHK    31100002
         N     WKREG3,CCHMASK3 .        AND OUT ANY UNWANTED BITS       31150002
         EX    WKREG3,CCHCLI .          TEST PARITY                     31200002
         BNE   FOUR(LINKRG1) .          RETURN, BAD PARITY              31250002
         LTR   WKREG2,WKREG2 .          IS TEST FINISHED                31300002
         BE    ZERO(LINKRG1) .          RETURN, GOOD PARITY             31350002
         LA    WKREG1,ONE(WKREG1) .     UP ADDR POINTER                 31400002
         B     CCH202 .                 SET UP TO TEST FOR PARITY       31450002
         EJECT                                                          31500002
         DS    0F                                                       31550002
CCHMASK1 DC    X'0000FFFF'        MASK FOR CLEARING OUT UNWANTED BITS   31600002
CCHMASK2 DC    X'00000007'        MASK FOR CLEARING OUT UNWANTED BITS   31650002
CCHMASK3 DC    X'00000001'         MASK FOR PARITY CHECK                31700002
ALLONES  DC    F'-1'              FOR LOG OUT COMPARISON                31750002
*                                  CORRECTION FACTOR FOR                31850002
CCHLENGH DC    H'8'                COMMAND ADDRESS                      31900002
LOGLNTH  DC    H'24'               LOGOUT LENGTH               @G51BPLC 31950051
*                                                                       32000002
CCHXOR   XI    PCCALGP2,ZERO       INSTR TO BE EXECUTED BY PARITY    CF 32050002
CCHCLI   CLI   PCCALGP2,ZERO       INSTR TOBE EXECUTED BY PARITY     CF 32100002
         SPACE 2                                                        32150002
******************************************************************      32200002
*                                                                *      32250002
*           MAINTAINENCE AREA                                    *      32300002
*                                                                *      32350002
******************************************************************      32400002
         SPACE 2                                                        32450002
         DS    0F                                                       32500002
C60PATCH DC    50X'00'         PATCH AREA                               32550045
MAINT    EQU   C60PATCH        FOR XREF                        @ZA32238 32580045
         CVT   DSECT=YES                                       @ZA32238 32610045
         IHAPSA                                                @ZA18585 32700051
         IHAASCB                                               @ZA18585 32750051
         IGFERPIB                  1                                    32800051
         IHAPCCA                                                        32850051
         END                                                            32900051
