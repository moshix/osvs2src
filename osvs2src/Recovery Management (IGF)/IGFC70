CC70     TITLE 'IGFC70 - 2870 CHANNEL ANALYSIS ROUTINE'                 00150002
IGFC70   CSECT                                                          00200002
         USING *,RTNADRG                                             CF 00250002
         USING PCCA,CCAREG           BASE FOR THE PCCA               CF 00300002
         USING IGFERPIB,WKERPIB      BASE FOR WORK ERPIB DSECT          00350002
******************************************************************      00400002
*                                                                       00450002
*FUNCTION                                                               00500002
*    THIS MODULE PERFORMS THE CHANNEL DEPENDENT ANALYSIS FOR THE 2870   00550002
*     CHANNEL. IT RECEIVES CONTROL FROM THE FILL ERPIB SUBROUTINE       00600002
*     (IGFCCHFE) OF THE CCH CENTRAL MODULE.                             00650002
*                                                                       00700002
*     USING THE INFORMATION CONTAINED WITHIN THE LOGOUT, THIS MODULE    00710002
*     DETERMINES THE CORRECT SETTINGS FOR THE VALIDITY BITS,SOURCE OF   00720002
*     ERROR INDICATORS, TERMINATION CODES, AND SEQUENCE CODES OF THE    00730002
*     WORK ERPIB. HAVING COMPLETED THE WORK ERPIB THE MODULE RETURNS    00740002
*     TO THE FILL ERPIB SUBROUTINE(IGFCCHFE).                           00742002
*                                                                       00750002
* STATUS  OS/VS2 REL3.7 SU851                                           00760045
*                                                                       00767045
*     APARS FIXED - OZ00516, OZ18585, OZ32238                           00774045
*                                                                       00790023
*ENTRY POINTS                                                           00800002
*   IGFC70 -  FROM THE CENTRAL ERPIBSET ROUTINE.                     CF 00850002
*                                                                       00900002
*INPUT                                                                  00950002
*   1. CHANNEL LOG                                                      01000002
*   2. GPR6     -IOS UNIT ADDRESS                                       01050002
*   3. GPR8     -ADDRESS OF THE PCCA                                    01100002
*   4. GPR14     -RETURN ADDRESS                                        01110002
*   5. GPR15     -BASE ADDRESS FOR THIS MODULE                          01120002
*   6. CSW                                                              01130002
*                                                                       01150002
*OUTPUT                                                                 01200002
*   1. APPROPRIATE BITS IN ERPIB                                        01250002
*   2. LOGOUT LENGTH AND ADDRESS TO PCCA                                01300002
*                                                                       01350002
*EXTERNAL REFERENCES                                                    01400002
*   CHANNEL LOG                                                         01450002
*   PCCA                                                                01460002
*   CSW                                                                 01470002
*                                                                       01500002
*EXIT,NORMAL                                                            01550002
*   RETURN IS VIA LINK REGISTER 14 TO A POINT IN THE                 CF 01600002
*   CENTRAL ERPIB FILL SUBROUTINE(IGFCCHFE).                         CF 01650002
*                                                                       01700002
*                                                                       01750002
*EXIT,ERROR                                                             01800002
*   NONE                                                                01850002
*                                                                       01900002
*TABLES/WORK AREA                                                       01950002
*   PCCAWERP (WORK ERPIB)                                            CF 02000002
*   C70PATCH MODULE PATCH AREA                                          02050045
*                                                                       02100002
*ATTRIBUTES                                                             02150002
*   IGFC70 IS DISABLED,PRIVILEGED AND REUSABLE                          02200002
*                                                                       02250002
*NOTES                                                                  02300002
*                                                                       02350002
*   ERPIB ERROR RECOVERY PROCEDURE INTERFACE BYTES                      02400002
*                                                                       02450002
*   BYTE 0 - ZERO                                                       02500002
*   BYTE 1,2,3 - UCB ADDRESS POINTER                                    02550002
*   BYTE 4 - PROGRAM FLAG BITS                                          02600002
*        BIT 0 - SIO INST STORED A CSW                                  02650002
*        BIT 1 - I/O INTERRUPT STORED A CSW                             02700002
*        BIT 2 - TIO INST STORED A CSW                                  02750002
*        BIT 3 - HIO INST STORED A CSW                                  02800002
*        BIT 4 - NOT USED                                               02850002
*        BIT 5 - SENSE DATA STORED                                      02900002
*        BIT 6 - VALID COUNT APPEARS IN CSW                             02950002
*        BIT 7 - UNCONDITIONAL NO RETRY                                 03000002
*   BYTE 5 - PROBABLE SOURCE OF ERROR INDICATOR BITS                    03050002
*        BIT 0 - CPU ERROR                                              03100002
*        BIT 1 - CHANNEL ERROR                                          03150002
*        BIT 2 - STORAGE CONTROL UNIT ERROR                             03200002
*        BIT 3 - STORAGE ERROR                                          03250002
*        BIT 4 - CONTROL UNIT ERROR                                     03300002
*        BIT 5,6,7 - NOT USED                                           03350002
*   BYTE 6 - VALIDITY INDICATOR BITS                                    03400002
*        BIT 0 - INTERFACE ADDRESS                                      03450002
*        BIT 1,2 - NOT USED  MUST BE ZERO                               03500002
*        BIT 3 - RETRY CODE                                             03550002
*        BIT 4 - DEVICE STATUS                                          03600002
*        BIT 5 - CHANNEL PROGRAM ADDRESS AND KEY                        03650002
*        BIT 6 - CHANNEL ADDRESS                                        03700002
*        BIT 7 - DEVICE ADDRESS                                         03750002
*   BYTE 7 - TERMINATION/RETRY CODE BITS                                03800002
*        BIT 0,1 - TERMINATION CODE BITS                                03850002
*        BIT 2,3 - NOT USED                                             03900002
*        BIT 4 - DISCONNECT-IN                                          03950002
*        BIT 5,6,7 - RETRY BITS                                         04000002
*   CHARACTER CODE DEPENDENCIES                                         04050002
*        THIS ROUTINE IS CHARACTER CODE INDEPENDENT                     04100002
*                                                                       04107051
* CHANGE AREA          APPROX                                           04114051
*    TYPE    APAR     SEQUENCE                                 @ZA18585 04121051
*     A     OZ18585    041000                                           04128051
*     A     OZ18585    206460                                           04135051
*     A     OZ18585    050100                                           04142051
*     C     @G51BPLC   301200                                           04143045
*     DA    OZ32238    081200,086600                                    04143545
*     CD    OZ32238    307000,308600                                    04144045
*                                                                       04145045
*                                                                       04146045
******************************************************************      04150002
         EJECT                                                          04200002
****************************************************************        04250002
*                                                                       04300002
*****    REGISTER DEFINITION       *****************************        04350002
*                                                                       04400002
****************************************************************        04450002
RTNADRG  EQU   3              BASE REG FOR THIS ROUTINE              CF 04460002
XLATEREG EQU   1              REG USED BY THE TRANSLATE ROUTINE      CF 04470002
UAREG    EQU   6 .            UNIT ADDRESS                              04500002
WKREG1   EQU   10 .           MAIN WORKING REGISTER                     04600002
WKREG2   EQU   5 .            WORKING REGISTER                       CF 04650002
WKREG3   EQU   4 .            SPACE WORK REGISTER                    CF 04700002
WKERPIB  EQU   9 .            ADDRESS OF WORK ERPIB                  CF 04750002
LINKRG1  EQU   2              LINK REGISTER                          CF 04800002
LINKRG2  EQU   14 .           SECONDARY LINK REGISTER                   04850002
WORK4    EQU   LINKRG1 .      WORK REG TO FIND INTERCEPT PT.            04900002
CCAREG   EQU   8              BASEREG FOR PCCA                       CF 05000002
LINKRG3  EQU   15             REG USED TO LINK TO THE ROUTINE        CF 05010002
REG0     EQU   0              ASID RTN FROM XLATE RTN          @ZA18585 05020051
TESTWK   EQU   7              WORK REGISTER                    @ZA18585 05030051
         SPACE 2                                                        05050002
****************************************************************        05100002
*                                                                       05150002
*****    FIXED CORE LOCATIONS     ******************************        05200002
*                                                                       05250002
****************************************************************        05300002
CSWLOC   EQU   64 .      CSW LOCATION                                   05350002
LOGLOC   EQU   304 .                    LOCATION OF LOG OUT AREA        05400002
LOGADDR  EQU   X'130'                   ADDR OF THE LOGOUT           CF 05410002
LOGWD1A  EQU   304 .                    1ST HALF OF LOG WORD 1          05450002
LOGWD1B  EQU   308 .                    2ND HALF OF LOG WORD 1          05500002
LOGWD2A  EQU   312 .                    1ST HALF OF LOG WORD 2          05550002
LOGWD2B  EQU   316 .                    2ND HALF OF LOG WORD 2          05600002
LOGWD3A  EQU   320 .                    1ST HALF OF LOG WORD 3          05650002
LOGWD3B  EQU   324 .                    2ND HALF OF LOG WORD 3          05700002
         SPACE 2                                                        05750002
****************************************************************        05800002
*                                                                       05850002
*        CSW STATUS INDICATORS                                          05900002
*                                                                       05950002
****************************************************************        06000002
CCCBIT   EQU   X'04' .                  CHANNEL CONTROL CHECK           06050002
ICCBIT   EQU   X'02' .                  INTERFACE CONTROL CHECK         06100002
*                                                                       06150002
*****************************************************************       06200002
*                                                                       06250002
*        GENERAL EQUATES                                                06300002
*                                                                       06350002
CCH23    EQU   23 .                     CONSTANT FOR DISPLACEMENT       06400002
CCH3     EQU   3 .                      CONSTANT FOR DISPLACEMENT       06450002
CCH4     EQU   4 .                      CONSTANT FOR DISPLACEMENT       06500002
ALLFS    EQU   X'FF' .                  CONSTANT FOR MASK               06550002
ZERO     EQU   0 .                      CONSTANT FOR DISPLACEMENT       06600002
SIX      EQU   6 .                      CONSTANT NUMBER OF LOGOUT WORDS 06650002
THREE    EQU   3 .                      CONSTANT FOR DISPLACEMENT       06700002
FOUR     EQU   4                        DISPLACEMENT                    06750002
HEX06    EQU   X'06' .                  CONSTANT FOR MASK               06800002
HEX40    EQU   X'40' .                  CONSTANT FOR MASK               06850002
HEX70    EQU   X'70' .                  CONSTANT FOR MASK               06900002
HEX82    EQU   X'82'                    CONSTANT FOR MASK               06910002
FIVE     EQU   5 .                      CONSTANT FOR DISPLACEMENT       06950002
TWO      EQU   2 .                      CONSTANT FOR DISPLACEMENT       07000002
HEX02    EQU   X'02' .                  CONSTANT FOR DISPLACEMENT       07050002
ONE      EQU   1 .                      CONSTANT FOR DISPLACEMENT       07100002
HEX20    EQU   X'20' .                  CONSTANT FOR MASK               07150002
HEX04    EQU   X'04' .                  CONSTANT FOR MASK               07200002
HEX80    EQU   X'80' .                  CONSTANT FOR MASK               07250002
HEX10    EQU   X'10' .                  CONSTANT FOR MASK               07300002
HEX08    EQU   X'08' .                  CONSTANT FOR MASK               07350002
HEX03    EQU   X'03' .                  CONSTANT FOR MASK               07400002
HEX01    EQU   X'01' .                  CONSTANT FOR MASK               07450002
HEXE0    EQU   X'E0' .                  MODE TRIGGERS 9,10,11           07500002
HEX0C    EQU   X'0C'                    CONSTANT FOR MASK               07550002
HEXC0    EQU   X'C0'                    CONSTANT FOR MASK               07600002
SEVEN    EQU   7                        CONSTANT FOR DISPLACEMENT       07650002
*****************************************************************       07700002
         SPACE 2                                                        07750002
*****************************************************************       07800002
*****    2870 DEPENDENT ERPIB FILL    **************************        07850002
*                                                                       07900002
*        THIS PORTION OF THE ERPIBSET ROUTINE ANALYZES A 2870           07950002
*        CHANNEL LOG AND SETS THE APPROPRIATE BITS IN THE ERPIB.        08000002
*                                                                       08050002
****************************************************************        08100002
         SPACE 2                                                        08120045
         OI    PCCACHF1,PCCACF18  SET FOOTPRINT ( IGFC70 ENTERED )   CF 08170045
         LR    RTNADRG,LINKRG3    SET UP THE BASE REG               CF  08220045
         MODID                                                 @ZA32238 08270045
         LA    WKERPIB,PCCAWERP         WORK ERPIB                  CF  08680002
CCH02870 LA    WKREG2,SIX         CTRL NUMBER OF WORDS TO CHECK         08700002
         LA    WORK4,LOGLOC        GET BEGIN OF CHAN LOGOUT AREA        08750002
CCH029A  CLC   ZERO(CCH4,WORK4),ALLONES   DID CHAN FAIL TO LOG          08850002
         BE    SETSYSTR            YES, BRANCH TO SET TERMINATION       08900002
         LA    WORK4,CCH4(WORK4)   POINT TO NEXT LOG WORD               08950002
         BCT   WKREG2,CCH029A      GO CONTINUE TEST                     09000002
         TM    LOGWD3B+ONE,HEX40   WORD 2 CHECK                         09050002
         BZ    CCH140 .                 NO,GO SET COMMAND ADDR VALID    09100002
         TM    LOGWD2B,HEX70 .          ANY PARITY BITS                 09150002
         BZ    CCH140 .                 NO,GO SET COMMAND ADDR VALID    09200002
         OI    IGBLAME,CCHCHNL .       YES,CHANNEL IS SOURCE OF ERROR   09250002
         B     CCH141 .                                                 09300002
         SPACE 2                                                        09350002
CCH140   TM    LOGWD3B+ONE,HEX10        IS STORAGE CHECK SET            09600002
         BZ    CCH140A                  NO,GO SET CMD ADDR VALID        09650002
         TM    LOGWD3A,HEX80            IS THIS CAW FETCH               09700002
         BO    CCH141                   YES, COMMAND ADDR INVALID       09750002
CCH140A  OI    IGVALIDB,CCHCMDV         SET COMMAND ADDR VALID          09800002
         SPACE 2                                                        09850002
CCH141   TM    CSWLOC+FIVE,CCCBIT .     IS CCC IN CSW                   10250002
         BO    CCH141B .                IF YES,CHK LS ADDER             10300002
CCH141H  TM    LOGWD3B+TWO,HEX02 .      IS ADDR-IN CHK UP               10350002
         BZ    CCH141D .                NO,BRANCH                       10400002
CCH141A  OI    IGBLAME,CCHINTFC .      I/F IS SOURCE OF ERROR           10450002
         B     CCH141F .                TEST FOR STG CHECK              10500002
         SPACE 1                                                        10550002
CCH141B  TM    LOGWD3B+ONE,HEX20 .      IS LOCAL STORE ADDER CHK UP     10600002
         BZ    CCH141C .                NO,BRANCH                       10650002
         TM    LOGWD3B,HEX04            BIT 37,LOGWD 3 SET              10700002
         BZ    CCH141E .                NO,BRANCH. CHANNEL ERROR        10750002
         OI    IGBLAME,CCHCPU .        YES,CPU(UAB0) ERROR              10800002
         B     CCH141F .                TEST FOR STG CHECK              10850002
         SPACE 1                                                        10900002
CCH141C  TM    LOGWD3B+TWO,HEX80 .      UNIT ADDR CHECK SET             10950002
         BO    CCH141E .                YES,BRANCH. CHANNEL ERROR       11000002
         B     CCH141H .                SEE IF ICC IN LOG               11050002
CCH141D  LA    WKREG2,LOGWD3A+THREE .   GET UA ADDR IN LOG AREA.        11100002
         SPACE 1                                                        11150002
*****************************************************************       11200002
*        THIS ROUTINE CHECKS TO INSURE THAT THE CORRECT UNIT ADDRESS    11250002
*        IS SET VALID. IF IOS UNIT ADDRESS IS NOT EQUAL TO THE UNIT     11300002
*        ADDRESS IN THE LOG, THE IOS UNIT ADDRESS IS INVALID            11350002
*****************************************************************       11400002
         SPACE 2                                                        11450002
UAVALSET TM    RECBYTE,CCHTIOB .        IS THIS AN ERROR ON A TIO       11700002
         BO    CCHTSTUA .               BRANCH TO TEST IF UA OK         11750002
CCHDAVAL OI    IGVALIDB,CCHDAV .        SET UA VALID BIT                11800002
         B     CCH141F                  TO RETURN TO NORMAL FLOW        11850002
         SPACE 1                                                        11900002
*                                      DOES THE UA FROM IOS EQ THE      11950002
TSTLOGUA CLI   ZERO(WKREG2),ZERO       UA IN THE LOG                    12000002
         SPACE 1                                                        12050002
CCHTSTUA EX    UAREG,TSTLOGUA .         SEE IF HAVE CORRECT UA          12100002
         BE    CCHDAVAL                YES,GO SET DEVICE ADDRESS VALID  12150002
         B     CCH141F .                GO TEST FOR STORAGE CHK         12200002
         SPACE 2                                                        12250002
CCH141E  OI    IGBLAME,CCHCHNL .       INTERNAL CHANNEL ERROR           12450002
         SPACE 1                                                        12500002
***************************************************************         12550002
*                                                                       12600002
*        CHECK FOR SOURCE OF ERROR ON STORAGE CHECK                     12650002
*                                                                       12700002
****************************************************************        12750002
         SPACE 2                                                        12800002
CCH141F  TM    LOGWD3B+ONE,HEX10 .      STORAGE CHECK                   13150002
         BZ    CCH142 .                 NO,GO CHECK COUNT VALID         13200002
         TM    LOGWD3B,HEX02 .          BIT 38 UP                       13250002
         BZ    CCH141G                 NO,BRANCH PROBABLE MEMORY ERR    13300002
         OI    IGBLAME,CCHCHNL .       CHANNEL IS SOURCE OF ERR         13350002
         TM    LOGWD3A,HEX40            CCW REQUIRED SET                13400002
         BZ    CCH142                   NO,CHECK COUNT VALID            13450002
         B     CCH143A                  DO NOT SET COUNT VALID          13500002
         SPACE 1                                                        13550002
CCH141G  OI    IGBLAME,CCHSTG .        STORAGE IS SOURCE OF ERR         13600002
         B     CCH143A                  DO NOT SET COUNT VALID          13650002
         SPACE 1                                                        13700002
*****************************************************************       13750002
*        CHECK FOR VALID COUNT                                          13800002
*****************************************************************       13850002
         SPACE 2                                                        13900002
CCH142   TM    LOGWD3B+ONE,HEX08 .      IS BYTE COUNT PARITY BAD        14250002
         BO    CCH142A .                YES,BRANCH.INVALID COUNT        14300002
         TM    LOGWD3B+TWO,HEX20 .      BYTE COUNT(SSC ONLY) BAD        14350002
         BO    CCH142A .                YES,BRANCH                      14400002
         TM    LOGWD3B+ONE,HEX80 .      UCW COUNT CHECK                 14450002
         BZ    CCH143 .                 NO,BRANCH-SET COUNT VALID       14500002
         TM    LOGWD2B,HEX03 .          ANY UCW PARITY BITS             14550002
         BZ    CCH143 .                 NO,BRANCH-SET COUNT VALID       14600002
         SPACE 1                                                        14650002
CCH142A  OI    IGBLAME,CCHCHNL .       CHANNEL IS SOURCE OF ERR         14700002
         B     CCH143A                  BY PASS COUNT VALID BIT         14750002
         SPACE 2                                                        14800002
CCH143   OI    IGPRGFLG,CCHCNTB .       SET COUNT VALID BIT             15400002
CCH143A  TM    LOGWD3B+ONE,HEX06 .      IS BIT 45 OR 46 ON,LOGWD 3      15450002
         BZ    CCH144 .                 NO,BRANCH                       15500002
         OI    IGBLAME,CCHCHNL .       YES,PROG CHANNEL ERR             15550002
         SPACE 1                                                        15600002
*****************************************************************       15650002
*        TEST  FOR VALID UNIT STATUS                                    15700002
*****************************************************************       15750002
         SPACE 1                                                        15800002
CCH144   TM    LOGWD3B+TWO,HEX01 .      IS STATUS-IN CHECK UP           15850002
         BNO   CCH145 .                 GO CHK FOR SYSTEM RESET         15900002
         SPACE 1                                                        15950002
CCH144A  OI    IGBLAME,CCHINTFC .      I/F IS SOURCE OF ERROR           16000002
         B     CCH145A             GO CHK FOR CHANNEL CHECK             16050002
         SPACE 1                                                        16100002
CCH145   TM    CSWLOC+FIVE,ICCBIT .     IS THIS ICC                     16150002
         BO    CCH145A .                NO,BRANCH                       16200002
         OI    IGVALIDB,CCHUSV .        NO,SET UNIT STATUS VALID        16250002
*                                       GET LOGWORD FOR TYPE TWO        16300002
CCH145A  L     WKREG2,LOGWD3B           CHANNEL CHECK                   16350002
         N     WKREG2,SYSRESET          CHECK FOR A TYPE II ERROR       16400002
         BZ    CCH146 .                 NO, CONTINUE                    16450002
         NI    IGPRGFLG,ALLFS-CCHCNTB   TURN OFF VALID COUNT FLAG    CF 16500002
         NI    IGVALIDB,ALLFS-CCHCMDV   TURN OFF CMD. ADDR. VALID FLGCF 16550002
         TM    LOGWD3B+TWO,HEX82        Q. ADDR-IN OR UNIT ADDR CHECKCF 16560002
         BO    CCH146H                  YES. RESET DEV ADDR & UA INV CF 16570002
         TM    LOGWD3B,HEX01            Q. SECOND CHECK              CF 16580002
         BO    CCH146I                  YES. SET CHAN AS SOURCE OF ERCF 16590002
         TM    LOGWD3B+ONE,HEX20        Q. LOCAL STOR ADDR CHECK     CF 16592002
         BO    CCH146J                  YES. MAKE UABO CHECK TEST    CF 16594002
         B     CCH146M                  SEND SELECTIVE RESET         CF 16596002
*                                                                       16600002
         SPACE 2                                                        16650002
CCH146   TM    CSWLOC+FIVE,CCCBIT+ICCBIT . CHECK FOR BOTH CATS          17100002
         BM    CCH147 .                 EITHER CCC OF ICC, BRANCH       17150002
CCH146A  OI    IGTERMSQ,COMPSEL+RTCODE7 .    SEL RESET+RETRY            17500002
         B     CCHRETRN .               TO COMMON RETURN POINT          17550002
*                                                                       17600002
         SPACE 2                                                        17650002
CCH146H  NI    IGVALIDB,ALLFS-CCHDAV    SET UNIT ADDR INVALID IN ERP CF 17660002
         TM    LOGWD3B+TWO,HEX02        Q. ADDRESS-IN CHECK          CF 17670002
         BNO   CCH146I                  NO. SET CHAN SRC OF ERROR    CF 17680002
         OI    IGBLAME,CCHINTFC         SET INTERFACE SRC OF ERROR   CF 17690002
         B     CCH146K                  GO SET TERM & SEQ. CODES     CF 17692002
*                                                                       17694002
         SPACE 1                                                        17696002
CCH146I  OI    IGBLAME,CCHCHNL          SET THE CHAN SRC OF ERROR    CF 17698002
         B     CCH146L                                               CF 17698402
*                                                                       17698802
        SPACE 1                                                         17699202
CCH146J  TM    LOGWD3B+TWO,HEX80        Q. UABO CHECK                CF 17699602
         BNO   CCH146M                  NO. CHECK FOR SEL. RESET SENTCF 17699702
         OI    IGBLAME,CCHCPU           SET CPU SRC OF THR EEROR     CF 17699802
         NI    IGVALIDB,ALLFS-CCHDAV    SET DEVICE ADDRESS INVALID   CF 17705402
         OI    IGTERMSQ,COMPFES+RTCODE4 SET FES TERM & RC OF 100     CF 17711002
         B     CCHRETRN                 RETURN                       CF 17716602
*                                                                       17726602
        SPACE 1                                                         17728602
CCH146M  TM    LOGWD3B,HEX80            SELECTIVE SENT?              CF 17730602
         BO    CCH146K                                               CF 17732602
*                                                                       17733002
CCH146L OI    IGTERMSQ,COMPFES+RTCODE7  SET FES TERM & 111 RC        CF 17733102
         B CCHRETRN                     RETURN                       CF 17733202
*                                                                       17738802
CCH146K  OI    IGTERMSQ,COMPSEL+RTCODE7 SET SEL TERM & 111 RC        CF 17740802
         B     CCHRETRN                 RETURN                       CF 17742802
*                                                                       17743202
         SPACE 2                                                        17900002
CCH147   TM    CSWLOC+FIVE,ICCBIT .        ICC ONLY                     17950002
         BO    CCH148 .                 YES,GO AND PROCESS              18000002
         OI    IGTERMSQ,COMPFES .       SET COMP CODE TO SHOW FES       18050002
         TM    RECBYTE,CCHTIOB .        IS CAUSE A TIO INSTRUCTION      18100002
         BO    CCH149 .                 YES, CHECK                      18150002
         TM    RECBYTE,CCHSIOB .        IS CAUSE A SIO INSTRUCTION      18200002
         BO    CCH150 .                 YES, CHECK                      18250002
         SPACE 2                                                        18300002
CCH162   TM    LOGWD3A,HEX01         PSUEDO SIO                         18650002
         BZ    CCH151 .                 NO, SKIP TIO BIT TEST           18700002
         TM    LOGWD3A,HEX04         IS TIO BIT SET IN LOG              18750002
         BO    CCH152 .                 YES, GO TO SET RETRY CODE       18800002
CCH151   TM    LOGWD3A+ONE,HEX04 .      MODE TRIGGER 6                  18850002
         BO    CCH153 .                 YES, CHECK COMMAND CHK SET      18900002
         TM    LOGWD3A+TWO,HEXE0 .      MODE TRIGGERS 9,10 OR 11        18950002
         BNZ   CCH153 .                 YES, CHECK COMMAND CHK SET      19000002
         TM    LOGWD3A+ONE,HEX70 .      MODE TRIGGERS 2,3 OR 4          19050002
         BZ    CCH154 .                 NONE, CHECK COUNT               19100002
CCH157   OI    IGTERMSQ,RTCODE3 .       SET RETRY CODE TO 011           19150002
         B     CCHRETRN .               TO COMMON RETURN POINT          19200002
*                                                                       19250002
         SPACE 2                                                        19300002
CCH153   TM    LOGWD3B+TWO,HEX40       COMMAND CHECK IN LOG             19800002
         BO    CCH165B .                YES,GO SET SOURCE OF ERROR      19850002
*                                       HAS THE COUNT VALIDITY BIT      19900002
CCH154   TM    IGPRGFLG,CCHCNTB .       BEEN SET                        19950002
         BO    CCH155 .                 YES, CHECK COMMAND ADDR         20000002
CCH156   OI    IGTERMSQ,RTCODE5 .       SET RETRY CODE TO 101           20050002
         B     CCHRETRN .               RETURN TO COMMON RETURN POINT   20100002
*                                                                       20150002
CCH156A  OI    IGTERMSQ,COMPSEL    SET TERM CODE = '10'                 20200002
         B     CCH156              GO SET SEQ CODE                      20250002
*                                                                       20300002
CCH155   TM    IGVALIDB,CCHCMDV .       IS THE COMMAND ADDR VALID       20350002
         BZ    CCH156 .                 INVALID COMMAND ADDR, BRANCH    20400002
         L     XLATEREG,CSWLOC          GET COMMAND ADDR             CF 20450002
         LA    XLATEREG,ZERO(XLATEREG)  CLEAR HIGH ORDER BYTE        CF 20500002
         SH    XLATEREG,CCHLENGH        GET CUURENT CCW ADDRESS      CF 20550002
         BM    CCH156                   BR IF NEGATIVE CMD ADDR         20600002
         ST    LINKRG2,PCCACHW2         SAVE THE FE RETURN ADDR      CF 20610002
         L     WKREG1,CVTPTR            GET POINTER TO CVT           CF 20620002
         USING CVTMAP,WKREG1            GET ADDRESSABILITY TO CVT    CF 20630002
         L     LINKRG3,CVTPTRV          CET PTR TO XLATE RTN         CF 20640002
         DROP  WKREG1                                                CF 20642002
         BALR  LINKRG2,LINKRG3          TRANSLATE THE VIRTUAL ADDR   CF 20644002
         L     LINKRG2,PCCACHW2         RESTORE RETURN REG           CF 20644402
         LTR   LINKRG3,LINKRG3          Q. THIS A VALID VIRTUAL ADDR.CF 20644802
         BNZ   CCH156                   NO. CCW ADDR INVALID.        CF 20646002
         L     TESTWK,PCCAPSAV         GET ADDR. OF PSA(V)     @ZA18585 20646251
         L     TESTWK,PSAAOLD-PSA(TESTWK) GET ADDR. OF PSAOLD  @ZA18585 20646451
         LH    TESTWK,ASCBASID-ASCB(TESTWK) GET ASID NUMBER    @ZA18585 20646651
         CR    TESTWK,REG0             COMPARE ASID            @ZA18585 20647051
         BNZ   CCH156                  NO - ASID'S NOT EQUAL   @ZA18585 20647251
         LR    WKREG1,XLATEREG          YES, GET ADDR IN REG 10.     CF 20648002
         LR    WKREG3,WKREG1 .          SAVE CCW ADDR                   20650002
         LH    WKREG1,SIX(WKREG1)       GET CURRENT CCW COUNT           20700002
         N     WKREG1,CCHMASK1 .        AND OUT UNWANTED BITS           20750002
         LH    WKREG2,CSWLOC+SIX        GET CSW COUNT                   20800002
         N     WKREG2,CCHMASK1 .        AND OUT UNWANTED BITS           20850002
         CR    WKREG2,WKREG1 .          COMPARE CSW CNT AND CCW CNT     20900002
*                                       BRANCH IF CSW COUNT GREATER     20950002
         BH    CCH155B .                THAN CCW COUNT                  21000002
         BNE   CCH157 .                 DATA TRANSFER, SET RETRY CODE   21050002
CCH155A  OI    IGTERMSQ,RTCODE2 .       SET RETRY CODE TO 010           21100002
         B     CCHRETRN .               RETURN TO COMMON RETURN POINT   21150002
*                                                                       21200002
CCH155B  TM    IGVALIDB,CCHDAV .        UNIT ADDRESS VALID              21250002
         BO    CCH155C .                YES,BRANCH                      21300002
         OI    IGTERMSQ,RTCODE7 .       SET NO RETRY CODE               21350002
         B     CCHRETRN .               RETURN TO COMMON POINT          21400002
         SPACE 2                                                        21450002
CCH155C  BAL   LINKRG1,CCH164A .        CHECK FOR MPX CHAN              21800002
         BNL   CCH156D .                NOT MPX-BRANCH CC SET IN SUBRTN 21850002
         NI    IGVALIDB,ALLFS-CCHCNTB . SET COUNT INVALID               21900002
         B     CCH156 .                 MPX-SET RETRY CODE TO 101       21950002
CCH156D  IC    WKREG2,THREE(WKREG3) .   GET LOW DATA ADDR BITS          22000002
         N     WKREG2,CCHMASK2 .        AND OUT UNWANTED BITS           22050002
         IC    WKREG1,LOGWD3B+THREE .   GET LOG BYTE COUNT BITS         22100002
         SRL   WKREG1,CCH4 .            ISOLATE IN LOW ORDER            22150002
         N     WKREG1,CCHMASK2 .        AND OUT UNWANTED BITS           22200002
         CR    WKREG2,WKREG1 .          CCW BITS TO LOG BITS            22250002
         BNE   CCH157 .                 NOT EQUAL-SET RETRY TO 011      22300002
         B     CCH155A .                SET RETRY CODE TO 010           22350002
*                                                                       22400002
         SPACE 2                                                        22450002
CCH148   OI    IGBLAME,CCHINTFC .      I/F IS SOURCE OF ERROR           23100002
         TM    LOGWD1A,HEX03 .          ARE THESE BITS SET(EITHER)      23150002
         BNZ   CCH148A .                YES,BRANCH                      23200002
CCH148C  TM    LOGWD3A+ONE,HEX08   WORD 3 BIT 12 ON                     23250002
         BO    CCH148A             YES, GO SET CODES                    23300002
CCH148B  OI    IGTERMSQ,COMPSEL+RTCODE3 .SEL RESET + RETRY 011          23350002
         B     CCHRETRN .               TO COMMON RETURN POINT          23400002
         SPACE 1                                                        23450002
CCH148A  TM    LOGWD3B+TWO,HEX04 .      INCORRECT TAG SEQUENCE          23500002
         BZ    CCH158 .                 NO, NEXT TEST                   23550002
         TM    LOGWD3A+ONE,HEX08   WORD 3 BIT 12 ON                     23600002
         BO    CCH156A             YES, GO SET CODES                    23650002
         B     CCH146A .                GO SET SEL TERM AND RETRY 111 I 23700002
*                                                                       23750002
CCH158   TM    LOGWD3B+TWO,HEX10        INCORRECT SELECTION             23800002
         BZ    CCH159 .                 NO, NEXT TEST                   23850002
         OI    IGTERMSQ,COMPSEL+RTCODE4 SET SEL TERM AND RETRY CODE 100 23900002
         B     CCHRETRN .               RETURN                          23950002
*                                                                       24000002
CCH159   TM    LOGWD3B+TWO,HEX08        NO RESPONSE BIT                 24050002
         BZ    CCH160 .                 NO, NEXT TEST                   24100002
         OI    IGTERMSQ,COMPFES+RTCODE4 SET FES TERM AND RETRY CODE 100 24150002
         OI    IGVALIDB,CCHCMDV .       SET COMMAND ADDRESS VALID       24200002
         B     CCHRETRN .               RETURN                          24250002
*                                                                       24300002
         SPACE 2                                                        24350002
CCH160   TM    LOGWD3B+TWO,HEX01 .      STATUS-IN CHECK                 24800002
         BO    CCH160A .                YES,BRANCH                      24850002
         SPACE 1                                                        24900002
*****************************************************************       24950002
*        CPU TIME-OUT CONDITION                                         25000002
*****************************************************************       25050002
         SPACE 1                                                        25100002
         TM    LOGWD3B,HEX01           Q. DISCONNECT-IN IN LOG       CF 25150002
         BZ    CCH160D                  NO. GO SET CODES AND RETURN  CF 25160002
         OI    IGTERMSQ,CCHDI           SET DISCONNECT-IN IN ERPIB   CF 25170002
         OI    IGTERMSQ,COMPSEL+RTCODE7  SET SEL TERM & RC OF 111    CF 25180002
         B     CCHRETRN .               TO COMMON RETURN POINT          25200002
         SPACE 1                                                        25250002
CCH160A  TM    RECBYTE,CCHSIOB .        IS ERROR CAUSED BY SIO          25300023
         NOP   CCH160B .                ALL STATUS IN CKS RESULT IN    *25350023
                              TERM CODE 10 AND SEQ CODE 001 JE @ZA00156 25360023
CCH160B  EQU   *                                            JE @ZA00516 25370023
         OI    IGTERMSQ,COMPSEL+RTCODE1 SET SEL TERM AND RETRY CODE 001 25400002
         B     CCHRETRN .                                               25450002
*                                                                       25500002
CCH160D  OI    IGTERMSQ,COMPSEL+RTCODE5 SET SEL TERM & RC 101        CF 25510002
         B     CCHRETRN                 RETURN                       CF 25520002
*                                                                       25530002
         SPACE 2                                                        25550002
CCH149   TM    LOGWD3A,HEX04            TEST I/O BIT IN LOG             25900002
         BO    CCH162 .                 YES, BRANCH                     25950002
         TM    IGVALIDB,CCHUSV .        IS UNIT STATUS VALID            26000002
*                                       NO BRANCH, DO NOT CHECK         26050002
         BZ    CCH163 .                 CHANNEL END OR DEVICE END       26100002
         TM    CSWLOC+FOUR,HEX0C        CHANNEL END OR DEVICE END       26150002
         BC    FIVE,CCH162 .            YES, CHECK PSUEDO SIO           26200002
         OI    IGTERMSQ,RTCODE3 .       NEW CODE.ORIGINAL WAS 000       26250002
         B     CCHRETRN .               RETURN                          26300002
*                                                                       26350002
CCH163   OI    IGTERMSQ,RTCODE7 .       SET RETRY CODE TO 111           26400002
         B     CCHRETRN .               RETURN                          26450002
*                                                                       26500002
         SPACE 2                                                        26550002
CCH150   TM    LOGWD3A,HEX80            CAW ERROR                       27050002
         BZ    CCH164 .                 NO, NEXT TEST                   27100002
CCH152   OI    IGTERMSQ,RTCODE4 .       SET RETRY CODE TO 100           27150002
         B     CCHRETRN .               RETURN                          27200002
*                                                                       27250002
CCH164   TM    IGVALIDB,CCHDAV          WAS VALID UNIT ADDR SET ?       27300002
         BZ    CCH152                   BRANCH, NO                      27350002
         LA    LINKRG1,CCH164B .        SET RETURN PT FROM MPX TEST     27400002
*                                                                       27450002
*     THE FOLLOWING IS AN IN-LINE SUBROUTINE WHICH MAY BE CALLED FROM   27500002
*     'CCH155C' OR FALLEN INTO FROM ABOVE.  IT SETS THE CONDITION CODE  27550002
*      WHICH IS TESTED BY THE CALLING ROUTINE.                          27600002
*                                                                       27650002
CCH164A  SR    WKREG1,WKREG1 .          CLEAR REGISTER                  27700002
         LA    WKREG1,HEXC0             LOAD MPX UNIT ADDR COMPARE CONT 27750002
         LA    WKREG2,ALLFS             LOAD AND OUT VALUE              27800002
         NR    WKREG2,UAREG .           LOAD UA AND DELETE CHANNEL &DDR 27850002
         CR    WKREG2,WKREG1 .          IS THIS AN MPX CHAN             27900002
         BR    LINKRG1 .                RETURN TO CALLER OR FALL THRU   27950002
*                                       TO NSI                          28000002
CCH164B  BNL   CCH165 .                 NOT MPX,BRANCH CC SET IN SUBRTN 28050002
         TM    LOGWD3A,HEX40            CCW REQUIRED ON                 28100002
         BO    CCH152 .                 YES, GO TO SET RETRY CODE       28150002
CCH165   TM    LOGWD3A,HEX04            TIO BIT ON                      28200002
         BO    CCH152 .                 YES, GO TO SET RETRY CODE       28250002
CCH165A  OI    IGBLAME,CCHCHNL .       CHANNEL IS SOURCE OF ERROR       28300002
         NI    IGTERMSQ,ALLFS-COMPFES . DELETE TERM=01 ALREADY SET      28350002
         OI    IGTERMSQ,COMPSEL .       SET TERM CODE=10                28400002
         B     CCH152 .                 SET RETRY TO 100,RETURN         28450002
*                                                                       28500002
         SPACE 2                                                        28550002
CCH165B  OI    IGBLAME,CCHCHNL .       CHANNEL IS SOURCE OF ERROR       28700002
         B     CCH152 .                 GO SET RETRY TO 100             28750002
*                                                                       28800002
         SPACE 2                                                        28850002
SETSYSTR OI    CCHSW1,CCHNOLST     SET RECORD ONLY SWITCH               29250002
         OI    CCHSW2,SYSTERM+NOLOG SET SYSTEM TERMINATION SWITCH    CF 29300002
CCHRETRN LA    WKREG1,LOGADDR           GET THE ADDR OF LOGOUT       CF 29350002
         ST    WKREG1,PCCALOGA          AND PLACE IT IN THE CCA      CF 29400002
         MVC   PCCALOGL,LOGLNTH      PUT LENGTH OF LOG IN PCCA @G51BPLC 29405051
         BR    LINKRG2                  RETURN TO CCH CENTRAL        CF 29600002
*                                                                       29750002
         EJECT                                                          29800002
*        CONSTANTS                                                      29850002
         SPACE                                                          29900002
         DS    0F                                                       29950002
CCHMASK1 DC    X'0000FFFF' MASK FOR CLEARING OUT UNWANTED BITS          30000002
CCHMASK2 DC    X'00000007' MASK FOR CLEARING OUT UNWANTED BITS          30050002
SYSRESET DC    X'05268200'         SYSTEM RESET CONSTANT                30100002
LOGLNTH  DC    X'0018'             LENGTH OF LOGOUT            @G51BPLC 30120051
ALLONES  DC    F'-1'              FOR LOG OUT COMPARISON                30150002
CCHLENGH DC    H'8'                CORRECTION FACTOR FOR CMD ADDR       30250002
*                                                                       30300002
********************************************************************    30350002
*                                                                  *    30400002
*         MAINTAINENCE AREA                                        *    30450002
*                                                                  *    30500002
********************************************************************    30550002
*                                                                       30600002
         DS    0F                                                       30650002
C70PATCH DC    50X'00'         PATCH AREA                               30700045
MAINT    EQU   C70PATCH        FOR XREF                        @ZA32238 30730045
         EJECT                                                          30760045
         CVT   DSECT=YES                                       @ZA32238 30790045
         EJECT                                                          30870002
         IHAPSA                                                @ZA18585 30876051
         EJECT                                                          30882051
         IHAASCB                                               @ZA18585 30888051
         EJECT                                                          30894051
         IGFERPIB 1                                                     30900002
         EJECT                                                          30950002
         IHAPCCA                                                        31000002
         END                                                            31050002
