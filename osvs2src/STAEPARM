         MACRO                                                          00050002
&NAME    STAEPARM  &FLAG=,                                             X00100002
               &PSEUDO=,                                               X00150002
               &LIST=,                                                 X00200002
               &ADDR=,                                                 X00250002
               &TYPE=A,                                                X00300002
               &MF=                                                     00350002
.*                                                                      00400002
.*       THIS MACRO BUILDS THE CONTROL BLOCK THAT IS PASSED TO THE      00450002
.*       IKJEHSIR EXIT ROUTINE VIA THE PARAM= PARAMETER OF THE STAE     00500002
.*       MACRO.                                                         00550002
.*                                                                      00600002
.*       &FLAG  IS THE FLAG TO SET IN THE BLOCK, CAN BE ONE OF TWO      00650002
.*             SETTINGS:  CP OR TMP.                                    00700002
.*             IF IT IS CP, THE DEFAULT, IT SAYS THE CP WANTS TO HANDLE 00750002
.*             SOME ABENDS.  IF IT IS TMP, IT SAYS THAT THE CP WANTS    00800002
.*             THE TMP TO HANDLE ALL ABENDS.                            00850002
.*       &PSEUDO  THE ADDRESS OF THE PSEUDO RETRY ROUTINE BUILT WITH    00900002
.*             THE PSEUDO MACRO.  FOR A FULL EXPLAINATION OF THE        00950002
.*             PSEUDO RETRY ROUTINE, SEE THE PSEUDO MACRO.              01000002
.*       &LIST  THIS IS THE ADDRESS OF THE RETRY CODE TABLE BUILD       01050002
.*             WITH THE STABUILD MACRO. FOR A FULLER EXPLAINATION,      01100002
.*             OF THE RETRY CODE TABLE SEE THE STABUILD MACRO.          01150002
.*       &ADDR  THIS IS THE ADDRESS OF THE MF=L FORM OF THIS MACRO.     01200002
.*             IT IS USED BY THE MF=E MACRO TO FILL IT IN, OR IT IS     01250002
.*             USED BY THE MF=L FORM AS A NAME IF NO NAME IS PROVIDED   01300002
.*       &TYPE TYPE OF MACRO EXPANSION FOR MF=E                         01350002
.*             A  SAYS THAT NEW BLOCK BEING BUILD                       01400002
.*             C  SAYS THAT OLD BLOCK BEING CHANGED                     01450002
.*                                                                      01500002
         LCLA  &FNUM                                                    01550002
         LCLB  &F(8)                                                    01600002
.*                                                                      01650002
.*       IF NO FLAG IS GIVEN, DEFAULT TO CP                             01700002
.*                                                                      01750002
         AIF   (T'&FLAG EQ 'O').SET1                                    01800002
.*                                                                      01850002
.*       LOOP THRU THE FLAG POSSIBILITIES AND SET SOME BITS             01900002
.*                                                                      01950002
&FNUM    SETA  N'&FLAG   NUMBER OF FLAG SETTINGS                        02000002
.LOOP1   AIF   ('&FLAG(&FNUM)' NE 'CP').LOOP2                           02050002
&F(8)    SETB  1                                                        02100002
.LOOP2   AIF   ('&FLAG(&FNUM)' NE 'TMP').LOOP3                          02150002
&F(7)    SETB  1                                                        02200002
.LOOP3   ANOP                                                           02250002
&FNUM    SETA  &FNUM-1                                                  02300002
         AIF   (&FNUM NE 0).LOOP1                                       02350002
         AIF   ('&TYPE' EQ 'C').SETTC      ARE WE CHANGING AN OLD BLOCK 02400002
         AIF   (&F(8) EQ 0 AND &F(7) EQ 0).WHOKNOW  BAD FLAG            02450002
.SETTC   AIF   (&F(8) EQ 1 AND &F(7) EQ 1).BADFLAG  CANT BE BOTH        02500002
&FNUM    SETA  &F(7)*2+&F(8)                                            02550002
         AGO   .SET                                                     02600002
.SET1    ANOP                                                           02650002
&FNUM    SETA  1        DEFAULT TO CP                                   02700002
.SET     ANOP                                                           02750002
.*                                                                      02800002
.*             IF ITS LIST FORM, GO CHECK ITS REQUIREMENTS              02850002
.*                                                                      02900002
         AIF   ('&MF' EQ 'L').MFL                                       02950002
.*                                                                      03000002
.*   EXECUTE FORM OF MACRO                                              03050002
.*                                                                      03100002
         AIF   ('&TYPE' EQ 'C').TYPEC   CHANGING PREVIOUS               03150002
         AIF   (T'&PSEUDO EQ 'O').NOPS   NEED PSEUDO PARM               03200002
         AIF   (&F(7) EQ 1).ADDR  DONT NEED LIST IF TMP SPECIFIED       03250002
         AIF   (T'&LIST EQ 'O').NOLST    NEED A LIST                    03300002
.ADDR    AIF   (T'&ADDR EQ 'O').NOADDR  NEED AN ADDRESS                 03350002
         SPACE 1                                                        03400002
.*                                                                      03450002
.*             CHECK FOR SOME THINGS BEING IN REGISTERS                 03500002
.*                                                                      03550002
         AIF   ('&ADDR'(1,1) EQ '(').REGAD   ADDRESS IN REG             03600002
         AIF   (T'&LIST EQ 'O').NLIST  MAY NOT HAVE A LIST              03650002
         AIF   ('&LIST'(1,1) EQ '(').RLIST  LIST IN REG                 03700002
&NAME    LA    15,&LIST            LIST OF CODES OR POINTERS            03750002
         AGO   .NLIST1             BYPASS                               03800002
.NLIST   ANOP                                                           03850002
&NAME    LA    15,0                LIST OMITTED                         03900002
.NLIST1  ST    15,&ADDR            STORE LIST POINTER                   03950002
         AGO   .RLIST1                                                  04000002
.RLIST   ANOP                                                           04050002
&NAME    ST    &LIST(1),&ADDR      LIST OF CODES                        04100002
.RLIST1  MVI   &ADDR,&FNUM         STORE FLAG                           04150002
         AIF   ('&PSEUDO'(1,1) EQ '(').RPS   PSEUDO IN REG              04200002
         LA    15,&PSEUDO          PSEUDO RETRY ROUTINE                 04250002
         ST    15,&ADDR+4          STORE PSEUDO POINTER                 04300002
         AGO   .END                                                     04350002
.RPS     ST    &PSEUDO(1),&ADDR+4  STORE PSEUDO RETRY ROUTINE           04400002
.END     SPACE 1                                                        04450002
         MEXIT                                                          04500002
.REGAD   AIF   (T'&LIST EQ 'O').NLIST2  NEED NOT HAVE IT                04550002
         AIF   ('&LIST'(1,1) EQ '(').REGAD1   LIST IN A REG             04600002
&NAME    LA    15,&LIST            LIST OF CODES OR POINTERS            04650002
         AGO   .NLIST3                                                  04700002
.NLIST2  ANOP                                                           04750002
&NAME    LA    15,0                LIST OMITTED                         04800002
.NLIST3  ST    15,0(&ADDR(1))      STORE LIST POINTER                   04850002
         AGO   .REGAD2                                                  04900002
.REGAD1  ANOP                                                           04950002
&NAME    ST    &LIST(1),0(&ADDR(1))  LIST OF CODES                      05000002
.REGAD2  MVI   0(&ADDR(1)),&FNUM   STORE FLAG BYTE                      05050002
         AIF   ('&PSEUDO'(1,1) EQ '(').REGAD3                           05100002
         LA    15,&PSEUDO          PSEUDO RETRY ROUTINE                 05150002
         ST    15,4(&ADDR(1))      STORE PSEUDO POINTER                 05200002
         AGO   .END                                                     05250002
.REGAD3  ST    &PSEUDO(1),4(&ADDR(1))  STORE PSEUDO RETRY ROUTINE       05300002
         AGO   .END                                                     05350002
.*                                                                      05400002
.*      CHANGING PREVIOUS STAEPARM  TYPE=C                              05450002
.*                                                                      05500002
.TYPEC   AIF   (T'&ADDR EQ 'O').NOADDR   NEED AN ADDRESS                05550002
         AIF   ('&ADDR'(1,1) EQ '(').REGTC  ADDRESS IN REG              05600002
         AIF   (T'&LIST EQ 'O' AND T'&FLAG EQ 'O').PSEUTC  ONLY PSEUDO  05650002
         AIF   (T'&LIST NE 'O' AND T'&FLAG NE 'O').BOTHTC   BOTH GIVEN  05700002
         AIF   (T'&LIST EQ 'O').FLAGTC   FLAG ONLY                      05750002
&NAME    IC    15,&ADDR      SAVE FLAG BYTE                             05800002
         AIF   ('&LIST'(1,1) EQ '(').LSTTCR   LIST IN REG               05850002
         LA    14,&LIST    NEW LIST ADDRESS                             05900002
         ST    14,&ADDR    STORE IT                                     05950002
.LSTTCR1 STC   15,&ADDR   RESTORE FLAG                                  06000002
         AGO   .PSEUTC1    GO ON                                        06050002
.LSTTCR  ST    &LIST(1),&ADDR   STORE NEW LIST ADDRESS                  06100002
         AGO   .LSTTCR1                                                 06150002
.FLAGTC  ANOP                                                           06200002
&NAME    MVI   &ADDR,&FNUM   STORE NEW FLAG                             06250002
         AGO   .PSEUTC1                                                 06300002
.BOTHTC  AIF   ('&LIST'(1,1) EQ '(').BOTHTCR  LIST IN REG               06350002
&NAME    LA    14,&LIST  NEW LIST ADDRESS                               06400002
         ST    14,&ADDR  STORE IT                                       06450002
.BOTHTC1 MVI   &ADDR,&FNUM   NEW FLAG BYTE                              06500002
         AGO   .PSEUTC1                                                 06550002
.BOTHTCR ANOP                                                           06600002
&NAME    ST    &LIST(1),&ADDR  STORE NEW LIST ADDRESS                   06650002
         AGO   .BOTHTC1                                                 06700002
.PSEUTC  AIF   (T'&PSEUDO EQ 'O').WHY  EVERYTHING MISSING               06750002
         AIF   ('&PSEUDO'(1,1) EQ '(').PSEUTCR  PSEUDO IN REG           06800002
&NAME    LA    15,&PSEUDO   NEW PSEUDO POINTER                          06850002
         ST    15,&ADDR+4   STORE IT                                    06900002
         AGO   .END                                                     06950002
.PSEUTCR ANOP                                                           07000002
&NAME    ST    &PSEUDO(1),&ADDR+4  STORE NEW PSEUDO POINTER             07050002
         AGO   .END                                                     07100002
.PSEUTC1 AIF   (T'&PSEUDO EQ 'O').END  PSEUDO OMITTED                   07150002
         AIF   ('&PSEUDO'(1,1) EQ '(').PSEUTC2  PSEUDO IN REG           07200002
         LA    15,&PSEUDO  NEW PSEUDO POINTER                           07250002
         ST    15,&ADDR+4  STORE IT                                     07300002
         AGO   .END                                                     07350002
.PSEUTC2 ST    &PSEUDO(1),&ADDR+4  STORE NEW PSEUDO POINTER             07400002
         AGO   .END                                                     07450002
.REGTC   AIF   (T'&LIST EQ 'O' AND T'&FLAG EQ 'O').PSEURC  ONLY PSEUDO  07500002
         AIF   (T'&LIST NE 'O' AND T'&FLAG NE 'O').BOTHRC  BOTH GIVEN   07550002
         AIF   (T'&LIST EQ 'O').FLAGRC  FLAG ONLY                       07600002
&NAME    IC    15,0(&ADDR(1))  SAVE FLAG BYTE                           07650002
         AIF   ('&LIST'(1,1) EQ '(').LSTRCR  LIST IN REG                07700002
         LA    14,&LIST   NEW LIST ADDRESS                              07750002
         ST    14,0(&ADDR(1))  STORE IT                                 07800002
.LSTRCR1 STC   15,0(&ADDR(1))  RESTORE FLAG                             07850002
         AGO   .PSEURC1  GO ON                                          07900002
.LSTRCR  ST    &LIST(1),0(&ADDR(1))  STORE NEW LIST ADDRESS             07950002
         AGO   .LSTRCR1                                                 08000002
.FLAGRC  ANOP                                                           08050002
&NAME    MVI   0(&ADDR(1)),&FNUM  STORE NEW FLAG                        08100002
         AGO   .PSEURC1  ONWARD                                         08150002
.BOTHRC  AIF   ('&LIST'(1,1) EQ '(').BOTHRCR  LIST IN REG               08200002
&NAME    LA    14,&LIST   NEW LIST ADDRESS                              08250002
         ST    14,0(&ADDR(1))  STORE IT                                 08300002
.BOTHRC1 MVI   0(&ADDR(1)),&FNUM  NEW FLAG BYTE                         08350002
         AGO   .PSEURC1                                                 08400002
.BOTHRCR ANOP                                                           08450002
&NAME    ST    &LIST(1),0(&ADDR(1)) STORE NEW LIST POINTER              08500002
         AGO   .BOTHRC1                                                 08550002
.PSEURC  AIF   (T'&PSEUDO EQ 'O').WHY  EVERYTHING MISSING               08600002
         AIF   ('&PSEUDO'(1,1) EQ '(').PSEURCR  PSEUDO IN REG           08650002
&NAME    LA    15,&PSEUDO  NEW PSEUDO POINTER                           08700002
         ST    15,4(&ADDR(1))    STORE IT                               08750002
         AGO   .END                                                     08800002
.PSEURCR ANOP                                                           08850002
&NAME    ST    &PSEUDO(1),4(&ADDR(1))  STORE NEW PSEUDO POINTER         08900002
         AGO   .END                                                     08950002
.PSEURC1 AIF   (T'&PSEUDO EQ 'O').END  PSEUDO OMITTED                   09000002
         AIF   ('&PSEUDO'(1,1) EQ '(').PSEURC2  PSEUDO IN REG           09050002
         LA    15,&PSEUDO  NEW PSEUDO POINTER                           09100002
         ST    15,4(&ADDR(1))  STORE IT                                 09150002
         AGO   .END                                                     09200002
.PSEURC2 ST    &PSEUDO(1),4(&ADDR(1))  STORE NEW PSEUDO POINTER         09250002
         AGO   .END                                                     09300002
.*                                                                      09350002
.*                            LIST FORM                                 09400002
.*                                                                      09450002
.*       NEED EITHER A NAME OR AN ADDR FOR MAKING A NAME FOR THIS       09500002
.*                                                                      09550002
.MFL     AIF   (T'&NAME EQ 'O' AND T'&ADDR EQ 'O').BOTH                 09600002
.*                                                                      09650002
.*             THE NAME WILL OVER-RIDE THE ADDR PARAMETER               09700002
.*                                                                      09750002
         AIF   (T'&ADDR EQ 'O').NAME                                    09800002
         AIF   (T'&NAME NE 'O').NAME                                    09850002
.*                                                                      09900002
.*             IF ADDR IS REG-FORM, NO GOOD                             09950002
.*                                                                      10000002
         AIF   ('&ADDR'(1,1) EQ '(').BADADR                             10050002
&ADDR    DS    0F                  FULLWORD ALIGNMENT                   10100002
         AGO   .DCNUM                                                   10150002
.NAME    ANOP                                                           10200002
&NAME    DS    0F                  FULLWORD ALIGNMENT                   10250002
.DCNUM   AIF   (T'&FLAG EQ 'O').DCNUM1     NEED NOT AHVE A FLAG         10300002
         DC    AL1(&FNUM)          FLAG BYTE                            10350002
         AGO   .DCNUM2      BYPASS                                      10400002
.DCNUM1  DC    AL1(0)               FLAG BYTE                           10450002
.DCNUM2  AIF   (T'&LIST EQ 'O').MFL1                                    10500002
         DC    AL3(&LIST)          POINTER TO CODES                     10550002
         AGO   .MFL2                                                    10600002
.MFL1    DC    AL3(0)              POINTER TO CODES                     10650002
.MFL2    AIF   (T'&PSEUDO EQ 'O').MFL3                                  10700002
         DC    A(&PSEUDO)          PSEUDO RETRY ROUTINE                 10750002
         AGO   .END                                                     10800002
.MFL3    DC    A(0)                PSEUDO RETRY ROUTINE                 10850002
         AGO   .END                                                     10900002
.NOPS    MNOTE 8,'PSEUDO PARAMETER OMITTED'                             10950002
         SPACE 1                                                        11000002
         MEXIT                                                          11050002
.BADFLAG MNOTE  8,'CANT HAVE BOTH FLAG=CP AND TMP'                      11100002
         SPACE 1                                                        11150002
         MEXIT                                                          11200002
.NOLST   MNOTE 8,'LIST PARAMETER OMITTED'                               11250002
         SPACE 1                                                        11300002
         MEXIT                                                          11350002
.NOADDR  MNOTE 8,'ADDR PARAMETER OMITTED'                               11400002
         SPACE 1                                                        11450002
         MEXIT                                                          11500002
.BOTH    MNOTE 8,'BOTH ADDR AND NAME PARAMETERS OMITTED'                11550002
         SPACE 1                                                        11600002
         MEXIT                                                          11650002
.BADADR  MNOTE 8,'CANNOT NAME STAEPARM WITH A REGISTER'                 11700002
         SPACE 1                                                        11750002
         MEXIT                                                          11800002
.WHOKNOW MNOTE 8,'UNKNOWN FLAG TYPE &FLAG'                              11850002
         SPACE 1                                                        11900002
         MEXIT                                                          11950002
.WHY     MNOTE 8,'NO OPERANDS ON TYPE=C'                                12000002
         SPACE 1                                                        12050002
         MEXIT                                                          12100002
         MEND                                                           12150002
