         TITLE 'IGCD610D - DISPLAY ADDR'                                00262810
IGCD610D CSECT                                                          00462810
**A000000-999999                                                 S99240 00662810
*C001050,001476,001872,001944,001350                           @Y17XART 00862810
*********************************************************************** 01062810
*                                                                     * 01262810
* MODULE NAME = IGCD610D (TCAM, OPERATOR CONTROL)              @Y17XARX 01462810
*                                                              @Y17XARX 01662810
* DESCRIPTIVE NAME = PROCESSES DISPLAY ADDRESS COMMANDS        @Y17XARX 01862810
*                                                              @Y17XARX 02062810
* COPYRIGHT = NONE                                             @Y17XARX 02262810
*                                                                     * 02462810
* STATUS = VERSION 10.0                                        @Y17XARX 02662810
*                                                                     * 02862810
*FUNCTION -- THIS ROUTINE PROCESSES OPERATOR CONTROL COMMANDS WHICH   * 03062810
*   REQUEST DISPLAY OF THE LINE ADDRESS AND RELATIVE LINE NUMBER      * 03262810
*   FOR A SPECIFIED TERMINAL OR GENERAL POLL CLUSTER. FOR 3705 @Y17XARX 03462810
*   PU'S & LU'S ARE ALSO VALID INPUT.                          @Y17XARX 03662810
*                                                                     * 03862810
*   WHEN ENTERED THE TERMINAL SPECIFIED IN THE COMMAND HAS BEEN FOUND * 04062810
*   VALID AND IT HAS BEEN INSERTED IN THE OPCAVT AT OPCDOUBL          * 04262810
*                                                                     * 04462810
*   THE NAME IN OPCDOUBL IS MOVED INTO THE MESSAGE WHICH IS BEING     * 04662810
*   BUILT IN THE OPERATOR CONTROL RESIDENT WORK AREA IN IEDQCA.       * 04862810
*   NEXT THE TERMINAL ENTRY ADDRESS IS OBTAINED FROM OCELEM IN THE    * 05062810
*   OPERATOR CONTROL ELEMENT (OPCE). IF THE ENTRY IS A                * 05262810
*   A PROCESS ENTRY (X'20' ON IN TRMSTATE) THE ENTRY HAS NO LINE      * 05462810
*   INFORMATION.  A MESSAGE SO STATING IS BUILT AND THE ROUTINE       * 05662810
*   RETURNS TO THE MESSAGE WRITER ROUTINE IGC0710D                    * 05862810
*                                                                     * 06062810
*   THE QCB ADDRESS IS OBTAINED FROM THE TERMINAL ENTRY (TRMDESTQ),   * 06262810
*   AND THE RELATIVE LINE NUMBER (QCBRELLN) IS OBTAINED, CONVERTED    * 06462810
*   TO PRINTABLE EBCDIC, AND STORED IN THE GOOD RESPONSE.  THE DCB    * 06662810
*   ADDRESS IS OBTAINED (QCBDCBAD) AND TESTED FOR A LINE OPEN         * 06862810
*   CONDITION.  IF THE LINE IS NOT OPEN, A NOT OPEN MESSAGE IS        * 07062810
*   BUILT AND CONTROL RETURNS TO THE ERROR WRITER ROUTINE IGC0310D.   * 07262810
*   THE LINE IS THEN TESTED FOR OPEN DD DUMMY (DEBUCBAD IS ZERO,DEB   * 07462810
*   OBTAINED FROM DCBDEBAD). IF SO, THE LINE NOT OPEN MESSAGE IS      * 07662810
*   GENERATED BY XCTCING TO IGC0310D.                                 * 07862810
*                                                                     * 08062810
*   IF THE LINE IS OPEN, THE UCB ADDRESS IS PLACED IN THE GOOD        * 08262810
*   RESPONSE, THE TCB ADDRESS IS OBTAINED FROM THE AVT (AVTTCB) AND   * 08462810
*   USED TO ACCESS THE TIOT ADDRESS AT TCB+12.  THE DCBTIOT VALUE     * 08662810
*   IS ADDED TO THE TIOT START AND THE DDNAME FIELD IS MOVED FROM     * 08862810
*   THE RESULTING LOCATION INTO THE RESPONSE. FOR 3705'S, THE  @Y17XARX 09062810
*   UCB ADDRESS OF THE NCP WILL BE PRINTED ALONG WITH THE      @Y17XARX 09262810
*   GROUP NAME AND RLN. FOR LU'S, THE PU NAME WILL BE ADDED TO @Y17XARX 09462810
*   THE MESSAGE. THE ROUTINE EXITS TO THE MESSAGE WRITER       @Y17XARX 09662810
*   IGC0710D.                                                  @Y17XARX 09862810
*                                                              @Y17XARX 10062810
* NOTES = COMMUNICATIONS BETWEEN THE VARIOUS TRANSIENT MODULES @Y17XARX 10262810
* IS PRIMARILY THROUGH THE OPERATOR CONTROL AVT.               @Y17XARX 10462810
*                                                              @Y17XARX 10662810
*    DEPENDENCIES = EBCDIC CHARACTER CODE DEPENDENCIES ARE     @Y17XARX 10862810
*    CORRECTABLE BY REASSEMBLY.                                @Y17XARX 11062810
*                                                              @Y17XARX 11262810
*    RESTRICTIONS = NA                                         @Y17XARX 11462810
*                                                              @Y17XARX 11662810
*    REGISTER CONVENTIONS = STANDARD OP CTL                    @Y17XARX 11862810
*                                                              @Y17XARX 12062810
*    PATCH LABEL = ZAPPATCH                                    @Y17XARX 12262810
*                                                              @Y17XARX 12462810
* MODULE TYPE = PROCEDURE                                      @Y17XARX 12662810
*                                                              @Y17XARX 12862810
*    PROCESSOR = ASSEMBLER                                     @Y17XARX 13062810
*                                                              @Y17XARX 13262810
*    MODULE SIZE = 930 BYTES                                   @Y17XARX 13462810
*                                                              @Y17XARX 13662810
*    ATTRIBUTES = SERIALLY REUSABLE,DISABLED,TRANSIENT(TYPE 4) @Y17XARX 13862810
*    SUPERVISOR MODE,NON-REENTRANT,REFRESHABLE                 @Y17XARX 14062810
*                                                                     * 14262810
*ENTRY POINTS --                                                      * 14462810
*        'IGCD610D' TO PROCESS DISPLAY ADDR OPERATOR CONTROL COMMANDS * 14662810
*                                                                     * 14862810
*INPUT-- REGISTER 1 - ADDRESS OF OPCAVT. AT THE LABEL OPCCOPCE IS THE * 15062810
*   ADDRESS OF THE OPERATOR CONTROL ELEMENT (OPCE). AT THE LABEL      * 15262810
*   OPCDOUBL IS THE DESIRED TERMINAL NAME.                            * 15462810
*   AT OCELEM IN OPCE IS THE ADDRESS OF THE TERMINAL ENTRY            * 15662810
*                                                                       15862810
*        OPCAVT                                                       * 16062810
*          OPCDOUBL - TERMINAL NAME                            @Y17XARX 16262810
*           OPCCOPCE - ADDRESS OF OPCE                         @Y17XARX 16462810
*           OPCWORK - ADDRESS OF RESIDENT WORKAREA             @Y17XARX 16662810
*                                                                     * 16862810
*        OPCE                                                         * 17062810
*          OCELEM - ADDRESS OF TERMINAL ENTRY                  @Y17XARX 17262810
*                                                                     * 17462810
*OUTPUT--REGISTER 1 - ADDRESS OF OPCAVT                               * 17662810
*        OPCAVT                                                       * 17862810
*           OPCWORK - ADDRESS OF RESIDENT WORKAREA             @Y17XARX 18062810
*        RESIDENT WORK AREA CONTAINS MESSAGE TO BE WRITTEN            * 18262810
*                                                                     * 18462810
*EXTERNAL ROUTINES -- N/A.                                            * 18662810
*                                                                     * 18862810
*EXITS,NORMAL--COMMAND IS PROCESSED. A RESPONSE MESSAGE IS BUILT IN   * 19062810
*   THE RESIDENT WORK AREA AND AN XCTL IS DONE TO IGC0710D            * 19262810
*                                                                     * 19462810
*   NORMAL EXIT MESSAGES ARE-                                         * 19662810
*   IED038I 'STATNAME' IS ON LINE 'DDNAME, RLN ADDRESS'        @Y17XARX 19862810
*   IED177I 'STATNAME' IS ON LINE 'GRPNAME, RLN' ON 3705       @Y17XARX 20062810
*        'UCBADDR' ( ON PU 'PUNAME' )                          @Y17XARX 20262810
*                                                                       20462810
*EXITS,ERROR-- COMMAND IS PROCESSED. AN ERROR MESSAGE IS GENERATED BY * 20662810
*   LOADING THE APPROPRIATE MESSAGE NUMBER IN REGISTER 0 AN XCTLING   * 20862810
*   TO IGC0310D THE ERROR WRITER.                                     * 21062810
*                                                                     * 21262810
*   ERROR MESSAGES ARE-                                               * 21462810
*   IED016I STATION 'STATNAME' NOT FOUND --GENERATED BY IGCD010D      * 21662810
*   IED090I 'STATNAME' IS NOT A SINGLE ENTRY                          * 21862810
*   IED091I LINE FOR 'STATNAME' NOT OPEN                              * 22062810
*   IED400I 'STATNAME' 3705 NOT ACTIVE COMMAND REJECTED.       @Y17XARX 22262810
*                                                                     * 22462810
*TABLES/WORK AREAS--OPCAVT,TCAM AVT,OPCE,DEB,DCB,QCB,TERMNAME TABLE,  * 22662810
*   TERMINAL ENTRY,RESIDENT WORK AREA IN IEOQCA                       * 22862810
*                                                                     * 23062810
* MACROS = DCBD, GETMAIN, XCTL                                 @Y17XARX 23262810
*                                                              @Y17XARX 23462810
*        THIS IS A MODULE RE-WRITE OF IEDQCG AS OF OS/TCAM LEVEL II   * 23662810
*********************************************************************** 23862810
         EJECT                                                          24062810
*        REGISTER EQUATES                                               24262810
RZERO    EQU   0                        REG ZERO                 S22024 24462810
R1       EQU   1                        PARAMETER                       24662810
ROPC     EQU   2                        OPERATOR CONTROL AVT BASE       24862810
ROPCAVT  EQU   2                   OPC AVT ADDRESSABILITY               25062810
ROPCE    EQU   3                   OPC ELEM ADDRESSABILITY              25262810
RAVT     EQU   4                        AVT BASE                        25462810
RWKAREA  EQU   5                   BASE FOR WORKAREA                    25662810
RTERM    EQU   6                        TERMINAL ENTRY BASE             25862810
RQCB     EQU   7                        QCB BASE                        26062810
RWORK2   EQU   7                        WORK                            26262810
RDCB     EQU   8                        DCB BASE                        26462810
RWORK3   EQU   8                        WORK                            26662810
REGEX    EQU   9                   REGISTER USED FOR EXECUTES           26862810
RDEB     EQU   9                        DEB BASE                        27062810
RSIDR    EQU   9                        RSID PTR                 S22024 27262810
RTNT     EQU   10                  TERMNAME TABLE BASE                  27462810
RUCB     EQU   10                       UCB BASE                        27662810
RWORK    EQU   11                       WORK                            27862810
RBASE    EQU   12                       ROUTINE BASE                    28062810
R13      EQU   13                       SAVE AREA                       28262810
R14      EQU   14                       RETURN                          28462810
R15      EQU   15                       ENTRY                           28662810
RMSG     EQU   15                  MSG BASE REGISTER                    28862810
         USING IEDQOPCD,ROPC       ADDRESSABILITY FOR OP CTL            29062810
         USING IEDQAVTD,RAVT            ADDRESSABILITY FOR AVT          29262810
         USING IEDNTRM,RTERM            ADDRESSABILITY FOR TERM@Y17XARX 29462810
         USING IEDQQCB,RQCB             ADDRESSABILITY FOR QCB          29662810
         USING IHADCB,RDCB              ADDRESSABILITY FOR DCB          29862810
         USING DEBNMSUB,RDEB            ADDRESSABILITY FOR DEB          30062810
         USING IEDQOPCE,ROPCE                                           30262810
         EJECT                                                          30462810
         BALR  RBASE,ZERO               ESTABLISH                       30662810
         USING *,RBASE             ADDRESSABILITY                       30862810
IGCD610D IEDHJN SKIPID                                                  31062810
         LR    ROPC,R1                  GET OP CTL AVT                  31262810
         L     RAVT,OPCAVTPT       GET ADDR OF AVT                      31462810
         L     RWKAREA,OPCWORK     GET ADDR OF WORKAREA                 31662810
         LA    RMSG,FOUR(RWKAREA)  GET ADDR OF START OF MSG FIELD       31862810
         ST    RMSG,OCMODNME            SAVE REG 15            @Y17XARX 32062810
         L     ROPCE,OPCCOPCE      GET ADDR OF OPC CTRL ELEM            32262810
         L     RTERM,OCELEM        GET ADDR OF TERM ENTRY               32462810
         LA    RWORK,TRMPRFSZ           GET ABSOLUTE TTE LENGTH@Y17XARX 32662810
         SR    RTERM,RWORK              BACKUP TO NEG PART TTE @Y17XARX 32862810
         LA    RZERO,BADSTAT            SET UP ERROR MSG NO      S22024 33062810
         MVC   OPCLDNME+THREE(TWO),ERRWRIT INSERT ERROR ID       S22024 33262810
         TM    TRMSTATE,SINGLINE        SINGLE LINE              S22024 33462810
         BO    ERROR                    YES                      S22024 33662810
         TM    TRMSTATE,PROC+DIST       PROCESS, DIST OR CASCADE S22024 33862810
*                                       ENTRY                    S22024 34062810
         BNZ   PE                  GET MSG NUM & PREPARE EXIT  @Y17XARX 34262810
         TM    TRMDSORG,TRMLGB          LGB                      S22024 34462810
         BO    ERROR                    YES                      S22024 34662810
         TM    TRMSTATE,TRMPREF         CHECK IF XTND RESOURCE @Y17XARX 34862810
         BZ    OLDIRON                  NO,270X RESOURCE,BRANCH@Y17XARX 35062810
         ST    RTERM,OPCWRKSP      SAVE TTE ADDR FOR RNCMD5 RTN@Y17XART 35262810
         CLI   TRMTYPE,TRMLNCP          IS IT TTE FOR NCP      @Y17XARX 35462810
         BE    ERROR               NCP TTE IS INVALID INPUT    @Y17XARX 35662810
         TM    TRMSTATE,TRMLINE         IS THIS LINE ENTRY     @Y17XARX 35862810
         BO    ERROR               LINE INPUT INVALID, GO EXIT @Y17XARX 36062810
         CLI   TRMTYPE,TRMSSCP          IS IT TTE FOR SSCP     @Y17XARX 36262810
         BE    ERROR                    SSCP INPUT INVALID     @Y17XARX 36462810
         B     RNCMD0                   ALL OTHER 3705 INPUT   @Y17XARX 36662810
*                                       IS INVALID, GO PROCESS @Y17XARX 36862810
*                                       VALID 3705 INPUT       @Y17XARX 37062810
         SPACE 3                                                        37262810
         SPACE                                                          37462810
OLDIRON  EQU   *                                               @Y17XARX 37662810
         LA    R1,GOODMSG          GET ADDRESS OF GOODMSG               37862810
         B     MOVEAMSG                 TO MOVE MSG IN WORKAREA         38062810
         SPACE                                                          38262810
RNCMD0   EQU   *                                                        38462810
         LA    R1,RNMESG                GET ADDR OF RN GOOD MSG         38662810
MOVEAMSG SR    RWORK2,RWORK2       CLEAR REGISTER                       38862810
         IC    RWORK2,LENOFF(R1)   GET LENGTH OF GOODMSG                39062810
         STH   RWORK2,LENOFF(RWKAREA) PLACE LENGHT IN WORKAREA          39262810
         BCTR  RWORK2,ZERO              DECREMENT FOR EXECUTE           39462810
         EX    RWORK2,MOVEMSG      MOVE MSG TO OPC WORKAREA             39662810
*                                                                       39862810
         SPACE 3                                                        40062810
         L     RQCB,TRMDESTQ-ONE        GET QCB ADDRESS                 40262810
         SR    RWORK,RWORK              CLEAR                           40462810
         IC    RWORK,QCBRELLN           GET RELATIVE LINE NUMBER        40662810
         STC   RWORK,OCWTG              SAVE IT IN OPCE          S22024 40862810
         CVD   RWORK,OPCDOUBL           COVERT RLN TO PACKED DEC        41062810
         UNPK  OPCDOUBL(THREE),OPCDOUBL+SIX(TWO)  MADE ZONED            41262810
         OI    OPCDOUBL+TWO,NUMERIC     SET ZONE FOR NUMERIC     S22024 41462810
         TM    TRMSTATE,TRMPREF         IS THERE NEG TTE PREFIX@Y17XARX 41662810
         BO    RNCMD1                   YES                      S22024 41862810
         SPACE                                                          42062810
         MVC   LINEDISP(THREE,RMSG),OPCDOUBL  PUT RLN IN MSG            42262810
         B     REGCMD                   SKIP RN MSG SETUP        S22024 42462810
         SPACE                                                          42662810
RNCMD1   EQU   *                                                 S22024 42862810
         MVC   RNLINE(THREE,RMSG),OPCDOUBL  PUT RLN IN MSG       S22024 43062810
         B     DCB3705                                         @Y17XARX 43262810
REGCMD   EQU   *                                                 S22024 43462810
         L     RDCB,QCBDCBAD-ONE        GET DCB ADDRESS                 43662810
         TM    DCBOFLGS,OPEN            IS DCB OR LGB OPEN       S22024 43862810
         BNO   NOTOP                    BRANCH ON NO             S22024 44062810
         B     XDISPLAY                 BRANCH                 @Y17XARX 44262810
DCB3705  EQU   *                        3705 DCB               @Y17XARX 44462810
         LR    R1,RTERM       LOAD ADDR OF TTE INTO REG  1     @Y17XARX 44662810
         LA    R15,TRMPRFSZ             NEG PREFIX SIZE        @YM06150 44862810
         AR    R1,R15                   BUMP TTE PRT           @YM06150 45062810
DCB3705A EQU   *              3705 PROCESSING                  @Y17XARX 45262810
         LA    R13,OPCSAVE              ADDR OPC SAVE AREA     @Y17XARX 45462810
         L     R15,OPCHORT    SET UP ADDR IEDQCA COHORT SEARCH @Y17XARX 45662810
         BALR  R14,R15        BRANCH TO IEDQCA TO OPEN         @Y17XARX 45862810
         LTR   RZERO,ZERO     CHECK FOR ERROR CONDITION        @Y17XARX 46062810
         BZ    NOTFND         ERROR CONDITION WAS FOUND        @Y17XARX 46262810
         LR    RTERM,R1       LOAD NEW TTE ADDR INTO BASE REG  @Y17XARX 46462810
         LA    R15,TRMPRFSZ             GET ABSOLUTE LENGTH    @Y17XARX 46662810
         SR    RTERM,R15                ADJUST FOR NEG PREFIX  @Y17XARX 46862810
         CLI   TRMTYPE,TRMLNCP          CHECK FOR NCP TTE      @Y17XARX 47062810
         BE    FNDNCP         YES, BRANCH TO GET QCB PTR       @Y17XARX 47262810
         CLI   TRMTYPE,TRMPUNT          CHECK FOR PU TTE       @Y17XARX 47462810
         BE    FNDPU          YES, BRANCH TO GET PU TNT ADDR   @Y17XARX 47662810
         TM    TRMSTATE,TRMLINE         IS THIS LINE ENTRY     @Y17XARX 47862810
         BNO   DCB3705A                 NO LINE, GO RELOOP     @Y17XARX 48062810
         ST    R1,OPCWRKSP+EIGHT        SAVE LINE TTE ADDRESS  @YM06854 48262810
         B     DCB3705A            RESTART COHORT SEARCH LOOP  @Y17XARX 48462810
NOTFND   EQU   *                                               @Y17XARX 48662810
         LA    RZERO,FOURHNRD           GET MESSAGE NUMBER     @Y17XARX 48862810
         MVC   OPCMODID,ERRWRIT             INSERT ID IGC030D  @Y17XARX 49062810
         B     ERROR                    PREPARE TO EXIT        @Y17XARX 49262810
FNDPU    EQU   *                                               @Y17XARX 49462810
         ST    RZERO,OPCWRKSP+FOUR      STORE OFFSET  PU TNT   @Y17XART 49662810
         B     DCB3705A            START LOOP AGAIN            @Y17XARX 49862810
FNDNCP   EQU   *                                               @Y17XARX 50062810
         L     RMSG,OCMODNME            RELOAD ORIG IN MSG REG @Y17XARX 50262810
         L     RQCB,TRMDESTQ-ONE   LOAD ADDR QCB FROM NCP TTE  @Y17XARX 50462810
         L     RDCB,QCBDCBAD-ONE        GET DCB PTR FROM QCB   @Y17XARX 50662810
         LA    RWORK,ONE                SET RLN TO ONE - ONLY 1  S22024 50862810
*                                       UCB PER 3705             S22024 51062810
         STC   RWORK,OCWTG              SAVE IT IN OPCE          S22024 51262810
XDISPLAY EQU   *                                                 S22024 51462810
         L     RDEB,DCBDEBAD            GET DEB ADDRESS                 51662810
         SLL   RWORK,TWO           MULTIPLY RLN BY FOUR                 51862810
         L     RUCB,DEBUCBAD-FOUR-DEBNMSUB(RWORK,RDEB)  GET UCB ADDR    52062810
         LA    RUCB,ZERO(RUCB)     CLEAR HIGH ORDER BYTE                52262810
         LTR   RUCB,RUCB                OPENED DD DUMMY                 52462810
         BZ    NOTOP                    YES - REJECT COMMAND            52662810
         CLC   OCWTG(ONE),DEBNMEXT      EXTENT HIGH              S22024 52862810
         BH    NOTOP                    YES, OPENED DD DUMMY            53062810
*                                                                       53262810
         TM    TRMSTATE,TRMPREF         NEG PREFIX TEXT        @Y17XARX 53462810
         BO    RNCMD3                   YES                    @Y17XARX 53662810
         SPACE                                                          53862810
         MVC   UCBDISP(THREE,RMSG),ADDR(RUCB) PUT UCB ADDR IN MSG       54062810
         B     RNCMD4                   SKIP RN MESSAGE SETUP    S22024 54262810
         SPACE                                                          54462810
RNCMD3   EQU   *                                                 S22024 54662810
         MVC   RNADDR(THREE,RMSG),ADDR(RUCB) PUT RN ADDR IN MSG  S22024 54862810
         MVC   STATOFF(EIGHT,RMSG),OCBACKUP  PUT STATNAME IN MSG S22024 55062810
         B     RNCMD5                   FIND LGB NAME            S22024 55262810
         SPACE                                                          55462810
RNCMD4   EQU   *                                                 S22024 55662810
         LH    RWORK,DCBTIOT            GET DD OFFSET IN TIOT           55862810
         L     RWORK2,AVTTCB            GET TCB ADDRESS                 56062810
         L     RWORK2,TWELVE(,RWORK2)   GET TIOT ADDRESS                56262810
         AR    RWORK2,RWORK             GET TO DDNAME                   56462810
         MVC   MSGDISP(EIGHT,RMSG),FOUR(RWORK2)  PUT IN MSG             56662810
*                                                                       56862810
         MVC   PEMDISP(EIGHT,RWKAREA),OCBACKUP MOVE TERMNAME     S22024 57062810
*                                       INTO MESSAGE             S22024 57262810
         B     EXIT                PREPARE TO XCTL TO IGC0701D1D        57462810
         SPACE 3                                                        57662810
PE       EQU   *                                                        57862810
         LA    RZERO,NINETY             GET MSG NUM                     58062810
         MVC   OPCLDNME+THREE(TWO),ERRWRIT    INSERT ID OF IGC0310D     58262810
         B     ERROR               PREPARE TO EXIT                      58462810
         SPACE 3                                                        58662810
RNCMD5   EQU   *                                                 S22024 58862810
         LA    R13,OPCSAVE              IDENTIFY REGISTER SAVE @Y17XARX 59062810
         L     R1,OPCWRKSP+EIGHT        GET LINE TTE ADDRESS   @YM06854 59262810
         L     R15,OPCHORTG        SETUP ADDR IEDQCA COHORTG   @Y17XARX 59462810
*                                  SEARCH                      @Y17XARX 59662810
         BALR  R14,R15             BRANCH TO IEDQCA TO OPEN    @Y17XARX 59862810
         LR    R1,RZERO                 PUT PARAMETER IN REG   @Y17XARX 60062810
         L     R15,OPCTNTEA             SETUP FOR BRANCH       @Y17XARX 60262810
         BALR  R14,R15                  GO TO CALC OFFSET TNT  @Y17XARX 60462810
         L     RMSG,OCMODNME            RESTORE REG FOR MSGBASE@Y17XARX 60662810
         USING IEDQTNTD,RTNT            SETUP TO USE TNT       @Y17XARX 60862810
         L     RTNT,AVTRNMPT            ESTAB ADDR TO TNT      @Y17XARX 61062810
         SR    RWORK3,RWORK3            CLEAR REGISTER         @Y17XARX 61262810
         IC    RWORK3,TNTENLEN          SAVE LENGTH            @Y17XARX 61462810
         DROP  RTNT                                            @Y17XARX 61662810
         LR    RTNT,R1                  LOAD OFFSET TNT        @Y17XARX 61862810
         BCTR  RWORK3,ZERO              DECREMENT FOR EXEC NEXT@Y17XARX 62062810
         EX    RWORK3,MOVENAME          MOVE LGBNAM TO WORKAREA@Y17XARX 62262810
         L     RTERM,OPCWRKSP           GET ORIG TTE           @Y17XART 62462810
         CLI   TRMTYPE,TRMLUNT          CHK FOR LU TTE         @Y17XARX 62662810
         BNE   EXIT                NO, BRANCH                  @Y17XARX 62862810
         LA    R1,PUMSG            GET MESSAGE ADDRESS         @Y17XARX 63062810
         SR    RWORK2,RWORK2       CLEAR REGISTER              @Y17XARX 63262810
         LR    RWORK3,RWORK2       CLEAR REGISTER              @Y17XARX 63462810
         LH    RWORK2,0(RWKAREA)   GET PRESENT MESSAGE LENGTH  @Y17XARX 63662810
         IC    RWORK3,0(R1)        GET PU MESSAGE LENGTH       @Y17XARX 63862810
         AR    RWORK2,RWORK3       UPDATE TOTAL MESSAGE LENGTH @Y17XARX 64062810
         STH   RWORK2,0(RWKAREA)   STORE TOTAL MESSAGE LENGTH  @Y17XARX 64262810
         BCTR  RWORK3,ZERO         DECREMENT LENGTH FOR EXECUTE@Y17XARX 64462810
         EX    RWORK3,MOVEPUMG     MOVE PU MSG SKELETON TO MSG @Y17XARX 64662810
         L     R1,OPCWRKSP+FOUR         GET PU TTCIN           @Y17XART 64862810
         L     R15,OPCTNTEA             GET ADDR CONV RTN      @Y17XARX 65062810
         BALR  R14,R15                  CALC TNT ADDRESS       @Y17XARX 65262810
         USING IEDQTNTD,RTNT            TNT DSECT              @Y17XARX 65462810
         L     RTNT,AVTRNMPT            TNT BASE ADDR          @Y17XARX 65662810
         SR    RWORK3,RWORK3            CLEAR REGISTER         @Y17XARX 65862810
         IC    RWORK3,TNTENLEN          GET LENGTH             @Y17XARX 66062810
         DROP  RTNT                                            @Y17XARX 66262810
         LR    RTNT,R1                  TNT ADDRESS            @Y17XARX 66462810
         BCTR  RWORK3,ZERO              DECREMENT FOR EXEC     @Y17XARX 66662810
         EX    RWORK3,PUNAME            MOVE PU NAME TO MESSAGE@Y17XARX 66862810
         B     EXIT                     GO PRINT OUT GOOD RN MSG S22024 67062810
         SPACE                                                          67262810
NOTOP    EQU   *                                                        67462810
         LA    RZERO,NINETY1            GET MSG NUM                     67662810
         MVC   OPCLDNME+THREE(TWO),ERRWRIT  INSERT ID OF IGC0310D       67862810
         B     ERROR               PREPARE TO EXIT                      68062810
         SPACE 3                                                        68262810
EXIT     EQU   *                                                        68462810
         MVC   OPCLDNME+THREE(TWO),MSGWRIT INSERT ID OF IGC0701D        68662810
ERROR    XC    OCPARM(THREE),OCPARM     CLEAR WTG FIELD AND PARM        68862810
         LR    R1,ROPCAVT          SETUP PARAM REGISTER                 69062810
         L     R13,OPCSAVE         GET ADDR OF OPC SAVE AREA            69262810
         XCTL  SF=(E,OPCXCTL)      XCTL TO IGC0701D                     69462810
         EJECT                                                          69662810
*        MESSAGES                                                       69862810
GOODMSG  DS    0H                                                       70062810
         DC    AL1(EGOODMSG-*-1)        LENGTH                          70262810
         DC    CL17'IED038I',CL27'IS ON LINE'  MESSAGE TEXT    @YM09031 70462810
EGOODMSG EQU   *                                                        70662810
INVALMSG DS    0H                                                       70862810
         DC    AL1(EIVALMSG-*-1)        LENGTH                          71062810
         DC    C'COMMAND REJECTED FOR TERMINAL ATTACHED TO 3705' S22024X71262810
                                        MESSAGE TEXT             S22024 71462810
EIVALMSG EQU   *                                                        71662810
RNMESG   DS    0H                                                       71862810
         DC    AL1(ENRNMESG-*-1)        LENGTH                   S22024 72062810
         DC    CL17'IED177I',CL11'IS ON LINE ',9CL1' ',4CL1' ',CL8'ON 3X72262810
               705 ',3CL1' '            MESSAGE TEXT           @YM08415 72462810
ENRNMESG EQU   *                                                 S22024 72662810
PUMSG    DS    0H                                              @Y17XARX 72862810
         DC    AL1(PUMSGLN1-*-1)   LENGTH                      @Y17XARX 73062810
         DC    CL7' ON PU ',CL8' ' MESSAGE TEXT                @Y17XARX 73262810
PUMSGLN1 EQU   *                                               @Y17XARX 73462810
         EJECT                                                          73662810
*        CONSTANTS                                                      73862810
ERRWRIT  DC    C'03'               ID OF ERROR WRITER IGC0310D          74062810
MSGWRIT  DC    C'07'               IF FOR IGC0701D RTN                  74262810
*        EQUATES                                                        74462810
RWORK6   EQU   1                   WORK REGISTER                        74662810
LENOFF   EQU   0                   OFFSET INTO WORKAREA FOR LEN         74862810
TWO      EQU   2                   OFFSET                               75062810
EIGHT    EQU   8                   OFFSET                               75262810
TWELVE   EQU   12                       TIOT OFFSET FROM TCB            75462810
ADDR     EQU   13                       THIRTEEN                 S22024 75662810
PROC     EQU   X'20'                    FLAGS                    S22024 75862810
ZONE     EQU   X'30'                    FLAG                     S22024 76062810
DIST     EQU   X'40'                    FLAG                     S22024 76262810
A        EQU   C'A'                     CHARACTER A              S22024 76462810
MSGDISP  EQU   28                       MSG NO. 28               S22024 76662810
UCBDISP  EQU   41                  OFFSET IN MSG FOR UCB ADDR           76862810
LINEDISP EQU   37                  OFFSET FOR RLN INSERTION             77062810
ZONEDISP EQU   39                  OFFSET FOR CONVERSION                77262810
TERMDISP EQU   9                        OFFSET                   S22024 77462810
EXTRAMG  EQU   56                       PU MESSAGE DISPLACEMENT@Y17XARX 77662810
ZERO     EQU   0                        OFFSET                   S22024 77862810
ONE      EQU   1                        OFFSET                   S22024 78062810
THREE    EQU   3                        OFFSET                   S22024 78262810
FOUR     EQU   4                        OFFSET                          78462810
PEMDISP  EQU   12                  OFFSET FOR DDNAME IN MSG             78662810
NOADISP  EQU   22                       OFFSET                   S22024 78862810
SIX      EQU   6                        OFFSET                          79062810
PUDISP   EQU   63                       OFFSET                 @Y17XARX 79262810
NINETY   EQU   90                       OFFSET                          79462810
NINETY1  EQU   91                       OFFSET                          79662810
FOURHNRD EQU   400                      OFFSET NOT FIND COHORT @Y17XARX 79862810
DSORGLGB EQU   X'80'                    DSORG VALUE FOR LGB      S22024 80062810
RNLINE   EQU   37                       OFFSET IN RN MSG FOR RLN S22024 80262810
RNADDR   EQU   49                       OFFSET IN RN MSG FOR ADDRS22024 80462810
STATOFF  EQU   8                        OFFSET FOR RN STATION  @YM08415 80662810
LGBNME   EQU   28                       MSG OFFSET FOR LGB NAME  S22024 80862810
ENLEN    EQU   X'28'                    ENTRY LENGTH             S22024 81062810
FIRSTEN  EQU   X'52'                    FIRST ENTRY              S22024 81262810
OPEN     EQU   X'10'                    FLAG                     S22024 81462810
NUMERIC  EQU   X'F0'                    ZONE FOR NUMERIC         S22024 81662810
BADSTAT  EQU   18                       INVALID STAT NAME MSG NO S22024 81862810
SINGLINE EQU   X'80'                    TEST FOR SINGLE LINE ENTRS22024 82062810
*        EXECUTED INSTRUCTIONS                                 @YA09108 82262810
         SPACE 2                                               @YA09108 82462810
*        MOVE  LGBNAME                                         @YA09108 82662810
         SPACE 1                                               @YA09108 82862810
MOVENAME MVC   LGBNME(0,RMSG),ZERO(RTNT) PUT NAME IN MSG       @YA09108 83062810
*                                                              @Y17XARX 83262810
PUNAME   MVC   PUDISP(ZERO,RWKAREA),ZERO(RTNT)  ACTUAL PUNAME  @YM06913 83462810
         SPACE 3                                                        83662810
*                                  MSG IN WORKAREA                      83862810
MOVEMSG  MVC   FOUR(ZERO,RWKAREA),ONE(R1) MOVE MSG TO WORKAREA          84062810
*                            PU NAME INSERTED INTO RNMESSAGE   @Y17XARX 84262810
MOVEPUMG MVC   EXTRAMG(ZERO,RWKAREA),ONE(R1) MOVE PUNAME MSG TO@Y17XARX 84462810
*                                   WORKAREA BEHIND RNMSG      @Y17XARX 84662810
ZAPPATCH DC    50X'FF'             ZAP AREA FOR PATCHES        @Y17XARX 84862810
*        MACROS                                                         85062810
         TAVTD                                                          85262810
         TOPCAVTD                                                       85462810
         TDEBD                                                          85662810
         TOPCED                                                         85862810
         DCBD  DSORG=TX                                                 86062810
         TQCBD                                                          86262810
         TTRMD                                                          86462810
         TTNTD                                                 @Y17XARX 86662810
         TLGBD                                                          86862810
         EJECT                                                          87062810
         TLCBD                                                          87262810
         EJECT                                                          87462810
         END                                                            87662810
