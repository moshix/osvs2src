         TITLE 'IGCD810D DISPLAY OPTION COMMAND'                        00200022
IGCD810D CSECT                                                          00400022
******************** MICROFICHE FLAGS *********************** SUPT CODE 00430056
*C402000-406000                                                @YA05454 00460056
*A000000-999999                                                  S99240 00500022
*A542000,274000                                                @OZ28887 00500186
* C788000                                                      @OZ35627 00540100
*A404000                                                       @OZ39371 00560100
*********************************************************************** 00600022
*                                                                     * 00800022
*TITLE--'IGCD810D' PROCESSOR OF DISPLAY OPTION COMMANDS               * 01000022
*                                                                     * 01200022
*STATUS--CHANGE LEVEL 5                                          S22024 01400000
*                                                                     * 01600022
*FUNCTION -- THIS ROUTINE PROCESSES OPERATOR CONTROL COMMANDS WHICH   * 01800022
*   REQUEST DISPLAY OF TERMINAL OPTION FIELDS.                        * 02000022
*                                                                     * 02200022
*   THIS ROUTINE INITIALLY VALIDITY CHECKS TO INSURE THERE ARE NO     * 02400022
*   IMBEDDED BLANKS FOLLOWING THE OPTION NAME IN THE COMMAND AND THAT * 02600022
*   A 'C','D',OR AN'X' WAS CODED. IF THE VALIDITY CHECK PROVES        * 02800022
*   UNSUCCESSFUL AN XCTL TO IGC0310D IS DONE IN ORDER TO WRITE OUT AN * 03000022
*   ERROR MESSAGE.                                                    * 03200022
*                                                                     * 03400022
*   THE DISPLAY OPTIONS ROUTINE THEN SERIALLY SEARCHES THE OPTION     * 03600022
*   NAMES TABLE FOR THE SPECIFIED OPTION FIELD.IF NO ENTRY IS FOUND,AN* 03800022
*   ERROR MESSAGE IS WRITTEN OUT BY IGC0310D THE ERROR WRITER. CONTROL* 04000022
*   IS PASSED TO IGC0310D VIA AN XCTL. IF THERE ARE                   * 04200022
*   NO OPTIONS FOR THE TERMINAL ENTRY (TRMOPTFN OFF IN TRMSTATE) AN   * 04400022
*   ERROR RESPONSE IS BUILT AND THE ROUTINE RETURNS TO THE ERRORWRITERX 04600022
*   MODULE.  IF THE SPECIFIED OPTION FIELD IS NOT DEFINED FOR THE     * 04800022
*   TERMINAL, AN ERROR MESSAGE IS WRITTEN OUT BY THE ERROR WRITER     * 05000022
*   OTHERWISE THE ADDRESS OF THE OPTION FIELD IS                      * 05200022
*   OBTAINED AND PROCESSING CONTINUES.                                * 05400022
*                                                                     * 05600022
*   FOR DISPLAY COMMANDS, THE OPTION FIELD IS MOVED INTO THE RESIDENT * 05800022
*   WORK AREA IN IEDQCA. IF THE DATA FORMAT IS CHARACTER AND THE FIELD* 06000022
*   IS ALL BLANK, 'ALL BLANKS' IS MOVED INTO THE RESPONSE. SINCE NO   * 06200022
*   CONVERSION IS NECESSARY FOR CHARACTER DATA, A RESPONSE MESSAGE    * 06400022
*   IS BUILT AND THE ROUTINE EXITS TO THE MESSAGE WRITER IGC0710D. ALL* 06600022
*   OTHER TYPES OF FIELD ARE TESTED FOR ZERO CONTENT, AND A DATA IS   * 06800022
*   ZERO MESSAGE IS BUILT AND CONTROL RETURNS TO THE MESSAGE WRITER   * 07000022
*   MODULE.  OTHERWISE THE DATA IS CONVERTED FROM ITS CURRENT         * 07200022
*   FORMAT INTO A PRINTABLE EQUIVALENT.  A RESPONSE IS BUILT AND      * 07400022
*   CONTROL RETURNS TO THE MESSAGE WRITER.                            * 07600022
*                                                                     * 07800022
*ENTRY POINTS --                                                      * 08000022
*        'IGCD810D' TO PROCESS DISPLAY OPTION COMMAND                 * 08200022
*                                                                     * 08400022
*INPUT-- REGISTER 1 - ADDRESS OF THE OPERATOR CONTROL AVT. AT THE     * 08600022
*   LABEL OPCCOPCE IS THE ADDRESS OF THE CURRENT ELEMENT.             * 08800022
*   FIELDS RELEVANT TO THIS ROUTINE IN THE OPCE ARE:                  * 09000022
*                                                                     * 09200022
*        +0 OCLINK - ADDRESS OF COMMA AFTER OPTION NAME               * 09400022
*        +4 OCELEM - ADDRESS OF ASSOCIATED TERMINAL ENTRY             * 09600022
*        +20 OCBACKUP - TERMINAL NAME                                 * 09800022
*                                                                       10000022
*   IN THE OPCAVT RELEVANT FIELDS ARE:                                * 10200022
*        +0 OPCDOUBL - OPTION NAME                                    * 10400022
*                                                                     * 10600022
*OUTPUT-- REGISTER 1 - ADDRESS OF OPCAVT                              * 10800022
*                                                                     * 11000022
*EXTERNAL ROUTINES -- NONE                                            * 11200022
*                                                                     * 11400022
*EXITS,NORMAL-- COMMAND IS PROCESSED. A RESPONSE MESSAGE IS BUILT IN  * 11600022
*   THE RESIDENT WORK AREA IN IEDQCA WHOSE ADDRESS IS IN OPCAVT       * 11800022
*                                                                     * 12000022
*   NORMAL EXIT MESSAGES ARE-                                         * 12200022
*   IED035I 'STATNAME' OPTION 'OPFLDNAME'='ENTRY'                     * 12400022
*                                                                     * 12600022
*EXITS,ERROR-- OPTION NOT FOUND, OR OPTION DOES NOT EXIST FOR THE     * 12800022
*   TERMINAL OR 'X','C',OR 'D' NOT SPECIFIED. ERROR MESSAGES ARE      * 13000022
*   WRITTEN OUT BY IGC0310D.                                            13200022
*                                                                     * 13400022
*   ERROR MESSAGES ARE-                                               * 13600022
*   IED016I STATION 'STATNAME' NOT FOUND --GENERATED BY IGCD010D      * 13800022
*   IED018I 'COMMAND' COMMAND INVALID'                                * 14000022
*   IED034I 'STATNAME' HAS NO 'OPFLDNAME' OPTION                      * 14200022
*                                                                     * 14400022
*TABLES/WORK AREAS--OPCAVT,TCAM AVT,TRANSLATE TABLES,TERMNAME TABLE   * 14600022
*   TERMINAL ENTRY,OPCE                                               * 14800022
*                                                                     * 15000022
*ATTRIBUTES--SERIALLY REUSABLE,TRANSIENT(TYPE 4),SUPERVISOR MODE,     * 15200022
*   DISABLED,NON-REENTRANT                                            * 15400022
*                                                                     * 15600022
*CHARACTER CODE DEPENDENCY                                            * 15800022
*        THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL        * 16000022
*        REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS        * 16200022
*        EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME.  THE CODING     * 16400022
*        HAS BEEN ARRANGED SO THAT REDEFINITION OF CHARACTER          * 16600022
*        CONSTANTS, BY REASSEMBLY WILL RESULT IN A CORRECT MODULE     * 16800022
*        FOR THE NEW DEFINITION.                                      * 17000022
*                                                                     * 17200022
*NOTES - COMMUNICATIONS BETWEEN THE VARIOUS TRANSIENT MODULES IS      * 17400022
*        PRIMARILY THROUGH THE OPERATOR CONTROL AVT.                  * 17600022
*                                                                     * 17800022
*        THIS IS A MODULE RE-WRITE OF IEDQCS AS OF OS/TCAM LEVEL II   * 17900022
*********************************************************************** 18000022
*                                                                       18200022
         EJECT                                                          18400022
*        REGISTER EQUATES                                               18600022
RZERO    EQU   0                        REG ZERO                 S22024 18800000
RPARM    EQU   1                   PARAMETER REGISTER                   19000022
ROPCAVT  EQU   2                   OPERATOR CONTROL AVT                 19200022
ROPCE    EQU   3                   OPERATOR CONTROL ELEMENT ADDR        19400022
RWKAREA  EQU   11                  OPERATOR CONTROL WORKAREA            19600022
R0       EQU   0                        REG ZERO                 S22024 19800000
R1       EQU   1                        PARAMETER                       20000022
ROPC     EQU   2                        OPERATOR CONTROL AVT BASE       20200022
RCIB     EQU   3                        BUFFER OR CIB BASE              20400022
RAVT     EQU   4                        AVT BASE                        20600022
ROPT     EQU   5                        OPTION CSECT BASE               20800022
RTNT     EQU   6                        TERMNAME TABLE BASE             21000022
RTERM    EQU   7                        TERMINAL ENTRY BASE             21200022
RWORK    EQU   8                        WORK                            21400022
RWORK2   EQU   9                        WORK                            21600022
RWORK3   EQU   10                       WORK                            21800022
RBASE    EQU   12                       ROUTINE BASE                    22000022
R13      EQU   13                       SAVE AREA                       22200022
R14      EQU   14                       RETURN                          22400022
R15      EQU   15                        ENTRY POINT                    22600022
ZERO     EQU   0                   ZERO                                 22800022
ONE      EQU   1                   ONE                                  23000022
TWO      EQU   2                   TWO                                  23200022
FOUR     EQU   4                   FOUR                                 23400022
FIVE     EQU   5                   FIVE                                 23600022
EIGHT    EQU   8                   EIGHT                                23800022
TWELVE   EQU   12                  TWELVE                               24000022
FOURTEEN EQU   14                  FOURTEEN                             24200022
TWOEIGHT EQU   28                  TWENTY-EIGHT                         24400022
FOURTWO  EQU   42                  FORTY-TWO                            24600022
FOURTHR  EQU   43                  FORTY-THREE                          24800022
OFF03    EQU   3                        OFFSET                          25000022
AL2      EQU   2                        OFFSET                          25200022
OPCZERO  EQU   0                        OFFSET                          25400022
OPCONE   EQU   1                   ONE                                  25600022
OPCTWO   EQU   2                   TWO                                  25800022
OPCFOUR  EQU   4                   FOUR                                 26000022
OFF04    EQU   4                   OFFSET FOUR                          26200022
OFF00    EQU   0                   OFFSET ZERO                          26400022
OFF02    EQU   2                   OFFSET TWO                           26600022
AL8    EQU     8                   OFFSET EIGHT                         26800022
TEN      EQU   10                       OFFSET TEN               S22024 27000000
LENOFF   EQU   0                        OFFSET INTO WORKAREA            27200022
C255     EQU   255                      LIMIT                    S22024 27400000
C256     EQU   256                                             @OZ28887 27450086
TWO98    EQU   298                                             @OZ28887 27500086
HEXF0    EQU   X'F0'                    FLAG BIT                 S22024 27600000
HEX0F    EQU   X'0F'                    FLAG BIT                 S22024 27800000
CZERO    EQU   C'0'                CHARACTER ZERO                       28000022
SEVEN    EQU   7                        OFFSETS                         28060022
EIGHTEEN EQU   18                       OFFSETS                         28120022
         EJECT                                                          28200022
         USING IEDQAVTD,RAVT            ADDRESSABILITY FOR AVT          28400022
         USING IEDQOPCD,ROPCAVT         ADDRESSABILITY FOR OP CTL AVT   28600022
         USING IEDQTNTD,RTNT            ADDRESSABILITY FOR TNT          28800022
         USING IEDQTRM,RTERM            ADDRESSABILITY FOR TERM         29000022
         USING IEDQOPCE,ROPCE           ADDRESSABILITY FOR OPC ELEMENT  29200022
         BALR  RBASE,ZERO               ADDRESSABILITY FOR THIS MOD     29400022
         USING *,RBASE                                                  29600022
IGCD810D IEDHJN SKIPID                                                  30100022
         LR    ROPC,R1                  GET OP CTL AVT BASE             30600022
         L     RAVT,OPCAVTPT            GET AVT BASE                    30800022
         L     RTNT,AVTRNMPT            GET TNT ADDRESS                 31000022
         L     RTERM,OCELEM             GET ADDR OF TERM ENTRY          31200022
         L     RWORK,OCLINK             GET ADDR OF COMMA AFTER OPNME   31400022
         CLI   OPCTWO(RWORK),AVTEBLNK     BALNK IN CMD                  31600022
         BNE   ERROPT                   ERROR                           31800022
         TM    TRMDSORG,TRMLGB          LINE GROUP BLOCK ?       S22024 31860022
         BO    ERROPT                   YES ERROR                S22024 31920022
         MVC   OCRLN,OPCONE(RWORK)      GET X,C, OR D AND SAVE          32000022
PROCESS  EQU   *                                                        32200022
         L     RWKAREA,OPCWORK          GET ADDR OF MSG AREA IN WKAREA  32400022
         LA    R1,MSG                   GET ADDR OF MSG                 32600022
         SR    R14,R14                  CLEAR REGISTER                  32800022
         IC    R14,LENOFF(R1)           GET LENGTH OF MSG               33000022
         STH   R14,LENOFF(RWKAREA)      STORE LENGTH IN OPC WKAREA      33200022
         BCTR  R14,ZERO                 DECREMENT FOR EXECUTE           33400022
         EX    R14,MOVEMSG              MOVE MSG INTO WORKAREA          33600022
*                                      THIS SECTION OF CODE FINDS       33800022
*        THE TERMINAL NAME AND INSERT IT IN MSG                         34000022
         MVC   TWELVE(EIGHT,RWKAREA),OCBACKUP INSERT TERM NAME          34200022
*                                       IN MESSAGE                      34400022
OPTFLLOC EQU   *                                                        34600022
         TM    TRMSTATE,TRMOPTFN        OPTIONS FIELDS PRESENT          34800022
         BNO   OPTERROR                 NO, RETURN TO ERROR RTN         35000022
         L     RWORK,AVTOPTPT           GET ADDR OF OPTION TABLES       35200022
         L     RWORK3,OFF04(RWORK)      GET OPT NAMES ADDRESS           35400022
         SR    RWORK2,RWORK2            CLEAR OFFSET REGISTER           35600022
OPTLOOP  EQU   *                                                        35800022
         LA    RWORK2,ONE(RWORK2)       BUMP OSFFET CONTER              36000022
         CLI   OFF00(RWORK3),AVTEFF     END OF OPTIONS                  36200022
         BE    OPTERROR                 YES, RETURN ERROR               36400022
         CLC   OFF02(AL8,RWORK3),OPCDOUBL COMPARES NAMES                36600022
         BE    GOODNAME                 GOOD COMPARE, BRANCE            36800022
         LA    RWORK3,TEN(RWORK3)  POINT TO NEXT NAME IN TABLE          37000022
         B     OPTLOOP                  CHECK FOR END OF TABLE          37200022
GOODNAME EQU   *                                                        37400022
         ST    RWORK3,OCELEM            SAVE ADDR OF GOOD OPT NAME      37600022
         STC   RWORK2,OCSWITCH          SAVE OPTION FIELD NAME OFFSET   37800022
         MVC   OCPARM(AL2),OFF00(RWORK3) SAVE LENGTH AND TYPE           38000022
         SR    RWORK3,RWORK3            CLEAR FOR NUM OF OPTIONS        38200022
         IC    RWORK3,TRMOPNO           PICK UP NUM OF OPTIONS FOR TERM 38400022
         CR    RWORK2,RWORK3            OFFWET GREATER THAN NUM SPEC    38600022
         BH    OPTERROR                 YES, RETURN ERROR               38800022
         AR    RWORK2,RTERM             ADD TERM ENTRY BASE             39000022
         CLI   TRMOPT-ONE-IEDQTRM(RWORK2),AVTEFF  OPTION SPECIFIED      39200022
         BE    OPTERROR                 NO ,RETURN WITH WRROR INDICAT   39400022
         SR    RPARM,RPARM              CLEAR FOR TABLE OFFSET          39600022
         IC    RPARM,TRMOPT-ONE-IEDQTRM(RWORK2)  PICK UP OPTION OFFSET  39800022
         SR    RWORK2,RTERM             SUBTRACT TO GET OFFSET          40000022
         LH    RWORK,TRMOPTBL           GET ENTRY OFFSET       @YA05454 40200056
         N     RWORK,AVTCLRHI           CLEAR HI HALF          @YA05454 40400056
         SLL   RWORK,2                  MULTIPLY OFFSET BY FOUR@OZ39371 40450000
         A     RWORK,AVTOPTPT           ADD OPT TBL OFFSET     @YA05454 40600056
         AR    RWORK,RPARM              GET ADDR BY ADDING OFFSETS      40800022
*                                       OF OPTION FIELD                 41000022
*                                       OF OPTION FIELD                 41200022
         LA    R13,GOODOPT              SETUP BRANCHING REGISTER        41400022
         B     OPTNAME                  FOUND A MATCH SO INSERT NAME    41600022
OPTERROR EQU   *                                                        41800022
         LA    R13,EXIT                 ERROR SO PREPARE EXIT BRANCE    42000022
         LA    R1,ERRMSG                GET ADDR OF ERROR MSG           42200022
         SR    R14,R14                  CLEAR REGISTER                  42400022
         IC    R14,LENOFF(R1)           GET LENGTH OF MSG               42600022
         STH   R14,LENOFF(RWKAREA)       INSERT LENGTH IN MSG           42800022
         BCTR  R14,ZERO                 DECREMENT FOR EXECUTE           43000022
         EX    R14,MOVEMSG              MOVE MSG INTO WORKAREA          43200022
         MVC   TWELVE(EIGHT,RWKAREA),OCBACKUP  MOVE TERMNAME            43400022
OPTNAME  EQU   *                                                        43600022
         MVC   TWOEIGHT(EIGHT,RWKAREA),OPCDOUBL MOVE OPTION NAME        43800022
         BR    R13                      BRANCH TO GOODOPT OR OUT        44000022
GOODOPT  EQU   *                                                        44200022
         SR    R1,R1                    CLEAR REG FOR IC                44400022
         L     RWORK3,OCELEM            GET ADDR OF GOOD OPT NAME       44600022
         LA    RWORK3,ZERO(RWORK3)      CLEAR HI ORDER BYTE             44800022
         IC    R1,ZERO(RWORK3)          GET LENGTH - 1                  45000022
         LR    R0,RWKAREA               SAVE ADDR OF WORKAREA AND MSG   45200022
         CLI   OCRLN,DEC                DEC FORMAT DESIRED              45400022
         BE    DFCK                     DECIMAL INFO WANTED             45600022
         CLI   OCRLN,CHARACTR           CHARACTER INFO DESIRED          45800022
         BE    CKLEAD                   YES                             46000022
         CLI   OCRLN,HEXFLG             HEXADECIMAL FORMAT DESIRED      46200022
         BNE   ERROPT                   ERROR CONDITION                 46400022
         B     HEXPACK                  YES PROCESS REQUEST             46600022
*              DISPLAY CHARACTER FORMAT OPTION FIELDS                   46800022
*                                                                       47000022
CKNEXT   EQU   *                                                        47200022
         LA    ROPT,ZERO(RWORK,R1)      ADDRESS OF OPTION FIELD END     47400022
         CLI   ZERO(ROPT),C' '          TEST FOR BLANK CHARACTER        47600022
         BNE   NOTALL                   BRANCH NOT A BLANK CHARACTER    47800022
         LTR   R1,R1                    WAS THAT THE LAST CHARACTER     48000022
         BZ    ALLBLNK                  BRANCH IF YES                   48200022
         BCTR  R1,R0                    REDUCE CHARACTER COUNT          48400022
         B     CKNEXT                   GO TEST NEXT CHARACTER          48600022
CKLEAD   EQU   *                                                        48630022
         CLI   ZERO(RWORK),C' '         CHECK FOR BLANK                 48660022
         BNE   CKNEXT                   NO - TEST CHARACTER             48690022
         LA    RWORK,ONE(RWORK)         YES - UPDATE DATA POINTER       48720022
         BCT   R1,CKLEAD                REDUCE DATA COUNT               48750022
ALLBLNK  EQU   *                                                        48800022
         MVC   FOURTWO(FOURTEEN,RWKAREA),ALLB PREPARE TO PRINT ALL      49000022
*                                       BLANK MESSAGE                   49200022
         LH    R1,ALLBLEN               DET MSG LENGTH                  49400022
          STH  R1,LENOFF(RWKAREA)       SET LEN IN WORKAREA             49600022
         B     EXIT                     PREPARE TO XCTL TO |07'         49800022
NOTALL   EQU   *                                                        50000022
         EX    R1,MOVEFROP              MOVE DATA TO DISPLAY MESSAGE    50200022
         AH    R1,LENOFF(RWKAREA)       COMBINE LEN OF DATA TO          50400022
*                                       EXISTING MSG LENGTH             50600022
         LA    R1,ONE(R1)               BUMP LENGTH BY ONE              50800022
         STH   R1,LENOFF(RWKAREA)       STORE LENGTH OF MSG             51000022
         B     EXIT                     PREPARE TO XCTL                 51200022
*                                                                       51400022
*             DISPLAY ALL TYPES OF DATA IN HEXIDECIMAL FORMAT           51600022
*                                                                       51800022
HEXPACK  EQU   *                                                        52000022
         SR    R14,R14                                                  52200022
HEX1     EQU   *                                                        52400022
         IC    R14,ZERO(RWORK)          PICK UP BYTE FROM OPTION FLD    52600022
         LTR   R14,R14                  STILL LEADING ZEROS             52800022
         BNZ   NONZERO                  BRANCH IF END OF LESDING ZEROS  53000022
         LTR   R1,R1                    LAST BYTE IN THE FIELD          53200022
         BZ    NONZERO                  BRANCH IF YES                   53400022
         BCTR  R1,R0                    SUB ONE FROM FIELD SIZE         53600022
         LA    RWORK,ONE(RWORK)         SKIP OVER LEADING ZERO          53800022
         B     HEX1                     GO CHECK THE NEXT BYTE          54000022
NONZERO  EQU   *                                                        54200022
         XC    FOURTWO(C256,RWKAREA),FOURTWO(RWKAREA)          @OZ28887 54250086
         XC    TWO98(C256,RWKAREA),TWO98(RWKAREA)              @OZ28887 54300086
*                                       CLEAR WORK AREA        @OZ28887 54350086
         LR    R15,R1                   COPY COUNT OF NONZERO DIGITS    54400022
AGAIN    EQU   *                                                        54600022
         IC    R14,ZERO(RWORK)          PICK UP DATA BYTE               54800022
         STC   R14,FOURTHR(RWKAREA)     STORE RIGHT MOST DIGIT          55000022
         NI    FOURTHR(RWKAREA),HEX0F   CLEAR UNWANTED BITS             55200022
         SRL   R14,FOUR                 POSITION LEFT HALF BYTE         55400022
         STC   R14,FOURTWO(RWKAREA)     STORE LEFT HALF BYTE            55600022
         LTR   R15,R15                  MORE BYTES TO SPILT             55800022
         BZ    LAST                     BRANCH IF ALL SPILT             56000022
         LA    RWORK,ONE(RWORK)         INCREMENT OPTION FIELD PTR      56200022
         LA    RWKAREA,TWO(RWKAREA)     INCREMENT WORKAREA PTR          56400022
         BCTR  R15,R0                   REDUCE CHARACTER COUNT          56600022
         B     AGAIN                    GO SPILT NEXT CHARACTER         56800022
LAST     EQU   *                                                        57000022
         SLL   R1,ONE                   CHARACTER COUNT TIMES TWO       57200022
         LA    R1,ONE(R1)               ADD ONE IN CASE OF HEX          57400022
         LR    R15,R1                   SAVE NEW CHARACTER COUNT        57600022
         LA    RWORK2,TRANSHEX          SET FOR HEX TRANSLATE           57800022
         L     RWORK,OPCWORK            REESTABLISH ADDR OF RESIDENT    58000022
*                                       WORKAREA WHERE MSG STARTS       58200022
         LA    RWORK,FOURTWO(RWORK)     GET ADDR OF START OF MSG        58400022
         B     TRANLATE                 GO TRANSLATE THE DATA           58600022
*                                                                       58800022
*                                                                       59000022
*                                                                       59200022
*              DISPLAY DECIMAL EQUIVALENT OF HEXIDECIMAL                59400022
*              EXCEPT FIELDS WHICH ARE DEFINED CHARACTER OR THOSE       59600022
*              WHOSE HEXIDECIMAN VLLUES ARE GREATER THAN 7FFFFF (31BIT) 59800022
*                                                                       60000022
*                                                                       60200022
*                                                                       60400022
DFCK     EQU   *                                                        60600022
         CLI   ONE(RWORK3),CHAR         IS FIELD CHARACTER TYPE         60800022
         BE    ERROPT                   YES REJECT COMMAND              61000022
         XC    OPCDOUBL(EIGHT),OPCDOUBL CLEAR FOR USEAGE                61200022
         LR    R15,RWORK                PTR TO OPTION FIELD             61400022
         LA    R1,ONE(R1)               LENGTH OF OPT FIELD             61600022
         BAL   R14,ZLOOP                FIND FIRST NON ZERO CHAR        61800022
*                                                                       62000022
*                                       ON RETURN                       62200022
*                                            R1 = LENGTH                62400022
*                                            R15= PTR TO FIRST NONZERO  62600022
*                                            CHARACTER                  62800022
*                                                                       63000022
*                                                                       63200022
         CH    R1,H4                     IS LENGTH GREATER THAN 4       63400022
         BH    ERROPT                   YES, REJECT COMMAND             63600022
         LTR   R1,R1                    IS LENGTH ZERO                  63800022
         BZ    DFZEROF                  YES, SKIP MOVE CHAR             64000022
         LA    R14,OPCDOUBL+FOUR        LENGTH PAST OPCDOUBL ADDR       64200022
         SR    R14,R1                   PTR TO FIRST CHARACTER          64400022
         BCTR  R1,R0                    DECREMENT LENGTH FOR MOVE       64600022
         EX    R1,MOV1DF                MOVE IT                         64800022
         L     R15,OPCDOUBL             FIELD IN REGISTER               65000022
         LTR   R15,R15                  FIELD GREATER THAN 31 BITS      65200022
         BM    ERROPT                   YES, GET OUT                    65400022
         CVD   R15,OPCDOUBL             CONVERT TO DECIMAL              65600022
         LA    R15,OPCDOUBL             PTER TO BEG OF FLD FOR ZLOOP    65800022
         LA    R1,EIGHT                 LENGTH                          66000022
         BAL   R14,ZLOOP                FIND FIRST OON ZERO CHAR        66200022
         OI    OPCDOUBL+SEVEN,HEX0F     CHANGE SIGN TO PRINTABLE        66400022
         BCTR  R1,R0                    LENGTH -1                       66600022
         SLL   R1,ONE                   2(N-1) UNPACK TO LENGTH         66800022
         TM    ZERO(R15),HEXF0           ODD NUMBER OF DIGETS           67000022
         BNZ   DFL1                     NO, IS LENGTH -1                67200022
         BCTR  R1,R0                    SUBTRACT 1 FOR EXECUTE          67400022
DFL1     EQU   *                                                        67600022
         LR    R14,R1                   USE R14, SAVE R1 FOR LATER      67800022
         SLL   R14,FOUR                 SET BITS TO 'OR' WITH 8-11      68000022
         EX    R14,UNPK1DF              UNPK INTO MSG AREA              68200022
         B     SENDOUT                  FINISH MESSAGE                  68400022
*                                                                       68600022
*                                                                       68800022
ZLOOP    EQU   *                                                        69000022
         CLI   ZERO(R15),ZERO           IS CHAR ZERO                    69200022
         BCR   7,R14                    NO, DONE                        69400022
         LA    R15,ONE(R15)             PTR TO NEXT CHAR                69600022
         BCT   R1,ZLOOP                 DECREMENT LENGTH                69800022
         BR    R14                      ALL ZERO                        70000022
DFZEROF  EQU   *                                                        70200022
         MVI   FOURTWO(RWKAREA),CZERO   PLACE DUMMY ZERO IN MSG         70400022
         SR    R1,R1                    LENGTH - 1  TO ZERO             70600022
         B     SENDOUT                  SEND OUT THE MESSAGE            70800022
*                                                                       71000022
*              TRANSLATE HEX AND PACKED EDCIMAL TYPE DATA               71200022
*                                                                       71400022
TRANLATE EQU   *                                                        71600022
         SR    RWORK3,RWORK3            CLEAR REGISTER                  71800022
         LR    R14,RWORK                COPY OPTION FIELD ADDR          72000022
         LA    RWORK3,C255              SET TO TRANSLATE MAX.           72200022
         EX    RWORK3,TRAN              TRANSLATE FIRST 256 BYTES       72400022
ONETR    EQU   *                                                        72600022
         LA    R14,ONE(R14,RWORK3)       COMPUTE ADDR RENAINING IF ANY  72800022
         EX    RWORK3,TRAN              TRAN REST OR GARBAGE            73000022
         L     RWKAREA,OPCWORK          REESTABLISH ADDR OF WORKAREA    73200022
         LA    R1,ONE(R1)               REAL DATA LENGTH                73400022
         AH    R1,LENOFF(RWKAREA)       COMBINE TO GET TOTAL LEN        73600022
         STH   R1,LENOFF(RWKAREA)       STORE LEN IN WORKAREA           73800022
         B     EXIT                     EXIT FROM ROUTINE               74000022
PREPEXIT EQU   *                                                        74200022
         LA    RWORK2,OPCFOUR(R0)       GET ADDR OF WHERE MSG STARTS    74400022
         SR    RWORK,RTNT               LENGTH OF OPTION FIELD          74600022
         AH    R1,ALLBLEN               LEN OF MSG SO FAR               74800022
         STH   R1,LENOFF(RWORK2)        STORE LEN FO MSG                75000022
         B     EXIT                     EXIT FROM RTN                   75200022
SENDOUT  EQU   *                        CODE TO PREPARE TO SEND OUT MSG 75400022
         LR    RWORK2,R0                GET ADDR OF START OF MSG        75600022
         LA    R1,ONE(R1)               GET REAL DATA LENGTH            75800022
         AH    R1,ZERO(RWORK2)          ADD LENGTH OF FIXED MSG         76000022
         STH   R1,ZERO(RWORK2)          STORE TOTAL MSG LENGTH          76200022
         B     EXIT                     XCTL TO IGC0710D                76400022
ERROPT   EQU   *                                                        76600022
         LA    RZERO,EIGHTEEN           GET MSG NUMBER                  76800022
         L     R13,OPCWORK              GET ADDRESS OF WORKAREA         77000022
         MVC   EIGHT(EIGHT,R13),INVALCMD MOVE CMD IN WORKAREA           77200022
         MVC   OPCLDNME+OFF03(AL2),ERMSG GET ERROR MSG ID               77400022
         B     OUT                      PREPARE TO BRANCH OUT           77600022
EXIT     EQU   *                                                        77800022
         MVC   OPCLDNME+OFF03(AL2),MSGWRIT INSSRT ID OF IGC0710D        78000022
OUT      XC    OCPARM(FOUR),OCPARM      ZERO OUT WTGNAD PARM FIELDS     78200022
         LA    R13,OPCSAVE              ESTABLISH SAVEAREA ADDRESS      78400022
         LR    RPARM,ROPCAVT            SET UP PARM REG                 78600022
         IEDXCTL                        XCTL TO IGC0710D      @OZ35627  78800000
         EJECT                                                          79000022
*                                                                       79200022
ERRMSG   DS    0H                                                       79400022
         DC    AL1(ENDERR-*-1)          LENGTH                          79600022
         DC    CL17'IED034I',CL17'HAS NO',CL7'OPTION'  MESSAGE TEXT     79800022
ENDERR   EQU   *                                                        80000022
TRANSHEX DC    C'0123456789ABCDEF'      TRANSLATION                     80200022
TRANSDEC DC    C'0123456789******'           TABLES                     80400022
MSG      DS    0H                                                       80600022
         DC AL1(ENDMSG-*-1)             LENGTH                          80800022
         DC    CL17'IED035I',CL16'OPTION',C'DATA='   MESSAGE TEXT       81000022
ENDMSG   EQU   *                                                        81200022
OI       OI    FOURTWO(RWKAREA),ZERO    EXECUTED                        81400022
MOVEFROP MVC   FOURTWO(ONE,RWKAREA),ZERO(RWORK)        EXECUTED         81600022
MOV1DF   MVC   ZERO(ONE,R14),ZERO(R15)  EXECUTED                        81800022
UNPK1DF  UNPK  FOURTWO(ONE,RWKAREA),OPCDOUBL(EIGHT)     EXECUTED        82000022
H4       DC    H'4'                     LENGTH CHECK                    82200022
ALLBLEN  DC    H'52'                    LENGTH OF MSG PAST =            82400022
ERMSG    DC    C'03'                    ERROR MSG ID                    82600022
MSGWRIT  DC    C'07'                    ID OF MSG WRITER                82800022
CHAR     EQU   X'00'                    MASK TO CHECK FOR CHARACTER     83000022
CHARACTR EQU   C'C'                     MASK TO CHECK FOR CHARACTER     83200022
DEC      EQU   C'D'                     MASK TO CHECK FOR DECIMAL       83400022
HEXFLG   EQU   C'X'                     MASK TO CHECK FOR HEXADECIMAL   83600022
INVALCMD DC    C'TP,OPTIO'              CMD THAT'S INVALID              83800022
ALLB     DC    C'**ALL BLANKS**'        MESSAGE TEXT                    84000022
TRAN     TR    ZERO(ONE,R14),ZERO(RWORK2)    EXECUTED                   84200022
MOVEMSG  MVC   OPCFOUR(OPCZERO,RWKAREA),OPCONE(R1) MOVE MSG TO WKAREA   84400022
         EJECT                                                          84600022
         TOPCAVTD                                                       84800022
         EJECT                                                          85000022
         TOPCED                                                         85200022
         EJECT                                                          85400022
         TAVTD                                                          85600022
         EJECT                                                          85800022
         TTNTD                                                          86000022
         EJECT                                                          86200022
         TTRMD                                                          86400022
         EJECT                                                          86600022
         TPRFD                                                          86800022
         EJECT                                                          87000022
         TPRIOR                                                         87200022
         EJECT                                                          87400022
         END                                                            87600022
