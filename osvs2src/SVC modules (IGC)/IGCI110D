         TITLE 'IGCI110D - ACTIVATE OR MOVE INVITATION LIST'            00200022
IGCI110D CSECT                                                          00210000
         SPACE 3                                                        00220000
* CHANGE-ACTIVITY = AS FOLLOWS:                                         00230000
********************* MICROFICHE FLAGS **********************SUPPT CODE 00260000
*000000-999999                                                 @Y16X5R0 00320000
*A258000,572000,580000                                          SA65381 00500152
*C592000-596000                                                 SA65381 00500252
*D686000-700000,708000,682000                                   SA60006 00550005
*A 604000                                                      @SA64382 00550156
*A388000                                                       @SA71091 00550256
*A258000,712000,846000                                         @Y17XARP 00552200
*D714000-734000                                                @Y17XARP 00570200
*C010006                                                       @Y17XARP 00580200
*C005000,008200,009400,042600                                  @Y17XART 00586200
*A042600,072000,166200                                         @Y17XART 00592200
*D022000,684000,736000                                         @Y17XART 00593200
*********************************************************************** 00600022
**                                                                    * 00800000
**MODULE-NAME=IGCI110D  (TCAM,OP CTL)                          @Y17XART 00810000
**                                                                    * 00840000
**DESCRIPTIVE-NAME=ACTIVATE OR MOVE INVITATION LIST                   * 00860000
**                                                                    * 00880000
**COPYRIGHT='NONE'                                                    * 00900000
**                                                                    * 00920000
** STATUS:  CHANGE LEVEL 10                                    @Y17XART 00940000
**                                                                    * 01200000
* FUNCTION -- THE PURPOSE OF THIS MODULE IS TO ACTIVATE AN            * 01400022
*    INVITATION LIST ENTRY OR MOVE A NEW INVITATION LIST.  THIS MODULE* 01600022
*    GETS CONTROL AFTER THE OTHER ICHANGE MODULE HAS DETERMINED IT IS * 01800022
*    EITHER AN ACTIVATE OR MOVE TYPE ICHANGE.  THE ROUTINE CHECKS TO  * 02000022
*    SEE IF THE LINE IS STOPPED.  IF NOT, THE LINE MUST BE STOPPED    * 02400022
*    BEFORE THE FUNCTION CAN BE PERFORMED.  TO STOP THE LINE, CONTROL * 02600022
*    IS TRANSFERRED TO STOPLINE.  THE X'40' BIT IS SET IN OCSWITCH TO * 02800022
*    INDICATE THAT THE LINE IS TO BE RESTARTED.  AFTER SUCCESSFULLY   * 03000022
*    STOPPING THE LINE FROM IGCI010D, THE ICHANGE FUNCTION CAN BE     * 03200022
*    PERFORMED.  THE FIRST WORD OF OCMODNME IS USED TO DETERMINE THE  * 03400022
*    TYPE OF ICHANE TO BE DONE.  IF THE FUNCTION IS TO ACTIVATE THE   * 03600022
*    INVITATION LIST ENTRY, THE ENTRIES ARE CHECKED TO SEE IF THEY    * 03800022
*    ARE ALL ACTIVE.  IF SO, OCSWITCH IS CHECKED TO SEE IF THE LINE   * 04000022
*    WILL NEEDRESTARTING.  IF SO, THE LCB IS DEQUEUED AND THE LCB IS  * 04200022
*    POSTED TO ITSELF AND POSTED TO THE ASYNCRONOUS READY Q    @Y17XART 04260000
*    TO START                                                  @Y17XART 04320000
*    THE LINE.  IF THERE ARE SOME INACTIVE ENTRIES, THE COUNT OF THE  * 04600022
*    NUMBER OF INACTIVE ENTRIES IS SET UP.  EACH ENTRY IS SWAPPED     * 04800022
*    FROM THE INACTIVE TO THE ACTIVE SIDE OF THE LIST, AND THEN       * 05000022
*    SWAPPED WITH OTHER ACTIVE ENTRIES UNTIL IT IS IN ITS PROPER PLACE* 05200022
*    IN THE LIST.  THE COUNT IS DECREMENTED.  WNEN ALL ENTRIES HAVE   * 05400022
*    BEEN MADE ACTIVE, THE NUMBER OF ACTIVE ENTRIES IS SET TO EQUAL   * 05600022
*    THE TOTAL ENTRY COUNT.  THE LCB IS DEQUEUED AND THE LINE STARTED * 05800022
*    AS ABOVE.  IF THE FUNCTION IS AN ICHANGE MOVE, THE LIST SIZE IS  * 06000022
*    CALCULATED AND THE DATA AT THE MOVE WORK AREA IS MOVED TO OVERLAY* 06200022
*    THE LIST.  THE LCB IS DEQUEUED AND THE LINE STARTED AS ABOVE.    * 06400022
*                                                                     * 06600022
* ENTRY POINTS --                                                     * 06800022
*    IGCI110D TO PERFORM THE ICHNG ACTIVATE OR MOVE FUNCTIONS FROM    * 07000022
*    AN MPP.                                                          * 07200022
*    PURPOSE - SEE FUNCTION                                    @Y17XART 07260000
*    LINKAGE - XCTL SF=(E,OPCXCTL)                             @Y17XART 07320000
*                                                                     * 07400022
* INPUT --                                                            * 07600022
*    REGISTER 1 - ADDRESS OF OPERATOR CONTROL AVT.  RELEVANT FIELDS   * 07800022
*    ARE OPCDOUBL.  THIS FIELD CONTAINS THE DDNAME OR THE UCB ADDRESS.* 08000022
*    ANOTHER FIELD IS OPCCOPCE WHICH IS THE ADDRESS OF THE CURRENT    * 08200022
*    OPERATOR CONTROL ELEMENT.  RELEVANT FIELDS ARE OCBACKUP.  THIS   * 08400022
*    FIELD CONTAINS EITHER A DDNAME OR A UCB ADDRESS.  ANOTHER        * 08600022
*    RELEVANT FIELD IS OCSWITCH.  IF THE X'40' BIT IS ON, THE LINE    * 08800022
*    SHOULD BE RESTARTED AFTER ACTIVATING THE ENTRIES OR ALTERING     * 09000022
*    THE INVITATION LIST BY THE CONTENTS OF THE SPECIFIED WORK AREA.  * 09200022
*    ICHANGE FUNCTION TO BE PERFORMED EITHER ACTIVATE OR MOVE.        * 09400022
*                                                                     * 09600022
* OUTPUT --                                                           * 09800022
*    REGISTER 0 - RETURN CODE VALUES.                                 * 10000022
*                                                                     * 10200022
* EXTERNAL REFERENCES --                                              * 10400022
*         NONE.                                                       * 10600022
*                                                                     * 10800022
* EXITS,NORMAL --                                                     * 11000022
*         ENTRIES WERE ACTIVATED OR MOVED AS REQUESTED, X'00' RETURN  * 11200022
*         CODE SET.                                                   * 11400022
*         ENTRIES HAVE ALREADY BEEN ACTIVATED OR MOVED, X'00' RETURN  * 11600022
*         CODE SET.                                                   * 11800022
*                                                                     * 12000022
* EXITS,ERROR --                                                      * 12200022
*         ENTRIES COULD NOT BE ACTIVATED OR MOVED, X'04' RETURN CODE  * 12400022
*         SET.                                                        * 12600022
*                                                                     * 12800022
* TABLES/WORK AREAS --                                                * 13000022
*    OPERATOR CONTROL AVT, OPERATOR CONTROL ELEMENT, TERMNAME TABLE,  * 13200022
*    DCB, TERMINAL ENTRY, QCB, LCB, DEB.                              * 13400022
*                                                                     * 13600022
* ATTRIBUTES --                                                       * 13800022
*    SERIALLY REUSABLE, REFRESHABLE, ENABLED, TRANSIENT,              * 14000022
*    SUPERVISOR MODE.                                                 * 14200022
*                                                                     * 14400022
* CHARACTER CODE DEPENDENCY --                                        * 14600022
*    THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL            * 14800022
*    REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS            * 15000022
*    EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME.  THE CODING         * 15200022
*    HAS BEEN ARRANGED SO THAT REDEFINITION OF CHARACTER              * 15400022
*    CONSTANTS, BY REASSEMBLY WILL RESULT IN A CORRECT MODULE         * 15600022
*    FOR THE NEW DEFINITION.                                          * 15800022
*                                                                     * 16000022
* NOTES --                                                            * 16200022
*    COMMUNICATIONS BETWEEN THE VARIOUS TRANSIENT MODULES IS          * 16400022
*    PRIMARILY THROUGH THE OPERATOR CONTROL AVT.                      * 16600022
*    RESTRICTIONS - NONE                                       @Y17XART 16620000
*    REGISTER CONVENTIONS - STANDARD OPCTL CONVENTIONS         @Y17XART 16640000
*    PATCH LABEL - ZAPAREA                                     @Y17XART 16660000
*                                                              @Y17XART 16680000
* MODULE TYPE = CSECT                                          @Y17XART 16700000
*    PROCESSOR - ASSEMBLER                                     @Y17XART 16720000
*    MODULE SIZE -                                             @Y17XART 16740000
*                                                                     * 16800022
*    THIS IS A MODULE RE-WRITE OF IEDQC1 AS OF OS/TCAM LEVEL II       * 16900022
*********************************************************************** 17000022
         EJECT                                                          17200022
         SPACE                                                          17400022
*                                 E Q U A T E S                         17600022
         SPACE                                                          17800022
RZERO    EQU   0                        PARAMETER REGISTER              18000022
RPARM    EQU   1                        PARAMETER REGISTER              18200022
ROPCAVT  EQU   2                        POINTER TO OP CONTROL AVT       18400022
ROPCE    EQU   3                        POINTER TO CURRENT OPCE         18600022
RAVT     EQU   4                        TCAM AVT ADDRESS                18800022
RWORKA   EQU   5                        POINTER TO RESIDENT WORKAREA    19000022
RRLN     EQU   5                        RELATIVE LINE NUMBER            19200022
RMOVE    EQU   5                        MOVE REG                        19400022
RLCB     EQU   5                        LCB ADDRESS                     19600022
RLCB1    EQU   6                        LCB ADDRESS                     19800022
RSCB     EQU   6                        SCB BASE                        20000022
RUCB     EQU   6                        UCB BASE                        20200022
RTNT     EQU   6                        TERMINAL NAME TABLE             20400022
RTERM    EQU   6                                                        20600022
RENTRY   EQU   6                        INVITATION LIST ENTRY ADDRESS   20800022
RTCB     EQU   7                        TCB BASE                        21000022
RDCB     EQU   7                        DCB BASE                        21200022
ROFF     EQU   7                        OFFSET TO DDNAME, ADDR, RLN     21400022
RSWAP    EQU   8                        TO SWAP LIST ENTRIES            21600022
RWORK    EQU   8                        WORK REGISTER                   21800022
RWORK1   EQU   9                        WORK REGISTER                   22000022
RPREFIX  EQU   9                                                        22200022
RLENGTH  EQU   10                       LENGTH OF ENTRY                 22400022
RDEB     EQU   10                       DEB BASE                        22600022
RLIST    EQU   11                       LIST ADDRESS                    22800022
RQCB     EQU   11                       QCB ADDRESS                     23000022
RBASE    EQU   12                       BASE FOR MODULE                 23200022
RSAVE    EQU   13                       PTR TO SAVE AREA                23400022
RRET     EQU   14                       SUBROUTINE RETURN ADDRESS       23600022
CURR     EQU   14                       REGISTER 14            @Y17XARP 23650000
RBRNCH   EQU   15                       SUBROUTINE BRANCH ENTRY POINT   23800022
         SPACE 2                                                        24000022
EIGHT    EQU   8                        LENGTH OF EIGHT                 24200022
NAMELEN  EQU   2                        NO. OF NAME CHANGE CHARACTERS   24400022
VARINAME EQU   3                        OFFSET TO CHANGE LOAD NAME      24600022
FOUR     EQU   4                                                        24800022
ONE      EQU   1                                                        25000022
THREE    EQU   3                                                        25200022
TWO      EQU   2                                                        25400022
EOT      EQU   X'80'                    EOT FLAG                        25600022
ENDACT   EQU   X'FE'                    END OF ACTIVE ENTRIES           25800022
INVSTAT  EQU   3                        OFFSET TO ILIST STATUS  SA65381 25860052
CKPSET2  EQU   X'04'                    ILIST CHECKPOINT STATUS SA65381 25920052
SEVEN    EQU   7                                                        26000022
X1A      EQU   X'1A'                    FLAG FOR BUFFERED TERMINALS     26200022
CVTPTR   EQU   16                       POINTER TO CVT                  26400022
BUSY     EQU   4                        QUEUE BUSY CONDITION            26600022
FIVE     EQU   5                                                        26800022
ZERO     EQU   0                                                        27000022
NINE     EQU   9                                                        27200022
X40      EQU   X'40'                    INDICATES LINE NEEDS RESTARTED  27400022
TWO56    EQU   256                      MOVE LENGTH                     27600022
TR       EQU   3                        DISPLACEMENT                    27800022
ON       EQU   1                        DISPLACEMENT                    28000022
ANYMOVED EQU   X'01'                    INDICATES ENTRIES MOVED         28200022
UCBNM    EQU   13                       OFFSET TO NAME IN UCB           28400022
LCBPRIOR EQU   X'E0'                    PRIORITY FOR LCB                28600022
SIX      EQU   6                        DISPLACEMENT                    28800022
ORDERBIT EQU   X'20'                    SET WHEN ARRANGING ENTRIES      29000022
SAVEOFF  EQU   0                        DISPLACEMENT OF SAVE AREA       29200022
         EJECT                                                          29400022
         USING IEDQAVTD,RAVT            TCAM AVT ADDRESSABILITY         29600022
         USING IEDQOPCD,ROPCAVT         OP CONTROL AVT ADDRESSABILITY   29800022
         USING IEDQOPCE,ROPCE           ELEMENT ADDRESSABILITY          30000022
         USING DEBNMSUB,RDEB            DEB ADDRESSABILITY              30200022
         USING IHADCB,RDCB              DCB ADDRESSABILITY              30400022
         USING IEDQQCB,RQCB             QCB ADDRESSABILITY              30600022
         USING IEDQTNTD,RTNT            TNT ADDRESSABILITY              30800022
         USING IEDQPRF,RPREFIX          BUFFER ADDRESSABILITY           31000022
         USING IEDQTRM,RTERM            TERM ENTRY ADDRESSABILITY       31200022
         USING IEDQSCB,RSCB             SCB ADDRESSABILITY              31400022
         SPACE                                                          31600022
         BALR  RBASE,ZERO               SET BASE REGISTER               31800022
         USING *,RBASE                  MODULE ADDRESSABILITY           32000022
BASE     EQU   *                                                        32200022
IGCI110D IEDHJN SKIPID                                                  32800022
*              INITIAL SETUP                                          * 33400022
         LR    ROPCAVT,RPARM            SET UP AVT ADDRESSING           33600022
         L     RAVT,OPCAVTPT            TCAM AVT ADDRESSING             33800022
         L     ROPCE,OPCCOPCE           GET CURRENT ELEMENT POINTER     34000022
         LM    RLCB1,RDCB,SAVEOFF(RSAVE) RESTORE REGS SAVED BY I0       34200022
         MVC   OPCDOUBL(EIGHT),OCBACKUP SET DDNAME                      34400022
INVLIST  EQU   *                                                        34600022
         LA    RLIST,DCBINVLI-IHADCB(RDCB)                              34800022
         SR    RWORK,RWORK                                              35000022
         IC    RWORK,LCBUCBX-IEDQLCB(RLCB1)  GET IOB INDEX              35200022
         SLL   RWORK,TWO                MULTIPLY BY FOUR                35400022
         AR    RLIST,RWORK              GET TO PROPER LIST ADDRESS      35600022
         L     RLIST,AVTEZERO(RLIST)    GET LIST                        35800022
         LA    RLIST,AVTEZERO(RLIST)    CLEAR HIGH ORDER BYTE           36000022
         CLC   OCMODNME(THREE),AVTFZERO ACTIVATE INVITATION LIST        36200022
         BNE   MOVE                     NO,MOVE                         36400022
         SPACE                                                          36600022
*              ACTIVATEINVITATION LIST                                * 36800022
ACT      EQU   *                                                        37000022
         SR    ROFF,ROFF                                                37200022
         STH   ROFF,OCMODNME+SIX        SET UP FIRST ENTRY              37400022
         SR    RLENGTH,RLENGTH          INIT REG                        37600022
         SR    RWORK1,RWORK1            INIT REG                        37800022
         IC    RLENGTH,TWO(RLIST)       LENGTH OF ENTRY                 38000022
         LR    RWORK1,RLENGTH                                           38200022
         BCTR  RWORK1,AVTEZERO          DECREMENT FOR EXECUTE           38400022
         SPACE                                                          38600022
GTENTRY1 EQU   *                                                        38800022
         NI    OPCFLAG1,AVTEFF-ORDERBIT TURN OFF BIT           @SA71091 38900056
         LH    ROFF,OCMODNME+SIX        GET NEXT ENTRY                  39000022
         LA    ROFF,ONE(ROFF)           INCREMENT BY ONE                39200022
         STH   ROFF,OCMODNME+SIX        GET NEXT ENTRY                  39400022
         CLC   AVTEZERO(ONE,RLIST),ONE(RLIST) ALL ENTRIES ACTIVE        39600022
         BE    EXIT                     YES                             39800022
         CLC   OCMODNME+SEVEN(ONE),AVTEZERO(RLIST) OUT OF ENTRIES       40000022
         BH    EXIT                     YES                             40200022
         SPACE                                                          40400022
         LA    RENTRY,SEVEN(RLIST)      POINTER TO FIRST ENTRY INDEX    40600022
         SR    RRET,RRET                CLEAR                           40800022
         IC    RRET,ONE(RLIST)          NUMBER OF ACTIVE ENTRIES        41000022
         LTR   RRET,RRET                ANY ACTIVE ENTRIES              41200022
         BZ    CKINACT                  NO CHECK INACTIVE               41400022
         SPACE                                                          41600022
CKNEXT1  EQU   *                                                        41800022
         AR    RENTRY,RLENGTH           GO TO NEXT ENTRY'S INDEX        42000022
         CLC   AVTEZERO(ONE,RENTRY),OCMODNME+SEVEN IS THIS ACTIVE ENTRY 42200022
         BE    GTENTRY1                 YES ITS ACTIVE GET ANOTHER      42400022
         BCT   RRET,CKNEXT1             IF NOT END OF ACTIVE,GO BACK    42600022
CKINACT  EQU   *                                                        42800022
         LA    RENTRY,ONE(RENTRY)       GET BY ACTIVE FLAG              43000022
         TM    THREE(RLIST),EOT         'EOT' ENTRY PRESENT             43200022
         BZ    CKINACT1                 NO,GO ON                        43400022
         LA    RENTRY,ONE(RENTRY)       SKIP IT,TOO                     43600022
CKINACT1 EQU   *                                                        43800022
         AR    RENTRY,RLENGTH           GO TO NEXT ENTRY'S INDEX        44000022
         CLC   AVTEZERO(ONE,RENTRY),OCMODNME+SEVEN IS THIS ACTIVE ENTRY 44200022
         BNE   CKINACT1                 NO,GET ANOTHER                  44400022
         SPACE                                                          44600022
         SR    RENTRY,RLENGTH           YES,SET FOR SWAP                44800022
         LR    RSWAP,RENTRY             ONE BYTE SHY OF                 45000022
         SR    RSWAP,RLENGTH            ENTRY AND PREVIOUS ENTRY        45200022
ACTIVATE EQU   *                                                        45400022
         CLI   AVTEZERO(RENTRY),ENDACT  FIRST INACTIVE ENTRY ?          45600022
         BE    MACTIVE                  YES,MAKE IT ACTIVE              45800022
         SPACE                                                          46000022
SWAPLOOP EQU   *                                                        46200022
         EX    RWORK1,SWAP1             SWAP TWO                        46400022
         EX    RWORK1,SWAP2             SUCCESSIVE                      46600022
         EX    RWORK1,SWAP3             ENTRIES                         46800022
         TM    OPCFLAG1,ORDERBIT        ARE ENTRIES BEING ARRANGED      47000022
         BO    CKPOS                    YES                             47200022
         SR    RENTRY,RLENGTH           BACK UP ONE ENTRY               47400022
         SR    RSWAP,RLENGTH            BACK PREVIOUS ENTRY,ONE ENTRY   47600022
         B     ACTIVATE                 GO CHECK INACT ENTRIES          47800022
         SPACE                                                          48000022
MACTIVE  EQU   *                                                        48200022
         OI    OPCFLAG1,ANYMOVED        INDICATE FUNCTION PERFORMED     48400022
         TM    THREE(RLIST),EOT         EOT ENTRY PRESENT               48600022
         BO    MACTIVE1                 YES,SPECIAL CARE                48800022
         EX    RWORK1,MOVACT            PLACE ENTRY ON AVTIVE SIDE      49000022
         LA    RRET,AVTEZERO(RLENGTH,RENTRY) POINT TO NEW END OF ACTIVE 49200022
         BCTR  RENTRY,AVTEZERO          DECREMENT BY ONE                49400022
         BCTR  RSWAP,AVTEZERO           DECREMENT BY ONE                49600022
         MVI   AVTEZERO(RRET),ENDACT    RESET ACTIVE POINTER            49800022
MACTIVE2 EQU   *                                                        50000022
         SR    RRET,RRET                                                50200022
         IC    RRET,ONE(RLIST)          GET NUMBER OF ACTIVE ENTRIES    50400022
         LA    RRET,ONE(RRET)           INCREMENT BY ONE                50600022
         SPACE                                                          50800022
ACTLOOP  EQU   *                                                        51000022
         BCT   RRET,ACTPOS              IF NOT LAST POSITION COMPARE    51200022
         SPACE                                                          51400022
RETURNA  EQU   *                                                        51600022
         IC    RRET,ONE(RLIST)          GET NUMBER OF ACTIVE ENTRIES    51800022
         LA    RRET,ONE(RRET)           INCREMENT BY ONE                52000022
         STC   RRET,ONE(RLIST)          STORE COUNT BACK                52200022
         B     GTENTRY1                     GET ANOTHER ENTRY           52400022
         SPACE                                                          52600022
ACTPOS   EQU   *                                                        52800022
         CLC   AVTEZERO(ONE,RENTRY),OCMODNME+SEVEN COMPARE ENTRY        53000022
         BL    RETURNA                  PREVIOUS ENTRY LOWER,DONE       53200022
         SPACE                                                          53400022
         OI    OPCFLAG1,ORDERBIT        TRYING TO ARRANGE               53600022
         B     SWAPLOOP                 PERFORM SWAP ENTRY OPERATION    53800022
         SPACE                                                          54000022
CKPOS    EQU   *                                                        54200022
         SR    RENTRY,RLENGTH           BACK UP                         54400022
         SR    RSWAP,RLENGTH               ONE ENTRY                    54600022
         B     ACTLOOP                  GO CHECK POSITION               54800022
         SPACE                                                          55000022
*              MOVE INVITATION LIST                                   * 55200022
MOVE     EQU   *                                                        55400022
         L     RMOVE,OCMODNME           GET WORK AREA ADDRESS           55600022
         BAL   RRET,INVLNGTH            FIND LIST LENGTH                55800022
         SPACE                                                          56000022
         TM    OPCSPEC,OPCRSTRT         RESTART IN PROGRESS             56200022
         BZ    MOVE1                    NO                              56400022
         SPACE                                                          56600022
MOVEA    EQU   *                                                        56800022
         SR    RZERO,RZERO              CLEAR REGISTER                  57000022
         L     RWORK,EIGHT(ROPCE)       FIRST INVITATION LIST UNIT      57200022
         MVC   OPCDOUBL(FOUR),FOUR(RLIST) SAVE CPUID ADDRESS    SA65381 57260052
         LR    RBRNCH,RLIST             SAVE ILIST ADDRESS      SA65381 57320052
MOVELOOP EQU   *                                                        57400022
         SR    RLENGTH,RLENGTH          CLEAR                           57600022
         IC    RLENGTH,OCCIB-IEDQOPCE(RWORK) LENGTH OF DATA-1           57800022
         LH    RLIST,OCFLAG-IEDQOPCE(RWORK) OFFSET TO DATA IN   SA65381 58000052
*                                        INVITATION LIST        SA65381 58060052
         AR    RLIST,RBRNCH             ADDRESS OF DATA IN ILISTSA65381 58120052
         LA    RMOVE,OCCIB+ONE-IEDQOPCE(RWORK) SET FROM ADDRESS         58200022
         EX    RLENGTH,MVC              MOVE INTO INVITATION LIST       58400022
         L     RWORK,EIGHT(RWORK)       GET NEXT UNIT ADDRESS           58600022
         LA    RWORK,AVTEZERO(RWORK)    CLEAR HIGH ORDER BYTE           58800022
         CH    RWORK,AVTHA4             LAST ELEMENT                    59000022
         BNL   MOVELOOP                 MOVE NEXT UNIT INTO LISTSA65381 59200052
         MVC   FOUR(FOUR,RBRNCH),OPCDOUBL RESET CPUID ADDRESS   SA65381 59300052
         OI    INVSTAT(RBRNCH),CKPSET2  RESET CHECKPOINT STATUS SA65381 59400052
         B     EXIT                     AND RETURN              SA65381 59500052
         SPACE                                                          59800022
MOVE1    EQU   *                                                        60000022
         LR    RRET,RLIST               SAVE LIST POINTER               60200022
         LR    RTCB,RPARM               KEEP THE LENGTH                 60400022
         NI    TR(RMOVE),ONE            TURN ON AUTOPOLL IN INPUT       60430056
*                                                              @SA64382 60460056
         NI    TR(RLIST),ENDACT         TURN OFF AUTOPOLL IN INVLIST    60490056
*                                                              @SA64382 60520056
         OC    TR(,RMOVE),TR(RLIST)     CHG ONLY AUTOPOLL BIT  @SA64382 60550056
         MVC   AVTEZERO(FOUR,RLIST),AVTEZERO(RMOVE) RESET CONTROL WORD  60600022
         CLI   LCBRSKEY-IEDQLCB(RLCB1),X1A IS THIS FOR BUFFERED TERM    60800022
         BE    MOVE2                    YES,SAVE ONLY ONE BYTE          61000022
         SPACE                                                          61200022
         MVC   FOUR(FOUR,RLIST),FOUR(RMOVE)  SET SECOND CONTROL WORD    61400022
MOVE2    EQU   *                                                        61600022
         MVC   FIVE(ONE,RLIST),FIVE(RMOVE)   SET BUFFERED BYTE          61800022
         LA    RLIST,EIGHT(RLIST)       RESET POINTER                   62000022
         LA    RMOVE,EIGHT(RMOVE)       RESET ALSO                      62200022
         SH    RPARM,H8                 RESET COUNT                     62400022
MOVE3    EQU   *                                                        62600022
         CH    RPARM,H256               WILL ONE MOVE DO IT             62800022
         BNH   MOVE4                    YES                             63000022
         MVC   ZERO(TWO56,RLIST),AVTEZERO(RMOVE)  MOVE 256 CHARACTERS   63200022
         LA    RLIST,TWO56(RLIST)       SET UP                          63400022
         LA    RMOVE,TWO56(RMOVE)         FOR                           63600022
         SH    RPARM,H256                    NEXT MOVE                  63800022
         B     MOVE3                    PERFORM NEXT MOVE               64000022
         SPACE                                                          64200022
MOVE4    EQU   *                                                        64400022
         BCTR  RPARM,AVTEZERO           DECREMENT FOR EXECUTE           64600022
         EX    RPARM,MVC                MOVE CHARACTER                  64800022
         LR    RLIST,RRET               GET LIST BACK                   65000022
         BAL   RRET,INVLNGTH            COMPUTE LENGTH OF INV LIST      65200022
         ST    RPARM,OCMODNME+FOUR      STORE INV LIST LENGTH           65400022
         ST    RLIST,OCMODNME           ADDRESS OF INVITATION LIST      65600022
*              DETERMINE ALL EXIT SITUATIONS                          * 65800022
EXIT     EQU   *                                                        66000022
         L     RLCB,SAVEOFF(RSAVE)      GET ADDRESS OF LCB              66200022
         TM    OPCSPEC,OPCRSTRT         RESTART IN PROGRESS             66400022
         BO    CKPOINT1                 YES,GO TO CHECKPOINT            66600022
         SPACE                                                          66800022
         SR    RZERO,RZERO              SET GOOD RETURN CODE            67000022
EXIT1    EQU   *                                                        67200022
         LA    RRET,EIGHT(RLIST)        POINTER TO BEGINNING OF LIST    67400022
         ST    RRET,LCBINVPT-IEDQLCB-ONE(RLCB) FOR RECEIVE SCHEDULER    67600022
         TM    OCSWITCH,X40             SHOULD LINE BE RESTARTED        67800022
         BZ    OUT1                     NO                              68000022
         SPACE                                                          70200022
NOTMPS   EQU   *                                                        70400022
         MVI   LCBPRI-IEDQLCB(RLCB),LCBPRIOR SET E0 PRIORITY IN LCB     70600022
         OI    LCBSTAT1-IEDQLCB(RLCB),LCBRECVN   SET STATUS FOR START   71000022
         ST    RLCB,AVTEZERO(RLCB)      MAKE LCB POINT TO ITSELF        71200022
         L     RWORK,CVTPTR             GET CVT ADDR           @Y17XARP 71210000
         L     RWORK,AVTCVTPT(RWORK)    GET ADDR OF TCX        @Y17XARP 71310000
         USING IEDQTCX,RWORK            ESTABLISH ADDR         @Y17XARP 71410000
         L     CURR,TCXREADY            GET ADDR OF CURRENT    @Y17XARP 71510000
*                                       ENTRY ON ASYN READY Q  @Y17XARP 71610000
NEWCURR  STCM  CURR,SEVEN,LCBLINK-IEDQLCB(RLCB)                @Y17XARP 71710000
*                                       LINK NEW ELEM TO CURR  @Y17XARP 71810000
         CS    CURR,RLCB,TCXREADY       WAS CURR ELEM UPDATED  @Y17XARP 71910000
*                                       ASYNCHRONOUSLY         @Y17XARP 72010000
         BNE   NEWCURR                  YES,LINK NEW ELEM TO   @Y17XARP 72110000
*                                       CURR ELEM. NO, ASYN    @Y17XARP 72210000
*                                       READY QUEUE NOW HAS    @Y17XARP 72310000
*                                       NEW ELEM               @Y17XARP 72410000
         TM    AVTBIT3,AVTSTAIN         CHECKPOINT THIS                 73800022
         BZ    OUT1                     GO CHECKPOINT THE ELEMENT       74000022
         SPACE                                                          74200022
CKPOINT1 EQU   *                                                        74400022
         MVC   OPCLDNME+VARINAME(NAMELEN),CHECKPT                       74600022
         STH   RZERO,OCPARM             SET FOR CHECKPOINT              74800022
         MVC   OCWTG(NAMELEN),WRITE     SET UP OUTPUT MODULE            75000022
         B     XCTL                     GO CHECKPOINT                   75200022
         SPACE                                                          75400022
OUT1     EQU   *                                                        75600022
         SR    RZERO,RZERO                                              75800022
OUT      EQU   *                                                        76000022
         MVC   OPCLDNME+VARINAME(NAMELEN),WRITE  SET MODULE NAME        76200022
XCTL     EQU   *                                                        76400022
         NI    OPCFLAG1,AVTEZERO        CLEAR FLAGS                     76600022
         LR    RPARM,ROPCAVT            PASS AVT ADDRESS                76800022
         XCTL  SF=(E,OPCXCTL)           CALL NEXT MODULE                77000022
MACTIVE1 EQU   *                                                        77200022
         LA    RRET,TWO                                                 77400022
         SR    RENTRY,RRET              BACK UP TWO                     77600022
         SR    RSWAP,RRET               BACK UP TWO                     77800022
         MVC   OCPARM,ONE(RENTRY)       SAVE EOT-FE                     78000022
         EX    RWORK1,MOVACT1           MOVE TO ACTIVE                  78200022
         LA    RRET,ONE(RLENGTH,RENTRY) ADDRESSING FOR EOTFE            78400022
         MVC   ZERO(TWO,RRET),OCPARM    RESET EOT-FE                    78600022
         B     MACTIVE2                 GO INCREMENT ACTIVE COUNT       78800022
         SPACE                                                          79000022
INVLNGTH EQU   *                                                        79200022
         SR    RPARM,RPARM              CLEAR                           79400022
         LR    RZERO,RPARM              REGISTERS                       79600022
         IC    RZERO,AVTEZERO(RLIST)    GET NUMBER OF ENTRIES           79800022
         IC    RPARM,TWO(RLIST)         GET ENTRY WIDTH                 80000022
         MR    RZERO,RZERO              GET LIST SIZE-1                 80200022
         LA    RPARM,NINE(RPARM)        ADD STATUS AND FE               80400022
         TM    THREE(RLIST),EOT         BSC?                            80600022
         BCR   EIGHT,RRET               NO                              80800022
         SPACE                                                          81000022
         LA    RPARM,ONE(RPARM)         YES,ADD ONE FOR EOT             81200022
         BR    RRET                     GO BACK                         81400022
         SPACE                                                          81600022
SWAP1    XC    ONE(AVTEZERO,RSWAP),ONE(RENTRY) SWAP TWO                 81800022
SWAP2    XC    ONE(AVTEZERO,RENTRY),ONE(RSWAP) SUCCESSIVE               82000022
SWAP3    XC    ONE(AVTEZERO,RSWAP),ONE(RENTRY) ENTRIES                  82200022
MOVACT   MVC   AVTEZERO(AVTEZERO,RENTRY),ONE(RENTRY)  TO ACTIVATE ENTRY 82400022
MVC      MVC   AVTEZERO(ONE,RLIST),AVTEZERO(RMOVE) MOVE NEW LIST IN     82600022
MOVACT1  MVC   ONE(ONE,RENTRY),THREE(RENTRY)                            82700022
H256     DC    H'256'                   TO DETERMINE IF ONE MOVE ENOUGH 82800022
H8       DC    H'8'                     RESET LENGTH OF INVITATION LIST 83000022
CHECKPT  DC    C'06'                    CHECKPOINT MODULE NAME          83200022
WRITE    DC    C'07'                    OUTPUT WRITER NAME              83400022
STOPLN   DC    C'V1'                    STOPLINE MODULE                 83600022
ICHNGMOD DC    C'I0'                    ICHANGE DEACTIVATE              83800022
FIRSTLD  DC    C'00'                    FIRST LOAD OF OP CTL            84000022
ICHNG1   DC    C'I1'                    ICHANGE ACTIVATE AND MOVE       84200022
ZAPAREA  DC   50X'FF'                   PATCH AREA             @Y17XART 84300000
         TITLE 'IGCI110D - DSECTS REFERENCED BY THIS MODULE'            84400022
         TAVTD                                                          84600022
         EJECT                                                          84660000
         TTCXD SVS                                                      84720000
         EJECT                                                          84800022
         DCBD DSORG=TX                                                  85000022
         EJECT                                                          85200022
         TDEBD                                                          85400022
         EJECT                                                          85600022
         TDISPD                                                         85800022
         EJECT                                                          86000022
         EJECT                                                          86400022
         TLCBD                                                          86600022
         EJECT                                                          86800022
         TOPCED                                                         87000022
         EJECT                                                          87200022
         TOPCAVTD                                                       87400022
         EJECT                                                          87600022
         TPRFD                                                          87800022
         EJECT                                                          88000022
         TPRIOR                                                         88200022
         EJECT                                                          88400022
         TQCBD                                                          88600022
         EJECT                                                          88800022
         TSCBD                                                          89000022
         EJECT                                                          89200022
         TTNTD                                                          89400022
         EJECT                                                          89600022
         TTRMD                                                          89800022
         END                                                            90000022
