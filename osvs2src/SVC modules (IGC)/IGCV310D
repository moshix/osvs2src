         TITLE 'IGCV310D - OPERATOR CONTROL START LINE MODULE'          00200022
IGCV310D CSECT                                                          00400022
         SPACE 3                                                        00420022
*  CHANGE ACTIVITY AS FOLLOWS:                                          00440022
******************** MICROFICHE FLAGS *********************** SUPT CODE 00460022
*A000000-999999                                                  S99240 00500022
*A258500,721150                                                 SA60314 00500122
*A046000,047200,106000-107000,168600-169200,178500,285570-285710 S22024 00500200
*A285850-285960,301000,374600-375200,652030-652510,652600-653350 S22024 00500300
*A653410,653470-653830,752500-753500                             S22024 00500400
*A041000,645000                                                  X02004 00550405
*C040000,042000,076000,644000                                    X02004 00600405
*D034000,174000,240000-242000,248000-250000,260000,284500-285000,X01948 00650405
*D746000                                                         X01948 00700405
*A226500,457600-459400,652271-652272                             Y06328 00700505
*C568000                                                       SA68020  00700654
*A478000                                                        SA67777 00700705
*D566000-574000                                                @XA03985 00700856
*C478000                                                       @SA75429 00740858
*A638000                                                       @SA75727 00760858
*A354000,360960,360980,361500,713000,730000,080000,753200,     @Y17XARX 00761800
*A745200                                                       @Y17XARX 00762800
*C010600,011000,046200-047400                                  @Y17XARX 00763800
*D010200-010300,110000-140000,374600-375200,458200-459400,     @Y17XARX 00764800
*D652030-653800,686100-687500,669000                           @Y17XARX 00765800
*D354500                                                       @Y17XART 00775800
*A361520,730040                                                @Y17XART 00776300
*C644000-645000                                                @OY15473 00783300
*A186000,645000                                                @OY15473 00790300
*C285500                                                       @OY14092 00794300
*C718000                                                       @OZ35627 00796312
         SPACE 3                                                        00800022
*********************************************************************** 01000022
*                                                                     * 01010022
*  MODULE NAME = IGCV310D (TCAM,OPERATOR CONTROL)              @Y17XARX 01040000
*                                                                     * 01050022
*  DESCRIPTIVE NAME = OPERATOR CONTROL START LINE                     * 01060022
*                                                                     * 01070022
*  COPYRIGHT =  NONE                                                  * 01080000
*                                                                     * 01090022
*  STATUS = VERSION 10.0                                       @Y17XARX 01100000
*                                                                     * 01200022
* FUNCTION -- THIS ROUTINE PROCESSES OPERATOR CONTROL COMMANDS WHICH  * 01400022
*    REQUEST THE STARTING OF ACTIVITY ON A LINE OR LINE GROUP.  THE   * 01600022
*    LCB ADDRESS IS OBTAINED FROM DCBIOBAD.  IF IT IS NOT STOPPED AND * 01800022
*    THE REQUEST IS FROM TOTE, THE LINE IS POSTED TO ITSELF TO RETURN * 02000022
*    IT FROM TOTE TO TCAM.  IF IT IS NOT FROM TOTE, THE NEXT LCB, IF  * 02200022
*    ANY, IS ACCESSED.  WHEN ALL THE LCBS HAVE BEEN PROCESSED, IF ANY * 02400022
*    HAVE BEEN STARTED A MESSAGE NUMBER IS SET UP SUCH THAT THE       * 02600022
*    MESSAGE MODULE WILL OUTPUT THE APPROPRIATE MESSAGE.  OTHERWISE A * 02800022
*    MESSAGE NUMBER IS SET UPSUCH THAT AN 'ALREADY STARTED' MESSAGE   * 03000022
*    WILL BE PRINTED OUT.  IF THE LINE IS STOPPED, ITS RECEIVE BIT IS * 03200022
*    SET ON.  A NOP COMMAND IS BUILT IF IT IS A DIAL LINE.  A SAD IS  * 03600022
*    BUILT IF IT IS A 2702 CONTROL UNIT.  AN ENABLE IS BUILT FOR A    * 03800022
*    NON-DIAL LINE, AND THE CHANNEL PROGRAM IS COMPLETED.  SVC   X02004 04000005
*    114 (EXCPVR) IS ISSUED TO START THE LINE, AND A LINE        X02004 04100005
*    STARTED BIT IS TURNED ON.                                   X02004 04200005
*    THE NEXT LCB IS ACCESSED AND PROCESSING CONTINUES UNTIL ALL LINES* 04400022
*    HAVE BEEN PROCESSED, AS ABOVE.  FOR COMMANDS ASSOCIATED WITHS22024 04600022
*    WITH DEVICES OFF AN RN A DIFFERENT APPROACH IS TAKEN.  FOR AS22024 04620022
*    AN LGB AS OPPOSED TO A DCB, THIS MODULE PASSES CONTROL    @Y17XARX 04640000
*    TO IGCVI10D.  IGCVI10D PROCESSES STARTLINE REQUESTS FOR   @Y17XARX 04644000
*    3705 RESOURCES.                                           @Y17XARX 04664000
*                                                              @Y17XARX 04665000
* NOTES = COMMUNICATION BETWEEN THE VARIOUS TRANSIENT MODULES  @Y17XARX 04666000
*    IS PRIMARILY THROUGH THE OPERATOR CONTROL AVT.            @Y17XARX 04667000
*    RESTRICTIONS = NA                                         @Y17XARX 04668000
*    REGISTER CONVENTIONS = STANDARD                           @Y17XARX 04669000
*    PATCH LABEL = ZAPPATCH                                    @Y17XARX 04670000
*                                                              @Y17XARX 04671000
* MODULE TYPE = PROCEDURE                                      @Y17XARX 04672000
*    PROCESSOR=ASSEMBLER                                       @Y17XARX 04673000
*    MODULE SIZE =  844 BYTES                                  @Y17XARX 04674000
*    ATTRIBUTES = SERIALLY REUSEABLE, REFRESHABLE,ENABLED,     @Y17XARX 04675000
*    TRANSIENT, SUPERVISOR MODE.                               @Y17XARX 04676000
*                                                                     * 04800022
* ENTRY POINTS --                                                     * 05000022
*    IGCV310D TO START A LINE.                                          05200022
*                                                                     * 05400022
* INPUT --                                                            * 05600022
*    REGISTER 1 - ADDRESS OF OPERATOR CONTROL AVT.  RELEVANT FIELDS   * 05800022
*    ARE OPCDOUBL.  IT CONTAINS EITHER THE DDNAME OR UCB ADDRESS LEFT * 06000022
*    JUSTIFIED.  OPCCOPCE IS THE ADDRESS OF THE CURRENT ELEMENT.      * 06200022
*                                                                     * 06400022
* OUTPUT --                                                           * 06600022
*    REGISTER 0 - NUMBER OF AN ERROR MESSAGE.                         * 06800022
*    REGISTER 1 - RETURN CODE.                                        * 07000022
*                                                                     * 07200022
* EXTERNAL REFERENCES --                                              * 07400022
*         EXCPVR (SVC 114)                                       X02004 07600005
*                                                                     * 07800022
* EXITS,NORMAL --                                                     * 08000022
*         EXIT TO IGCVI10D FOR 3705 STARTLINE REQUESTS.        @Y17XARX 08002000
*         OCWTG CONTAINS THE ID OF THE REQUESTOR - THIS        @Y17XARX 08004000
*         FIELD IS NOT CHANGED BY IGCV310D.   OCTRMTBL         @Y17XARX 08006000
*         CONTAINS THE TTCIN OF THE FIRST LINE OR THE          @Y17XARX 08008000
*         TTCIN OF THE GROUP AS SET BY THE SUBROUTINE,         @Y17XARX 08010000
*         DCBLOCAT.                                            @Y17XARX 08012000
*         ALLRLN(X'20') IS SET IN OPCFLAG1 FOR GRP,ALL REQUEST.@Y17XARX 08014000
*         LINE STARTED.  APPROPRIATE MESSAGE NUMBER SET UP TO GO TO   * 08200022
*         MESSAGE MODULE, X'00' RETURN CODE SET.                      * 08400022
*         LINE ALREADY STARTED.  APPROPRIATE MESSAGE NUMBER SET UP TO * 08600022
*         TO MESSAGE MODULE, X'08' RETURN CODE SET.                   * 08800022
*                                                                     * 09000022
* EXITS,ERROR --                                                      * 09200022
*         TOTE IN CONTROL OF LINE, DUAL CODES WHEN TWO CODES NOT      * 09400022
*         ALLOWED.  ERROR NUMBER SET UP IN REGISTER, X'04' RETURN CODE* 09600022
*         SET.                                                        * 09800022
*                                                                     * 10000022
* TABLES/WORK AREAS --                                                * 10200022
*    OPERATOR CONTROL AVT, OPERATOR CONTROL ELEMENT, STOPLINE REQUEST * 10400022
*    ELEMENT, DEB, DCB, LCB, QCB, TERMNAME TABLE, TERMINAL ENTRY,S22024 10600022
*    AVT, BTU, AND RSID TABLE.                                   S22024 10700022
*    AVT.                                                             * 10800022
*                                                                     * 11000022
*                                                                     * 14200022
*    THIS IS A MODULE RE-WRITE OF IEDQCU AS OF OS/TCAM LEVEL II       * 14300022
*********************************************************************** 14400022
         EJECT                                                          14600022
         SPACE                                                          14800022
*                                 E Q U A T E S                         15000022
         SPACE                                                          15200022
RZERO    EQU   0                        PARAMETER REGISTER              15400022
RPARM    EQU   1                        PARAMETER REGISTER              15600022
ROPCAVT  EQU   2                        OP CONTROL AVT BASE             15800022
ROPCE    EQU   3                        CURRENT ELEMENT BASE            16000022
RAVT     EQU   4                        TCAM AVT POINTER                16200022
RDCB     EQU   5                        BASE FOR DCB DSECT              16400022
RUCB     EQU   6                        UCB POINTER                     16600022
RDEB     EQU   7                        DEB POINTER                     16800022
RPRF     EQU   7                        PREFIX BASE REGISTER     S22024 16860022
RBTU     EQU   6                        ADDR OF BTU              S22024 16920022
RWORK    EQU   8                        WORK REGISTER                   17000022
RALSTAT  EQU   8                        REGISTER FOR SAVING    @ZM08380 17050000
*                                       ALL OPTION STATUS FLAG @ZM08380 17100000
RRLN     EQU   9                        RELATIVE LINE NUMBER (BINARY)   17200022
RLCB     EQU   10                       POINTER TO LCB                  17600022
RLOOP    EQU   11                       LOOP COUNTER FOR START ALL      17800022
RTERM    EQU   11                       TERM ENTRY ADDRESS       S22024 17850022
RWORK1   EQU   11                       WORK REG                        17900022
RBASE    EQU   12                       MODULE BASE REGISTER            18000022
RRET     EQU   14                       SUBROUTINE RETURN ADDRESS       18200022
RBRNCH   EQU   15                       SUBROUTINE ENTRY                18400022
*                                                                     * 18500000
         SPACE 2                                                        18600022
AD       EQU   7                        MASK FOR STCM          @OY15473 18700000
ADDRLEN  EQU   3                        LENGTH OF ADDRESS FIELD         18800022
ADDROFF  EQU   4                        OFFSET TO CCW OFFSET FIELD      19000022
ALLRLN   EQU   X'20'                    RLN SPECIFIED AS 'ALL'          19200022
BISYNC   EQU   X'90'                    BSC ADAPTER ON CONTROL UNIT     19400022
CHAIN    EQU   0                        OFFSET TO UPDATE CHAINING FLAGS 19600022
CMDCHAIN EQU   X'40'                    CCW COMMAND CHAINING BIT        19800022
CODES    EQU   4                        DUAL CODE IN UCB                20000022
C2703    EQU   3                        CONTROL UNIT IS 2703            20200022
DIAL     EQU   X'90'                    LINE HAS AUTOCALL-AUTOANSW      20400022
DISABLE  EQU   X'2F'                    DISABLE CCW OP CODE             20600022
DLE      EQU   11                       OFFSET TO DISABLE WRITE DLE-EOT 20800022
DOUBLWD  EQU   8                        LENGTH OF DOUBLE WORD           21000022
DUALCODE EQU   8                        DUAL CODE SET IN DCB            21200022
DUALINT  EQU   X'20'                    DUAL INTERFACE IN UCB           21400022
EIGHT    EQU   8                        LENGTH OF EIGHT IN UNPACK       21600022
ENABLE   EQU   X'27'                    ENABLE CCW OP CODE              21800022
ERROR19  EQU   19                       ERROR-LINE ALREADY STARTED      22000022
ERROR49  EQU   49                       ERROR-TOTE CONTROLS LINE        22200022
ERROR47  EQU   47                        ERROR-SYSTEM DELAY ACTIVE      22400022
ERROR92  EQU   92                       ERROR-BSC INCOMPATABLE MODE     22600022
ERROR443 EQU   443                      ERROR-LINE IN EP MODE    Y06328 22650005
ERR210   EQU   210                      ACTIVATE DEACT ERROR MSG S22024 22700000
FIRSTRLN EQU   1                        FIRST VALID RLN FOR DCB         22800022
FLAGS    EQU   8                        OFFSET TO LAST OF CCW           23000022
FOUR     EQU   4                        OFFSET OF 4                     23200022
GOODSTRT EQU   20                       STARTED MESSAGE                 23400022
INTFB    EQU   X'20'                    INTERFACE B SET IN DCB          23600022
INTRUPT  EQU   4                        PERMIT INTERRUPT MODE OP        23800022
LINK     EQU   0                        OFFSET TO LINK FIELD            24400022
LOCAL    EQU   X'10'                    DEVICE CLASS OF 2260 LOCAL      24600022
NAMELEN  EQU   2                        LENGTH OF NAME CHANGE           25200022
NEXT     EQU   1                        UPDATE FOR RLN START ALL        25400022
NEXTCCW  EQU   8                        LENGTH TO GET TO NEXT CCW       25600022
NOP      EQU   3                        NOP FOR STARTLINE               25800022
NOTCA    EQU   2                                                SA60314 25850022
NUMBER   EQU   X'F0'                    COVER SIGN AFTER UNPACK         26000022
NWRTEOT  EQU   X'FE'                    NO DLE-EOT WRITTEN              26200022
ONE      EQU   1                        OFFSET OF ONE                   26400022
OPCODE   EQU   4                        OFFSET TO CCW COMMAND CODE      26600022
REMOTE   EQU   X'82'                    2701 WITH IBM III ADAPTER       26800022
SETMODE  EQU   X'23'                    SETMODE CCW OP CODE             27000022
STARTONE EQU   128                      AT LEAST ONE LINE STARTED       27200022
TOTE     EQU   X'F1'                    TOE HAS LCB FOR LINE            27400022
TWO      EQU   2                        LENGTH OF TWO                   27600022
UCBCLASS EQU   18                       OFFSET TO DEVICE CLASS IN UCB   27800022
UCBCNTRL EQU   19                       OFFSET TO UCB CONTROL UNIT TYPE 28000022
UCBFEAT  EQU   17                       OFFSET TO UCB FEATURES IN TYPE  28200022
VARINAME EQU   3                        OFFSET TO CHANGEABLE NAME       28400022
ERROR17  EQU   17                       DCB NOT OPEN           @OY14092 28550000
ERROR18  EQU   18                       COMMAND INVALID          S22024 28557022
ERR400   EQU   400                      NOT ACTIVE MESSAGE       S22024 28560000
ZERO     EQU   0                        INIT QUANTITY            S22024 28564022
DSLGB    EQU   X'80'                    LGB IDENTIFIER           S22024 28571022
POST     EQU   X'0C'                    INDICATE THAT THIS IS A TPOST   28578022
ENDOFLST EQU   X'80'                    END OF LIST INDICATOR    S22024 28585022
CLRSZ    EQU   22                       FOR BTU AREA             S22024 28592022
THREE    EQU   3                        LENGTH OF COMMAND        S22024 28596022
         EJECT                                                          28600022
         USING IEDQAVTD,RAVT            TCAM AVT ADDRESSABILITY         28800022
         USING DEBNMSUB,RDEB            DEB ADDRESSABILITY              29000022
         USING IHADCB,RDCB              DCB ADDRESSABILITY              29200022
         USING IEDQLCB,RLCB             LCB ADDRESSABILITY              29400022
         USING IEDQOPCE,ROPCE           ELEMENT ADDRESSABILITY          29600022
         USING IEDQOPCD,ROPCAVT         OP CONTROL AVT ADDRESSABILITY   29800022
         SPACE                                                          30000022
         USING IEDQTRM,RTERM                                     S22024 30100022
         BALR  RBASE,RZERO              SET BASE REGISTER               30200022
         USING *,RBASE                  SET ADDRESSABILITY              30400022
IGCV310D IEDHJN  SKIPID                                                 30900022
*              INITIAL SETUP                                          * 31400022
         LR    ROPCAVT,RPARM            ADDRESS OF OP CONTROL AVT       31600022
         L     RAVT,OPCAVTPT            ADDRESS OF TCAM AVT             31800022
         L     ROPCE,OPCCOPCE           CURRENT ELEMENT POINTER         32000022
         MVC   OPCLDNME+VARINAME(NAMELEN),ERRXCTL SET FOR ERROR MODULE  32200022
         LA    RZERO,ERROR47            SET DELAY ERROR MESSAGE         32600022
         TM    AVTBIT1,AVTDLAYN         SYSTEM DELAY IN PROGRESS?       32800022
         BO    ERRRTN                   YES, ERROR MSG NEEDED    S22024 33000000
         SPACE                                                          33200022
         MVC   OPCDOUBL(DOUBLWD),OCBACKUP PLACE INPUT FOR VERIFY        33400022
         SR    RPARM,RPARM              CLEAR FOR RLN                   33600022
         TM    OCFLAG,OCLINE            LINE NUMBER IN DATA?            33800022
         BO    VERIFY                   YES, ALL PARAMETERS ARE SET     34000022
         SPACE                                                          34200022
         IC    RPARM,OCRLN              PICK UP RELATIVE LINE NUMBER    34400022
         LTR   RPARM,RPARM              ALL SPECIFIED?                  34600022
         BNE   VERIFY                   NO, FIND DCB AND UCB            34800022
         SPACE                                                          35000022
         LA    RPARM,FIRSTRLN           SET RLN TO ONE                  35200022
         OI    OPCFLAG1,ALLRLN          INDICATE RLN OF ALL             35400022
VERIFY   EQU   *                                                        35600022
         IC    RALSTAT,OCALSTAT         SAVE THE 'ALL OPTION'  @ZM08380 35650000
*                                       STATUS FLAG            @ZM08380 35700000
         L     RBRNCH,OPCDCBLK          GET LOOK-UP ADDRESS             35800022
         BALR  RRET,RBRNCH              LOCATE UCB AND DCB              36000022
         B     PROCESS                  NORMAL RETURN GO START LINE     36030022
         SPACE                                                          36060022
         LTR   RDCB,RPARM               SET DCB BASE REG                36090022
         BZ    SET17                    BRANCH IF DDNAME NOT FOUND      36092022
         TM    DCBOFLGS,OPEN            WAS DCB OPEN                    36094022
         BZ    SET17                    NO,SEND ERROR MESSAGE           36096022
         TM    DCBDSORG,DSLGB           IS THIS AN LGB         @Y17XARX 36096200
         BO    RLNOK                    YES, BYPASS RLN CHECK  @Y17XARX 36096400
         BCT   RZERO,SET17              BRANCH IF RLN TOO HIGH          36098022
RLNOK    EQU   *                                               @Y17XARX 36098500
         TM    OPCFLAG1,ALLRLN          RLN OF 'ALL'                    36100022
         BO    PROCESS                  YES,ENTER LOOP                  36102022
SET17    EQU   *                                                        36104022
         LA    RZERO,ERROR17            SET ERROR MSG NUMBER            36106022
ERRRTN   EQU   *                                                 S22024 36107000
         LA    RPARM,FOUR               SET ERROR RETURN CODE           36108022
         B     ERROR                    AND SEND ERROR MSG              36110022
OPEN     EQU   X'10'                                                    36112022
         SPACE                                                          36120022
PROCESS  EQU   *                                                        36150022
         SPACE 1                                               @Y17XARX 36152000
         STC   RALSTAT,OCALSTAT         RESTORE 'ALL OPTION'   @ZM08380 36152100
*                                       STATUS FLAG            @ZM08380 36152200
         TM    OPCFLAG1,ALLRLN          ALL PROCESSING ?       @Y17XART 36152400
         BNO   NOTALL                   BRANCH IF NOT ALL      @ZM08380 36152800
         OI    OCALSTAT,OCALLFLG        SET ALL INDICATOR      @Y17XART 36153200
         B     AROUND                   GO GET DCB/LGB ADDRESS @ZM08380 36153300
NOTALL   EQU   *                        NOT PROCESSING ALL     @ZM08380 36153400
         NI    OCALSTAT,OCFOXES-OCALLFLG  SET 'ALL' FLAG OFF   @ZM08380 36153500
AROUND   EQU   *                                               @Y17XART 36153600
         LR    RDCB,RPARM               SET UP DCB/LGB ADDR    @Y17XARX 36154000
         TM    DCBDSORG,DSLGB           IS THIS AN LGB         @Y17XARX 36156000
         BO    CCU3705A                 YES, BRANCH            @Y17XARX 36158000
         SPACE                                                          36200022
         TM    OPCFLAG1,ALLRLN          RLN OF 'ALL'?                   36400022
         BNO   GOODRLN                  NO, DO NOT RESET                36600022
         MVI   OCRLN,AVTEZERO           SET RLN TO ZERO                 36800022
         LA    RWORK,OPCWAITL           WAIT LIST                       36850022
LOCATEND EQU   *                                                        36900022
         L     RWORK1,AVTEZERO(RWORK)   GET NEXT ELEM                   36950022
         LA    RWORK1,AVTEZERO(RWORK1)  CLEAR HIGH BYTE                 37000022
         LTR   RWORK1,RWORK1            IS THIS ELEM                    37050022
         BZ    GOODRLN                  NO PROCESS COMMAND              37100022
         CLC   OCBACKUP-IEDQOPCE(EIGHT,ROPCE),OCBACKUP-IEDQOPCE(RWORK1) 37150022
         BE    ATTACH                   YES                             37200022
         LR    RWORK,RWORK1             NEW ELEMENT BECOMES OLD         37250022
         B     LOCATEND                 FIND END OF LIST                37300022
GOODRLN  EQU   *                                                        37350022
         LR    RDCB,RPARM               SAVE RETURNED DCB ADDRESS       37400022
         L     RDEB,DCBDEBAD            PICK UP DEB ADDRESS FROM DCB    37600022
         LA    RRLN,FIRSTRLN            SET RLN TO ONE                  37800022
         SR    RLOOP,RLOOP              CLEAR FOR NUMBER TO EXTENTS     38000022
         IC    RLOOP,DEBNMEXT           GET EXTENTS FROM DEB            38200022
         CLI   OCRLN,AVTEZERO           ALL FOR RELATIVE LINE NUMBER    38400022
         BE    START                    YES, START THE LINES            38600022
         SPACE                                                          38800022
         LR    RLOOP,RRLN               SET LOOP COUNTER TO ONE         39000022
         IC    RRLN,OCRLN               PICK UP ACTUAL RLN              39200022
*              START LINE                                             * 39400022
START    EQU   *                                                        39600022
         SLL   RRLN,TWO                 RLN TIMES FOUR                  39800022
         L     RUCB,DEBUCBAD-FOUR(RRLN) PICK UP UCB ADDRESS             40000022
         LA    RUCB,AVTEZERO(RUCB)      CLEAR HIGH BYTE                 40200022
         SRL   RRLN,TWO                 RESET RLN VALUE                 40400022
         LTR   RUCB,RUCB                OPENED DD DUMMY?                40600022
         BZ    CHEKLAST                 YES, CHECK FOR MORE TO PROCESS  40800022
         SPACE                                                          41000022
         L     RLCB,DCBIOBAD            GET IOB BASE ADDRESS FROM DCB   41200022
         LA    RLCB,AVTEZERO(RLCB)      CLEAR HIGH ORDER BYTE           41400022
         LR    RZERO,RRLN               SAVE RELATIVE LINE NUMBER       41600022
         SR    RPARM,RPARM              CLEAR FOR IOB SIZE              41800022
         IC    RPARM,DCBEIOBX           PICK UP IOB/LCB LENGTH          42000022
         MR    RZERO,RZERO              MULTIPLY RLN TIMES IOB SIZE     42200022
         AR    RLCB,RPARM               ADD OFFSET TO BASE ADDRESS      42400022
         LA    RPARM,LCBFLAG1-IEDQLCB   GET OFFSET TO IOB IN LCB        42600022
         SR    RLCB,RPARM               SUBTRACT OFFSET TO GET LCB ADDR 42800022
         TM    OCFLAG,OCTOTE            IS THIS A REQUEST FROM TOTE     43000022
         BZ    NOTTOTE                  NO                              43200022
         SPACE                                                          43400022
         CLI   LCBCPA,TOTE              LCB UNDER TOTE'S CONTROL        43600022
         BNE   NOTOTE                   NO                              43800022
         SPACE                                                          44000022
         NI    LCBCPA,OCFOXES-TOTE      TURN OFF TOTE CONTROL FLAG      44200022
NOTTOTE  EQU   *                                                        44400022
         CLI   LCBCPA,TOTE              LCB UNDER TOTE CONTROL?         44600022
         BNE   NOTOTE                   NO, TOTE DOES NOT HAVE LINE     44800022
TOTE3705 EQU   *                                                 S22024 44900000
         SPACE                                                          45000022
         LA    RZERO,ERROR49            SET TOTE CONTROLS LINE ERROR    45400022
         B     ERRRTN                   COMMAND NOT VALID        S22024 45700000
         SPACE                                                          45760005
         SPACE                                                          46000022
NOTOTE   EQU   *                                                        46200022
         CLI   LCBSTAT1,AVTEZERO        LINE STOPPED?                   46400022
         BNE   CHEKLAST                 NO, CHECK FOR MORE LINES        46600022
         SPACE                                                          46800022
READY    EQU   *                                                        47000022
         CLC   ONE(ADDRLEN,RLCB),AVTSUPPT+ONE LCB ON START UP READY Q?  47200022
         BE    READY                    YES, WAIT UNTIL OFF             47400022
         SPACE                                                          47600022
         MVI   LCBSTAT1,LCBRECVN+LCBSENDN SET IF NO END STATUS @SA75429 47800058
         NI    LCBERBST,AVTEFF-LCBPRCPG  TURN OFF FLAG          SA67777 47900005
         ST    RLCB,LINK(RLCB)          POST LCB TO ITSELF              48000022
         OI    OPCFLAG1,STARTONE        INDICATE ONE LIEN STARTED       48200022
         LA    RPARM,LCBCPA             GET CHANNEL PROGRAM AREA ADDR   48400022
         ST    RPARM,LCBSIOCC           SET CHANNEL PROG START ADDRESS  48600022
         SH    RPARM,AVTHA4             BACKUP BEFORE CCW        S22024 48800000
         ST    ROPCE,ADDROFF(RPARM)     SET VALID ADDRESS IN CCW        49000022
         MVI   OPCODE(RPARM),NOP        SET NOP COMMAND CODE            49200022
         MVC   FLAGS(FOUR,RPARM),VALIDCCW FINISH CCW FLAGS AND LENGTH   49400022
         CLI   UCBCLASS(RUCB),LOCAL     LOCAL 2260 LINE?                49600022
         BE    EXCP                     YES, ISSUE EXCP FOR LINE        49800022
         SPACE                                                          50000022
         CLI   UCBCNTRL(RUCB),REMOTE    2701 WITH IBM III (2260R)       50200022
         BE    EXCP                     YES, START THE LINE             50400022
         SPACE                                                          50600022
         IC    RWORK,UCBCNTRL(RUCB)     PICK UP CONTROL UNIT FIELD      50800022
         N     RWORK,CNTRLMSK           CLEAR ALL BUT CU BITS           51000022
         BZ    EXCP                     NO CONTROL UNIT, START THE 7770 51200022
         SPACE                                                          51400022
         MVI   OPCODE(RPARM),DISABLE    SET DISABLE COMMAND             51600022
         LA    RPARM,NEXTCCW(RPARM)     POINT FOR NEXT CCW              51800022
         BCT   RWORK,NOT02              BRANCH FOR 2703 OR 2701         52000022
         SPACE                                                          52200022
*              2702 CONTROL UNIT                                      * 52400022
         IC    RWORK,UCBFEAT(RUCB)      GET FEATURES BYTE               52600022
         N     RWORK,CNTRLMSK           CLEAR ALL EXCEPT SAD BUTS       52800022
         IC    RWORK,SADCODES(RWORK)    INSERT ACTUAL SAD COMMAND       53000022
         ST    ROPCE,ADDROFF(RPARM)     PLACE VALID ADDRESS IN CCW      53200022
         STC   RWORK,OPCODE(RPARM)      PLACE SAD COMMAND IN CCW        53400022
         MVC   FLAGS(FOUR,RPARM),VALIDCCW FINISH CCW FLAGS AND LENGTH   53600022
         OI    CHAIN(RPARM),CMDCHAIN    SET COMMAND CHAIN IN PREV CCW   53800022
         LA    RPARM,NEXTCCW(RPARM)     POINT TO NEXT CCW               54000022
*              2703 OR 2701 CONTROL UNIT                              * 54200022
NOT02    EQU   *                                                        54400022
         TM    UCBCNTRL(RUCB),BISYNC    BSC ADAPTER?                    54600022
         BNO   NOBSC                    NO, SKIP SETMODE PROCESSING     54800022
         SPACE                                                          55000022
         NI    LCBTPCD+DLE,NWRTEOT      DO NOT WRITE DLE-EOT            55200022
         LA    RWORK,DCBINVLI           LOAD FIRST INVL ADDR FROM DCB   55400022
         LR    RZERO,RRLN               SAVE RLN                        55600022
         BCTR  RRLN,RZERO               DECREMENT BY ONE FOR OFFSET     55800022
         SLL   RRLN,TWO                 MULTIPLY BY FOUR FOR OFFSET     56000022
         AR    RWORK,RRLN               ADD OFFSET TO FIRST ADDR        56200022
         LR    RRLN,RZERO               RESTORE RLN FOR LATER           56400022
         TM    UCBCNTRL(RUCB),C2703     CONTROL=2703?                   57600022
         BO    MODESET                  YES, SET UP SETMODE COMMAND     57800022
         SPACE                                                          58000022
         TM    AVTEZERO(RWORK),INTFB    INTERFACE B SET IN DCB?         58200022
         BNO   TESTCODE                 NO, CHECK DUAL CODE             58400022
         SPACE                                                          58600022
         TM    UCBFEAT(RUCB),DUALINT    DUAL INTERFACE SET IN UCB?      58800022
         BZ    ERR92                    NO, INTERFACE IN DCB IS WRONG   59000022
         SPACE                                                          59200022
TESTCODE EQU   *                                                        59400022
         TM    AVTEZERO(RWORK),DUALCODE DUAL CODE INDICATED?            59600022
         BZ    MODESET                  NO, DO THE SET MODE             59800022
         SPACE                                                          60000022
         TM    UCBFEAT(RUCB),CODES      TWO CODES ALLOWED?              60200022
         BZ    ERR92                    NO, UCB NOT AGREEABLE WITH DCB  60400022
         SPACE                                                          60600022
MODESET  EQU   *                                                        60800022
         ST    RWORK,ADDROFF(RPARM)     PLACE MODE BYTE ADDR IN CCW     61000022
         MVI   OPCODE(RPARM),SETMODE    PLACE SETMODE OP CODE           61200022
         MVC   FLAGS(FOUR,RPARM),VALIDCCW FINISH CCW FLAGS AND LENGTH   61400022
         OI    CHAIN(RPARM),CMDCHAIN    SET COMMAND CHAIN IN LAST CCW   61600022
         LA    RPARM,NEXTCCW(RPARM)     POINT TO NEXT CCW               61800022
*              NO BISYNC ADAPTER                                      * 62000022
NOBSC    EQU   *                                                        62200022
         TM    UCBFEAT(RUCB),DIAL       DIAL LINE?                      62400022
         BNZ   EXCP                     YES, SKIP ENABLE CCW            62600022
         SPACE                                                          62800022
         ST    ROPCE,ADDROFF(RPARM)     SET VALID ADDRESS IN CCW        63000022
         MVC   FLAGS(FOUR,RPARM),VALIDCCW FINISH CCW FLAGS AND LENGTH   63200022
         MVI   OPCODE(RPARM),ENABLE     SET OP CODE TO ENABLE           63400022
         OI    CHAIN(RPARM),CMDCHAIN    CHAIN THIS TO PREVIOUS CCW      63600022
EXCP     EQU   *                                                        63800022
         SR    RRET,RRET                CLEAR A REGISTER       @SA75727 63860058
         STH   RRET,LCBTTCIN            CLEAR TTCIN            @SA75727 63920058
         MVC   LCBTPCD(FOUR),TPOPCODE   SET TP OP CODES IN LCB          64000022
         SPACE                                                          64200022
         L     RPARM,AVTHK              GET ADDRESS OF IEDQHK  @OY15473 64400000
         ST    RPARM,LCBQCBA-1          SET TO POST LCBTO HK   @OY15473 64420000
         XC    LCBLINK,LCBLINK          CLEAR LINK FIELD       @OY15473 64440000
         MVI   LCBPRI,PRIPRIME          SET LCB PRIORITY       @OY15473 64460000
         STCM  RLCB,AD,OPCAQFST         SET FIRST AQCTL PARM   @OY15473 64480000
         STCM  RLCB,AD,OPCAQSEC         SET SECOND PARM        @OY15473 64500000
         MVI   OPCAQREQ,POST            SET TO POST LCB        @OY15473 64520000
         MVI   OPCAQRQ2,ENDOFLST        SET END OF LIST        @OY15473 64540000
         LA    RPARM,OPCAQCTL           POINT TO AQCTL LIST    @OY15473 64560000
         AQCTL ,                        POST LCB TO IEDQHK     @OY15473 64580000
         SPACE 2                                                        64600022
CHEKLAST EQU   *                                                        64800022
         LA    RRLN,NEXT(RRLN)          UPDATE RLN NUMBER FOR ALL       65000022
         BCT   RLOOP,START              LOOP UNTIL ALL STARTED          65200022
         SPACE                                                          65400022
         LA    RPARM,EIGHT              SET ALREADY DONE RET CODE       65460022
         TM    OCFLAG,OCMPP             MPP INPUT                       65520022
         BZ    TESTART                  NO                              65580022
         SR    RPARM,RPARM              SET ZERO COND CODE FOR ALREADY  65640022
TESTART  EQU   *                                                        65700022
         LA    RZERO,ERROR19            SET POSSIBLE ERROR NUMBER       65800022
         TM    OPCFLAG1,STARTONE        AT LEAST ONE LINE STARTED?      66000022
         BNO   ERROR                    NO, ALL ALREADY STARTED         66200022
         SPACE                                                          66400022
*              CHECK ALL EXIT SITUATIONS                              * 66600022
         SR    RPARM,RPARM              SET GOOD RETURN                 66800022
         LH    RZERO,OCPARM             SET RETURN PARAMETER            67000022
         MVC   OPCLDNME+VARINAME(NAMELEN),OCWTG SET RETURN NAME         67200022
         CH    RPARM,OCWTG              RETURN ASKED ?           S22024 67400000
         STH   RPARM,OCWTG              CLEAR OCWTG              S22024 67500000
         BNE   XCTL                     YES, RETURN TO CALLING   S22024 67600000
*                                       MODULE                   S22024 67700000
         SPACE                                                          67800022
         MVC   OPCLDNME+VARINAME(NAMELEN),CHECKPT SET CHECKPOINT NAME   68000022
         MVC   OCWTG(NAMELEN),WRTMSG    WTG TO MESSAGE MODULE           68200022
         MVI   OCPARM,AVTEZERO          CLEAR FIRST BYTE                68400022
         MVI   OCPARM+ONE,GOODSTRT      SET MESSAGE NUMBER              68600022
         TM    OCFLAG,OCTOTE            TOTE COMMAND             S22024 68603000
         BO    TOTEEXIT                 YES, BRANCH              S22024 68606000
ERROR    EQU   *                                                        68800022
         TM    OCFLAG,OCTOTE+OCMPP      TOTE OR MPP INPUT?              69000022
         BZ    XCTL                     NO, GO TO ERROR MSG MODULE      69200022
TOTEEXIT EQU   *                                                 S22024 69300000
         SPACE                                                          69400022
         MVC   OCWTG(NAMELEN),WRITE     SET TO GO TO WRITER             69600022
         STH   RPARM,OCPARM             GET PARAMETER FIELD             69800022
         LTR   RPARM,RPARM              GOOD RETURN?                    70000022
         BZ    XCTL                     YES, GO ON TO CHECKPOINT        70200022
         SPACE                                                          70400022
         LR    RZERO,RPARM              NO, SET RETURN CODE IN ZERO     70600022
         MVC   OPCLDNME+VARINAME(NAMELEN),WRITE GO DIRECTLY TO WRITER   70800022
XCTL     EQU   *                                                        71000022
         NI    OPCFLAG1,AVTEFF-ALLRLN-STARTONE CLEAR FLAGS SET HERE     71200022
         MVC   OCUNIT+ONE(VARINAME),INVTIC  SET TIC COMMAND             71300054
IMMDXCTL EQU   *                                               @Y17XARX 71320000
         LR    RPARM,ROPCAVT            PASS OP CONTROL AVT ADDRESS     71400022
         SPACE                                                          71600022
         IEDXCTL                                               @OZ35627 71800012
         SPACE 2                                                        72000022
ATTACH   EQU   *                                                        72010022
         TM    OCFLAG-IEDQOPCE(RWORK1),OCATTACH ATTACHED ELEMENT        72020022
         BNO   ATTACHIT                 NO QUEUE IT UP                  72030022
         L     RWORK1,FOUR(RWORK1)      GET NEXT ELEMENT IN CHAIN       72040022
         B     ATTACH                   ATTACH ELEM TO LIST             72050022
         SPACE                                                          72060022
ATTACHIT EQU   *                                                        72070022
         ST    ROPCE,FOUR(RWORK1)       QUEUE UP NEW ELEMENT            72080022
         OI    OCFLAG-IEDQOPCE(RWORK1),OCATTACH UNIT IS ATTACHED        72090022
         MVC   OCWTG(NAMELEN),STRTLINE  START LINE MODULE               72100022
         MVC   OPCLDNME+VARINAME(NAMELEN),FRSTLOAD SET TO GO TO 00      72110022
         LA    RZERO,NOTCA              ENTRY CODE FOR NOT CA   SA60314 72115022
         B     XCTL                     GO TO NEXT MOD                  72120022
         SPACE   3                                                      72130022
*    THE DCB AND UCB PARAMETERS FOR DUAL CODE DO NOT AGREE              72200022
ERR92    EQU   *                                                        72400022
         LA    RZERO,ERROR92            SET POSSIBLE ERROR CODE         72800022
         B     ERRRTN                   RETURN ERROR INDICATION  S22024 73000000
         SPACE 2                                               @Y17XARX 73002000
CCU3705A EQU   *                                               @Y17XARX 73004000
         NI    OCALSTAT,AVTEFF-OCRENTRD CLEAR VI 1ST ENTRY FLG @Y17XART 73004200
         MVC   OPCLDNME+VARINAME(NAMELEN),NCPLOAD   SET FOR VI @Y17XARX 73006000
         MVI   OPREPRI-IEDOPRED(ROPCE),AVTEZERO CLEAR PRIORITY @Y17XARX 73008000
         MVI   OPCFLAG1,AVTEZERO        CLEAR FLAG BYTE        @Y17XARX 73010000
         NI    OCFLAG,AVTEFF-OCRESP     CLEAR RESPONSE BIT     @Y17XARX 73012000
         XC    OCPARM,OCPARM            CLEAR PARM FIELD       @Y17XARX 73014000
         B     IMMDXCTL                 XCTL TO IGCVI10D       @Y17XARX 73016000
         SPACE 2                                                        73200022
CNTRLMSK DC    F'3'                     MASK TO ISOLATE LOW ORDER BITS  73600022
ERRXCTL  DC    C'03'                    FIRST ERROR LOAD NAME           73800022
CHECKPT  DC    C'06'                    CHECKPOINT MODULE NAME          74000022
WRITE    DC    C'07'                    MESSAGE WRITER/ROUTER           74200022
WRTMSG   DC    C'09'                    GOOD MESSAGES FOR VARY MODULE   74400022
STRTLINE DC    C'V3'                    START LINE MODULE               74460022
FRSTLOAD DC    C'00'                    FIRST LOAD OF OP CTL            74520022
NCPLOAD  DC    C'VI'                    ID OF 3705 PROCESSOR   @Y17XARX 74570000
TPOPCODE DC    X'02020202'              TP OP CODES FOR LCB             74800022
SADCODES DC    X'13171B1F'              ACTUAL SAD COMMAND CODES        75000022
VALIDCCW DC    X'20000001'              VALID LAST HALF OF CCW          75200022
ACTLINE  DC    X'089880'                ACTIVATE LINE AND FLAGS  S22024 75250000
INVTIC   DC    X'000002'                INVALID TIC ADDRESS             75320054
ZAPPATCH DC    25X'FF'                  PATCH AREA             @Y17XARX 75340000
         TITLE 'IGCV310D - DSECTS REFERENCED BY THIS MODULE'            75400022
         TAVTD                                                          75600022
         EJECT                                                          75800022
         TDEBD                                                          76000022
         EJECT                                                          76200022
         DCBD  DSORG=TX                                                 76400022
         EJECT                                                          76600022
         TLCBD                                                          76800022
         EJECT                                                          77000022
         TOPCED                                                         77200022
         EJECT                                                          77400022
         TOPCAVTD                                                       77600022
         TOPRED                                                @Y17XARX 77605000
         TTRMD                                                   S22024 77620022
         EJECT                                                          77640022
         TPRFD                                                   S22024 77660022
         EJECT                                                          77680022
         TPRIOR                                                         77740022
         EJECT                                                          77760022
         TLGBD                                                   S22024 77780022
         END                                                            77800022
