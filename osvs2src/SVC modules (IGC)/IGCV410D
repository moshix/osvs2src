         TITLE 'IGCV410D- OPERATOR CONTROL START TERMINAL MODULE'       00100022
IGCV410D CSECT                                                          00200022
         SPACE 3                                                        00200105
* CHANGE-ACTIVITY = AS FOLLOWS:                                         00200205
********************* MICROFICHE FLAGS ********************** SUPT CODE 00200305
*000000-999999                                                 @Y16X5R0 00220300
*D425300-425600                                                 SA60316 00250105
*D471000-478000,482000,469000                                   SA60006 00250205
*A203000-253000                                                 SA61772 00250305
*A481000                                                        SA67777 00270305
*C312000                                                        SA70182 00270405
*A402000                                                        SA70182 00270505
*A427000                                                       @SA74217 00270658
*A130000,484000,537000                                         @Y17XARP 00270800
*D485000-495000                                                @Y17XARP 00272600
*C005000,005600,002500                                         @Y17XART 00273600
*A044000,047000,099000                                         @Y17XART 00274600
*C318100-318700 MOVED AFTER 342000                             @OX16689 00279600
*A501000                                                       @OY16378 00284600
*C312000,427600                                                @OY16378 00289600
*D402200-402600,403300,403600,409000                           @OY16378 00294600
         SPACE 3                                                 S21903 00300005
*D470000,4960000                                               @Y17XARP 00320000
*********************************************************************** 00400005
*                                                                     * 00450005
*  MODULE NAME = IGCV410D (TCAM ,OP CTL)                       @Y17XART 00470000
*                                                                     * 00510022
*  DESCRIPTIVE NAME = OPERATOR CONTROL START TERMINAL MODULE          * 00520022
*                                                                     * 00530022
*  COPYRIGHT = 'NONE'                                                 * 00540022
*                                                                     * 00550022
*  STATUS: CHANGE LEVEL 10                                     @Y17XARP 00552000
*                                                                     * 00600022
* FUNCTION -- THIS ROUTINE PROCESSES OPERATOR CONTROL COMMANDS WHICH  * 00700022
*    REQUEST THAT A SPECIFIED TERMINAL BE ACTIVATED FOR ENTERING, OR  * 00800022
*    ACTIVATED FOR BOTH ENTERING AND ACCEPTING.  THE ROUTINE SEARCHES * 00900022
*    THE TERMNAME TABLE SERIALLY FOR AN ENTRY MATCHING THAT SPECIFIED * 01000022
*    IN THE COMMAND.  IF NO MATCH IS FOUND, AN ERROR MESSAGE NUMBER   * 01100022
*    IS SET UP AND CONTROL IS TRANSFERRED TO THE ERROR MODULE.  WHEN  * 01200022
*    THE ENTRY IS FOUND IT IS TESTED FOR A PROCESS ENTRY (X'20' IN    * 01300022
*    TRMSTATE).  IF IT IS, SINCE PROCESS ENTRIES HAVE NO INVITATION   * 01400022
*    LISTS, AN ERROR MESSAGE NUMBER IS SET UP AND CONTROL IS          * 01500022
*    TRANSFERRED TO THE ERROR MODULE.  THE QCB AT TRMDESTQ IS USED TO * 01600022
*         OBTAIN THE RLN (QCBRELLN) AND THE DCB (QCBDCBAD).  THE DCB  * 01700022
*    IS TESTED FOR OPEN STATUS, AND IF NOT AN ERROR MESSAGE NUMBER IS * 01800022
*    SET UP AND CONTROL IS TRANSFERRED TO THE ERROR MODULE.  THE DEB  * 01900022
*    IS ACCESSED FROM DCBDEBAD AND ITS UCB AT DEBUCBAD IS TESTED FOR  * 02000022
*    ZERO.  IF ZERO, THE LINE HAS BEEN OPENED DD DUMMY, SO THAT AN    * 02100022
*    ERROR MESSAGE NUMBER IS SET UP AND CONTROL IS TRANSFERRED TO THE * 02200022
*    ERROR MODULE.  THE LCB ADDRESS IS DETERMINED FROM DCBIOBAD AND   * 02300022
*    THE LCB TESTED FOR DIAL (LCBDIAL ON IN LCBSTAT2).  IF IT IS DIAL,* 02400022
*    AN ERROR MESSAGE NUMBER IS SET UP AND CONTROL IS TRANSFERRED TO  * 02500022
*    THE ERROR MODULE.  THE LINE IS TESTED FOR STOPPED.  IF NOT,      * 02600022
*    CONTROL IS TRANSFERRED TO STOPLINE TO STOP THE LINE.  THE X'40'  * 02700022
*    BIT IS TURNED ON IN OCSWITCH TO INDICATE THAT THE LINE SHOULD BE * 02800022
*    STARTED BACK AFTER ACTIVATING THE TERMINAL.  ALL THE        S22024 02920022
*    PREVIOUS PREPROCESSING IS DONE IN IGCV210D AND CONTROL IS   S22024 02940022
*    PASSED TO THIS MODULE.  THE LIST IS TESTED                  S22024 02960022
*    TO SEE IF ALL ENTRIES ARE ACTIVE.  IF THEY ARE, A RESPONSE       * 03000022
*    NUMBER IS SET UP AND CONTROL IS TRANSFERRED TO THE VARY MESSAGE  * 03100022
*    MODULE.  IF THE REQUEST IS TO START GENERAL POLL CONTROL IS      * 03150022
*    PASSED TO THE START GENERAL POLL MODULE (IGCV610D) IN ORDER TO   * 03200022
*    START IT.  EACH ENTRY IS TESTED TO SEE IF IT IS THE CORRECT      * 03250022
*    TERMINAL.  IF IT IS AND IS NOT ALREADY ACTIVE, IT IS SWAPPED FROM* 03300022
*    THE INACTIVE TO THE ACTIVE PART OF THE LIST AND SWAPPED FORWARD  * 03400022
*    IF NECESSARY UNTIL IT REACHES ITS PROPER LOCATION IN THE LIST,   * 03500022
*    AND A BIT IS SET TO INDICATE THAT THE LIST WAS CHANGED.  WHEN THE* 03600022
*    END OF THE LIST IS REACHED, OCSWITCH IS TESTED TO SEE IF THE     * 03700022
*    LINE NEEDS TO BE RESTARTED.  IF SO, THE LINE IS RESTARTED.       * 03800022
*    OTHERWISE, OR AFTER THE LINE IS RESTARTED, THE MOVED BIT IS      * 03900022
*    CHECKED TO SEE IF THE INVITATION LIST HAS BEEN CHANGED.  IF OFF, * 04000022
*    THE TERMINAL WAS ALREADY ACTIVE, SO THAT A RESPONSE MESSAGE IS   * 04100022
*    SET UP TO INDICATE THE TERMINAL WAS ALREADY STARTED.             * 04200022
*    OTHERWISE, A RESPONSE MESSAGE IS SET UP TO INDICATE THAT THE     * 04300022
*    TERMINAL IS STARTED.                                             * 04400022
*    THE POST HAS BEEN CHANGED FROM THE DISABLED READY Q TO    @Y17XART 04430000
*    ASYNCRONOUS READY Q.                                      @Y17XART 04460000
*                                                                     * 04500022
* ENTRY POINTS --                                                     * 04600022
*    IGCV410D TO PROCESS VARY TERMINAL,ONTP OPERATOR CONTROL COMMANDS.* 04700022
*    PURPOSE - SEE FUNCTION                                    @Y17XART 04730000
*    LINKAGE - XCTL SF=(E,OPCXCTL)                             @Y17XART 04760000
*                                                                     * 04800022
* INPUT --                                                            * 04900022
*    REGISTER 1 - ADDRESS OF OPERATOR CONTROL AVT.  RELEVANT FIELDS   * 05000022
*    ARE OPCDOUBL.  THIS FIELD CONTAINS THE DDNAME OR THE UCB ADDRESS.* 05100022
*    ANOTHER FIELD IS OPCCOPCE WHICH IS THE ADDRESS OF THE CURRENT    * 05200022
*    OPERATOR CONTROL ELEMENT.  RELEVANT FIELDS ARE OCBACKUP.  THIS   * 05300022
*    FIELD CONTAINS EITHER A DDNAME OR A UCB ADDRESS.  ANOTHER        * 05400022
*    RELEVANT FIELD IS OCSWITCH.  THIS INDICATES WHETHER TO START     * 05500022
*    THE TERMINAL FOR ENTERING OR START THE TERMINAL FOR BOTH ENTERING* 05600022
*    AND RECEIVING.  IF THE X'40' BIT IS ON, THE LINE SHOULD BE       * 05700022
*    RESTARTED AFTER ACTIVATING THE TERMINAL.  FOR TOTE, OCOPTCDE IS  * 05800022
*    IS CHECKED TO SEE IF THE TERMINAL SHOULD BE BOTH ACTIVATED AND   * 05900022
*    RELEASED OR JUST ACTIVATED.                                      * 06000022
*                                                                     * 06100022
* OUTPUT --                                                           * 06200022
*    REGISTER 0 - NUMBER OF APPROPRIATE ERROR MESSAGE.                * 06300022
*    REGISTER 1 - RETURN CODE VALUES.                                 * 06400022
*                                                                     * 06500022
* EXTERNAL REFERENCES --                                              * 06600022
*         NONE.                                                       * 06700022
*                                                                     * 06800022
* EXITS,NORMAL --                                                     * 06900022
*         TO MODULE IGCV610D TO PROCESS GENERAL POLL REQUESTS.        * 06950022
*         TERMINAL WAS ACTIVATED AS REQUESTED.  RESPONSE MESSAGE      * 07000022
*         NUMBER SET UP, X'00' RETURN CODE SET.                       * 07100022
*         TERMINAL WAS ALREADY ACTIVATED.  RESPONSE MESSAGE NUMBER    * 07200022
*         SET UP, X'08' RETURN CODE SET.                              * 07300022
*                                                                     * 07400022
* EXITS,ERROR --                                                      * 07500022
*         TERMINAL COULD NOT BE FOUND OR IS A PROCESS ENTRY.  ERROR   * 07600022
*         MESSAGE NUMBER SET UP, X'04' RETURN CODE SET.               * 07700022
*         LINE IS NOT OPEN, OPEN DD DUMMY, OR DIAL LINE.  ERROR       * 07800022
*         MESSAGE NUMBER SET UP, X'04' RETURN CODE SET.               * 07900022
*                                                                     * 08000022
* TABLES/WORK AREAS --                                                * 08100022
*    OPERATOR CONTROL AVT, OPERATOR CONTROL ELEMENT, TERMNAME TABLE,  * 08200022
*    DCB, TERMINAL ENTRY, QCB, LCB, DEB.                              * 08300022
*                                                                     * 08400022
* ATTRIBUTES --                                                       * 08500022
*    SERIALLY REUSABLE, REFRESHABLE, ENABLED, TRANSIENT,              * 08600022
*    SUPERVISOR MODE.                                                 * 08700022
*                                                                     * 08800022
* CHARACTER CODE DEPENDENCY --                                        * 08900022
*    THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL            * 09000022
*    REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS            * 09100022
*    EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME.  THE CODING         * 09200022
*    HAS BEEN ARRANGED SO THAT REDEFINITION OF CHARACTER              * 09300022
*    CONSTANTS, BY REASSEMBLY WILL RESULT IN A CORRECT MODULE         * 09400022
*    FOR THE NEW DEFINITION.                                          * 09500022
*                                                                     * 09600022
* NOTES --                                                            * 09700022
*    COMMUNICATIONS BETWEEN THE VARIOUS TRANSIENT MODULES IS          * 09800022
*    PRIMARILY THROUGH THE OPERATOR CONTROL AVT.                      * 09900022
*    RESTRICTIONS - NONE                                       @Y17XART 09910000
*    REGISTER CONVENTIONS - STANDARD OP CTL                    @Y17XART 09920000
*    PATCH LABEL - ZAPAREA                                     @Y17XART 09930000
*                                                                       09940000
* MODULE TYPE - CSECT                                          @Y17XART 09950000
*    PROCESSOR - ASSEMBLER                                     @Y17XART 09960000
*    MODULE SIZE -                                             @Y17XART 09970000
*                                                                     * 10000022
*    THIS IS A MODULE RE-WRITE OF IEDQCO AS OF OS/TCAM LEVEL II       * 10050022
*********************************************************************** 10100022
         EJECT                                                          10200022
         SPACE                                                          10300022
*                                 E Q U A T E S                         10400022
         SPACE                                                          10500022
RZERO    EQU   0                        PARAMETER REGISTER              10600022
RPARM    EQU   1                        PARAMETER REGISTER              10700022
ROPCAVT  EQU   2                        POINTER TO OP CONTROL AVT       10800022
ROPCE    EQU   3                        POINTER TO CURRENT OPCE         10900022
RAVT     EQU   4                        TCAM AVT ADDRESS                11000022
RWORKA   EQU   5                        POINTER TO RESIDENT WORKAREA    11100022
RRLN     EQU   5                        RELATIVE LINE NUMBER            11200022
RLCB     EQU   5                        BASE FOR LCB                    11300022
RLCB1    EQU   6                        BASE FOR LCB                    11400022
RSCB     EQU   6                        SCB BASE                        11500022
RENTRY   EQU   6                        CURRENT ENTRY POINTER           11600022
RTERM    EQU   6                        TERM ENTRY BASE                 11700022
RTNT     EQU   6                        TERM NAME TABLE ADDR            11800022
RUCB     EQU   6                        UCB ADDRESS                     11900022
RDCB     EQU   7                        DCB BASE                        12000022
ROFF     EQU   7                        OFFSET TO DDNAME, ADDR, RLN     12100022
RWORK    EQU   8                        WORK REGISTER                   12200022
RWORK1   EQU   9                        WORK REGISTER                   12300022
RPREFIX  EQU   9                        BUFFER ADDRESS                  12400022
RLENGTH  EQU   10                       LENGTH OF ENTRY                 12500022
RDEB     EQU   10                       DEB BASE                        12600022
RLIST    EQU   11                       INVITATION LIST PTR             12700022
RQCB     EQU   11                       QCB ADDRESS                     12800022
RBASE    EQU   12                       BASE FOR MODULE                 12900022
RSAVE    EQU   13                       OP CTL SAVE AREA REG            12950022
RRET     EQU   14                       SUBROUTINE RETURN ADDRESS       13000022
CURR     EQU   14                       REGISTER 14            @Y17XARP 13020000
RBRNCH   EQU   15                       SUBROUTINE BRANCH ENTRY POINT   13100022
         SPACE 2                                                        13200022
EIGHT    EQU   8                        LENGTH OF EIGHT                 13300022
NAMELEN  EQU   2                        NO. OF NAME CHANGE CHARACTERS   13400022
PICKUP   EQU   5                        LOCATION OF DATA AFTER CONVERT  13500022
VARINAME EQU   3                        OFFSET TO CHANGE LOAD NAME      13600022
FOUR     EQU   4                        VALUE 4                  S22024 13700000
ONE      EQU   1                        VALUE 1                  S22024 13780000
THREE    EQU   3                        VALUE 3                  S22024 13860000
TWO      EQU   2                        VALUE 2                  S22024 13940000
ZERO     EQU   0                        ZERO                            14020022
X01      EQU   X'01'                    HEX ONE                         14040022
X80      EQU   X'80'                    HEX EIGHTY                      14060022
ERROR16  EQU   16                       ERROR -STATNAME NOT FOUND       14100022
ERROR90  EQU   90                       ERROR -NOT SINGLE ENTRY         14200022
ERROR46  EQU   46                       ERROR-OUTPUT ONLY STATION       14300022
LINOPN   EQU   X'C1'                    TO CHECK FOR OPEN DCB           14400022
STRTBOTH EQU   X'08'                    INDICATES ACTIVATE AND RELEASE  14500022
ERROR19  EQU   19                       TERMINAL ALREADY STARTED        14600022
ANYMOVED EQU   X'01'                    TO CHECK IF ENTRIES MOVED       14700022
EOT      EQU   X'80'                    TO CHECK EOT IN INVLIST         14800022
ENDACT   EQU   X'FE'                    END OF ACTIVE ENTRIES           14900022
PROC     EQU   X'20'                    PROCESS ENTRY                   15000022
DIST     EQU   X'40'                    DISTRIBUTION ENTRY              15100022
ERROR91  EQU   91                       ERROR-LINE NOT OPEN             15200022
ERROR88  EQU   88                       ERROR-TERMINAL IS DIAL          15300022
SEVEN    EQU   7                        VALUE 7                  S22024 15400000
GOODRST  EQU   20                       TERMINAL STARTED MESSAGE NUMBER 15500022
X40      EQU   X'40'                    INDICATE LINE NEEDS RESTARTED   15600022
STATERM  EQU   X'0A'                    ACTIVATE TERM SWITCH VALUE      15700022
CVTPTR   EQU   16                       POINTER TO CVT                  15800022
BUSY     EQU   4                        QUEUE BUSY CONDITION            15900022
ON       EQU   1                        DISPLACEMENT                    16000022
TR       EQU   3                        DISPLACEMENT                    16100022
STRTSNGL EQU   X'46'                    CODE FOR TOTE START TERM (E)    16200022
UCBNM    EQU   13                       OFFSET TO NAME IN UCB           16300022
LCBPRIOR EQU   X'E0'                    PRIORITY FOR LCB                16400022
ORDERBIT EQU   X'20'                    SET WHEN ARRANGING ENTRIES      16500022
NORDER   EQU   X'DF'                    TO TURN OFF FLAG                16600022
STARTB   EQU   X'0B'                    START TERM (B)                  16700022
GOODREL  EQU   52                       SUCCESSFUL RELEASE PERFORMED    16800022
CONCERR  EQU   156                      VALUE 156                S22024 16850000
         EJECT                                                          16900022
         USING IEDQAVTD,RAVT            TCAM AVT ADDRESSABILITY         17000022
         USING IEDQOPCD,ROPCAVT         OP CONTROL AVT ADDRESSABILITY   17100022
         USING IEDQOPCE,ROPCE           ELEMENT ADDRESSABILITY          17200022
         USING DEBNMSUB,RDEB            DEB ADDRESSABILITY              17300022
         USING IHADCB,RDCB              DCB ADDRESSABILITY              17400022
         USING IEDQQCB,RQCB             QCB ADDRESSABILITY              17500022
         USING IEDQTNTD,RTNT            TNT ADDRESSABILITY              17600022
         USING IEDQPRF,RPREFIX          BUFFER ADDRESSABILITY           17700022
         USING IEDQTRM,RTERM            TERM ENTRY ADDRESSABILITY       17800022
         USING IEDQSCB,RSCB             SCB ADDRESSABILITY              17900022
         SPACE                                                          18000022
         BALR  RBASE,RZERO              SET BASE REGISTER               18100022
         USING *,RBASE                  MODULE ADDRESSABILITY           18200022
BASE     EQU   *                                                        18300022
IGCV410D IEDHJN  SKIPID                                                 18600022
*              SET UP INITIAL REGISTERS                               * 18900022
         LR    ROPCAVT,RPARM            SET UP AVT ADDRESSING           19000022
         L     RAVT,OPCAVTPT            TCAM AVT ADDRESSING             19100022
         L     ROPCE,OPCCOPCE           GET CURRENT ELEMENT POINTER     19200022
         LM    RLCB1,RDCB,ZERO(RSAVE)   RESTORE REGISTERS        S22024 19250022
         MVC   OPCDOUBL(EIGHT),OCBACKUP SET UP TERMINAL NAME            19300022
         TM    OPCSPEC,OPCRSTRT         RESTART IN PROGRESS     SA61772 20300005
         BZ    INVLIST                  NO, BRANCH              SA61772 21300005
         TM    AVTBIT3,AVTSTAIN         S=I SPECIFIED           SA61772 22300005
         BZ    STATANAL                 NO, DON'T UPDATE I-LIST SA61772 23300005
*                                       FROM INCIDENT RECORD IF SA61772 24300005
*                                       S=I NOT SPECIFIED       SA61772 25300005
         SPACE                                                          27800022
         SPACE                                                          27900022
*              SET UP CONTROL FOR INVITATION LIST                     * 28000022
INVLIST  EQU   *                                                        28100022
         LA    RLIST,DCBINVLI-FOUR      START OF LIST                   28200022
         SR    RRLN,RRLN                CLEAR REGISTER                  28300022
         IC    RRLN,OCRLN               RELATIVE LINE NUMBER            28400022
         SLL   RRLN,TWO                 MULTIPLY RELATIVE LINE NUMBER   28500022
         L     RLIST,AVTEZERO(RRLN,RLIST)  LIST ADDRESS                 28600022
         LA    RLIST,AVTEZERO(RLIST)    CLEAR HIGH BYTE                 28700022
         LR    RLCB,RLCB1               SAVE ADDRESS OF LCB FOR LATER   28800022
         CLI   AVTEZERO(RLIST),AVTEZERO NO ENTRIES                      28900022
         BNE   OKTOSTRT                 ENTRIES EXIST                   29000022
         SPACE                                                          29030022
         LA    RZERO,ERROR46            OUTPUT ONLY ERROR MSG           29060022
         B     ERROR                    GO PRINT OUT ERROR MSG          29090022
         SPACE                                                          29120022
OKTOSTRT EQU   *                                                        29150022
         LA    RPARM,EIGHT(RLIST)       PTR TO BEGINNING OF LIST        29200022
         ST    RPARM,LCBINVPT-IEDQLCB-ONE(RLCB) SAVE FOR LINE ACTIVITY  29300022
*              START MANIPULATING INVITATION LIST TO START TERMINAL   * 29400022
STRTTERM EQU   *                                                        29500022
         SR    ROFF,ROFF                CLEAR                           29600022
         STH   ROFF,OCMODNME+TWO        SET UP FIRST ENTRY              29700022
         SR    RLENGTH,RLENGTH          INIT REG                        29800022
         SR    RWORK1,RWORK1            INIT REG                        29900022
         IC    RLENGTH,TWO(RLIST)       LENGTH OF ENTRY                 30000022
         LR    RWORK1,RLENGTH           LENGTH OF ENTRY                 30100022
         BCTR  RWORK1,AVTEZERO          DECREMENT FOR EXECUTE           30200022
*              CHECK STATUS OF INVITATION LIST                        * 30300022
NXENTRY1 EQU   *                                                        30400022
         LH    ROFF,OCMODNME+TWO        GET NEXT ENTRY                  30500022
         LA    ROFF,ONE(ROFF)           BUMP ONE                        30600022
         STH   ROFF,OCMODNME+TWO        STORE BACK                      30700022
         CLC   AVTEZERO(ONE,RLIST),ONE(RLIST)  ALL ENTRIES ACTIVE       30800022
         BE    CKMOVED                  YES                             30900022
         SPACE                                                          31000022
         CLC   OCMODNME+THREE(ONE),AVTEZERO(RLIST) OUT OF ENTRIES       31100022
         BH    CKMOVED                  YES, CHK ANY MOVED     @OY16378 31200000
         SPACE                                                          31300022
         SLL   ROFF,ONE                 MULTIPLY BY TWO                 31400022
         LR    RENTRY,RLIST             BEGINNING                       31500022
         SR    RENTRY,ROFF              BACK TO ENTRY                   31600022
         CLC   AVTEZERO(TWO,RENTRY),OCTRMTBL TO BE ACTIVATED            31700022
         BNE   NXENTRY1                 GET NEXT ENTRY                  31800022
         SPACE                                                          31900022
         LA    RENTRY,SEVEN(RLIST)      POINTER TO FIRST ENTRY INDEX    32000022
         SR    RRET,RRET                CLEAR                           32100022
         IC    RRET,ONE(RLIST)          NUMBER OF ACTIVE ENTRIES        32200022
         LTR   RRET,RRET                ANY ACTIVE ENTRIES              32300022
         BZ    CKINACT                  NO CHECK INACTIVE ENTRIES       32400022
         SPACE                                                          32500022
CKNEXT1  EQU   *                                                        32600022
         AR    RENTRY,RLENGTH           GO TO NEXT ENTRY'S INDEX        32700022
         CLC   AVTEZERO(ONE,RENTRY),OCMODNME+THREE IS THIS AN ACTIVE EN 32800022
         BE    NXENTRY1                 ACTIVE GET ANOTHER              32900022
         SPACE                                                          33000022
         BCT   RRET,CKNEXT1             NOT END OF AVTIVE               33100022
         SPACE                                                          33200022
CKINACT  EQU   *                                                        33300022
         LA    RENTRY,ONE(RENTRY)       SKIP ACTIVE FLAG                33400022
         TM    THREE(RLIST),EOT         EOT ENTRY PRESENT               33500022
         BZ    CKINACT1                 NO GO ON                        33600022
         SPACE                                                          33700022
         LA    RENTRY,ONE(RENTRY)       SKIP EOT ENTRY                  33800022
CKINACT1 EQU   *                                                        33900022
         AR    RENTRY,RLENGTH           GET TO NEXT INDEX               34000022
         CLC   AVTEZERO(ONE,RENTRY),OCMODNME+THREE IS THIS AN ENTRY     34100022
         BNE   CKINACT1                 NO GET ANOTHER                  34200022
         TM    OCSWITCH,X80             IS THIS GENERAL POLL            34210000
         BZ    SPECPOLL                 NO, BRANCH                      34220000
GPEXIT   EQU   *                        GENERAL POLL EXIT               34230000
         NI    OCSWITCH,OCFOXES-X80     RESET SWITCH                    34240000
         MVC   OPCLDNME+VARINAME(NAMELEN),STRTGP SET UP G.P. MOD        34250000
         B     EXIT                     XCTL TO GENERAL POLL MOD        34260000
SPECPOLL EQU   *                                                        34270000
         SPACE                                                          34300022
         OI    OPCFLAG1,ANYMOVED        SET 'MOVED' INDICATOR           34400022
         SR    RENTRY,RLENGTH           POINT TO PREVIOUS ENTRY         34500022
         LR    RWORK,RENTRY                                             34600022
         SR    RWORK,RLENGTH            POINT TO PREVIOUS ENTRY         34700022
ACTIVATE EQU   *                                                        34800022
         CLI   AVTEZERO(RENTRY),ENDACT  FIRST INACTIVE ENTRY            34900022
         BE    MACTIVE                  YES MAKE IT ACTIVE              35000022
         SPACE                                                          35100022
SWAPLOOP EQU   *                                                        35200022
         EX    RWORK1,SWAP1             SWAP ONE POSITION               35300022
         EX    RWORK1,SWAP2             WITH THE NEXT                   35400022
         EX    RWORK1,SWAP3             SUCCESSIVE POSITION             35500022
         SPACE                                                          35600022
         TM    OPCFLAG1,ORDERBIT        TRYING TO ARRANGE               35700022
         BO    CKPOS                    YES                             35800022
         SPACE                                                          35900022
         SR    RENTRY,RLENGTH           BACK UP ONE ENTRY               36000022
         SR    RWORK,RLENGTH            BACK PREVIOUS ENTRY,ONE ENTRY   36100022
         SPACE                                                          36200022
         B     ACTIVATE                 GO CHECK FOR INACTIVE ENTRY     36300022
         SPACE                                                          36400022
MACTIVE  EQU   *                                                        36500022
         TM    THREE(RLIST),EOT         EOT ENTRY PRESENT               36600022
         BO    MACTIVE1                 YES                             36700022
         SPACE                                                          36800022
         EX    RWORK1,MOVACT            PLACE ENTRY ON ACTIVE SIDE      36900022
         LA    RRET,AVTEZERO(RLENGTH,RENTRY) POINT TO NEW END OF ACTIVE 37000022
         BCTR  RENTRY,AVTEZERO          GET ONE LESS                    37100022
         SPACE                                                          37200022
         BCTR  RWORK,AVTEZERO           GET ONE LESS                    37300022
         SPACE                                                          37400022
         MVI   AVTEZERO(RRET),ENDACT    RESET ACTIVE POINTER            37500022
MACTIVE2 EQU   *                                                        37600022
         SR    RRET,RRET                CLEAR REG                       37700022
         IC    RRET,ONE(RLIST)          GET NUMBER OF AVTIVE ENTRIES    37800022
         LA    RRET,ONE(RRET)           INCREMENT FOR BCT               37900022
ACTLOOP  EQU   *                                                        38000022
         BCT   RRET,ACTPOS              IF NOT LAST POSITION COMPARE    38100022
         SPACE                                                          38200022
RETURNA  EQU   *                                                        38300022
         IC    RRET,ONE(RLIST)          GET                             38400022
         LA    RRET,ONE(RRET)           AND INCREMENT                   38500022
         STC   RRET,ONE(RLIST)          NUMBER OF ACTIVE ENTRIES        38600022
         NI    OPCFLAG1,NORDER          TURN OFF BIT                    38700022
         B     NXENTRY1                 RETURN FOR ANOTHER              38800022
         SPACE                                                          38900022
ACTPOS   EQU   *                                                        39000022
         CLC   AVTEZERO(ONE,RENTRY),OCMODNME+THREE ENTRY IN RIGHT POSIT 39100022
         BL    RETURNA                  PREVIOUS ENTRY LESS,FINISHED    39200022
         SPACE                                                          39300022
         OI    OPCFLAG1,ORDERBIT        TRYING TO ARRANGE               39400022
         B     SWAPLOOP                 GO SWAP ENTRIES                 39500022
         SPACE                                                          39600022
CKPOS    EQU   *                                                        39700022
         SR    RENTRY,RLENGTH           BACK UP ONE                     39800022
         SR    RWORK,RLENGTH            ENTRY                           39900022
         B     ACTLOOP                  GO CHECK FOR CORRECT ORDER      40000022
         SPACE                                                          40100022
*              DETERMINE IF INVITATION LIST CHANGED                   * 40200022
CKMOVED  EQU   *                                                        40300022
         TM    OPCFLAG1,ANYMOVED        ANY ENTRIES MOVED               40400022
         BNO   ALREADY                  NO                              40500022
         LA    RZERO,GOODRST            TERMINAL STARTED                40550022
         SPACE                                                          40600022
         SPACE                                                          40700022
         B     STATANAL                 SEE IF LINE NEEDS RESTARTED     40800022
ALREADY  EQU   *                                                        41000022
         TM    OCFLAG,OCMPP+OCTOTE      TOTE OR MPP                     41100022
         LA    RZERO,ERROR19            ALREADY STARTED MESSAGE         41200022
         BZ    ERROR                    NO                              41260022
         LA    RPARM,EIGHT              SET ALREADY DONE CODE           41320022
         SPACE                                                          41400022
         B     STATANL1                 CHECK IF LINE NEEDS RESTARTED   41500022
         SPACE                                                          41600022
GORELEAS EQU   *                                                        41700022
         MVC   OPCLDNME+VARINAME(NAMELEN),RELEASE  TO GO TO RELEASE     41800022
         MVC   OCWTG(TWO),CHECKPT       TO INDICATE RELEASE TO CHECKPNT 41900022
         TM    OCFLAG,OCTOTE+OCMPP      TOTE OR MPP                     41930022
         BO    TOTEMPP                  YES                             41960022
         SPACE                                                          41990022
         STH   RZERO,OCPARM             SET UP PARAMETER                42020022
         B     TERMINAL                 GO TO RELEASE                   42050022
         SPACE                                                          42080022
TOTEMPP  EQU   *                                                        42110022
         STH   RPARM,OCPARM             SET RETURN CODE                 42140022
TERMINAL EQU   *                                                        42170022
         OI    OCSWITCH,X40             TO INDICATE TERMINAL            42200022
         B     EXIT                     GO XCTL TO PROPER MODULE        42300022
         SPACE                                                          42400022
ERROR    EQU   *                                                        42500022
         LA    RPARM,FOUR               SET BAD RETURN CODE             42600022
         MVC   OPCLDNME+VARINAME(NAMELEN),XCTL03 TO GO TO ERROR MODULE  42700022
         TM    OCSWITCH,X80             IS THIS FOR GEN POLL   @SA74217 42730058
         BO    NORESTRT                 YES, CK LNE RESTART    @OY16378 42760000
         B     STATANL1                 GO PROCESS ERROR CONDITION      42800022
CHPTMSG  EQU   *                                                        42900022
         NI    OCFLAG,OCFOXES-OCLINE    TURN OFF BIT                    43000022
         OI    OCSWITCH,X40             INDICATES A TERMINAL TO MSG MOD 43100022
         MVC   OPCLDNME+VARINAME(NAMELEN),CHECKPT  SET CHECKPOINT NAME  43200022
         MVC   OCWTG(NAMELEN),WRTMSG    WTG TO MESSAGE MODULE           43300022
         STH   RZERO,OCPARM             SET OCPARM FOR CHECKPOINT       43400022
TESTERR  EQU   *                                                        43600022
         TM    OCFLAG,OCTOTE+OCMPP      TOTE OR MPP?                    43700022
         BZ    EXIT                     NO                              43800022
         SPACE                                                          43900022
         MVC   OCWTG(NAMELEN),WRITE     SET TO GO TO WRITER             44000022
         STH   RPARM,OCPARM             GET PARAMETER FIELD             44100022
         LR    RZERO,RPARM              SET RETURN CODE IN REG 0        44300022
         SPACE                                                          44500022
         MVC   OPCLDNME+VARINAME(NAMELEN),WRITE  GO TO WRITER           44600022
EXIT     EQU   *                                                        44700022
         NI    OPCFLAG1,AVTEZERO        RESET FLAGS                     44800022
         LR    RPARM,ROPCAVT            OPERATOR CONTROL AVT            44900022
         XCTL  SF=(E,OPCXCTL)                                           45000022
STATANAL EQU   *                                                        45100022
         SR    RPARM,RPARM              SET GOOD RETURN                 45200022
STATANL1 EQU   *                                                        45300022
         TM    OPCSPEC,OPCRSTRT         RESTART SITUATION               45400022
         BZ    NORESTRT                 NO                              45500022
         SPACE                                                          45600022
         TM    OCSWITCH,STARTB          START TERM AND RELEASE TERM     45700022
         BO    GORELEAS                 YES                             45800022
         SPACE                                                          45900022
         SPACE                                                          46200022
         B     CHPTMSG                  GO TO CHECKPOINT                46300022
         SPACE                                                          46400022
NORESTRT EQU   *                                                        46500022
         TM    OCSWITCH,X40             SHOULD LINE BE RESTARTED        46600022
         MVC   OPCDOUBL(EIGHT),OCBACKUP SET UP GOOD NAME                46660022
         BZ    CKRELEAS                 NO                              46720022
         SPACE                                                          46800022
         SPACE                                                          47900022
NOTMPS   EQU   *                                                        48000022
         MVI   LCBPRI-IEDQLCB(RLCB),LCBPRIOR SET E0 PRIORITY IN BUFFER  48100022
         USING IEDQLCB,RLCB                                             48150005
         NI    LCBERBST,AVTEFF-LCBPRCPG  TURN OFF FLAG          SA67777 48200005
         DROP  RLCB                                                     48250005
         OI    LCBSTAT1-IEDQLCB(RLCB),LCBRECVN   SET STATUS FOR START   48300022
         ST    RLCB,AVTEZERO(RLCB)      MAKE LCB POINT TO ITSELF        48400022
         L     RWORK,CVTPTR             GET CVT ADDR           @Y17XARP 48407000
         L     RWORK,AVTCVTPT(RWORK)    GET ADDR OF TCX        @Y17XARP 48413000
         USING IEDQTCX,RWORK            ESTABLISH ADDR         @Y17XARP 48419000
         L     CURR,TCXREADY            GET ADDR OF CURRENT    @Y17XARP 48425000
*                                       ENTRY ON ASYN READY Q  @Y17XARP 48431000
NEWCURR  STCM  CURR,SEVEN,LCBLINK-IEDQLCB(RLCB)                @Y17XARP 48437000
*                                       LINK NEW ELEM TO CURR  @Y17XARP 48443000
         CS    CURR,RLCB,TCXREADY       WAS CURR ELEM UPDATED  @Y17XARP 48449000
*                                       ASYNCHRONOUSLY         @Y17XARP 48455000
         BNE   NEWCURR                  YES,LINK NEW ELEM TO   @Y17XARP 48461000
*                                       CURR ELEM. NO, ASYN    @Y17XARP 48467000
*                                       READY QUEUE NOW HAS    @Y17XARP 48473000
*                                       NEW ELEM               @Y17XARP 48479000
CKRELEAS EQU   *                                                        49650022
         CLI   OCOPTCDE,STRTSNGL        ACTIVATE TERMINAL ONLY FOR TOTE 49700022
         BE    TESTERR                  GO CHECKPOINT                   49800022
         SPACE                                                          49900022
         TM    OCFLAG,OCTOTE+OCMPP      IS THIS TOTE OR MPP?            50000022
         BNZ   GORELEAS                 YES                             50100022
         TM    OCSWITCH,X80             IS THIS FOR GEN POLL   @OY16378 50130000
         BO    TESTERR                  YES, ERROR SITUATION   @OY16378 50160000
         SPACE                                                          50200022
         TM    OCSWITCH,STARTB          RELEASE TERMINAL ALSO           50300022
         BO    GORELEAS                 YES                             50400022
         SPACE                                                          50500022
         LTR   RPARM,RPARM              IS THIS AN ERROR SITUATION      50600022
         BZ    CHPTMSG                  NO                              50700022
         SPACE                                                          50800022
         B     TESTERR                  YES, ERROR SITUATION            50900022
         SPACE                                                          51000022
MACTIVE1 EQU   *                                                        51100022
         LA    RRET,TWO                 TO DECREMENT BY TWO             51200022
         SR    RENTRY,RRET              BACK UP TWO FOR EOT-FE          51300022
         SR    RWORK,RRET                                               51400022
         MVC   OCMODNME+FOUR(TWO),ONE(RENTRY)  SAVE EOT-FE              51500022
         EX    RWORK1,MOVEACT1          MOVE TO ACTIVE                  51600022
         LA    RRET,ONE(RLENGTH,RENTRY) ADDRESSING FOR EOT-FE           51700022
         MVC   AVTEZERO(TWO,RRET),OCMODNME+FOUR  RESET EOT-FE           51800022
         B     MACTIVE2                 GO TO MACTIVE2                  51900022
         SPACE                                                          52000022
SWAP1    XC    ONE(AVTEZERO,RWORK),ONE(RENTRY)   SWAP                   52100022
SWAP2    XC    ONE(AVTEZERO,RENTRY),ONE(RWORK) ONE ENTRY                52200022
SWAP3    XC    ONE(AVTEZERO,RWORK),ONE(RENTRY)   WITH ANOTHER           52300022
MOVACT   MVC   AVTEZERO(AVTEZERO,RENTRY),ONE(RENTRY) MOVE        S22024 52400000
MOVEACT1 MVC   ONE(AVTEZERO,RENTRY),THREE(RENTRY)   MOVE         S22024 52500000
ERRORTAB DS    0F                                                       52600022
         SPACE 2                                                        52700022
XCTL03   DC    C'03'                    ERROR MODULE NAME               52800022
CHECKPT  DC    C'06'                    CHECKPOINT MODULE NAME          52900022
WRITE    DC    C'07'                    OUTPUT WRITER NAME              53000022
WRTMSG   DC    C'09'                    HOLD, RELEASE, AND VARY MESSAGE 53100022
STOPLN   DC    C'V1'                    STOPLINE MODULE                 53200022
RELEASE  DC    C'R0'                    RELEASE MODULE                  53400022
STRTERM  DC    C'V4'                    START TERMINAL MODULE           53500022
STRTGP   DC    C'V6'                    START GENERAL POLL MODULE       53550022
ZAPAREA  DC    50X'FF'                  PATCH AREA             @Y17XART 53570000
         TITLE 'IGCV410D - DSECTS REFERENCED BY THIS MODULE'            53600022
         TAVTD                                                          53700022
         EJECT                                                          53730000
         TTCXD SVS                                                      53760000
         EJECT                                                          53800022
         DCBD DSORG=TX                                                  53900022
         EJECT                                                          54000022
         TDEBD                                                          54100022
         EJECT                                                          54200022
         TDISPD                                                         54300022
         EJECT                                                          54400022
*        DELETE IEAQFX CALL-NOT REQUIRED FOR VS                       * 54500058
         EJECT                                                          54600022
         TLCBD                                                          54700022
         EJECT                                                          54800022
         TOPCED                                                         54900022
         EJECT                                                          55000022
         TOPCAVTD                                                       55100022
         EJECT                                                          55200022
         TPRFD                                                          55300022
         EJECT                                                          55400022
         TPRIOR                                                         55500022
         EJECT                                                          55600022
         TQCBD                                                          55700022
         EJECT                                                          55800022
         TSCBD                                                          55900022
         EJECT                                                          56000022
         TTNTD                                                          56100022
         EJECT                                                          56200022
         TTRMD                                                          56300022
         END                                                            56400022
