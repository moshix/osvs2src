*%GOTO MNIPBSL ;                      /*                                00050001
         MACRO                                                          00100001
&NAME    IEAPMNIP  &TYPE=,&MSGST=,&MSGEND=,&RMSGST=,&RMSGL=,&MOD=,     X00150002
               &ASYNCH=,&PARAM=,&BUFREL=,&OPT=,&CPUID=,&ADDR=           00200002
         LCLC  &NB,&LB,&LC,&SX,&LM,&LI,&AU,&RS,&US                      00250002
.********************************************************************** 00400001
.*           06/27/74   VS2-3 VERSION                                   00450003
.*       INITIALIZE ALL FLAG-SETTING SWITCHES TO ZERO                   00500001
.*                                                                      00550001
.********************************************************************** 00600001
&NB      SETC  '0'                    OPEN - NON-NUCLEUS DEB            00650001
&LB      SETC  '0'                    OPEN - END OF NUCLEUS DEB         00700001
.*                                    MOUNT - UCB ADDRESS GIVEN         00750001
&LC      SETC  '0'                    OPEN,MOUNT - CONDITIONAL REQUEST  00800001
&SX      SETC  '0'                    OPEN - SINGLE EXTENT              00850001
&LM      SETC  '0'                    OPEN - SUPPRESS OPEN MSGS         00900001
&LI      SETC  '0'                    OPEN,MOUNT - DS NAME PTR GIVEN    01000001
&AU      SETC  '0'                    OPEN - AUTHORINZED LIBRARY        01050002
&RS      SETC  '0'                    OPEN - PERMANENTLY RESIDENT DS    01100002
&US      SETC  '0'                    MOUNT- UCB SCAN ONLY              01150002
         AIF   ('&TYPE' EQ 'PLIST').LISTMAC                             01200002
         AIF   ('&TYPE' EQ 'MOUNTPL').LISTMAC                           01250002
         AIF   ('&TYPE' EQ 'OPENPL').LISTMAC                            01300002
         AIF   ('&TYPE' EQ 'SCHEDL').LISTMAC                            01400002
&NAME    DS    0H                                                       01450002
         AGO   .EXMACRO                                                 01500002
.LISTMAC ANOP                                                           01550002
&NAME    DS    0F                                                       01600002
.EXMACRO ANOP                                                           01650002
         AIF   ('&TYPE' NE 'MOUNTPL').NOMPL                             05800002
.**************                                                         05850001
.*                                                                      05900001
.*        NIPMOUNT  PARAMETER  LIST                                     05950001
.*                                                                      06000001
.**************                                                         06050001
         AIF   ('&OPT' NE 'PTR').MNOPTR                                 06100001
         AIF   (K'&PARAM EQ 0).MNOPRM                                   06150001
         DC    A(&PARAM)              DATA SET NAME ADDRESS             06200001
         AGO   .MZER                                                    06250001
.MNOPRM  ANOP                                                           06300001
         DC    A(0)                   DATA SET NAME ADDRESS             06350001
.MZER    ANOP                                                           06400001
         DC    XL8'0000000000000000'  RESERVED                          06450001
         AGO   .MPTR                                                    06500001
.MNOPTR  ANOP                                                           06550001
         DC    CL12'&PARAM '          DATASET NAME                      06600001
.MPTR    ANOP                                                           06650001
         DC    CL6' '                 VOLUME SERIAL OR UCB ADDRESS      06700001
         DC    XL2'0000'              DEVICE TYPE                       06750001
         DC    X'00'                  NIPMOUNT FLAGS                    06800001
         DC    XL3'000000'            UNASSIGNED                        06850001
         AGO   .MACEND                                                  06900001
.NOMPL   AIF   ('&TYPE' NE 'OPENPL').NOOPL                              06950001
.**************                                                         07000001
.*                                                                      07050001
.*        NIPOPEN  PARAMETER  LIST                                      07100001
.*                                                                      07150001
.**************                                                         07200001
         AIF   ('&OPT' NE 'PTR').ONOPTR                                 07250001
         AIF   (K'&PARAM EQ 0).ONOPRM                                   07300001
         DC    A(&PARAM)              DATA SET NAME ADDRESS             07350001
         AGO   .OZER                                                    07400001
.ONOPRM  ANOP                                                           07450001
         DC    A(0)                   DATA SET NAME ADDRESS             07500001
.OZER    ANOP                                                           07550001
         DC    XL8'0000000000000000'  RESERVED                          07600001
         AGO   .OPTR                                                    07650001
.ONOPTR  ANOP                                                           07700001
         DC    CL12'&PARAM '          DATASET NAME                      07750001
.OPTR    ANOP                                                           07800001
         DC    A(0)                   DCB ADDRESS                       07850001
         DC    A(0)                   UCB ADDRESS                       07900001
         DC    X'00'                  NIPOPEN FLAGS                     07950001
         DC    X'00'                  NIPOPEN RETURN CODE               08000001
         DC    XL2'0000'              UNASSIGNED                        08050001
         AGO   .MACEND                                                  08100001
.NOOPL   AIF   ('&TYPE' NE 'MOUNT').NOMNT                               08150001
.**************                                                         08200001
.*                                                                      08250001
.*        LINKAGE TO  NIPMOUNT                                          08300001
.*                                                                      08350001
.**************                                                         08400001
         AIF   ('&OPT(1)' NE 'LB').MNOLB                                08650001
&LB      SETC  '1'                                                      08700001
.MNOLB   AIF   ('&OPT(2)' NE 'LC').MNOLC                                08750001
&LC      SETC  '1'                                                      08800001
.MNOLC   AIF   ('&OPT(3)' NE 'LI').MNOLI                                08850001
&LI      SETC  '1'                                                      08900001
.MNOLI   AIF   ('&OPT(4)' NE 'US').MNOUS                                08910002
&US      SETC  '1'                                                      08920002
.MNOUS   ANOP                                                           08950002
         LA    RPARM,&PARAM           LOAD PARM LIST ADDRESS            09050001
         MVI   NMNTFL(RPARM),B'0000&US&LI&LC&LB'  SET FLAG BITS         09100002
         AGO   .ELOAD                                                   09160002
.NOMNT   AIF   ('&TYPE' NE 'OPEN').NOOPN                                09550001
.**************                                                         09600001
.*                                                                      09650001
.*        LINKAGE TO  NIPOPEN                                           09700001
.*                                                                      09750001
.**************                                                         09800001
         AIF   ('&OPT(1)' NE 'NB').NONB                                 10050001
&NB      SETC  '1'                                                      10100001
.NONB    AIF   ('&OPT(2)' NE 'LB').NOLB                                 10150001
&LB      SETC  '1'                                                      10200001
.NOLB    AIF   ('&OPT(3)' NE 'LC').NOLC                                 10250001
&LC      SETC  '1'                                                      10300001
.NOLC    AIF   ('&OPT(4)' NE 'SX').NOSX                                 10350001
&SX      SETC  '1'                                                      10400001
.NOSX    AIF   ('&OPT(5)' NE 'LM').NOLM                                 10450001
&LM      SETC  '1'                                                      10500001
.NOLM    AIF   ('&OPT(6)' NE 'LI').NOLI                                 10550001
&LI      SETC  '1'                                                      10600001
.NOLI    AIF   ('&OPT(7)' NE 'AU').NOAU                                 10610002
&AU      SETC  '1'                                                      10620002
.NOAU    AIF   ('&OPT(8)' NE 'RS').NORS                                 10630002
&RS      SETC  '1'                                                      10640002
.NORS    ANOP                                                           10650002
         LA    RPARM,&PARAM           LOAD PARM LIST ADDRESS            10750001
         MVI   NOPNFL(RPARM),B'&RS&AU&LM&LI&NB&LB&LC&SX' SET FLAG BITS  10800002
         AGO   .ELOAD                                                   10810002
.NOOPN   AIF   ('&TYPE' NE 'HEADER').NOHEAD                             11250001
.**************                                                         11300001
.*                                                                      11350001
.*        NIPWTO  MESSAGE  HEADER                                       11400001
.*                                                                      11450001
.**************                                                         11500001
         DC    Y(&MSGEND-&MSGST)      MESSAGE LENGTH                    11550001
         AIF   ('&OPT(1)' NE 'NHC').NONHC                               11600001
         AIF   ('&OPT(2)' NE 'NBC').NONBC1                              11610001
         DC    X'C0'                  FLAG BYTE 1                       11620001
         AGO   .HEADB2                                                  11630001
.NONBC1  DC    X'80'                  FLAG BYTE 1                       11640001
         AGO   .HEADB2                                                  11642001
.NONHC   AIF ('&OPT(2)' NE 'NBC').NONBC2                                11644001
         DC    X'40'                 FLAG BYTE 1                        11646001
         AGO   .HEADB2                                                  11648001
.NONBC2  DC    X'00'                 FLAG BYTE 1                        11648401
.HEADB2  DC    X'00'                 FLAG BYTE 2                        11648801
         AGO   .MACEND                                                  11650001
.NOHEAD  AIF   ('&TYPE' NE 'PLIST').NOLIST                              11700001
.**************                                                         11750001
.*                                                                      11800001
.*        NIPWTOR  PARAMETER  LIST                                      11850001
.*                                                                      11900001
.**************                                                         11950001
         AIF   ('&RMSGST' EQ 'SQARBUF').ZERRPA                          12000001
         DC    A(&RMSGST)             REPLY AREA ADDRESS                12050001
         DC    A(0)                   REPLY ECB ADDRESS                 12100001
         DC    H'&RMSGL'              REPLY AREA LENGTH                 12150001
         AIF   ('&ASYNCH' EQ 'YES').ASYN1                               12200001
         DC    XL1'00'                NIP WTOR FLAGS                    12250001
         AGO   .PARM1                                                   12300001
.ASYN1   DC    XL1'80'                NIP WTOR FLAGS                    12350001
         AGO   .PARM1                                                   12400001
.ZERRPA  DC    A(0)                   REPLY AREA ADDRESS                12450001
         DC    A(0)                   REPLY ECB ADDRESS                 12500001
         DC    H'0'                   REPLY AREA LENGTH                 12550001
         AIF   ('&ASYNCH' EQ 'YES').ASYN2                               12600001
         DC    XL1'08'                NIP WTOR FLAGS                    12650001
         AGO   .PARM1                                                   12700001
.ASYN2   DC    XL1'88'                NIP WTOR FLAGS                    12750001
.PARM1   DC    XL1'00'                RESERVED                          12800001
         DC    A(&MSGST)              WTOR MESSAGE ADDRESS              12850001
         AGO   .MACEND                                                  12852002
.********************                                                   12860002
.*                                                                      12870002
.*            NIPSCHED PARAMETER LIST                                   12880002
.*                                                                      12890002
.**********************                                                 12892002
.NOLIST  AIF   ('&TYPE' NE 'SCHEDL').NOSCHDL                            12894002
         AIF   (K'&ADDR EQ 0).SCHDL1                                    12896002
         DC    A(&ADDR)                                                 12898002
         AGO   .SCHDL2                                                  12898402
.SCHDL1  DC    A(0)                                                     12898802
.SCHDL2  AIF   (K'&PARAM EQ 0).SCHDL3                                   12898902
         DC     A(&PARAM)                                               12899002
         AGO    .SCHDL4                                                 12899102
.SCHDL3  DC     A(0)                                                    12899302
.SCHDL4  DC     XL2'0000'                                               12899702
         AGO   .MACEND                                                  12900002
.********************                                                   12949602
.*                                                                      12949702
.*       LINKAGE FOR THE NIPSCHED  ROUTINE                              12949802
.*                                                                      12949902
.*********************                                                  12966602
.NOSCHDL AIF   ('&TYPE' NE 'SCHED').NOSCHED                             12976602
         LA    RPARM,&PARAM                                             12978602
         MVC   8(2,RPARM),0(&CPUID)                                     12980602
         AIF   (K'&ADDR EQ 0).ELOAD                                     12982602
         ST    &ADDR,0(0,RPARM)                                @YM1995P 12983002
         AGO   .ELOAD                                                   12983102
.NOSCHED AIF   ('&TYPE' NE 'PRMPT').NOPRMPT                             12983202
.*******************************************                            12988802
.*                                                                      12990802
.*      LINKAGE FOR THE NIPPRMPT ROUTINE                                12992802
.*                                                                      12993202
.******************************************                             12993602
         LA     RPARM,&PARAM       ADDRESS OF PARAMETER FOR PROMPT      13018202
         AIF   ('&OPT' NE 'ERROR').PRMPT1                               13018602
         LCR   RPARM,RPARM         COMPLEMENT PARAM REGISTER (ERROR)    13019002
.PRMPT1  AGO   .ELOAD                                                   13019402
.**************                                                         13032602
.*                                                                      13036102
.*        LINKAGE  FOR:  NIPSENSE, NIPSWAIT,NIPTIME,NIPUCBFN,           13050001
.*                       NIPWTO, NIPWTOR, NIPWTOR2                      13100001
.*                       NIPOPIO,NIPVIRT,NIPREAL,NIPPRMPT               13110002
.*                                                                      13150001
.**************                                                         13200001
.NOPRMPT AIF   ('&TYPE' EQ 'SENSE').PLOAD                               13250002
         AIF   ('&TYPE' EQ 'SWAIT').ELOAD                               13300001
         AIF   ('&TYPE' EQ 'OPIO').ELOAD                                13310002
         AIF   ('&TYPE' EQ 'VIRT').ELOAD                                13320002
         AIF   ('&TYPE' EQ 'REAL').ELOAD                                13330002
         AIF   ('&TYPE' NE 'TIME').NTIME                                13350001
         AIF   ('&PARAM' NE 'DEC').BTIME                                13400001
         SR    RPARM,RPARM            REQUEST DECIMAL TIME              13450001
         AGO   .ELOAD                                                   13500001
.BTIME   LA    RPARM,4(0,0)           REQUEST BINARY TIME      @YM1995P 13550002
         AGO   .ELOAD                                                   13600001
.NTIME   AIF   ('&TYPE' EQ 'UCBFN').PLOAD                               13650001
         AIF   ('&TYPE' EQ 'WTOR2').WTOR2                               13700001
         LA    RPARM,&PARAM           LOAD PARM REGISTER                13750001
         AGO   .ELOAD                                                   13800001
.WTOR2   AIF   ('&BUFREL' NE 'YES').PLOAD                               13850001
         LCR   RPARM,&PARAM           INDICATE SQA RELEASE              13900001
         AGO   .ELOAD                                                   13950001
.PLOAD   AIF   ('&PARAM' EQ 'RPARM').ELOAD                              14000001
         LR    RPARM,&PARAM           LOAD PARM REGISTER                14050001
.ELOAD   AIF   ('&MOD' EQ 'NIPM').ELOAD1                                14050402
         L     RENTRY,NVT&TYPE.(0,RNVT)   LOAD BRANCH ADDRESS  @YM1995P 14050802
         BALR  REXIT,RENTRY               CALL THE ROUTINE              14051202
         AGO   .MACEND                                                  14051602
.ELOAD1  ANOP                                                           14052002
         L     RENTRY,NVT&TYPE        LOAD BRANCH ADDRESS               14100002
         BALR  REXIT,RENTRY           BRANCH TO ROUTINE                 14150001
.MACEND  ANOP                                                           14200001
         MEND  ,                      */                                14250001
*%MNIPBSL: ;                                                            14300001
*        /***             NIPWTO  MESSAGE  HEADER                  ***/ 14350001
*        /* THE FOLLOWING STRUCTURE MAPS THE NIPWTO MESSAGE HEADER   */ 14360002
*        /* THAT IS GENERATED BY THE BAL MACRO -IEAPMNIP TYPE=HEADER-*/ 14370002
*        /* AND IS USED TO PASS INFORMATION TO THE NIPWTO SERVICE    */ 14380002
*        /* ROUTINE. THE MESSAGE LENGTH FIELD, NWTOLNG, INCLUDES THE */ 14390002
*        /* LENGTH OF THE MESSAGE HEADER ITSELF.                     */ 14392002
*        DECLARE 1 NWTOHDR BASED ,        /* MESSAGE HEADER START    */ 14400001
*                 2 NWTOLNG  FIXED(15),   /* MESSAGE LENGTH          */ 14450001
*                 2 NWTOFL   CHAR(2),     /* NIP MESSAGE FLAGS       */ 14500001
*                  3 NWTOFLNH  BIT(1),    /* NOT TO BE HARDCOPIED    */ 14550001
*                  3 NWTOFLNC  BIT(1),    /* SUPRESS BLANK COMPRESS  */ 14560001
*                  3 *         BIT(14);   /* RESERVED                */ 14600001
*        /***        END  OF  NIPWTO  MESSAGE  HEADER              ***/ 14650001
*                                                                       14700001
*        /***             NIPWTOR  PARAMETER  LIST                 ***/ 14750001
*        /* THE FOLLOWING STRUCTURE MAPS THE NIPWTOR PARAMETER LIST  */ 14760002
*        /* THAT IS GENERATED BY THE BAL MACRO -IEAPMNIP TYPE=PLIST- */ 14770002
*        /* AND IS USED TO PASS INFORMATION TO THE NIPWTOR SERVICE   */ 14780002
*        /* ROUTINE.                                                 */ 14790002
*        DECLARE 1 NWTORLST BASED,        /* PARM LIST START         */ 14800001
*                 2 NWTORRPA PTR(31),     /* REPLY AREA ADDRESS      */ 14850001
*                 2 NWTORECB PTR(31),     /* REPLY ECB ADDRESS       */ 14900001
*                 2 NMTORRDL FIXED(15),   /* REPLY AREA LENGTH       */ 14950001
*                 2 NWTORFL  CHAR(1),     /* NIP WTOR FLAGS          */ 15000001
*                  3 NWTORFLA BIT(1),     /* ASYNCHRONOUS REQUEST    */ 15050001
*                  3 *        BIT(3),     /* RESERVED                */ 15100001
*                  3 NWTORFLB BIT(1),     /* PROVIDE SQA BUFFER      */ 15150001
*                  3 *        BIT(3),     /* RESERVED                */ 15200001
*                 2   *      CHAR(1),     /* RESERVED                */ 15250001
*                 2 NWTORMSG PTR(31);     /* WTOR MESSAGE HEADER        15300002
*                                            ADDRESS.                */ 15310002
*        /***        END  OF  NIPWTOR  PARAMETER  LIST             ***/ 15350001
*                                                                       15400001
*        /***        NIP SYSTEM PARAMETER QUEUE ENTRY              ***/ 15450001
*        /* THE FOLLOWING STRUCTURE MAPS THE NIP SYSTEM PARAMETER    */ 15460002
*        /* ENTRY WHICH IS AN INTERNAL CONTROL BLOCK USED BY NIP TO  */ 15470002
*        /* TRACK SQA BUFFERS USED TO CONTAIN WTOR REPLIES.  THIS    */ 15480002
*        /* IS DONE SO THAT IEAVNIPX CAN FREE THESE AREAS AT THE END */ 15490002
*        /* OF NIP PROCESSING.  ONE SPE IS CONTAINED IN THE NVTSPE   */ 15494002
*        /* FIELD OF THE NVT. IF OTHERS ARE REQUIRED, THEY ARE       */ 15496002
*        /* OBTAINED DYNAMICALLY FROM SQA AND CHAINED TO THE ONE IN  */ 15498002
*        /* THE NVT.                                                 */ 15498402
*        DECLARE 1 NIPSPE BASED ,         /* NIPSPE START            */ 15500001
*                 2 NIPSPEQ PTR(31),      /* CHAIN PTR               */ 15550001
*                 2 NIPSPEA PTR(31);      /* PARAMETER PTR           */ 15600001
*        /***                  END OF NIPSPE                       ***/ 15650001
*                                                                       15700001
*        /***              NIPMOUNT PARAMETER LIST                 ***/ 15750001
*        /* THE FOLLOWING STRUCTURE MAPS THE NIPMOUNT PARAMETER LIST */ 15760002
*        /* THAT IS GENERATED BY THE BAL MACRO -IEAPMNIP TYPE=MOUNTPL*/ 15770002
*        /* AND IS USED TO PASS INFORMATION TO THE NIPMOUNT SERVICE  */ 15780002
*        /* ROUTINE.  THIS MAPPING MUST BE INCLUDED IN ANY MODULE    */ 15790002
*        /* THAT USES THE NIPMOUNT SERVICE, SINCE IT IS REFERRED TO  */ 15792002
*        /* BY THE NIPMOUNT BAL CALL MACRO, IEAPMNIP TYPE=MOUNT.     */ 15794002
*                                                                       15796002
*        DECLARE 1 NIPMNTPL BASED,        /* PARM LIST START         */ 15800001
*               2 NMNTDS CHAR(12),        /* DATASET NAME  OR        */ 15850002
*                3 NMNTDSA PTR(31) BDY(BYTE), /* DS NAME ADDRESS     */ 15900001
*               2 NMNTVS CHAR(6),         /* VOLUME SERIAL  OR       */ 15950002
*                3 NMNTUCB FIXED(31) BDY(BYTE), /* UCB ADDRESS       */ 16000001
*               2 NMNTDT CHAR(2),         /* DEVICE TYPE             */ 16050001
*               2 NMNTFL CHAR(1),         /* NIPMOUNT FLAGS          */ 16100001
*                3 *        BIT(4),       /* ** RESERVED **          */ 16150002
*                3 NMNTFUS  BIT(1),       /* UCB SCAN ONLY REQUEST   */ 16160002
*                3 NMNTFLI  BIT(1),       /* DS NAME ADDR GIVEN      */ 16200001
*                3 NMNTFLC  BIT(1),       /* CONDITIONAL REQUEST     */ 16250001
*                3 NMNTFLB  BIT(1),       /* UCB ADDRESS GIVEN       */ 16300001
*               2 *      CHAR(3);         /* ** RESERVED **          */ 16350001
*        /***            END OF NIPMOUNT PARAMETER LIST            ***/ 16400001
*                                                                       16450001
*        /***                NIPOPEN PARAMETER LIST                ***/ 16500001
*        /* THE FOLLOWING STRUCTURE MAPS THE NIPOPEN PARAMETER LIST  */ 16510002
*        /* THAT IS GENERATED BY THE BAL MACRO -IEAPMNIP TYPE=OPENPL-*/ 16520002
*        /* AND IS USED TO PASS INFORMATION TO THE NIPOPEN SERVICE   */ 16530002
*        /* ROUTINE.  THIS MAPPING MUST BE INCLUDED IN ANY MODULE    */ 16540002
*        /* THAT USES THE NIPOPEN SERVICE, SINCE IT IS REFERRED TO BY*/ 16542002
*        /* THE NIPOPEN BAL CALL MACRO, IEAPMNIP TYPE=OPEN.          */ 16544002
*                                                                       16546002
*        DECLARE 1 NIPOPNPL BASED,        /* PARM LIST START         */ 16550001
*                 2 NOPNDS CHAR(12),      /* DATASET NAME OR         */ 16600002
*                  3 NOPNDSA PTR(31) BDY(BYTE), /* DS NAME ADDRESS   */ 16650001
*                 2 NOPNDCB PTR(31),      /* DCB ADDRESS             */ 16700001
*                 2 NOPNUCB PTR(32),      /* UCB ADDRESS     @Z30AAXC*/ 16750003
*                 2 NOPNFL CHAR(1),       /* NIPOPEN FLAGS           */ 16800001
*                  3 NOPNFRS  BIT(1),     /* PERMANENTLY RESIDENT DS */ 16850002
*                  3 NOPNFAU  BIT(1),     /* AUTHORIZED LIBRARY FLAG */ 16860002
*                  3 NOPNFLM  BIT(1),     /* SUPRESS DS NOT FOUND MSG*/ 16900001
*                  3 NOPNFLI  BIT(1),     /* DS NAME ADDR GIVEN      */ 16950001
*                  3 NOPNFLNB BIT(1),     /* INITIALIZE BASIC DEB    */ 17000002
*                  3 NOPNFLLB BIT(1),     /* END OF NUCLEUS DEB      */ 17050001
*                  3 NOPNFLLC BIT(1),     /* CONDITIONAL REQUEST     */ 17100001
*                  3 NOPNFLSX BIT(1),     /* FIRST EXTENT ONLY       */ 17150001
*                 2 NOPNRC   CHAR(1),     /* NIPOPEN RETURN CODE     */ 17200001
*                 2 *        CHAR(2);     /* ** RESERVED **          */ 17250001
*        /***           END OF NIPOPEN PARAMETER LIST              ***/ 17300001
*                                                                       17310002
*       /***     NIPSCHED    PARAMETER   LIST               *****/      17350002
*        /* THE FOLLOWING STRUCTURE MAPS THE NIPSCHED PARAMETER LIST */ 17360002
*        /* THAT IS GENERATED BY THE BAL MACRO -IEAPMNIP TYPE=SCHEDL-*/ 17370002
*        /* AND IS USED TO PASS INFORMATION TO THE NIPSCHED SERVICE  */ 17380002
*        /* ROUTINE.                                                 */ 17390002
*                                                                       17392002
*        DECLARE 1 NIPSCHDL BASED,                                      17400002
*                  2 NSCHDPTR PTR(31),  /*ADDRESS OF ROUTINE TO SCHED*/ 17450002
*                  2 NSCHDPRM PTR(31),  /* ADDRESS OF CALLERS AREA   */ 17460002
*                  2 NSCHDCPU CHAR(2);  /* CPU ADDRESS FOR SCHEDULE  */ 17500002
*      /*****  END OF NIPSCHED  PARAMETER  LIST             ********/   17550002
