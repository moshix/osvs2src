         TITLE 'IEAVEDR - IPC DIRECT SIGNAL SERVICE ROUTINE'            00050002
*/*IEAVEDR: CHART DSGNL SR */                                           00050802
*/* HEADER                                                              00051602
*/*                                                                     00052402
*/*                                                                     00053202
*/*                                                                     00054002
*/*                                                  ID: 3.1.7.13.10.1  00054802
*/* DSGNL SERVICE ROUTINE                                               00055602
*/*                                                  PAGE # */          00056402
*/*IEAVEDR: E IEAVEDR */                                                00057202
*/* P DISABLE EXT & I/O SAVE SYSTEM MASK IN MODEL STOSM */              00058002
*/* P SAVE REG 14 IN PSA */                                             00058802
*/* P LOAD REG 14 WITH LCCA ADDR AND SAVE REGS */                       00059802
*/* P ESTABLISH ADDRESSABILITY  */                                      00060802
*/* D (NO,DRINVPCA,YES,) VALID PCCA ADDR? */                            00066002
*/*IEAVEDR2: E ENTRY FOR IEAVERI & IEAVERP */                           00066502
*/* D (YES,IEAVEDRU,NO,) IS SYSTEM A UP? */                             00067002
*/* P INITIALIZE RETRY COUNT TO 0 */                                    00067502
*/* P GET RECEIVER CPU ADDR FROM ITS PCCA ENTRY ADDR */                 00068002
*/* P SET UP SIGP PARM R3=R0 */                                         00069002
*/* P INITIALIZE R15=0 */                                               00071002
*/*DREQCKLP: P NOP */                                                   00072002
*/*DRBUSYLP: P INITIALIZE R1=0 */                                       00073002
*/* P SIGP R1,R2,0(R3) */                                               00074002
*/* D (YES,DRRC0,NO,) SUCCESS? (CC=0) */                                00076002
*/*DRTSTL1: D (NO,DRCC1OR3,YES,) BUSY? (CC=2) */                        00080002
*/* D (YES,DRBUSYT1,NO,) R15=0? */                                      00080502
*/*DRTSTL2: P DECREMENT COUNT BY 1 */                                   00081002
*/* D (NO,DRBUSYLP,YES,) BUSY COUNT=0? */                               00082002
*/* P STORE CLOCK IN LCCADRT2 */                                        00082202
*/* D (NO,DRBUSYRC,YES,) GOOD CLOCK? */                                 00082402
*/* D (YES,DRBUSYCT,NO,) LCCADRT1 > LCCADRT2? */                        00082602
*/*DRBUSYRC: P (,DREXIT) RC = 4 */                                      00082902
*/*DRBUSYT1: P STORE CLOCK IN LCCADRT1 */                               00083202
*/* D (NO,DRBUSYCT,YES,) GOOD CLOCK? */                                 00083402
*/* P ADD 1 MINUTE INCREMENT TO LCCADRT1 */                             00083602
*/*DRBUSYCT: P (,DRBUSYLP) SET R15=4096 */                              00083802
*/*DRRC0: P (,DREXIT) RC = 0 */                                         00084002
*/*IEAVEDRU: P (,DREXIT) RC = 16 */                                     00084502
*/*DRCC1OR3: D (YES,DRRC12,NO,) NOT OPERATIONAL? (CC=3) */              00085002
*/* D (NO,DROPINVT,YES,) EQUIPMENT CHECK? */                            00086002
*/*DRTSTL3: D (YES,DRRC8,NO,) RESTART? */                               00087002
*/*DRBUSYCK: P UPDATE RETRY COUNT BY ONE */                             00090002
*/* D (NO,DREQCKLP,YES,) RETRY COUNT GE 256? */                         00090802
*/*DRRC8: P RETURN CODE = 8 */                                          00091602
*/*DREXIT: D (NO,DREXIT2,YES,) WAS CALL IEAVERI OR IEAVERP? */          00092402
*/* R RETURN BR 14 */                                                   00093202
*/*DREXIT2: P RESTORE REGS */                                           00094002
*/* P RESTORE CALLER'S SYSTEM MASK */                                   00094802
*/* R RETURN BR 14 */                                                   00095602
*/*DRRC12: P (,DREXIT) RETURN CODE = 12 */                              00096402
*/*DROPINVT: D (NO,DRRECCK,YES,DRBUSYCK) OPERATOR INTERVENTION? */      00097202
*/*DRRECCK: D (NO,DRRC8,YES,DRBUSYCK) RECIEVER CHECK? */                00098002
*/*DRINVPCA: P RESTORE CALLER'S REGISTERS */                            00098202
*/* P RESTORE CALLER'S SYSTEM MASK */                                   00098402
*/* S () ABEND:X'07B' DUMP SYSTEM */                                    00098502
*/* FOOTING                                                             00098602
*/*                                                                     00098702
*/*  */                                                                 00098802
*/*IEAVEDR: END DSGNL SR */                                             00099102
*MODULE NAME= IEAVEDR                                                   00099502
*                                                                       00099902
*DESCRIPTIVE NAME= INTERPROCESSOR COMMUNICATION DIRECT SIGNAL ROUTINE   00100302
*                                                                       00100702
*COPYRIGHT= N/A                                                         00101102
*                                                                       00101902
*STATUS= REL. 02.0                                                      00102402
*                                                                       00102902
*FUNCTION= PROVIDES THE USER WITH THE NECESSARY INTERFACE TO SIGNAL     00103402
*          (SIGP) OTHER CPUS IN ORDER TO INVOKE ANY ONE OF THE 12       00103902
*          HARDWARE FUNCTIONS (ORDERS).  THESE ARE SENSE, EXTERNAL      00104602
*          CALL, EMERGENCY SIGNAL, START, STOP, RESTART, INITIAL        00105602
*          PROGRAM RESET, PROGRAM RESET, STOP AND STORE STATUS, INITIAL 00106902
*          MICROPROGRAM LOAD, INITIAL CPU RESET, AND CPU RESET.         00107602
*                                                                       00108302
*  OPERATION= THE DIRECT SIGNAL SERVICE ROUTINE TESTS TO SEE IF IT WAS  00109002
*             PASSED A VALID PCCA ADDRESS.  IF THE PCCA ADDRESS         00109702
*             WAS VALID, THE PHYSICAL ADDRESS IN IN THE PCCA WILL BE    00110702
*             USED AS AN OPERAND FOR THE SIGP INSTRUCTION IN ORDER      00111702
*             TO DIRECT THE REQUEST TO THE APPROPRIATE CPU.  THE ORDER  00112502
*             CODE THAT WAS PASSED IN REGISTER 0 IS USED AS ANOTHER     00113002
*             SIGP OPERAND TO INDICATE WHICH ORDER SHOULD BE INITIATED. 00114102
*             REGISTER 1 WILL BE CLEAR AND PASSED AS ANOTHER OPERAND    00114702
*             TO SIGP  SO THAT STATUS INDICATORS MAY BE RETURNED        00115102
*             IF THE ORDER WAS UNSUCCESSFUL. DIRECT SIGNAL DISABLES     00115802
*             FOR EXTERNAL AND IO INTERRUPTS VIA STNSM AND RETURNS TO   00117102
*             ITS CALLER IN THE SAME STATE AS CALLED.  DIRECT SIGNAL    00117702
*             DOES NOT OBTAIN ANY LOCKS DURING ITS PROCESSING AND       00118302
*             RETURNS ITS RETURN CODE IN REGISTER 15.  IF THE RETURN    00119102
*             CODE IS 8, STATUS INDICATORS ARE RETURNED IN REG 1.       00119902
*             IF THE SYSTEM IS A UNIPROCESSOR (NOT EQUIPPED TO SEND     00120702
*             OR RECEIVING SIGP SIGNALS) A PROGRAM EXCEPTION WILL       00121502
*             OCCUR ON THE SIGP INSTRUCTION.  DIRECT SIGNAL WILL        00122502
*             RETRY FROM ITS FRR AND RETURN THE CALLER A RETURN         00123702
*             CODE OF 16.                                               00124302
*                                                                       00125302
*NOTES                                                                  00126102
*  DEPENDENCIES=  THE DIRECT SIGNAL SERVICE ROUTINE IS DEPENDENT        00126502
*             OF THE SIGNAL PROCESSOR (SIGP) INSTRUCTION AS DEFINED     00127302
*             IN "EXTENSIONS TO SYSTEM/370", PKD005808, DATED 12/30/71. 00128202
*             THE DIRECT SIGNAL SERVICE ROUTINE IS DEPENDENT ON         00129202
*             RMS USING ITS OWN SIGP INSTRUCTION TO SIGNAL OTHER        00130002
*             CPUS FOR ACR.                                             00130802
*    CHARACTER-CODE DEPENDENCIES= NONE                                  00131402
*  RESTRICTIONS=NONE.                                                   00132002
*  REGISTER-CONVENTIONS= REFERENCE EQUATE SECTION                       00133302
*  PATCH-LABEL= NONE, THIS IS A RESIDENT NUCLEUS MODULE.                00134302
*                                                                       00135302
*MODULE TYPE= PROCEDURE                                                 00136302
*  PROCESSOR= ASSEM                                                     00137302
*  MODULE SIZE= N/A                                                     00138802
*  ATTRIBUTES= NUCLEUS,ZERO PROTECT KEY,DISABLED,REFRESHABLE,           00140802
*              SUPERVISOR MODE,ADDRSPC=FIXED.                           00141202
*                                                                       00142802
*ENTRY POINT= IEAVEDR                                                   00143202
*  PURPOSE= TO SIGNAL ANOTHER OR ONES OWN CPU.                          00143602
*  LINKAGE= THE DSGNL MACRO WHICH EXPANDS INTO A BALR 14,15.            00144002
*  INPUT= REGISTER 1 - CONTAINS THE PCCA ADDRESS.                       00144102
*         REGISTER 0 - CONTAINS THE REQUESTED ORDER CODE.               00144202
*         REGISTER 14 - CONTAINS THE RETURN ADDRESS.                    00145602
*         REGISTER 15 - CONTAINS THE ENTRY POINT ADDRESS.               00147602
*  REGISTERS SAVED= REGISTER 2 THROUGH 5.                               00148602
*  REGISTER USAGE= REGISTER 1 IS STATUS REGISTER FOR THE SIGP INSTR.    00150002
*                  REGISTER 2 CONTAINS THE ADDRESS OF THE CPU TO        00152002
*                  SIGNALLED BY THE SIGP INSTRUCTION.                   00154402
*                  REGISTER 3 CONTAINS THE ORDER CODE FOR THE SIGP      00156002
*                  INSTRUCTION.                                         00158002
*                  REGISTER 4 CONTAINS A LOOP CONTROL FOR EQUIPMENT     00158102
*                  CHECK, RECEIVER CHECK AND OPERATOR INTERVENING       00161402
*                  CONDITIONS.                                          00163402
*                  REGISTER 5 USED AS THE BASE REGISTER.                00163802
*                  REGISTER 15 CONTAINS A LOOP CONTROL FOR TEMPORARY    00164202
*                  BUSY CONDITIONS.                                     00164602
*  REGISTERS RESTORED= REGISTER 2 THROUGH 5.                            00164902
*                                                                       00168202
*EXIT - NORMAL= IEAVEDR.                                                00170202
*  CONDITIONS= NORMAL COMPLETION OF FUNCTION REGARDLESS OF ORDER        00170602
*             BEING INITIATED.                                          00170702
*  OUTPUT= AN INITIATED ORDER IF SUCCESSFUL AND RETURN CODES            00171002
*         INDICATING THE STATUS.                                        00171102
*  RETURN CODES= AS LISTED BELOW.                                       00171402
*  R.C. - 0  - SIGP FUNCTION SUCCESSFULLY INITIATED.  THE FUNCTION      00171502
*              IS NOT NECESSARILY COMPLETED UPON RETURN TO THE          00171602
*              CALLER.                                                  00171702
*  R.C. - 4  - SIGP FUNCTION NOT COMPLETED BECAUSE ACCESS PATH TO       00171802
*              THE ADDRESSED PROCESSOR WAS BUSY OR THE ADDRESSED        00172502
*              PROCESSOR WAS IN A STATE WHERE IT COULD NOT ACCEPT       00172902
*              AND RESPOND TO THE FUNCTION CODE.                        00173302
*  R.C. - 8  - SIGP FUNCTION UNSUCCESSFUL.  STATUS RETURNED IN          00173702
*              REGISTER 0.                                              00174102
*              REGISTER 0 STATUS ON R.C. 8.                             00174202
*              BIT   0 = EQUIPMENT CHECK.                               00174302
*                  1-23 = RESERVED                                      00174402
*                    24 = EXTERNAL CALL PENDING                         00174702
*                    25 = STOPPED                                       00175102
*                    26 = OPERATOR INTERVENING                          00175502
*                    27 = CHECK STOP                                    00175702
*                    28 = NOT READY                                     00175802
*                    29 = RESERVED                                      00176702
*                    30 = INVALID FUNCTION                              00177502
*                    31 = RECEIVER CHECK                                00179102
*  R.C. - 12 - NOT OPERATIONAL.  THE SPECIFIEC CPU IS EITHER NOT        00179402
*              INSTALLED OR IS NOT CONFIQURED INTO THE SYSTEM OR        00180702
*              IS POWERED OFF.                                          00181102
*  R.C. - 16 - SIGP FUNCTION UNSUCCESSFUL.  CPU IS AN UNIPROCESSOR      00182102
*              AND DOES NOT HAVE SIGP SENDING AND RECEIVING             00183402
*              CAPABILITIES.                                            00184402
*                                                                       00185402
*EXIT - ERROR= NONE.                                                    00186202
*  RETURN CODES= NONE.                                                  00190002
*                                                                       00190902
*EXTERNAL REFERENCES=                                                   00191802
*  ROUTINES= NONE                                                       00192702
*  DATA AREAS= NONE.                                                    00193602
*  CONTROL BLOCKS=                                                      00194502
*                 PSA - U,M.  LCCA - U,M.                               00195402
*                 PCCA - U,M.  CVT - U.  CSD - U.                       00196302
*                                                                       00197202
*TABLES= NONE.                                                          00198102
*                                                                       00199002
*MACROS= ABEND.                                                         00199902
*  SERIALIZATION= DISABLEMENT.                                          00200802
*                                                                       00202502
*CHANGE ACTIVITY= N/A                                                   00212502
*                                                                       00214502
*MESSAGES= XXXXXXX - MSG. TEXT                                          00218402
*                                                                       00220402
*ABEND CODES= SYSTEM ABEND CODE 07B - INVALID PCCA ADDRESS.             00222402
*                                                                       00226302
*********************************************************************** 00228302
IEAVEDR  CSECT                                                          00234302
         ENTRY IEAVEDR2                                                 00236302
         ENTRY IEAVEDR3                                        @YM08101 00237202
*        SYMBOLIC EQUATES                                               00238202
REG0     EQU   0                        REGISTER 0                      00250002
REG1     EQU   1                        REGISTER 1                      00300002
REG2     EQU   2                        REGISTER 2                      00350002
REG3     EQU   3                        REGISTER 3                      00400002
REG4     EQU   4                        REGISTER 4                      00450002
REG5     EQU   5                        REGISTER 5                      00500002
REG6     EQU   6                        REGISTER 6                      00550002
REG15    EQU   15                       REGISTER 15                     00600002
ZERO     EQU   0                        ZERO VALUE                      00650002
STATUSR1 EQU   1                        REG 1 WITH SIGP STATUS BITS     00700002
NOTOPER  EQU   1                        CC 3 - NOT OPERATIVE            00750002
CPUID    EQU   2                        REG 2 WITH PHYSICAL CPUID       00800002
FUNCCDE  EQU   3                        REG 3 WITH SIGP FUNCTION CODE   00850002
FOUR     EQU   4                        RETURN CODE AND CONSTANT        00900002
EQCHKLPC EQU   4                        REG 4 - LOOP CONTROL REGISTER   00950002
*                                       FOR EQ CHK, RECEIVER CHK, AND   01000002
*                                       OPERATOR INTERVENING CONDITIONS 01050002
MULTBY4  EQU   2                        MASK TO SLL TO MULTIPLY BY 4    01100002
CVTREG2  EQU   2                        CVT BASE REGISTER               01150002
DRCC3    EQU   3                        BAD CLOCK                       01170002
NOTBUSY  EQU   5                        CC 1 OR 3, BUT NOT 2 (BUSY CC)  01200002
BASER5   EQU   5                        REG 5 - BASE REGISTER           01250002
SUCCESS  EQU   8                        CC 0 SIGP SUCCESSFUL            01300002
EIGHT    EQU   8                        RETURN CODE AND CONSTANT        01350002
TWELVE   EQU   12                       RETURN CODE                     01400002
REG14    EQU   14                       REGISTER 14                     01410002
RETURN   EQU   14                       REG 14 - RETURN ADDRESS         01450002
RC16     EQU   16                       UNIPROCESSOR R.C.               01460002
K256     EQU   256                      CONTROL LOOP VALUE              01500002
DISABLE  EQU   X'FC'                    TO DISABLE EXT & I/O INTERRUPTS 01600002
XFF      EQU   X'FF'                    BYTE MASK OF FF                 01610002
         EJECT                                                          01650002
         USING LCCA,REG14                                               01700002
         USING PCCA,REG1                                                01750002
         USING PSA,0                                                    01800002
         USING *,REG15                                                  01850002
BASEADR  EQU   *                                                        01852002
IEAVEDR  MODID BR=YES                                                   01860002
         STNSM PSAIPCD+1,DISABLE        DISABLE FOR EXT & I/O INT AND   01900002
*                                       SAVE CALLERS SYSTEM MASK        01950002
         ST    RETURN,PSAIPCSA          SAVE REG 14 VALUE               02000002
         L     REG14,PSALCCAV           LOCATE LCCA                     02050002
         STM   REG2,REG5,LCCADSR2       SAVE CALLERS REGS 2-5           02100002
         DROP  REG14                                                    02300002
         LR    BASER5,REG15             EST ADDRESSABILITY FROM 15 AND  02460002
*                                       SAVE SWAIT INDICATOR (HIGH BIT) 02470002
         USING BASEADR,BASER5                                           02480002
         DROP  REG15                                                    02490002
         LA    REG14,DREXITR            CALLERS AT THIS ENTRY POINT     02493002
*                                       WILL TAKE NORMAL EXIT           02496002
         EJECT                                                          02500002
**********************************************************************  02550002
*        ROUTINE VALIDITY CHECKS THE PCCA ADDRESS                       02600002
**********************************************************************  02650002
         LTR   REG1,REG1                DOES PCCA ADDR = 0              02700002
         BZ    DRINVPCA                 INVALID PCCA ADDRESS            02750002
         L     CVTREG2,CVTPTR           LOCATE CVT                      02800002
         USING CVT,CVTREG2                                              02850002
         LH    REG4,PCCACPUA            OBTAIN PURPORTED CPUID          02900002
         SLL   REG4,MULTBY4             INDEX VALUE FOR PCCAVT          02950002
         L     REG3,CVTPCCAT            LOCATE PCCAVT                   03000002
         L     REG15,0(REG4,REG3)       OBAIN PCCA ADDRESS FOR CPUID    03050002
         CR    REG1,REG15               DOES PCCA ADDRESSES AGREE?      03100002
         BNE   DRINVPCA                 INVALID PCCA ADDRESS            03150002
**********************************************************************  03200002
*        PCCA ADDRESS IS VALID, LOCATE PROCESSOR ADDR FOR SIGP          03250002
**********************************************************************  03300002
IEAVEDR2 DC    0H'0'                                                    03305002
         L     REG15,CVTCSD            GET CSD ADDRESS                  03315002
         TM    CSDFLAGS-CSD(REG15),CSDMP IS SYSTEM IN MP MODE           03325002
         BNO   IEAVEDRU                IF NO RETURN CODE 16             03335002
         LA    EQCHKLPC,K256            ESTABLISH CONTROL FOR EQ CHK,   03350002
*                                       RECEIVER CHK & OPERATOR INTVEN. 03400002
         LH    CPUID,PCCACPUA           OBTAIN PHYSICAL CPUID FOR       03450002
*                                       RECEIVING CPU                   03500002
         LR    FUNCCDE,REG0             PLACE FUNCTION CODE TO SIGP     03550002
         SLR   REG15,REG15              BUSY LOOP CONTROL               03570002
DREQCKLP EQU   *                                                        03600002
DRBUSYLP EQU   *                                                        03700002
         LA    STATUSR1,ZERO            INITIALIZE STATUS REG TO ZERO   03750002
         SIGP  STATUSR1,CPUID,ZERO(FUNCCDE)  SIGNAL APPROPRIATE CPU     03800002
         BC    SUCCESS,DRRC0            SIGP SUCCESSFUL INITIATED       03850002
         EJECT                                                          03900002
**********************************************************************  03950002
*        A CONDITION CODE OF 1,2, OR 3 HAS BEEN GENERATED               04000002
*        THE SIGP INSTRUCTION WILL BE RETRIED AS FOLLOWS                04050002
*            CC 1 - SIGP UNSUCCESSFUL - STATUS STORED IN R1 OPERAND     04100002
*        FOR CC 1 - IF STATUS WAS:                                      04150002
*                   1.  RECEIVER CHECK                                  04200002
*                   2.  OPERATOR INTERVENING                            04250002
*                   3.  EQUIPMENT CHECK - EXCEPT FOR A RESTART ORDER    04300002
*                        WHICH WILL NOT BE RETRIED.                     04350002
*                       *  THESE CONDITIONS WILL BE RETRIED 256 TIMES   04400002
*                          BEFORE GIVING THE CALLER A R.C. 8.           04450002
*                   4.  ALL OTHER CONDITIONS WILL RESULT IN A R.C. 8    04500002
*                       IMMEDIATELY WITH STATUS RETURNED IN REG 0.      04550002
*            CC 2 - SIGP UNSUCCESSFUL - TEMPORARY BUSY                  04600002
*        FOR CC 2 - ALL ORDERS WILL BE RETRIED 1 MIN. OR 1024 TIMES,    04650002
*                   IF STCK FAILS,                                      04670002
*                   BEFORE GIVING THE CALLER A R.C. 4                   04700002
*            CC 3 - ADDRESSED CPU NOT OPERATIVE                         04750002
*        FOR CC 3 - A R.C. 12 WILL BE RETURNED TO THE CALLER            04800002
*                                                                       04850002
**********************************************************************  05100002
DRTSTL1  EQU   *                                                        05150002
         BC    NOTBUSY,DRCC1OR3         EITHER A CC 1 OR 3              05200002
DRTSTL2  EQU   *                                                        05250002
         LTR   REG15,REG15              WAS THIS A 1ST BUSY CONDITION?  05260002
         BZ    DRBUSYT1                 FIRST BUSY CONDITION            05270002
         BCT   REG15,DRBUSYLP           RECEIVING CPU IN TEMPORARY      05300002
*                                       BUSY CONDITION THAT PRECLUDED   05350002
*                                       SIGP OPERATION, TRY AGAIN       05400002
         L     REG1,PSALCCAV            OBTAIN LCCA ADDR                05406002
         STCK  LCCADRT2-LCCA(REG1)      STORE CLOCK VALUE               05412002
         BC    DRCC3,DRBUSYRC           CLOCK INOPERATIVE GIVE RC 4     05418002
         L     REG15,LCCADRT1-LCCA(REG1) OBTAIN INITIAL CLOCK VALUE     05424002
         C     REG15,LCCADRT2-LCCA(REG1) HAS ONE MINUTE ELASPED?        05430002
         BH    DRBUSYCT                 SET BUSY LOOP CTRL              05436002
DRBUSYRC DC    0H'0'                                                    05442002
         LA    REG15,FOUR               TEMPORARY BUSY CONTROL EXHAUST- 05450002
*                                       ED, HANDLE AS IF PERMANENT.     05500002
         B     DREXIT                   GO TO COMMON EXIT CODE          05550002
DRBUSYT1 DC    0H'0'                                                    05550902
         L     REG1,PSALCCAV            OBTAIN LCCA ADDR                05551802
         STCK  LCCADRT1-LCCA(REG1)      STORE CLOCK VALUE               05552702
         BC    DRCC3,DRBUSYCT           SET BUSY LOOP CTRL              05553602
         L     REG15,LCCADRT1-LCCA(REG1) OBTAIN CLOCK VALUE STORED      05554502
         AL    REG15,DRONEMIN           ADD ONE MIN INCREMENT           05555402
         ST    REG15,LCCADRT1-LCCA(REG1) NEW VALUE FOR 1 MIN TIME-OUT   05556302
DRBUSYCT DC    0H'0'                                                    05557202
         L     REG15,DR1MEG            BUSY LOOP CONTROL       @ZA10845 05557603
         B     DRBUSYLP                REPEAT SIGP                      05559002
         EJECT                                                          05560002
*********************************************************************** 05570002
*        RETURN CODE 0  - SIGP SUCCESSFULLY INITIATED                 * 05580002
*********************************************************************** 05590002
DRRC0    EQU   *                                                        05592002
         LA    REG15,ZERO               RC 0 - SUCCESSFUL               05594002
         B     DREXIT                   RETURN TO CALLER                05596002
         SPACE 2                                                        05598002
*********************************************************************** 05598102
*        RETRY FOR UNIPROCESSOR PGM CHK - R.C. 16                       05598202
*********************************************************************** 05610002
IEAVEDRU EQU   *                                                        05610102
         LA    REG15,RC16              INDICATE UP - PGM CHECK RETRY    05610402
         B     DREXIT                  RETURN TO CALLER                 05610802
         EJECT                                                          05611202
*********************************************************************** 05611602
*        CONDITION CODE 1 OR 3                                          05618002
*              CC 3 - GIVE CALLER A RC 12                               05624402
*              CC 1 - DETERMINE IF SIGP SHOULD BE RETRIED               05630802
*********************************************************************** 05637202
DRCC1OR3 EQU   *                                                        05643602
         BC    NOTOPER,DRRC12           RECEIVING CPU NOT OPERATIVE     05650002
         LR    REG0,STATUSR1            IF RETURN IS TO CALLER          05700002
*                                       INSURE STATUS IS IN REG 0       05750002
         LTR   STATUSR1,STATUSR1        WAS EQ CHECK BIT ON?            05800002
         BNM   DROPINVT                 NO, CHECK FOR OTHER STATUS      05850002
DRTSTL3  EQU   *                                                        05900002
         C     FUNCCDE,RESTART          WAS THIS A RESTART REQUEST?     05950002
         BE    DRRC8                    YES, DO NOT RETRY SIGP          06000002
DRBUSYCK EQU   *                                                        06050002
         BCT   EQCHKLPC,DREQCKLP        RETRY EQ CHK, RECEIVER CKH AND  06100002
*                                       OPERATOR INTERVENING CONDITIONS 06150002
DRRC8    EQU   *                                                        06200002
         LA    REG15,EIGHT              CONDITION STILL EXISTS RETURN   06250002
*                                       TO CALLER WITH STATUS IN REG 1  06300002
         EJECT                                                          06350002
DREXIT   EQU   *                                                        06650002
         BR    RETURN                  RETURN TO CALLER IF CALLER IS    06670002
*                                      IEAVERP OR IEAVERI; OTHERWISE    06690002
*                                      BRANCH TO DREXITR FOR NORMAL RET 06710002
DREXITR  DC    0H'0'                                                    06730002
         USING LCCA,REG2                                                06750002
         L     REG2,PSALCCAV            LOCATE CPUS LCCA                06800002
         LM    REG2,REG5,LCCADSR2       RESTORE CALLERS REGS 2-5        06850002
         L     RETURN,PSAIPCSA          RESTORE REG 14                  06860002
         EX    REG0,PSAIPCD             RESTORE CALLERS SYSTEM MASK     06900002
         BR    RETURN                   RETURN TO CALLER                06950002
         EJECT                                                          07000002
*********************************************************************** 07050002
*        RETURN CODE 12 - RECEIVING CPU NOT OPERATIVE                 * 07100002
*********************************************************************** 07150002
DRRC12   EQU   *                                                        07200002
*********************************************************************** 07203002
*        THIS IS CODE MODIFICATION FOR HANDLING CC3                     07206002
*********************************************************************** 07209002
*                                                              @YM08101 07210002
         LR    REG0,REG2                OBTAIN CPUID FOR COMMON ROUTINE 07212002
         LR    REG4,REG14               SAVE CALLER'S RETURN ADDRESS    07215002
         L     REG1,PSALCCAV            OBTAIN LCCA ADDR FOR CR@YM08101 07218002
         BAL   REG14,IEAVEDR3           CALL COMMON ROUTINE    @YM08101 07224002
         LR    REG14,REG4               RESTORE CALLER'S RET ADDR       07227002
*********************************************************************** 07230002
*        END OF CODE MODIFICATION FOR HANDLING CC3                      07233002
*********************************************************************** 07236002
         LA    REG15,TWELVE             RC 12 - NOT OPERATIVE           07250002
         B     DREXIT                   CHECK IF SWAIT OPTION ON        07300002
         SPACE 3                                                        07700002
*********************************************************************** 07750002
*        TEST IS STATUS RETURNED FROM SIGP IN REGISTER 1 INDICATES    * 07800002
*        SIGP NOT SUCCESSFULLY INITIATED BECAUSE OF OPERATOR          * 07850002
*        INTERVENING.                                                 * 07900002
*********************************************************************** 07950002
DROPINVT EQU   *                                                        08000002
         EX    STATUSR1,DROPINTM        EX TM TO DETERMINE IF STATUS    08050002
*                                       FOR OPERATOR INTERVENING IS ON  08100002
         BZ    DRRECCK                  NOT OPERATOR INTERVENING        08150002
DRTSTL4  EQU   *                                                        08200002
         B     DRBUSYCK                 OPERATOR INTERVENING CHECK IF   08250002
*                                       LOOP CONTROL EXHAUSTED          08300002
         SPACE 3                                                        08350002
*********************************************************************** 08400002
*        TEST IF STATUS RETURNED FROM SIGP IN REGISTER 1 INDICATES    * 08450002
*        SIGP NOT SUCCESSFULLY INITIATED BECAUSE OF RECEIVER CHECK    * 08500002
*********************************************************************** 08550002
DRRECCK  EQU   *                                                        08600002
         EX    STATUSR1,DRRECCKT        EX TM TO DETERMINE IF STATUS    08650002
*                                       BIT INDICATES A RECEIVER CHECK  08700002
         BZ    DRRC8                    NOT A RECEIVER CHECK            08750002
DRTSTL5  EQU   *                                                        08800002
         B     DRBUSYCK                 RECEIVER CHECK, CHECK IF LOOP   08850002
*                                       CONTROL EXHAUSTED.              08900002
         SPACE 3                                                        08950002
*********************************************************************** 09000002
*        TM INSTRUCTION OBJECT OF EX TO DETER IF OPERATOR INTERVENING * 09050002
*        STATUS IS INDICATED.                                         * 09100002
*********************************************************************** 09150002
DROPINTM EQU   *                                                        09200002
         TM    DROPINON,ZERO            WITH EX DETERMINE IF OPERATOR   09250002
*                                       INTERVENING STATUS BIT IS ON    09300002
         SPACE 3                                                        09350002
*********************************************************************** 09400002
*        TM INSTRUCTION OBJECT OF EX TO DETERMINE IF RECEIVER CHECK   * 09450002
*        STATUS BIT IS ON                                             * 09500002
*********************************************************************** 09550002
DRRECCKT EQU   *                                                        09600002
         TM    DRRECCKO,ZERO            WITH EX DETERMINE IF RECEIVER   09650002
*                                       CHECK STATUS BIT IS ON          09700002
         EJECT                                                          09750002
**********************************************************************  10100002
*        PCCA ADDRESS INVALID ABEND THE CALLER                          10150002
**********************************************************************  10200002
DRINVPCA EQU   *                                                        10250002
         L     REG2,PSALCCAV            LOCATE THE LCCA                 10350002
         USING LCCA,REG2                                                10400002
         LM    REG2,REG5,LCCADSR2       RESTORE CALLERS REGS 2-5        10450002
         EX    REG0,PSAIPCD             RESTORE CALLERS SYSTEM MASK     10500002
         ABEND X'07B',DUMP,,SYSTEM      ABEND THE CALLER                10550002
         SPACE 3                                                        10600002
**********************************************************************  10650002
*        CONSTANTS                                                      10700002
**********************************************************************  10750002
DRONEMIN DC    F'64'                   APPROXIMATELY ONE MINUTE         10950002
RESTART  DC    F'6'                     RESTART FUNCTION CODE           11000002
*                                       OPERATOR INTERVENING AND        11050002
*                                       RECEIVER CHECKS.                11100002
DR1MEG   DC    X'00100000'              BUSY CNTRL LOOP VALUE  @ZA10845 11130003
DROPINON DC    X'20'                    OPERATOR INTERVENING STATUS BIT 11160002
DRRECCKO DC    X'01'                    RECEIVER CHECK STATUS BIT       11170002
         TITLE 'IEAVEDR - IPC CC3 HANDLER ROUTINE'                      11171002
*********************************************************************** 11171202
*        THIS IS CODE FOR HANDLING CC3                                  11171402
*********************************************************************** 11171602
         DROP  REG2                                            @YM08101 11172002
         DROP  REG1                                            @YM08101 11173002
         DROP  BASER5                                          @YM08101 11174002
*                                       CC3 HANDLING RTN       @YM08101 11175002
*                                       INITIATE ACR ON CC3 FOR  A SIGP 11179002
*                                       TO A LOGICALLY ONLINE CPU       11179502
IEAVEDR3 DC    0H'0'                                                    11180002
R0       EQU   0                                                        11181002
R1       EQU   1                                                        11182002
R2       EQU   2                                                        11183002
R3       EQU   3                                                        11184002
R4       EQU   4                                                        11185002
R5       EQU   5                                                        11186002
R6       EQU   6                                                        11200002
R7       EQU   7                                                        11204002
R8       EQU   8                                                        11208002
R9       EQU   9                                                        11212002
R10      EQU   10                                                       11216002
R11      EQU   11                                                       11220002
R12      EQU   12                                                       11224002
R13      EQU   13                                                       11228002
R14      EQU   14                                                       11232002
R15      EQU   15                                                       11236002
H16      EQU   16                                                       11240002
RC4      EQU   4                                                        11244002
D64      EQU   64                                                       11250002
         USING PSA,0                                                    11253002
         USING CVT,R6                                                   11254002
         USING LCCA,R1                                                  11255002
         USING CSD,R2                                                   11256002
         USING PCCA,R5                                                  11257002
         L     R15,LCCACPUS             GET CPU RELATED SAVE AREA       11258002
         L     R15,WSACEDS0-WSAC(R15) ADDR OF DISPATCHER COMM S.A.      11259002
         STM   R4,R7,D64(R15)           SAVE WORK REGS                  11260002
         BALR  R7,R0                    EST ADDRESSABILITY              11261002
         USING *,R7                                                     11262002
         L     R6,CVTPTR           OBTAIN CVT ADDRESS                   11263002
         L     R2,CVTCSD           OBTAIN CSD ADDRESS                   11264002
         LR    R3,R0               RECEIVING CPU'S ID                   11265002
         STH   R3,PSASPAD          EST. OTHER CPU AS SENDER             11266002
         SLL   R3,MULTBY4          GET PCCAVT INDEX VALUE               11267002
         L     R4,CVTPCCAT         OBTAIN PCCAVT ADDRESS                11268002
         L     R5,0(R3,R4)         OBTAIN PCCA ADDR OF CPU              11269002
         LH    R4,PCCACAFM         OBTAIN CPU'S ALIVE MASK              11270002
         LH    R3,CSDCPUAL         OBTAIN SYSTEM ALIVE MASK             11271002
         NR    R4,R3               AND IT WITH SYSTEM ALIVE MASK        11272002
         BZ    IPCRC0              CC3 FOR NOT ALIVE CPU, OKAY - R.C. 0 11273002
         TM    CVTDSSAC,X'FF'           IS DSS ACTIVE                   11274002
         BO    IPCIFDSS                 YES, WAIT SYSTEM                11275002
         LH    R3,PSACPULA         OBTAIN LOGICAL CPU ID                11276002
         SLL   R3,H16              PLACE IN HIGH ORDER BYTES            11277002
         O     R3,IPCRSTWD         OBTAIN OWN RESTART WORD              11278002
         SLR   R0,R0               CLEAR REG FOR C&S                    11279002
IPCRSTST EQU   *                                                        11280002
         CS    R0,R3,CVTRSTWD      TRY TO OBTAIN RESTART RESOURCE       11281002
         BZ    IPCRSTIT            TEST OWNER OF RESTART RESOURCE       11300002
         LH    R5,PSASPAD          OBTAIN OTHER CPUID                   11303002
         SLL   R5,H16              PLACE IN HI ORDER BYTES              11306002
         O     R5,IPCRSTWD         MAKE RESTART WORD                    11309002
         CR    R5,R0               DOES OTHER CPU OWN RS WD?            11312002
         BE    IPCRSTQT            YES, WAIT THIS CPU                   11315002
         B     IPCRSTST            OTHERWISE, STEAL RESTART RESOURCE    11318002
IPCRSTIT EQU   *                                                        11321002
         LM    R2,R5,FLCRNPSW      OBTAIN RESTART NEW/OLD               11327002
         STM   R2,R5,LCCADRT1      SAVE RESTART NEW/OLD                 11330002
         LM    R2,R3,IPCRSNEW      OBTAIN RESTART NEW FOR OPERATOR      11333002
         STM   R2,R3,FLCRNPSW      INITIATED RESTART                    11336002
IPCNOSAV EQU   *                                                        11350002
         LPSW  IPCWSCC3            PLACE CPU IN WAIT STATE WITH         11351002
*                                  WAIT STATE CODE THAT REQUIRES        11352002
*                                  OPERATOR ACTION TO RESTART SYSTEM.   11353002
IPCRSCC3 EQU   *                                                        11354002
         L     R5,PSAPCCAV         OBTAIN PCCA ADDR                     11355002
         L     R4,PCCAPSAV         OBTAIN OWN PSA'S VIRTUAL ADDR        11356002
         CLC   FLCFLA-FLC(8,4),IPCWSCC3  WAS SS AND RESTART             11357002
*                                  DONE BY OPERATOR. IF NOT RELOAD WAIT 11358002
*                                  PSW.                                 11359002
         BNE   IPCNOSAV            RELOAD WAIT STATE                    11360002
         XC    FLCFLA-FLC(8,4),FLCFLA-FLC(4) CLEAR PSW SAVE AREA        11361002
         LH    R4,IPCMFACD         OBTAIN MFA CODE                      11362002
         STH   R4,FLCEICOD         SIMULATED MFA                        11363002
         TM    LCCASPN1,LCCASIGP        IS SPIN BIT ON?                 11363202
         BO    IPCSPNR1                 YES DON,T TURN IT ON            11363402
         SLR   R4,R4                    SET TURN OFF INDICATOR          11363602
         OI    LCCASPN1,LCCASIGP   TURN ON SPIN BIT TO                  11364002
*                                  GET CONTROL BACK AFTER EXT INT       11365002
IPCSPNR1 EQU   *                                                        11365502
         LM    R2,R3,IPCPSWRT      SAVE RETURN ADDRESS                  11366002
         STM   R2,R3,FLCEOPSW      SET UP RETURN FROM EXT FLIH          11367002
         LPSW  FLCENPSW            FORCE ENTRY TO EXT FLIH              11368002
IPCEXTRT EQU   *                                                        11369002
         LTR   R4,R4                    WAS SPIN BIT TURNED ON?         11369202
         BNZ   IPCSPNR2                 NO, AVOID TURNING OFF           11369402
         NI    LCCASPN1,XFF-LCCASIGP  TURN OFF SPIN BIT                 11369602
IPCSPNR2 EQU   *                                                        11370002
         LM    R2,R5,LCCADRT1      RESTORE RESTART NEW/OLD              11371002
         STM   R2,R5,FLCRNPSW      RESTORE RESTART NEW/OLD              11372002
         ST    R0,CVTRSTWD         RESTORED RESTART RESOURCE            11374002
IPCEXIT  EQU   *                                                        11375002
         LM    R4,R7,D64(R15)      RESTORE REGISTERS OF CALLER          11376002
         LA    R15,RC4                  SET RETURN CODE TO 4            11377002
         BR    R14                 RETURN TO CALLER                     11378002
         EJECT                                                          11379002
IPCRC0   EQU   *                                                        11380002
         LM    R4,R7,D64(R15)      RESTORE REGISTERS OF CALLER          11381002
         SLR   R15,R15             SET RETURN CODE TO ZERO              11382002
         BR    R14                 RETURN TO CALLER                     11383002
         SPACE 3                                                        11384002
IPCRSTQT EQU   *                                                        11385002
         LPSW  IPCWSCPU            WAIT STATE THIS CPU                  11386002
         SPACE 3                                                        11387002
IPCIFDSS DC    0H'0'                                                    11388002
         LPSW  IPCWSDSS                 DSS ACTIVE, WAIT SYSTEM         11389002
         EJECT                                                          11390002
IPCMFACD DC    X'1200'             MFA INTERRUPT CODE                   11400002
IPCRSTWD DS    0F                                                       11403002
         DC    X'0000',CL2'IP'     RESTART RESOURCE WORD                11406002
         DS    0D                                                       11409002
IPCRSNEW DC    X'040C0000'                                              11412002
         DC    A(IPCRSCC3)                                              11415002
IPCWSCC3 DC    X'040E0000'                                              11418002
         DC    XL4'02A'                                                 11421002
IPCWSCPU DC    X'040E0000'                                              11424002
         DC    XL4'02B'                                                 11427002
IPCPSWRT DC    X'040C0000'                                              11430002
         DC    A(IPCEXTRT)                                              11433002
IPCWSDSS DC    X'040E0000'                                              11436002
         DC    XL4'02C'                                                 11439002
         EJECT                                                          11442002
         IHAPCCA                                                        11445002
         EJECT                                                          11448002
         IHALCCA                                                        11451002
         EJECT                                                          11454002
         IHAPSA                                                         11457002
         EJECT                                                          11460002
         IHACSD                                                         11463002
         EJECT                                                          11466002
         IHAWSAVT CLASS=CPU,DSECT=YES                                   11469002
         EJECT                                                          11472002
         CVT     LIST=YES,DSECT=YES,PREFIX=NO                           11475002
         EJECT                                                          11478002
         IHAFRRS                                                        11481002
         END   IEAVEDR                                                  11600002
