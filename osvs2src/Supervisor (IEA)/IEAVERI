         TITLE 'IEAVERI - REMOTE IMMEDIATE SERVICE ROUTINE'             00050002
*/*IEAVERI: CHART RISGNL SR */                                          00050802
*/* HEADER                                                              00052402
*/*                                                                     00053202
*/*                                                                     00054002
*/*                                                                     00054802
*/*                                                  ID: 3.1.7.13.10.3  00055602
*/* RISGNL SERVICE ROUTINE                                              00056402
*/*                                                  PAGE # */          00057202
*/*IEAVERI: E IEAVERI */                                                00058002
*/* P DISABLE EXT & I/O INT SAVE SYS MASK IN MODEL STOSM */             00058802
*/* P SAVE REG 1 IN PSA, GET LCCA AND SAVE REGS */                      00060002
*/* P ESTABLISH ADDRESSABILITY AND FRR */                               00061002
*/* D (NO,RINVPCCA,YES,) PCCA ADDR VALID? */                            00062002
*/* D (NO,RIRC4,YES,RIBUFCON) RECEIVING CPU ONLINE? */                  00063002
*/*RIRC4: P (,RIEXIT) RETURN CODE = 4 */                                00064002
*/*RIBUFCON: P BUILD SENDING CPU ES BUFFER (IN SENDING CPU'S PCCA) */   00065002
*/* P WORD 0 = PARALLEL/SERIAL RMS INDICATOR */                         00066002
*/* P WORD 1 = ADDR RECEIVING RTN'S PARMS */                            00067002
*/* P WORD 2 = ADDR RECEIVING RTN'S ENTRY POINT */                      00068002
*/* P WORD 3 = RECEIVING CPU'S PCCA ADDR */                             00069002
*/*RISIGP: P PLACE EMERGENCY SIGNAL FUNCTION CODE IN REG 0 */           00071602
*/* P SAVE CALLERS RETURN REG */                                        00071802
*/* P SAVE OWN REGS AND EST. IEAVEDR'S ADDR. */                         00072002
*/* S IEAVEDR2:ISSUE EMERGENCY SIGNAL SIGP */                           00072402
*/* P RESTORE REGS SAVED */                                             00072802
*/* D (YES,RILOPCK,NO,) SUCCESS? (RC=0) */                              00073202
*/* D (NO,RIRETCHK,YES,RISIGP) BUSY? (RC=4) */                          00074002
*/*RIRETCHK: P (,RIEXIT) CLEAR RIBUFFER */                              00074802
*/*RINVPCCA: P DELETE FRR FROM STACK */                                 00077202
*/* P RESTORE CALLER'S REGISTERS */                                     00078002
*/* P RESTORE CALLER'S SYSTEM MASK */                                   00078802
*/* S () ABEND:X'07B' DUMP SYSTEM */                                    00080002
*/*RILOPCK: D (NO,%SER,YES,) PARALLEL REQUEST?    */                    00080402
*/* D (NO,RIWINDOW,YES,RIEXIT) HAS RECEIVING ROUTINE BEEN INITIATED ?*/ 00080802
*/*%SER: D (NO,RIWINDOW,YES,RIEXIT) HAS RECEIVING ROUTINE BEEN          00081402
*/*COMPLETED ? */                                                       00082002
*/*RIEXIT: P DELETE FRR FROM STACK */                                   00082402
*/* P RESTORE CALLER'S REGISTERS */                                     00082802
*/* P RESTORE CALLER'S SYSTEM MASK */                                   00083202
*/* R RETURN */                                                         00083602
*/*RIWINDOW: P TURN ON SIGP SPIN INDICATOR */                           00084002
*/* P ENABLE/DISABLE FOR ES/MFA INTERRUPTS */                           00084402
*/* P TURN OFF SPIN INDICATOR */                                        00084802
*/* D (YES,RILOPCK,NO,) RECEIVING CPU ONLINE? */                        00085202
*/*RICSOFF: P (,RIEXIT) RC=20 REG0=0 */                                 00085602
*/* FOOTING                                                             00086002
*/*                                                                     00086402
*/*  */                                                                 00086802
*/*IEAVERI: END RISGNL SR */                                            00087202
*MODULE NAME= IEAVERI                                                   00087602
*                                                                       00088002
*DESCRIPTIVE NAME= INTERPROCESSOR COMMUNICATION REMOTE IMMEDIATE        00090002
*                  SERVICE ROUTINE.                                     00091002
*                                                                       00092002
*COPYRIGHT= N/A                                                         00094002
*                                                                       00096002
*STATUS= REL. 02.0                                                      00099802
*                                                                       00099902
*FUNCTION= PROVIDES THE USER WITH THE NECESSARY INTERFACE TO SIGNAL     00101902
*          (SIGP) OTHER CPUS IN ORDER TO INVOKE A SPECIFIC PROGRAM      00103902
*          ON ANY OF THE ONLINE MP CONFIGURED CPUS.  REMOTE IMMEDIATE   00105902
*          SERVICE ROUTINE WILL GENERATE AN EMERGENCY SIGNAL            00107902
*          TO THE SPECIFIED CPU WHOSE EMERGENCY SIGNAL SLIH WILL        00109902
*          ROUTE CONTROL TO THE SPECIFIED PROGRAM.                      00111902
*                                                                       00116602
*  OPERATION= THE RI SIGNAL SERVICE ROUTINE TESTS TO SEE IF IT WAS      00126602
*             PASSED A VALID PCCA ADDRESS.  IF THE PCCA ADDRESS         00128602
*             WAS VALID, THE PHYSICAL ADDRESS IN IN THE PCCA WILL BE    00130602
*             USED AS AN OPERAND FOR THE SIGP INSTRUCTION IN ORDER      00132602
*             TO DIRECT THE REQUEST TO THE APPROPRIATE CPU. THE REQUEST 00133002
*             CODE THAT WAS PASSED IN REGISTER 0 IS USED  TO POST A     00133102
*             BIT IN A BUFFER IN THE SENDING CPU'S PCCA.  THIS          00133202
*             BIT INDICATES TO THE RECEIVING CPU'S EMERGENCY SIGNAL     00138802
*             SLIH THAT A SPECIFIC PROGRAM IS TO BE GIVEN CONTROL       00140802
*             EITHER IN SERIAL OR PARALLEL EXECUTION MODE WITH THE      00141202
*             SENDING CPU.  OTHER INFORMATION THE SENDING CPU BUILDS    00141602
*             IN THE BUFFER IS THE ENTRY POINT ADDRESS OF THE           00142002
*             ROUTINE TO RECEIVE CONTROL, THE PCCA ADDRESS OF           00142402
*             THE PHYSICAL CPU BEING SIGNALLED, AND THE ADDRESS         00142502
*             OF A PARAMETER IF ONE WAS PROVIDED OR ZEROS.              00142602
*             AFTER BUILDING THE BUFFER, IEAVERI CALLS IEAVEDR REQUEST- 00142802
*             ING A EMERGENCY SIGNAL ORDER CODE BE SIGP'D TO THE        00143202
*             RECEIVING CPU.                                            00143302
*             IF THE RETURN CODE IS A 0 THE SENDING CPU SPINS ON THE    00143602
*             BIT IN THE BUFFER UNTIL THE EMERGENCY SIGNAL SLIH TURNS   00143702
*             THE BIT OFF.  IF THE REQUEST WAS A PARALLEL REQUEST       00143802
*             THE BIT WILL BE TURN OFF BEFORE GIVING THE RECEIVING      00144002
*             ROUTINE CONTROL.  IF THE REQUEST WAS A SERIAL REQUEST     00144102
*             THE BIT WILL NOT BE TURN OFF UNTIL CONTROL IS RETURNED    00144202
*             TO THE EMERGENCY SIGNAL SLIH.  WHENEVER THE EMS SLIH      00144302
*             TURNS THE BIT OFF, THE REMOTE IMMEDIATE SERVICE ROUTINE   00154302
*             WILL TERMINATE ITS SPIN AND RETURN TO THE CALLER WITH A   00156302
*             RETURN CODE OF 0.  DURING THE SPIN, THE SENDING ROUTINE   00156702
*             MUST CONTINUE TO CHECK TO SEE IF THE RECEIVING CPU IS     00157102
*             ONLINE.  IF NOT, THE CPU MUST TERMINATE ITS SPIN AND      00158202
*             GIVE ITS CALLER A RETURN CODE 20. IF THE RETURN CODE      00168202
*             FROM IEAVEDR IS 4 (BUSY CONDITION) THE IEAVEDR RTN        00168302
*             WILL BE CALLED AGAIN.  ALL OTHER RETURN CODES             00168602
*             FROM IEAVEDR WILL BE RETURNED TO IEAVERI'S CALLER.        00170202
*             IF THE SYSTEM IS A UNIPROCESSOR (NOT EQUIPPED TO SEND     00170602
*             OR RECEIVING SIGP SIGNALS) A PROGRAM EXCEPTION WILL       00171002
*             OCCUR ON THE SIGP INSTRUCTION.  DIRECT SIGNAL WILL        00171402
*             RETRY FROM ITS FRR AND RETURN THE CALLER A RETURN         00171802
*             CODE OF 16.                                               00171902
*                                                                       00172002
*NOTES                                                                  00172102
*  DEPENDENCIES=  THE MACHINE CHECK HANDLER MUST ISSUE ITS OWN SIGP     00176702
*                EMERGENCY SIGNAL REQUEST TO INVOKE ACR PROCESSING.     00177102
*                THE SYSTEM TERMINATION FACILITY MUST ALSO ISSUE ITS    00177502
*                OWN SIGP EMERGENCY SIGNAL.  ANY CALLER OF REMOTE       00177902
*                IMMEDIATE SERVICE ROUTINE THAT HAS A RECEIVING         00178302
*                ROUTINE THAT OPENS A WINDOW MUST OBTAIN THE SALLOC     00178402
*                LOCK BEFORE CALLING IEAVERI.                           00178502
*    CHARACTER-CODE DEPENDENCIES= NONE                                  00178702
*  RESTRICTIONS=AVAILABLE ONLY TO SUPERVISOR STATE, ZERO PROTECT KEY    00180702
*               CALLERS.                                                00181102
*  REGISTER-CONVENTIONS= REFERENCE EQUATE SECTION                       00181202
*  PATCH-LABEL= NONE, THIS IS A RESIDENT NUCLEUS MODULE.                00181302
*                                                                       00181402
*MODULE TYPE= PROCEDURE                                                 00186002
*  PROCESSOR= ASSEM                                                     00188002
*  MODULE SIZE= N/A                                                     00190002
*  ATTRIBUTES= NUCLEUS,ZERO PROTECT KEY,DISABLED,REFRESHABLE,           00190402
*              SUPERVISOR MODE,ADDRSPC=FIXED.                           00190502
*                                                                       00190602
*ENTRY POINT= IEAVERI                                                   00190702
*  PURPOSE= TO EXECUTE A ROUTINE VIA EMERGENCY SIGNAL ON ANY OF THE     00202502
*          ONLINE MP CONFIGURED CPUS.                                   00212502
*  LINKAGE= THE RISGNL MACRO WHICH EXPANDS INTO A BALR 14,15.           00214502
*  INPUT= REGISTER 1 - CONTAINS THE PCCA ADDRESS.                       00218402
*         REGISTER 0 - CONTAINS THE SPECIFIC REQUEST CODE.              00220402
*         REGISTER 11 - CONTAINS THE PARAMETER ADDRESS.                 00220802
*         REGISTER 12 - CONTAINS THE RECEIVING ROUTINE'S ENTRY          00221202
*                       POINT ADDRESS.                                  00221602
*         REGISTER 14 - CONTAINS THE RETURN ADDRESS.                    00222402
*         REGISTER 15 - CONTAINS THE ENTRY POINT ADDRESS AND            00226302
*                       ITS HIGH ORDER BIT INDICATES IF THE             00228302
*                       SWAIT OPTION WAS SELECTED.                      00230302
*  REGISTERS SAVED= REGISTER 2 THROUGH 5.                               00234202
*  REGISTER USAGE= REGISTER 1 IS USED TO PASS THE PCCA ADDR TO IEAVEDR. 00236202
*                  REGISTER 2 IS USED AS ADDRESSABILITY TO LCCA TO SAVE 00238202
*                  REGISTERS BEFORE IEAVEDR CALL.                       00248202
*                  REGISTER 4 IS USED AS A WORK REGISTER.               00258702
*                  REGISTER 5 USED AS THE BASE REGISTER.                00262102
*                  REGISTER 15 IS USED AS A WORK REG AND EP TO IEAVEDR. 00264102
*  REGISTERS RESTORED= REGISTER 2 THROUGH 5.                            00264502
*                                                                       00264902
*EXIT - NORMAL= IEAVERI.                                                00265302
*  CONDITIONS= NORMAL COMPLETION OF FUNCTION REGARDLESS OF REQUEST      00265402
*             BEING INITIATED OR NOT.                                   00265502
*  OUTPUT= AN INITIATED EMERGENCY SIGNAL IF SUCCESSFUL AND RETURN CODES 00265602
*         INDICATING THE STATUS.                                        00269002
*  RETURN CODES= AS LISTED BELOW.                                       00271002
*  R.C. - 0  -  FUNCTION SUCCESSFULLY INITIATED.  THE FUNCTION          00271402
*              IS NOT NECESSARILY COMPLETED UPON RETURN TO THE          00271802
*              CALLER IF THE REQUEST WAS A PARALLEL REQUEST.  CONTROL   00272202
*              WILL NOT BE RETURNED TO THE CALLER UNTIL THE RECEIVING   00272302
*              ROUTINE HAS COMPLETED FOR A SERIAL REQUEST.              00272902
*  R.C. - 4  -  FUNCTION NOT COMPLETED BECAUSE CPU THAT WAS TO RECEIVE  00274202
*              THE CALL WAS NOT ONLINE.                                 00274802
*  R.C. - 8  -  FUNCTION UNSUCCESSFUL.  STATUS RETURNED IN              00275402
*              REGISTER 0.                                              00276002
*              REGISTER 0 STATUS ON R.C. 8.                             00277902
*              BIT   0 = EQUIPMENT CHECK.                               00278302
*                  1-24 = RESERVED                                      00278702
*                    25 = STOPPED                                       00279102
*                    26 = OPERATOR INTERVENING                          00279202
*                    27 = CHECK STOP                                    00279302
*                    28 = NOT READY                                     00279402
*                    29 = RESERVED                                      00282802
*                    30 = INVALID FUNCTION                              00284802
*                    31 = RECEIVER CHECK                                00285202
*  R.C. - 12 - NOT OPERATIONAL.  THE SPECIFIED CPU IS EITHER NOT        00285602
*              INSTALLED OR IS NOT CONFIGURED INTO THE SYSTEM OR        00286002
*              IS POWERED OFF.                                          00286102
*  R.C. - 16 - SIGP FUNCTION UNSUCCESSFUL.  CPU IS AN UNIPROCESSOR      00286202
*              AND DOES NOT HAVE SIGP SENDING AND RECEIVING             00286302
*              CAPABILITIES.                                            00287402
*                                                                       00289402
*  R/C/ - 20 - UNABLE TO COMPLETE.  THE CPU'S ALIVE BIT WAS             00289502
*             TURNED OFF DURING THE REMOTE IMMEDIATE WINDOW SPIN        00289602
*              ROUTINE.                                                 00290502
*                                                                       00290902
*EXIT - ERROR= NONE                                                     00292302
*  RETURN CODES= NONE.                                                  00304202
*                                                                       00306202
*EXTERNAL REFERENCES=                                                   00306602
*  ROUTINES= NONE                                                       00306702
*  DATA AREAS=REMOTE IMMEDIATE BUFFER.                                  00311702
*  CONTROL BLOCKS=                                                      00316702
*                 PSA - R,M.  LCCA - R,M.                               00321702
*                 PCCA - R,M.  CVT - R.  CSD - R.                       00324702
*                                                                       00328902
*TABLES= NONE.                                                          00329302
*                                                                       00329702
*MACROS= ABEND, WINDOW,                                                 00330102
*                                                                       00330202
*  SERIALIZATION= DISABLEMENT.                                          00330302
*                                                                       00331102
*CHANGE ACTIVITY= N/A                                                   00331502
*                                                                       00331902
*MESSAGES= XXXXXXX - MSG. TEXT                                          00332302
*                                                                       00332702
*ABEND CODES= SYSTEM ABEND CODE 07B - INVALID PCCA ADDRESS.             00335602
*                                                                       00337602
*********************************************************************** 00339602
IEAVERI  CSECT                                                          00342002
*        SYMBOLIC EQUATES                                               00353602
REG0     EQU   0                        REGISTER 0                      00365202
REG1     EQU   1                        REGISTER 1                      00376802
REG2     EQU   2                        REGISTER 2                      00388402
REG3     EQU   3                        REGISTER 3                      00400002
REG4     EQU   4                        REGISTER 4                      00450002
REG5     EQU   5                        REGISTER 5                      00500002
REG6     EQU   6                        REGISTER 6                      00550002
REG14    EQU   14                       REGISTER 14                     00600002
REG15    EQU   15                       REGISTER 15                     00650002
ZERO     EQU   0                        0 VALUE                         00700002
RRCPUID1 EQU   1                        RECEIVING RTNS PCCA ADDRESS     00760002
MULTBY4  EQU   2                        SLL CONSTANT TO MULT BY 4       00900002
FUNCCDE  EQU   0                        REG 0 SIGP FUNCTION CODE        00960002
EMSCODE  EQU   3                        EMS FUNCTION CODE               00970002
RIBUFNEW EQU   3                        REG 3 - C&S REPLACE VALUE       00980002
PARLIND  EQU   3                        CC 3 PARALLEL REQUEST           00990002
SERLIND  EQU   3                        CC 3 SERIAL REQUEST             00992002
CVTREG4  EQU   4                        REG4 BASE FOR CVT               01000002
CSDREG4  EQU   4                        REGISTER 4 BASE FOR CSD         01010002
FOUR     EQU   4                        VALUE 4                         01050002
BASER5   EQU   5                        REG 5 BASE FOR IEAVERP          01100002
EIGHT    EQU   8                        CONSTANT 8                      01110002
PARMR11  EQU   11                       CALLERS PARAMETER ADDRESS       01142002
RECRTNEP EQU   12                       CALLERS RECEIVING ROUTINES      01144002
*                                       ENTRY POINT                     01146002
RETURN   EQU   14                       CALLERS RETURN ADDRESS          01150002
RIBUFFER EQU   15                       REG 15 ADDR OF EMS BUFFER       01160002
RIBUFOLD EQU   15                       REG 15  C&S COMPERAND           01170002
RETCODE  EQU   15                       REG 15, RETURN CODE REG         01200002
TWENTY   EQU   20                       RETURN CODE 20 - SPINNING       01210002
*                                       ON CPU THAT DIED                01220002
DISABLE  EQU   X'FC'                    DISABLE MASK                    01250002
SIGPNACT EQU   X'BF'                    MASK OFF SPIN INDICATOR         01300002
XFF      EQU   X'FF'                    BYTE BASK OF FF                 01350002
         EJECT                                                          01400002
         USING *,REG15                                                  01404002
BASEADR  EQU   *                                                        01406002
IEAVERI  MODID BR=YES                                                   01410002
         USING LCCA,REG1                                                01550002
         USING PSA,0                                                    01600002
         STNSM PSAIPCR+1,DISABLE        DISABLE CALLER & SAVE SYS MASK  01700002
         ST    REG1,PSAIPCSA            SAVE CALLERS REG 1              01750002
         L     REG1,PSALCCAV            LOCATE LCCA                     01800002
         STM   REG2,REG5,LCCARIR2       SAVE CALLERS REGS 2-5           01850002
         DROP  REG1                                                     01900002
         L     REG1,ARIF                ADDR OF IEAVERI'S RECOVERY RTN  01950002
         SETFRR A,FRRAD=(1),WRKREGS=(3,4)                               02200002
         OI    PSASUP2,PSAIPCRI         RI'S SUPER BIT                  02200402
         L     REG1,PSAIPCSA            RESTORE CALLERS REG 1           02202002
         USING PCCA,REG1                                                02210002
         LR    BASER5,REG15             EST ADDRESSABILITY AND SAVE     02212002
*                                       SWAIT OPTION                    02214002
         USING BASEADR,BASER5                                           02220002
         DROP  REG15                                                    02230002
         EJECT                                                          02250002
**********************************************************************  02350002
*        ROUTINE VALIDITY CHECKS THE PCCA ADDRESS                       02400002
**********************************************************************  02450002
         LTR   REG1,REG1                IS PCCA ADDRESS 0?              02480002
         BZ    RINVPCCA                 INVALID PCCA ADDRESS            02490002
         L     CVTREG4,CVTPTR           LOCATE THE CVT                  02492002
         USING CVT,REG4                                                 02494002
         L     REG3,CVTPCCAT            LOCATE PCCAVT                   02496002
         LH    REG2,PCCACPUA            OBTAIN PURPORTED CPUID          02500002
         SLL   REG2,MULTBY4             INDEX VALUE FOR PCCAVT          02560002
         L     REG15,0(REG2,REG3)       OBTAIN PCCA ADDRESS FOR CPUID   02620002
         CR    REG1,REG15               DOES PCCA ADDRESSES AGREE?      02700002
         BNE   RINVPCCA                 INVALID PCCA ADDRESS            02750002
**********************************************************************  02800002
*        PCCA IS A VALID PCCA ADDRESS, DETERMINE IF CPU IS ALIVE        02850002
**********************************************************************  02900002
         L     CSDREG4,CVTCSD           LOCATE THE CSD                  02950002
         USING CSD,REG4                                                 02960002
         LH    REG3,CSDCPUAL            LOCATE SYSTEM ALIVE BITS        03000002
         LH    REG4,PCCACAFM            LOCATE THE RECEIVING CPU ALIVE  03050002
*                                       MASK                            03100002
         NR    REG4,REG3                AND THE MASKS TO DETERMINE IF   03150002
*                                       THE RECEIVING CPU IS ONLINE     03200002
         BZ    RIRC4                    THE RECEIVING CPU IS NOT ONLINE 03250002
         DROP  REG4                                                     03360002
         L     REG4,PSAPCCAV            LOCATE SENDING CPUS PCCA        03362002
         DROP  REG1                                                     03362402
         USING PCCA,REG4                                                03364002
         ST    PARMR11,PCCAEMSP         CALLERS PARAMETER ADDR          03380002
         ST    RRCPUID1,PCCAEMSA        RECEIVING CPUS PCCA ADDR        03390002
         ST    RECRTNEP,PCCAEMSE        RECEIVING ROUTINES ENTRY POINT  03392002
         ST    FUNCCDE,PCCAEMSI         INSERT THE FUNCTION CODE        03450002
*                                       ON IN THE REMOTE IMMEDIATE      03500002
*                                       BUFFER IN SENDING CPUS PCCA     03550002
RIBUFCON EQU   *                                                        03950002
         L     REG2,PSALCCAV           OBTAIN LCCA FOR SAVING REGS      03980002
         ST    REG14,LCCADSR2-LCCA(REG2) SAV R14 IN IEAVEDR'S S.A.      04050002
         ST    REG1,LCCADSR3-LCCA(REG2) SAV R1 IN IEAVEDR'S S.A.        04060002
         ST    REG5,LCCADSR4-LCCA(REG2) SAV R5 (BASE REG)               04070002
RISIGP   EQU   *                                                        04100002
         LA    REG0,EMSCODE             LOAD EMS FUNCTION CODE          04150002
         L     REG2,CVTPTR              SET CVT ADDR IN R2 FOR IEAVEDR  04160002
         L     REG15,AIEAVDR2           IEAVEDR'S SECONDARY ENTRY       04170002
         L     REG5,AIEAVEDR            ESTABLISH BASE FOR IEAVEDR      04200002
         BALR  REG14,REG15              ENTER IEAVEDR AT SECONDARY ENTY 04214002
         L     REG2,PSALCCAV            OBTAIN LCCA FOR RESTORING REGS  04221002
         L     REG14,LCCADSR2-LCCA(REG2) RESTORE RETURN REG             04228002
         L     REG5,LCCADSR4-LCCA(REG2) RESTORE BASE REG                04235002
         L     REG4,PSAPCCAV            RE-OBTAIN PCCA'S ADDRESS        04242002
         LTR   RETCODE,RETCODE          WAS DIRECT SIGNAL SUCCESSFUL    04250002
         BZ    RILOPCK                  SUCCESSFUL SIGP INITIATED       04300002
         C     RETCODE,BUSY             WAS SIGP TEMPRORARY BUSY        04350002
         BNE   RIRETCHK                 NOT BUSY, CONTINUE RC CHECK     04400002
         L     REG1,LCCADSR3-LCCA(REG2) RESTORE RECEIVING CPU'S PCCA    04450002
         B     RISIGP                   REPEAT SIGP UNTIL TEMPORARY     04500002
*                                       BUSY CONDITION CLEARS           04550002
RIRETCHK EQU   *                                                        04600002
         LA    REG1,ZERO               GET A ZERO VALUE                 04692002
         ST    REG1,PCCAEMSI           CLEAR EMS INDICATORS             04692402
         ST    REG1,PCCAEMSA           ZERO OUT PCCA ADDR               04694002
         B     RIEXIT                   EXIT                            04800002
**********************************************************************  05020402
*        RECEIVING CPU IS LOGICALLY NOT ONLINE - R.C. 4              *  05020802
**********************************************************************  05021202
RIRC4    EQU   *                                                        05022002
         LA    RETCODE,FOUR             RETURN CODE FOUR NOT ONLINE     05024002
         B     RIEXIT                   RETURN TO CALLER                05026002
         EJECT                                                          05030002
RINVPCCA EQU   *                                                        05050002
         SETFRR D,WRKREGS=(3,4)         DELETE IEAVERPS FRR             05100002
         NI    PSASUP2,XFF-PSAIPCRI     TURN OFF RI'S SUPER BIT         05110002
         L     REG2,PSALCCAV            LOCATE LCCA                     05150002
         USING LCCA,REG2                                                05200002
         LM    REG2,REG5,LCCARIR2       RESTORE CALLERS REGS 2-5        05250002
         EX    REG0,PSAIPCR             RESTORE CALLERS SYSTEM MASK     05260002
         ABEND X'07B',DUMP,,SYSTEM                                      05300002
         EJECT                                                          05310002
RILOPCK  EQU   *                                                        05350002
         L     REG2,LCCADSR3-LCCA(REG2) RESTORE RECEIVING CPU'S PCCA    05380002
*                                       FOR WINDOW SPIN RTN             05410002
RILOPCK1 EQU   *                                                        05440002
         L     RIBUFOLD,PCCAEMSI        OBTAIN CURRENT BUFFER VALUE     05500002
         ALR   RIBUFOLD,RIBUFOLD        BUFFER HAS SOME VALUE           05600002
         BZ    RIEXIT                   BUFFER WAS EMPTY                05610002
*                                       NOTE IF BUFFER EMPTY, R15=0     05620002
         BC    PARLIND,RIWINDOW         PARALLEL REQUEST                05650002
         ALR   RIBUFOLD,RIBUFOLD        NOT PARALLEL, TEST SERIAL       05700002
         BC    SERLIND,RIWINDOW         SERIAL REQUEST                  05750002
*                                       EITHER SERIAL OR PARALLEL       05760002
*                                       BUT RECEIVING RTN HAS COMPLETED 05770002
*                                       OR HAS BEEN INITIATED.          05780002
         LA    RETCODE,ZERO             SET R15 TO A RETURN CODE 0      05790002
RIEXIT   EQU   *                                                        05800002
         SETFRR D,WRKREGS=(3,4)         DELETE IEAVERPS FRR             05950002
         NI    PSASUP2,XFF-PSAIPCRI     TURN OFF RI'S SUPER BIT         05960002
         L     REG2,PSALCCAV                                            06000002
         USING LCCA,REG2                                                06050002
         LM    REG2,REG5,LCCARIR2       RESTORE CALLERS REGS 2-5        06100002
         DROP  REG2                                                     06110002
         EX    REG0,PSAIPCR             RESTORE CALLERS SYSTEM MASK     06150002
         BR    RETURN                   RETURN TO CALLER                06200002
RIWINDOW EQU   *                                                        06210002
         L     REG1,PSALCCAV            LOCATE LCCA                     06220002
         USING LCCA,REG1                                                06230002
         OI    LCCASPN1,LCCASIGP        INDICATE RI SPIN BEING ENTERED  06240002
         L     REG0,PSAIPCR             SAVE LOW CORE VALUE IN CASE OF  06242002
*                                       AN INTERRUPT                    06244002
         WINDOW LCCAICR0                OPEN A WINDOW FOR MFA AND EMS   06246002
         ST    REG0,PSAIPCR             RESTORE CALLERS SYSTEM MASK     06248002
         NI    LCCASPN1,SIGPNACT        TURN OFF RI SPIN INDICATOR      06248402
         L     REG1,CVTPTR                                              06249202
         USING CVT,REG1                                                 06249602
         L     REG15,CVTCSD             LOCATE THE CSD                  06249702
         USING CSD,REG15                                                06253902
         LH    REG3,CSDCPUAL            LOCATE THE CPU ALIVE MASK       06258202
         DROP  REG4                                                     06262202
         USING PCCA,REG2                                                06264202
         LH    REG1,PCCACAFM            CHECK RECEIVING CPUS ALIVE MASK 06266602
         DROP  REG2                                                     06276602
         USING PCCA,REG4                                                06277002
         NR    REG1,REG3                AND THE TWO MASKS               06278602
         BNZ   RILOPCK1                 RECEIVING CPU STILL ON LINE     06280602
RICSOFF  EQU   *                                                        06281102
         ST    REG1,PCCAEMSI            COMPARE AND SWAP RI OFF         06282202
         ST    REG1,PCCAEMSA            ZERO OUT PCCA ADDR              06288402
         LA    RETCODE,TWENTY           RECEIVING CPU NO LONGER ALIVE   06289502
         B     RIEXIT                   RETURN TO CALLER                06293002
         DS    0F                                                       06296502
AIEAVEDR DC    V(IEAVEDR)               IEAVEDR'S MAIN ENTRY POINT      06297302
AIEAVDR2 DC    V(IEAVEDR2)              IEAVEDR'S 2ND ENTRY POINT FOR   06298102
*                                       IEAVERI AND IEAVERP             06298902
BUSY     DC    F'4'                     CONSTANT TO TEST FOR BUSY COND. 06300002
RC16     DC    F'16'                    CONSTANT TO TEST FOR RC 16.     06450002
ARIF     DC    V(IEAVEIP1)              IEAVERIS FRR                    06500002
         EJECT                                                          06520002
         IHAFRRS                                                        06550002
         IHAPCCA                                                        06600002
         IHALCCA                                                        06650002
         IHACSD                                                         06700002
         IHAPSA                                                         06750002
         CVT   LIST=YES,DSECT=YES,PREFIX=NO                             06800002
         END   IEAVERI                                                  06950002
