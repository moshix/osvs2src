         TITLE 'IEAVERP - REMOTE PENDABLE SERVICE ROUTINE'              00080002
*/*IEAVERP: CHART RPSGNL SR */                                          00080202
*/* HEADER                                                              00080402
*/*                                                                     00080602
*/*                                                                     00080802
*/*                                                                     00081002
*/*                                                  ID: 3.1.7.13.10.2  00081202
*/* RPSGNL SERVICE ROUTINE                                              00081402
*/*                                                  PAGE # */          00082002
*/*IEAVERP: E IEAVERP */                                                00083002
*/* P DISABLE EXT & I/O SAVE SYS MASK IN MODEL STOSM INSTR */           00084002
*/* P SAVE REG 1 IN PSA GET LCCA ADDR TO SAVE CALLER'S REGS */          00084202
*/* P EST ADDRESSABILITY AND FRR */                                     00084402
*/* D (NO,RPINVPCA,YES,) PCCA ADDR VALID? */                            00084602
*/* D (NO,RPRC4,YES,) RECEIVING CPU ON LINE? */                         00084802
*/* P TURN ON PROPER FUNCTION CODE IN RECEIVING CPU PENDABLE BUFFER */  00085002
*/* RPSIGPRT: P PLACE EXT CALL FCN CODE IN R0 */                        00085202
*/* P SAVE CALLERS RETURN REGISTER */                                   00085302
*/* P SAVE OWN REGS & EST. ADDR. FOR IEAVEDR */                         00086002
*/* S IEAVEDR2:ISSUE EXT. CALL SIGP REQUEST */                          00086102
*/* D (YES,RPEXIT,NO,) SUCCESS? (RC=0) */                               00090602
*/* D (YES,RPSIGPRT,NO,) BUSY? (RC=4?) */                               00091602
*/* RPRCCHK: D (NO,RPEXIT,YES,) STATUS STORED? (RC=8) */                00092402
*/* D (NO,RPEXIT,YES,) EXT CALL PENDING? */                             00093202
*/* P (,RPEXIT) SET RC=0 */                                             00094002
*/*RPINVPCA: P RESTORE CALLER'S REGISTERS */                            00094802
*/* P RESTORE CALLER'S SYSTEM MASK */                                   00095602
*/*S () ABEND:X'07B' DUMP SYSTEM */                                     00096302
*/*RPRC4: P (,RPEXIT2) SET RC=4 */                                      00097002
*/*RPEXIT: P RESTORE CALLER'S RETURN REGISTER */                        00100002
*/*RPEXIT2: P RESTORE CALLER'S REGISTERS */                             00101002
*/* P RESTORE CALLER'S SYSTEM MASK  */                                  00101502
*/* R RETURN */                                                         00102002
*/* FOOTING                                                             00103002
*/*                                                                     00103502
*/*  */                                                                 00104002
*/*IEAVERP: END RPSGNL SR */                                            00105002
*MODULE NAME= IEAVERP                                                   00105702
*                                                                       00106402
*DESCRIPTIVE NAME= INTERPROCESSOR COMMUNICATION REMOTE PENDABLE SERVICE 00107102
*                  ROUTINE.                                             00107802
*                                                                       00108502
*COPYRIGHT= N/A                                                         00109202
*                                                                       00109902
*STATUS= REL. 02.0                                                      00110602
*                                                                       00111302
*FUNCTION= PROVIDES THE USER WITH THE NECESSARY INTERFACE TO SIGNAL     00112002
*          (SIGP) OTHER CPUS IN ORDER TO INVOKE A SPECIFIC PROGRAM      00112702
*          ON ANY OF THE ONLINE MP CONFIGURED CPUS.  REMOTE PENDABLE    00113402
*          SERVICE ROUTINE WILL GENERATE A EXTERNAL CALL SIGNAL         00114102
*          TO THE SPECIFIED CPU WHOSE EXTERNAL CALL SLIH WILL           00114802
*          ROUTE CONTROL TO THE SPECIFIED PROGRAM.                      00115502
*                                                                       00116202
*  OPERATION= THE RP SIGNAL SERVICE ROUTINE TESTS TO SEE IF IT WAS      00116902
*             PASSED A VALID PCCA ADDRESS.  IF THE PCCA ADDRESS         00117602
*             WAS VALID, THE PHYSICAL ADDRESS IN IN THE PCCA WILL BE    00118302
*             USED AS AN OPERAND FOR THE SIGP INSTRUCTION IN ORDER      00119002
*             TO DIRECT THE REQUEST TO THE APPROPRIATE CPU. THE REQUEST 00119902
*             CODE THAT WAS PASSED IN REGISTER 0 IS USED  TO POST A     00121902
*             BIT IN A BUFFER IN THE RECEIVING CPU'S PCCA.  THIS        00122302
*             BIT INDICATES TO THE RECEIVING CPU'S EXTERNAL CALL        00122702
*             SLIH THAT A SPECIFIC PROGRAM IS TO BE GIVEN CONTROL.      00123102
*             AFTER BUILDING THE BUFFER, IEAVERP CALLS IEAVEDR REQUEST- 00123202
*             ING A EXTERNAL CALL ORDER CODE BE SIGP'D TO THE RECEIVING 00123402
*             CPU.  IF THE RETURN CODE IS A 0 OR A 8 WITH THE EXTERNAL  00127702
*             CALL PENDING STATUS SET, THE CALLER IS GIVEN A RETURN     00129702
*             CODE 0.  IF THE RETURN CODE FROM IEAVEDR IS 4 (BUSY COND) 00131702
*             THE REQUEST IS RETRIED.  ALL OTHER IEAVEDR RETURN CODES   00132102
*             ARE PASSED DIRECTLY BACK TO THE CALLER.  EVEN THOUGH THE  00132202
*             SIGP WAS UNSUCCESSFUL THE REQUEST BIT IN THE BUFFER IS    00133302
*             NOT TURNED OFF.  THIS IS BECAUSE THE REQUESTS ARE OR'ABLE 00135302
*             AND ANOTHER CPU MAY HAVE ALSO REQUESTED THE SAME PROGRAM. 00135402
*             IF THE SYSTEM IS A UNIPROCESSOR (NOT EQUIPPED TO SEND     00135602
*             OR RECEIVING SIGP SIGNALS) A RETURN CODE OF 16 WILL BE    00136702
*             GENERATED.                                                00138602
*                                                                       00141102
*NOTES                                                                  00152802
*  DEPENDENCIES=  NONE.                                                 00162802
*    CHARACTER-CODE DEPENDENCIES= NONE                                  00171902
*  RESTRICTIONS=AVAILABLE ONLY TO SUPERVISOR STATE, ZERO PROTECT KEY    00172302
*               CALLERS.                                                00172402
*  REGISTER-CONVENTIONS= REFERENCE EQUATE SECTION                       00172502
*  PATCH-LABEL= NONE, THIS IS A RESIDENT NUCLEUS MODULE.                00172602
*                                                                       00172702
*MODULE TYPE= PROCEDURE                                                 00176502
*  PROCESSOR= ASSEM                                                     00178502
*  MODULE SIZE= N/A                                                     00178902
*  ATTRIBUTES= NUCLEUS,ZERO PROTECT KEY,DISABLED,REFRESHABLE,           00179302
*              SUPERVISOR MODE,ADDRSPC=FIXED.                           00179702
*                                                                       00180102
*ENTRY POINT= IEAVERP                                                   00180202
*  PURPOSE= TO EXECUTE A ROUTINE VIA EXTERNAL CALL ON ANY OF THE        00180302
*          ONLINE MP CONFIGURED CPUS.                                   00180402
*  LINKAGE= THE RPSGNL MACRO WHICH EXPANDS INTO A BALR 14,15.           00180502
*  INPUT= REGISTER 1 - CONTAINS THE PCCA ADDRESS.                       00184302
*         REGISTER 0 - CONTAINS THE SPECIFIC REQUEST CODE.              00186302
*         REGISTER 14 - CONTAINS THE RETURN ADDRESS.                    00186702
*         REGISTER 15 - CONTAINS THE ENTRY POINT ADDRESS.               00187102
*  REGISTERS SAVED= REGISTER 2 THROUGH 5.                               00188002
*  REGISTER USAGE= REGISTER 1 IS USED TO PASS THE PCCA ADDR TO IEAVEDR. 00188102
*                  REGISTER 2 IS USED TO SAVE REGS THROUGH LCCA.        00188202
*                  REGISTER 4 IS USED AS A WORK REGISTER.               00208702
*                  REGISTER 5 USED AS THE BASE REGISTER.                00214502
*                  REGISTER 15 IS USED AS A WORK REG AND EP TO IEAVEDR. 00214902
*  REGISTERS RESTORED= REGISTER 2 THROUGH 5.                            00215402
*                                                                       00215502
*EXIT - NORMAL= IEAVERP.                                                00215602
*  CONDITIONS= NORMAL COMPLETION OF FUNCTION REGARDLESS OF REQUEST      00219002
*             BEING INITIATED OR NOT.                                   00221002
*  OUTPUT= AN INITIATED EXT CALL ORDER IF SUCCESSFUL AND RETURN CODES   00221402
*         INDICATING THE STATUS.                                        00221802
*  RETURN CODES= AS LISTED BELOW.                                       00222202
*  R.C. - 0  -  FUNCTION SUCCESSFULLY INITIATED.  THE FUNCTION          00222302
*              IS NOT NECESSARILY COMPLETED UPON RETURN TO THE          00222402
*              CALLER.                                                  00222502
*  R.C. - 4  -  FUNCTION NOT COMPLETED BECAUSE CPU THAT WAS TO RECEIVE  00225902
*              THE CALL WAS NOT ONLINE.                                 00227902
*  R.C. - 8  -  FUNCTION UNSUCCESSFUL.  STATUS RETURNED IN              00229102
*              REGISTER 0.                                              00229202
*              REGISTER 0 STATUS ON R.C. 8.                             00229302
*              BIT   0 = EQUIPMENT CHECK.                               00229402
*                  1-24 = RESERVED                                      00232802
*                    25 = STOPPED                                       00235202
*                    26 = OPERATOR INTERVENING                          00235602
*                    27 = CHECK STOP                                    00236002
*                    28 = NOT READY                                     00236102
*                    29 = RESERVED                                      00236202
*                    30 = INVALID FUNCTION                              00236302
*                    31 = RECEIVER CHECK                                00237402
*  R.C. - 12 - NOT OPERATIONAL.  THE SPECIFIEC CPU IS EITHER NOT        00239402
*              INSTALLED OR IS NOT CONFIQURED INTO THE SYSTEM OR        00239502
*              IS POWERED OFF.                                          00239602
*  R.C. - 16 - SIGP FUNCTION UNSUCCESSFUL.  CPU IS AN UNIPROCESSOR      00248202
*              AND DOES NOT HAVE SIGP SENDING AND RECEIVING             00250202
*              CAPABILITIES.                                            00252202
*                                                                       00254202
*EXIT - ERROR= NONE.                                                    00256202
*  RETURN CODES= NONE.                                                  00268502
*                                                                       00278502
*EXTERNAL REFERENCES=                                                   00278902
*  ROUTINES= NONE                                                       00279302
*  DATA AREAS=REMOTE PENDABLE BUFFER.                                   00279502
*  CONTROL BLOCKS=                                                      00279702
*                 PSA - R,M.  LCCA - R,M.                               00279902
*                 PCCA - R,M.  CVT - R.  CSD - R.                       00280502
*                                                                       00281102
*TABLES= NONE.                                                          00281502
*                                                                       00281902
*MACROS= ABEND.                                                         00282302
*                                                                       00282402
*  SERIALIZATION= DISABLEMENT.                                          00282702
*                                                                       00285602
*CHANGE ACTIVITY= N/A                                                   00287602
*                                                                       00289602
*MESSAGES= XXXXXXX - MSG. TEXT                                          00290002
*                                                                       00290402
*ABEND CODES= SYSTEM ABEND CODE 07B - INVALID PCCA ADDRESS.             00290802
*                                                                       00291202
*********************************************************************** 00291302
IEAVERP  CSECT                                                          00292002
*        SYMBOLIC EQUATES                                               00303602
REG0     EQU   0                        REGISTER 0                      00315202
REG1     EQU   1                        REGISTER 1                      00326802
REG2     EQU   2                        REGISTER 2                      00338402
REG3     EQU   3                        REGISTER 3                      00350002
REG4     EQU   4                        REGISTER 4                      00400002
REG5     EQU   5                        REGISTER 5                      00450002
REG6     EQU   6                        REGISTER 6                      00500002
REG14    EQU   14                       REGISTER 14                     00550002
REG15    EQU   15                       REGISTER 15                     00600002
ZERO     EQU   0                        0 VALUE                         00650002
STATUSR0 EQU   0                        REGISTER 0                      00660002
MULTBY4  EQU   2                        SLL CONSTANT TO MULT BY 4       00760002
RPBUFFER EQU   3                        REG 3 ADDR OF EXT CALL BUFFER   00800002
CVTREG4  EQU   4                        REG4 BASE FOR CVT               00810002
CSDREG4  EQU   4                        REGISTER 4 BASE FOR CSD         00812002
FOUR     EQU   4                        VALUE 4                         00820002
BASER5   EQU   5                        REG 5 BASE FOR IEAVERP          00850002
EIGHT    EQU   8                        CONSTANT 8                      00860002
RETURN   EQU   14                       CALLERS RETURN ADDRESS          00900002
FUNCCDE  EQU   15                       REG 15 SIGP FUNCTION CODE       00910002
RETCODE  EQU   15                       REG 15, RETURN CODE REG         00950002
DISABLE  EQU   X'FC'                    DISABLE MASK                    01000002
EXTCALL  EQU   2                        EXTERNAL CALL FUNCTION CODE     01100002
         EJECT                                                          01150002
         USING LCCA,REG1                                                01350002
         USING PSA,0                                                    01400002
         USING *,REG15                                                  01450002
BASEADR EQU *                                                           01452002
IEAVERP  MODID BR=YES                                                   01460002
         STNSM PSAIPCR+1,DISABLE        DISABLE CALLER & SAVE SYS MASK  01500002
         ST    REG1,PSAIPCSA            SAVE CALLERS REG 1              01550002
         L     REG1,PSALCCAV            LOCATE LCCA                     01600002
         STM   REG2,REG5,LCCARPR2       SAVE CALLERS REGS 2-5           01650002
         DROP  REG1                                                     01660002
         LR    BASER5,REG15             EST ADDRESSABILITY              01960002
         USING BASEADR,BASER5                                           01972002
         DROP  REG15                                                    01974002
         L     REG1,PSAIPCSA            RESTORE CALLERS REG 1           01980002
         USING PCCA,REG1                                                01990002
         EJECT                                                          02000002
**********************************************************************  02020002
*        ROUTINE VALIDITY CHECKS THE PCCA ADDRESS                       02030002
**********************************************************************  02040002
         LTR   REG1,REG1                WAS PCCA ADDRESS 0?             02040102
         BZ    RPINVPCA                 0 ADDRESS IS INVALID            02040202
         L     CVTREG4,CVTPTR           LOCATE THE CVT                  02040402
         USING CVT,REG4                                                 02040802
         L     REG3,CVTPCCAT            LOCATE PCCAVT                   02041202
         LH    REG2,PCCACPUA            OBTAIN PURPORTED CPUID          02042002
         SLL   REG2,MULTBY4             INDEX VALUE FOR PCCAVT          02044002
         L     REG15,0(REG2,REG3)       OBTAIN PCCA ADDRESS FOR CPUID   02046002
         CR    REG1,REG15               DOES PCCA ADDRESSES AGREE?      02048402
         BNE   RPINVPCA                 INVALID PCCA ADDRESS            02048802
         SPACE 2                                                        02048902
**********************************************************************  02049202
*        PCCA IS A VALID PCCA ADDRESS, DETERMINE IF CPU IS ALIVE        02049602
**********************************************************************  02049702
         L     CSDREG4,CVTCSD           LOCATE THE CSD                  02050002
         USING CSD,REG4                                                 02060002
         LH    REG3,CSDCPUAL            LOCATE SYSTEM ALIVE BITS        02100002
         LH    REG4,PCCACAFM            LOCATE THE RECEIVING CPU ALIVE  02150002
*                                       MASK                            02200002
         NR    REG4,REG3                AND THE MASKS TO DETERMINE IF   02250002
*                                       THE RECEIVING CPU IS ONLINE     02300002
         BZ    RPRC4                    THE RECEIVING CPU IS NOT ONLINE 02350002
         LR    FUNCCDE,REG0             LOAD CALLERS FUNCTION CODE      02400002
         LA    RPBUFFER,ZERO            CHECK FOR EMPTY BUFFER MASK     02450002
RPCSLOOP EQU   *                                                        02500002
         CS    RPBUFFER,FUNCCDE,PCCARPB COMPARE AND SWAP THE FUNCTION   02550002
*                                       ON IN THE REMOTE PENDABLE       02600002
*                                       BUFFER IN RECEIVING CPUS PCCA   02650002
         BZ    RPSIGP                   FUNCTION CODE WAS C&S ON        02700002
         LR    FUNCCDE,RPBUFFER         BUFFER IS NOT EMPTY INCLUDE     02750002
*                                       PRIOR BITS IN C&S               02800002
         OR    FUNCCDE,REG0             CALLERS FUNCTION CODE ADDED     02810002
*                                       TO VALUE ALREADY IN BUFFER      02820002
         B     RPCSLOOP                 REPEAT THE COMPARE AND SWAP     02850002
         EJECT                                                          02890002
**********************************************************************  02892002
*        REMOTE PENDABLE'S BUFFER HAS BEEN ESTABLISHED FOR THIS         02894002
*        REQUEST.  THE CALLERS RETURN ADDRESS IS SAVED.  THE PCCA       02896002
*        ADDRESS IS SAVED IN CASE OF A BUSY CONDITION IS RETURNED       02898002
*        FROM DIRECT SIGNAL.  DIRECT SIGNAL IS CALLED TO ISSUED A       02898402
*        SIGP WITH AN EXTERNAL CALL ORDER CODE.                         02898802
**********************************************************************  02899202
RPSIGP   EQU   *                                                        02900002
         L     REG2,PSALCCAV            OBTAIN LCCA FOR SAVING REGS     02950002
         ST    REG14,LCCADSR2-LCCA(REG2) SAV R14 IN IEAVEDR'S S.A.      02970002
         ST    REG1,LCCADSR3-LCCA(REG2)  SAV R1 IN IEAVEDR'S S.A.       02990002
         ST    REG5,LCCADSR4-LCCA(REG2)  SAV R5 (BASE REG)              03010002
RPSIGPRT EQU   *                                                        03050002
         LA    REG0,EXTCALL             LOAD EXT CALL FUNCTION CODE     03100002
         L     REG2,CVTPTR              SET CVT ADDR IN R2 FOR IEAVEDR  03110002
         L     REG15,AIEAVDR2           OBTAIN IEAVEDR SECONDARY ENTRY  03120002
         L     REG5,AIEAVEDR            ESTABLISH BASE FOR IEAVEDR      03150002
         BALR  REG14,REG15              ENTER IEAVEDR AT SECONDARY ENTY 03170002
         L     REG2,PSALCCAV            OBTAIN LCCA ADDRESS FOR S.A     03180002
         L     REG5,LCCADSR4-LCCA(REG2) RE-ESTABLISH ADDRESSABILITY     03190002
         LTR   RETCODE,RETCODE          WAS DIRECT SIGNAL SUCCESSFUL    03200002
         BZ    RPEXIT                   SUCCESSFUL SIGP INITIATED       03250002
         C     RETCODE,BUSY             WAS SIGP TEMPRORARY BUSY        03300002
         BNE   RPRCCHK                  NOT BUSY, CONTINUE RC CHECK     03350002
         L     REG1,LCCADSR3-LCCA(REG2) RE-OBTAIN SAVED PCCA ADDR       03400002
         B     RPSIGPRT                 REPEAT SIGP UNTIL TEMPORARY     03450002
*                                       BUSY CONDITION CLEARS           03500002
RPRCCHK  EQU   *                                                        03550002
         C     RETCODE,RCEIGHT          WAS SIGP UNSUCCESSFUL AND       03600002
*                                       STATUS STORED?                  03650002
         BNE   RPEXIT                   NO, CHECK FOR UNIPROCESSOR      03700002
         C     STATUSR0,EXTCPEND        EXTERNAL CALL PENDING           03750002
         BNE   RPEXIT                   NO, RETURN IEAVEDR'S RET CODE   03800002
         LA    RETCODE,ZERO             EXT CALL PENDING, TREAT LIKE    03850002
*                                       SUCCESSFUL INITIATION           03900002
         B     RPEXIT                   RETURN TO CALLER                03950002
         EJECT                                                          03950102
**********************************************************************  03950402
*        THE PCCA ADDRESS SUPPLIED BY THE CALLER IS INVALID             03950802
*        CALLER IS ABENDED WITH A SYSTEM ABEND CODE OF XXX              03951202
**********************************************************************  03951602
RPINVPCA EQU   *                                                        03952002
         L     REG2,PSALCCAV                                            03970002
         USING LCCA,REG2                                                03980002
         LM    REG2,REG5,LCCARPR2       RESTORE CALLERS REGS 2-5        03990002
         EX    REG0,PSAIPCR             RESTORE CALLERS SYSTEM MASK     03990402
         ABEND X'07B',DUMP,,SYSTEM                                      03992002
RPRC4    EQU   *                                                        04450002
         LA    RETCODE,FOUR             RETURN CODE FOUR NOT ONLINE     04460002
         L     REG2,PSALCCAV                                            04463002
         USING LCCA,REG2                                                04466002
         B     RPEXIT2                  RETURN TO CALLER                04470002
RPEXIT   EQU   *                                                        04500002
         L     REG14,LCCADSR2-LCCA(REG2) RESTORE RETURN REG             04510002
RPEXIT2  EQU   *                                                        04520002
         LM    REG2,REG5,LCCARPR2       RESTORE CALLERS REGS 2-5        04700002
         EX    REG0,PSAIPCR             RESTORE CALLERS SYSTEM MASK     04750002
         BR    RETURN                   RETURN TO CALLER                04800002
         DS    0F                                                       04850002
AIEAVEDR DC    V(IEAVEDR)               ADDRESS OF IEAVEDR MAIN ENTRY   04860002
AIEAVDR2 DC    V(IEAVEDR2)              ADDRESS OF IEAVEDR'S 2ND ENTRY  04870002
*                                       USED BY IEAVERP AND IEAVERI     04880002
BUSY     DC    F'4'                     CONSTANT TO TEST FOR BUSY COND. 04900002
RCEIGHT  DC    F'8'                     CONSTANT TO TEST FOR RC 8.      04950002
EXTCPEND DC    X'00000080'              EXTERNAL CALL PENDING MASK      05000002
         IHAFRRS                                                        05100002
         IHAPCCA                                                        05150002
         IHALCCA                                                        05200002
         IHACSD                                                         05250002
         IHAPSA                                                         05260002
         CVT   LIST=YES,DSECT=YES,PREFIX=NO                             05300002
         END   IEAVERP                                                  05350002
