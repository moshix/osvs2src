         TITLE 'IEDAYY TSO TIME DELAY REMOVAL RTN'                      01000020
IEDAYY   CSECT                                                          02000020
         SPACE 3                                                        02000105
*  CHANGE ACTIVITY AS FOLLOWS                                           02000205
******************** MICROFICHE FLAGS *********************** SUPT CODE 02000305
*A550000                                                        SA44022 02000405
*C280000                                                         S21008 02000505
*C270000-290000                                                  S21903 02000605
*C430000,440000                                                 SA67755 02000705
*C430000-440000                                                @OZ07823 02000858
*D640000                                                       @OZ07823 02000958
*********************************************************************** 02010020
*TITLE  'IEDAYY'  ASYNCHRONOUS TIME DELAY REMOVAL ROUTINE             * 02020020
*                                                                     * 02030020
*  MODULE NAME = IEDAYY                                               * 02040005
*                                                                     * 02041005
*  DESCRIPTIVE NAME = ASYNCHRONOUS TIME DELAY REMOVAL ROUTINE         * 02042005
*                                                                     * 02043005
*  COPYRIGHT = 'NONE'                                                 * 02044005
*                                                                     * 02045005
*  STATUS:  CHANGE LEVEL 5                                            * 02046005
*                                                                     * 02050020
*FUNCTION -- THE ASYNCHRONOUS TIME DELAY REMOVAL ROUTINE REMOVES      * 02060020
*   QCB'S FROM THE TIME DELAY QUEUE WHEN A SEND OR RECEIVE OPERATION  * 02070020
*   IS TO BE INITIATED.  THE ROUTINE FIRST CHECKS TO SEE IF IT HAS    * 02080020
*   BEEN ACTIVATED (POSTED TO THE READY QUEUE) BY A TIOC MODULE.  IF  * 02090020
*   NOT, IT RETURNS CONTROL TO THE DISPATCHER TO DISPATCH THE NEXT    * 02100020
*   SUBTASK.  IF IT HAS BEEN ACTIVATED, IT DEACTIVATES ITSELF (MARKS  * 02110020
*   ITS QCB NOT POSTED) AND GETS THE ADDRESSES OF THE PREVIOUS AND    * 02120020
*   CURRENT ELEMENTS ON THE TIME DELAY QUEUE.  IT THEN SCANS THE      * 02130020
*   TIME DELAY QUEUE LOOKING FOR A TIME-SHARING QCB WITH BOTH THE     * 02140020
*   WRITE BREAK FLAG AND THE TPUT REQUEST FLAG ON.  EACH TIME IT      * 02150020
*   FINDS ONE, IT UPDATES THE LINK FIELD OF ELEMENTS ON THE TIME      * 02160020
*   DELAY QUEUE, SETS THE TIME DELAY FLAG IN THE QCB TO INDICATE      * 02170020
*   THAT IT IS NO LONGER IN THE TIME DELAY QUEUE, POSTS THE QCB TO    * 02180020
*   ITSELF TO INITIATE A SEND OPERATION, LOADS THE POST REGISTER      * 02190020
*   (R1) WITH THE ADDRESS OF THE QCB TO BE POSTED TO THE READY QUEUE, * 02200020
*   AND LINKS TO THE DISPATCHER POST FUNCTION TO POST THE QCB.  UPON  * 02210020
*   RETURN FROM THE DISPATCHER, THIS ROUTINE CONTINUES ITS SCAN OF    * 02220020
*   THE TIME DELAY QUEUE, LOOKING FOR THE NEXT QCB TO BE REMOVED.     * 02230020
*   WHEN THE ENTIRE QUEUE HAS BEEN SEARCHED, AND ALL APPLICABLE QCB'S * 02240020
*   REMOVED, THIS ROUTINE BRANCHES BACK TO THE BEGINNING TO SEE IF    * 02250020
*   ANOTHER INTERRUPT HAS OCCURRED TO ACTIVATE IT AGAIN.  IF NOT, IT  * 02260020
*   RETURNS TO THE DISPATCHER.  IF IT HAS, THE ENTIRE PROCEDURE IS    * 02270020
*   REPEATED.                                                         * 02280020
*                                                                     * 02290020
*ENTRY POINT -- IEDAYY - TO REMOVE QCB'S FROM THE TIME DELAY QUEUE,   * 02300020
*   WHEN POSTED ASYNCHRONOUSLY FROM TIOC MODULES.                     * 02310020
*   CALLING SEQUENCE          L    R15,TSIDYQCB                       * 02320020
*                             BR   R15                                * 02330020
*                                                                     * 02340020
*INPUT -- IEDAYY IS ENTERED FROM THE DISPATCHER, AFTER BEING POSTED   * 02350020
*   ASYNCHRONOUSLY TO THE READY QUEUE BY A TIOC MODULE, WITH THE      * 02360020
*   FOLLOWING REGISTERS SET.                                          * 02370020
*   R11 HAS THE DISPATCHER ADDRESS                                    * 02380020
*   R13 HAS THE ADDRESS OF AVTSAVE2                                   * 02390020
*   R15 HAS THE ENTRY POINT ADDRESS                                   * 02400020
*                                                                     * 02410020
*OUTPUT -- IEDAYY LINKS TO THE DISPATCHER POST FUNCTION WITH THE      * 02420020
*   FOLLOWING REGISTERS SET.                                          * 02430020
*   R1 HAS THE ADDRESS OF THE QCB TO BE POSTED TO THE READY QUEUE     * 02440020
*   R14 HAS THE RETURN ADDRESS IN IEDAYY                              * 02450020
*                                                                     * 02460020
*EXTERNAL ROUTINES -- DISPATCHER POST FUNCTION - POSTS THE QCB(S) TO  * 02470020
*   THE READY QUEUE BY PRIORITY.                                      * 02480020
*                                                                     * 02490020
*EXITS-NORMAL -- TO THE DISPATCHER DISPATCH FUNCTION                  * 02500020
*                                                                     * 02510020
*EXITS-ERROR  -- NONE                                                 * 02520020
*                                                                     * 02530020
*TABLES/WORKAREAS -- AVT, QCB, TSID.                                  * 02540020
*                                                                     * 02550020
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, ENABLED, PROBLEM       * 02560020
*   PROGRAM MODE                                                      * 02570020
*                                                                     * 02580020
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         * 02590020
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      * 02600020
*   SET.                                                              * 02610020
*                                                                     * 02620020
*********************************************************************** 02630020
RECB     EQU   1                       ADDR OF QCB TO POST TO RDY Q.    03000020
RWORK    EQU   3                       WORK REGISTER                    04000020
RELE     EQU   4                       ADDR OF PREVIOUS ELEMENT ON      05000020
*                                      TIME DELAY QUEUE                 06000020
RQCB     EQU   5                       ADDR OF CURRENT ELEMENT ON       07000020
*                                      TIME DELAY QUEUE                 08000020
RTSINPUT EQU   6                       ADDR OF TSINPUT QCB              09000020
RDISP    EQU   11                      BASE ADDR OF DISPATCHER          10000020
RBASE    EQU   12                      BASE ADDR OF IEDAYY              11000020
RAVT     EQU   13                      ADDR OF AVTSAVE2 IN AVT          12000020
RLINK    EQU   14                      LINKAGE REGISTER TO DSPPOSTR     13000020
R15      EQU   15                      ADDR OF ENTRY POINT TO IEDAYY    14000020
         USING AYY010,RBASE                                             15000020
         USING IEDQTSI,RTSINPUT                                         16000020
         USING AVTSAVE2,RAVT                                            17000020
         USING IEDQDISP,RDISP                                           18000020
         USING IEDQQCB,RQCB                                             19000020
QCBMASK  EQU   X'FF'                   MASK TO TEST IF CURRENT ELEMENT  20000020
*                                      ON TIME DELAY QUEU IS A QCB      21000020
ELEINDEX EQU   X'12'                   ELEMENT TYPE IDENTIFIER          22000020
ELELINK  EQU   X'1D'                   LINK FKELD OF ELEMENTS ON TIME Q 23000020
EQUAL    EQU   8                                                        24000020
         DC    AL1(DSPMCPL2),AL1(0)    IEDAYY STCB                      25000020
         USING *,R15                                                    26000020
IEDAYY   IEDHJN AYY000                  MODULE NAME AND DATE     S21903 27000005
         DROP  R15                                                      30000020
         BALR  RBASE,0                                                  31000020
AYY010   EQU   *                                                        31500020
         L     RTSINPUT,AVTTSOPT       OBTAIN ADDR OF TSINPUT QCB       32000020
         TM    TSIDYQFG,TSIDYPOS       DELAY REMOVAL RTN ACTIVATED?     34000020
         BZ    DSPDISP                 NO. RETURN TO DISPATCHER.        35000020
         NI    TSIDYQFG,255-TSIDYPOS   RESET POSTED FLAG                36000020
AYY020   EQU   *                                                        36500020
         LA    RELE,AVTDELYB           START WITH TIME DELAY QCB        37000020
AYY025   EQU   *                                                        38000020
         L     RQCB,ELELINK-1(RELE)    GET NEXT ELEMENT ON DELAY QUEUE  39000020
         LA    RQCB,0(,RQCB)           CLEAR HI-ORDER BYTE              40000020
         C     RQCB,AVTDELYB           HAVE WE SEARCHED ENTIRE QUEUE?   41000020
         BE    AYY010                  YES. CHECK FOR POST.             42000020
         CLI   QCBRETCT,AVTEZERO        ELEMENT A QCB          @OZ07823 43000058
         BE    AYY040                   BRANCH YES             @OZ07823 44000058
*                                      OTHERWISE, SCAN TO NEXT ELEMENT  45000020
AYY030   EQU   *                                                        46000020
         LR    RELE,RQCB               UPDATE PREVIOUS ELEMENT POINTER  47000020
         B     AYY025                                                   48000020
AYY040   EQU   *                                                        49000020
         TM    QCBFLAG,QCBTSSES        IS THIS A TIME-SHARING QCB?      50000020
         BZ    AYY030                  NO. SCAN TO NEXT ELEMENT.        51000020
         TM    QCBTSOF1,QCBWRBRK+QCBTPUT+QCBTGET  Q HAS A SEND OR 21008 51700021
*                                       RECEIVE OPERATION BEEN    21008 52400021
*                                       REQUESTED FOR THIS QCB    21008 53100021
         BZ    AYY030                  NO. SCAN TO NEXT ELEMENT.        54000020
         LR    RECB,RQCB                LOAD PARAMETER REGISTER WITH    54200020
*                                       QCB ADDRESS FOR TIME DELAY RTN  54400020
*                                       AND DISPATCHER POST RTN         54600020
         L     R15,AVTHG02              ADDR OF TIME DELAY REMOVAL RTN  54800020
         BALR  RLINK,R15                GO REMOVE ELEMENT FROM DELAY Q  55000020
         NI    QCBTSOF1,X'FF'-QCBDELAY     TURN OFF DELAY BIT    A44022 55200030
         OI    QCBTSOF2,QCBPOSTO        INDICATE QCB POSTED             55500020
         IC    RWORK,QCBDSFLG          POST QCB TO ITSELF IN ORDER TO   57000020
         ST    RQCB,QCBELCHN-1         INITIATE A SEND OPERATION.       58000020
         STC   RWORK,QCBDSFLG          RESTORE QCB FLAG BYTE            59000020
         BAL   RLINK,DSPPOSTR          GO POST ELEMENT TO READY QUEUE   62000020
         B     AYY020                                                   63000020
         TAVTD                                                          65000020
         TDISPD                                                         66000020
         TQCBD                                                          67000020
         TTSID                                                          68000020
         END                                                            69000020
