*/*                                                                     00010010
         MACRO                                                          00020010
&NAME    IEDDCT    &REG=1,&FLD=,&LEN=4                                  00030010
         LCLA      &NDX                                                 00040010
         LCLB      &KGT1,&LGT6,&L1LT0                                   00050010
         LCLC      &OPS,&MLEN                                           00060010
.*A000000-999999                                               @Y17XAYO 00070010
.*A003820-004060,005650-005850                                 @Y17XAMO 00073010
.*C001700,002600,002900,003400,003800,006500-007060            @Y17XAMO 00076010
&NDX     SETA      2                                                    00080010
&NAME    SR        &REG,&REG      CLEAR FOR IC                          00090010
         IC        &REG,TRMCHCIN  PICK UP DCT INDEX                     00100010
         BCTR      &REG,0         DECREMENT FOR MULTIPLY                00110010
         MH        &REG,AVTDCTLN  GET DCT ADDRESS OFFSET                00120010
         A         &REG,AVTCSTCS  GET ADDRESS OF DCT ENTRY              00130010
         AIF       ('&FLD' EQ '').DONE   NO MOVE ->                     00140010
         AIF       ('&LEN' GT '1').NON0LEN CHECK FOR 0 LEN SPEC         00150010
         MNOTE     0,'ZERO/ONE LENGTH MOVE SPECIFIED - NO MOVE DONE'    00160010
         AGO       .DONE  EXIT                                 @Y17XAMO 00170010
* THE FOLLOWING SEVEN LINES DETERMINE IF THE &LEN SPEC IS A SYMBOL OR   00180010
* A CONSTANT, AND IF A CONSTANT THAT THE VALUE IS SIX OR LESS.          00190010
.NON0LEN ANOP                                                           00200010
&MLEN    SETC      '&LEN'                                               00210010
&KGT1    SETB      (K'&LEN GT 1)                                        00220010
&LGT6    SETB      ('&LEN' GT '6')                                      00230010
&L1LT0   SETB      ('&LEN'(1,1) LT '0')                                 00240010
         AIF   ((&KGT1 AND &L1LT0) OR (NOT(&KGT1 OR &LGT6))).LENOK      00250010
&MLEN    SETC      '4'                                         @Y17XAMO 00260010
.LENOK   AIF       ('&FLD'(K'&FLD,1) EQ ')').BASED                      00270010
         MVC       &FLD.(&MLEN),0(&REG) MOVE THE DCT ENTRY              00280010
         AGO       .CHECK                                      @Y17XAMO 00290010
.BASED   AIF       ('&FLD'(&NDX,1) EQ '(').FOUND                        00300010
&NDX     SETA      &NDX+1                                               00310010
         AIF       (&NDX LT K'&FLD).BASED                               00320010
         MNOTE     12,'INVALID ''TO'' OPERAND SPECIFIED: &FLD'          00330010
         AGO       .DONE  EXIT                                 @Y17XAMO 00340010
.FOUND   ANOP                                                           00350010
&OPS     SETC     '&FLD'(1,&NDX).'&MLEN'.','.'&FLD'(&NDX+1,K'&FLD-&NDX) 00360010
         MVC       &OPS,0(&REG)                                         00370010
*                                MOVE THE DCT ENTRY                     00375010
.CHECK   TM        TRMSTATE,TRMPREF   IS THERE A PREFIX?       @Y17XAMO 00380010
         BNO       $DCT&SYSNDX   NO ->                         @Y17XAMO 00382010
         CLI       TRMTYPE,TRMLUNT   SNA LOGICAL UNIT?         @Y17XAMO 00384010
         BNE       $DCT&SYSNDX  NO ->                          @Y17XAMO 00386010
         ICM       &REG,7,TRMSIBPT PICK UP THE SIB ADDRESS     @Y17XAMO 00388010
         TM        SIBSTAT0-IEDSIBD(&REG),X'7C' LUTYPE 0,1,2,3 @Y17XAMO 00390010
         BNZ       $DCT&SYSNDX  NO ->                          @Y17XAMO 00392010
         TM        SIBSTAT0-IEDSIBD(&REG),X'03' LUTYPE 0 OR 3? @Y17XAMO 00394010
         BNM       #DCT&SYSNDX  YES ->                         @Y17XAMO 00396010
         TM        SIBSTAT0-IEDSIBD(&REG),X'02' IS IT LUTYPE 2 @Y17XAMO 00398010
         BO        #DCT&SYSNDX  YES ->                         @Y17XAMO 00400010
$DCT&SYSNDX LA        &REG,&FLD       PICK UP TARGET ADDRESS   @Y17XAMO 00402010
         B         @DCT&SYSNDX  SKIP AROUND 3270 BIT SETTING   @Y17XAMO 00404010
#DCT&SYSNDX LA        &REG,&FLD       PICK UP TARGET ADDRESS   @Y17XAMO 00406010
         OI        DCTBYTE1-IEDDCT(&REG),DCT3270    SET 3270 BIT        00410010
@DCT&SYSNDX DS        0H                                                00420010
         AGO       .DONE                             */                 00430010
*%IEDDCT: MACRO KEYS(REG,FLD,LEN);                                      00440010
*         DCL (REG,FLD,LEN)    CHAR;                                    00450010
*         DCL (MREG,MFLD,MLEN,SLEN) CHAR;                               00460010
*         DCL FLEN             FIXED;                                   00470010
*         MREG='?'||MACLABEL||' '||MACNAME||' '||MACKEYS||';';          00480010
*         ANS(COMMENT(MREG)) SKIP  COL(MACCOL);                         00490010
*         IF REG(1)='' THEN                                             00500010
*            ANS('/* THE ''REG'' KEYWORD MUST BE SPECIFIED */')  SKIP   00510010
*            MSG('/* THE ''REG'' KEYWORD MUST BE SPECIFIED */');        00520010
*         ELSE DO;                                                      00540010
*              MREG=REG(1);   MFLD=FLD(1);                              00550010
* ANS('/* SET UP ADDRESSABILITY TO THE DCT ENTRY */') COL(9);           00555010
* ANS(MACLABEL||MREG||'=AVTCSTCS+(TRMCHCIN-1)*AVTDCTLN;') SKIP COL(10); 00560010
*              IF MFLD^='' THEN DO; /* MOVE REQUESTED            */     00570010
*              MLEN=LEN(1);                                             00590010
*              IF MLEN='' THEN MLEN='4';                                00600010
*              ELSE IF INDEX('0123456789',MLEN(1:1))^=0 THEN DO;        00610010
*              FLEN=FIXED(MLEN); IF FLEN>6 THEN MLEN='4';               00620010
*              ELSE IF FLEN<2 THEN MLEN='4';                 END;       00625010
*              IF INDEX(MFLD,'(')=0 THEN SLEN='(1:'||MLEN||')';         00630010
*              ELSE SLEN='';                                            00640010
* ANS('/* MOVE THE DCT ENTRY TO THE SPECIFIED WORKAREA */') COL(9);     00645010
* ANS(MFLD||SLEN||' = '||MREG||'->IEDDCT(1:'||MLEN||');') SKIP COL(10); 00650010
* ANS('/* SET POINTER TO THE TARGET ADDRESS */') COL(9);                00651010
* ANS(MREG||'=ADDR('||MFLD||'); /*SET UP PTR*/') SKIP COL(15);          00652010
* ANS('/* IS THE DCT FOR AN LU WITH 3270 DATA STREAM */') COL(9);       00653010
* ANS('IF (TRMPREF=''1''B  & TRMTYPE=TRMLUNT & (TRMSIBPT->SIBLUTYP = '||00660010
*'''0000000''B | TRMSIBPT->SIBLUTYP=''0000010''B | TRMSIBPT->SIBLUTYP'||00663010
*' = ''0000011''B)) THEN') COL(10);                                     00666010
* ANS('/* YES - THEN SET THE DCT3270 BIT IN THE COPY MADE */') COL(9);  00669010
* ANS(MREG||'->DCT3270 = ''1''B; /* SET 3270 BIT */') SKIP COL(15);     00698010
*              END; END;                                                00710010
*%END IEDDCT;                         /*                                00720010
.DONE    ANOP                                                           00730010
         MEND                                */                         00740010
