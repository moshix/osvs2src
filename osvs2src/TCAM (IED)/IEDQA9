         TITLE 'IEDQA9  -  REDIAL ROUTINE'                              00400022
IEDQA9   CSECT                                                          00800022
*A652000                                                        SA68666 01000005
*A508000                                                       @OX12501 01100010
*********************************************************************** 01200022
*                                                                     * 01500022
* STATUS -- CHANGE LEVEL 10                                           * 01800010
*                                                                     * 02100022
* FUNCTION -- IF THE CPU FAILS TO INITIATE CONTACT WITH A             * 02400022
*    SWITCHED TERMINAL AND THE RETRY MACRO HAS BEEN CODED IN          * 02700022
*    THE INMSG SUBGROUP, THE REDIAL ROUTINE WILL CAUSE THE CPU        * 03000022
*    TO TRY AGAIN TO INITIATE CONTACT AT AN INTERVAL OF TIME          * 03300022
*    SPECIFIED IN THE RETRY MACRO. THIS ROUTINE IS ACTIVATED BY       * 03600022
*    BUFFER DISPOSITION, (IEDQBD), AND GOES TO THE TIME DELAY         * 03900022
*    ROUTINE TO PLACE A QCB ON THE TIME DELAY QUEUE.                  * 04200022
*                                                                     * 04500022
* ENTRY POINTS --                                                     * 04800022
*                                                                     * 05100022
*         FIRST EXECUTABLE INSTRUCTION.                               * 05400022
*                                                                     * 05700022
* INPUT --                                                            * 06000022
*                                                                     * 06300022
*    R1 HAS THE ADDRESS OF A LIST OF THINGS TO BE POSTED              * 06600022
*    R4 HAS THE LCB ADDRESS                                           * 06900022
*    R11 HAS THE DISPATCHER ADDRESS                                   * 07200022
*    R12 HAS THE ENTRY POINT ADDRESS                                  * 07500022
*    R13 HAS THE ADDRESS OF AVTSAVE2                                  * 07800022
*                                                                     * 08100022
* OUTPUT --                                                           * 08400022
*                                                                     * 08700022
*    THE QCB WILL BE GIVEN TO THE TIME DELAY ROUTINE.                 * 09000022
*                                                                     * 09300022
* EXTERNAL REFERENCES --                                              * 09600022
*                                                                     * 09900022
*         .IEDQHG01 -- TIME DELAY SUBTASK - TO INSERT A QCB ON THE    * 10200022
*         TIME DELAY QUEUE.                                           * 10500022
*                                                                     * 10800022
*         .IEDQTNT -- TO FIND THE TERMINAL ENTRY FROM THE DESTINATION * 11100022
*         OFFSET.                                                     * 11400022
*                                                                     * 11700022
*         .IGG019RB OR IGG019RO -- TCAM DISPATCHER                    * 12000022
*                                                                     * 12300022
* EXITS,NORMAL --                                                     * 12600022
*                                                                     * 12900022
*         RETURN IS MADE ON R11 WITH AN OFFSET TO HAVE THE DISPATCHER * 13200022
*         PERFORM THE DESIRED FUNCTION.                               * 13500022
*                                                                     * 13800022
* EXITS,ERROR --                                                      * 14100022
*                                                                     * 14400022
*         NONE                                                        * 14700022
*                                                                     * 15000022
* TABLES/WORK AREAS --                                                * 15300022
*    AVT                                                              * 15600022
*    QCB                                                              * 15900022
*    LCB                                                              * 16200022
*    SCB                                                              * 16500022
*    TERMINAL TABLE                                                   * 16800022
*                                                                     * 17100022
* ATTRIBUTES --                                                       * 17400022
*    REUSABLE,ENABLED,RESIDENT                                        * 17700022
*                                                                     * 18000022
* CHARACTER CODE DEPENDENCY --                                        * 18300022
*    THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A              * 18600022
*    PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL               * 18900022
*    CHARACTER SET.                                                   * 19200022
*                                                                     * 19500022
* NOTES --                                                            * 19800022
*    STANDARD TCAM LINKAGES AND DEVIATIONS FROM STANDARDS.            * 20100022
*                                                                     * 20800022
*********************************************************************** 21200022
         EJECT                                                          21600022
*********************************************************************** 22000022
*                      REGISTER EQUATES                               * 22400022
*********************************************************************** 22800022
*                                                                     * 23200022
R0       EQU   0                        WORK REGISTER                   23600022
R1       EQU   1                        LIST OF ELEMENTS TO BE POSTED   24000022
R2       EQU   2                        WORK REGISTER                   24400022
R3       EQU   3                        SCB ADDRESS                     24800022
R4       EQU   4                        LCB ADDRESSABILITY REGISTER     25200022
RLCB     EQU   4                        LCB ADDRESS                     25600022
R5       EQU   5                        WORK REGISTER                   26000022
RPREFIX  EQU   6                        WORK REGISTER                   26400022
RQCB     EQU   7                        QCB ADDRESS                     26800022
R8       EQU   8                        ADDRESS OF PARAMETER LIST       27200022
R9       EQU   9                        WORK REGISTER                   27600022
R10      EQU   10                       WORK REGISTER                   28000022
RDISP    EQU   11                       TCAM DISPATCHER ADDRESS         28400022
RBASE    EQU   12                       BASE REGISTER                   28800022
RAVT     EQU   13                       SAVE AREA ADDRESS               29200022
R14      EQU   14                       RETURN ADDRESS                  29600022
R15      EQU   15                       ENTRY POINT ADDRESS             30000022
*                                                                     * 30400022
*********************************************************************** 30800022
*                      USING STATEMENTS                               * 31200022
*                                                                     * 31600022
*********************************************************************** 32000022
         USING IEDQQCB,RQCB             QCB                             32300022
         USING IEDQDISP,RDISP           DISPATCHER                      32600022
         USING IEDQTRM,R1               TERMINAL ENTRY                  32900022
         USING IEDQSCB,R3               SCB                             33200022
         USING IEDQLCB,R4               LCB                             33500022
         USING AVTSAVE2,RAVT            AVT                             33800022
         USING *,RBASE                  CSECT BASE                      34100022
*********************************************************************** 34800022
         DS    0F                                                       35200022
REDIAL   EQU   *                                                        35600022
         B     REDIAL1                  BRANCH AROUND MODULE ID         36000022
         DC    C'IEDQA9'                MODULE NAME                     36400022
         DC    X'2054'                  02/23/72                        36800022
REDIAL1  EQU   *                                                        37200022
         ST    R1,LCBERBLK-1            SAVE POST LIST                  37600022
         L     R5,AVTMSGS-1             ADDR OF LIST OF VCONS           38000022
         L     R5,0(R5)                 ADDR OF BD                      38400022
         ST    R5,LCBERBQB-1            TPOST ERB TO BD QCB             38800022
         MVI   LCBERBPY,PRIRCQCB        SET ERB PRIORITY                39200022
         XC    LCBERBCH(3),LCBERBCH     ADDR CHAIN TO BE ASSIGND        39600022
*                                         BUFFERS                       40000022
         LH    R1,LCBTTCIN              DESTINATION OFFSET              40800022
         N     R1,AVTCLRHI              CLEAR HIGH ORDER HALF WORD      40900022
         BZ    TPOSTERB                 IF ZERO, TPOST ERB              41000022
         L     R15,AVTRNMPT                                             41200022
         BALR  R14,R15                  GET TERMINAL TABLE ENTRY        41600022
         L     RQCB,TRMDESTQ-1          GET QCB ADDRESS                 42000022
         CLI   QCBSTPRI,CALLOPT         THIS CLOCK OR CINTVL   @Y17XAMG 42100010
         BL    TPOSTERB                 BR NO                  @YM08436 42120010
         L     R3,LCBSCBA-1             GET SCB ADDRESS                 42400022
         TM    SCBERR4,SCBCONNN         ERROR IN CONNECTION             42600022
         BZ    TPOSTERB                 IF NOT, TPOST ERB TO BD         42800022
         SR    R9,R9                    CLEAR REGISTER                  43200022
         LH    R9,TRMDEVFL              GET DEVICE FLAGS                43600022
         SLL   R9,TWEN6                 SHIFT TO RETRY BIT              44000022
         LTR   R9,R9                    RETRY VALUE SPECIFIED           44400022
         BNL   TPOSTERB                 IF NOT, GO TPOST ERB            44800022
         TM    TRMSTATE,TRMOPTFN        OPTION FIELDS SPECIFIED         45200022
         BO    OPTFL                    BRANCH IF YES                   45600022
         LA    R5,TRMOPNO               GET ADDR OF FIRST DEVICE        46000022
*                                         DEPENDENT FIELD               46400022
         B     SCAN                     GO FIND RETRY COUNT FIELD       46800022
OPTFL    EQU   *                                                        47200022
         SR    R2,R2                    CLEAR REGISTER TO ZERO          47600022
         IC    R2,TRMOPNO               NUMBER OF OPTION FIELDS         48000022
         LA    R5,TRMOPT                ADDR OF OPTION FLDS TABLE       48400022
         AR    R5,R2                    ADDR OF FIRST DEVICE DEP        48800022
*                                         FIELD                         49200022
SCAN     EQU   *                                                        49600022
         SR    R2,R2                    CLEAR REGISTER TO ZERO          50000022
         SR    R3,R3                    CLEAR REGISTER TO ZERO          50400022
         LH    R2,TRMDEVFL              DEV DEP FIELD FLAGS             50800022
         N     R2,AVTCLRHI              CLEAR HIGH ORDER 2 BYTE@OX12501 51000010
         SR    R10,R10                  CLEAR CNTR OF D.D. FIELDS       51200022
         SRL   R2,FIVE                  ELIMINATE FIVE BITS             51600022
SHIFT    EQU   *                                                        52000022
         SRDL  R2,ONE                   MOVE IN ONE BIT                 52400022
         SRL   R3,THIRTY1               MAKE REG ONE OR ZERO            52800022
         AR    R10,R3                   BUMP D.D. COUNTER               53200022
         LTR   R2,R2                    ALL BITS TESTED                 53600022
         BNZ   SHIFT                    BRANCH IF NOT                   54000022
BUMP     EQU   *                                                        54400022
         CH    R10,HONE                 RETRY COUNT FOUND               54800022
         BE    UPDATE                   BRANCH IF YES                   55200022
         IC    R2,0(0,R5)               SIZE OF D.D. FIELD              55600022
         LA    R5,1(R2,R5)              BUMP PAST THIS D.D.FIELD        56000022
         BCT   R10,BUMP                 CONTINUE LOOP TO RETRY COUNT    56400022
UPDATE   EQU   *                                                        56800022
         IC    R10,1(R5)                GET SPECIFIED RETRY COUNT       57200022
         SR    R3,R3                    CLEAR REGISTER TO ZERO          57600022
         IC    R3,2(R5)                 GET NO. TIMES ALREADY           58000022
*                                         RETRIED                       58400022
         CR    R3,R10                   COMPARE THE TWO VALUES          58800022
         BL    DELAY                    BRANCH IF RETRY COUNT HAS       59200022
*                                         NOT BEEN REACHED              59600022
         MVI   2(R5),AVTEZERO           ZERO OUT RETRY COUNT            60000022
         B     TPOSTERB                 GO TPOST ERB                    60400022
DELAY    EQU   *                                                        60800022
         LA    R3,1(R3)                 UPDATE RETRY COUNT              61200022
         STC   R3,2(R5)                 STORE COUNT                     61600022
         LR    R2,RLCB                  SAVE LCB ADDRESS                62000022
         LR    R10,RQCB                 SAVE QCB ADDR                   62400022
         LR    R3,RQCB                  SET FOR UNAVR                   62800022
         LR    RQCB,RLCB                FOR UNAVAR                      63200022
         BAL   R14,DSPUNAVR             REMOVE SEND SCHEDULER           63600022
         LR    RQCB,R10                 RESTORE QCB BASE                64000022
         LH    R10,2(R8)                GET AMOUNT OF DELAY             64800022
*                                         FROM PARAMETER LIST           65200022
         N     R10,AVTCLRHI             CLEAR HIGH ORDER HALF   SA68666 65240005
         NI    QCBSTAT,AVTEFF-QCBTIME   RESET TWELVE HOUR DELAY SA68666 65280005
         C     R10,TWELVHRS             IS DELAY OVER 12 HRS    SA68666 65320005
         BNH   LESS12                   YES, QCB OK             SA68666 65360005
         S     R10,TWELVHRS             DECREMENT BY 12 HRS     SA68666 65400005
         OI    QCBSTAT,QCBTIME          INDICATE MORE THAN      SA68666 65440005
*                                        12 HOUR DELAY          SA68666 65480005
LESS12   EQU   *                                                SA68666 65520005
         STH   R10,QCBEOLDT             SET DELAY REQUEST               65600022
         MVI   QCBLKRLN,AVTEZERO        SET THE OFFSET TO THE QCB       66000022
         IC    R9,QCBELCHN-1            SAVE HIGH ORDER BYTE            66400022
         ST    RQCB,QCBELCHN-1          QCB FOR POST                    66800022
         STC   R9,QCBELCHN-1            SET HIGH ORDER BYTE             67200022
         MVC   QCBSTCHN,QCBSLINK        REMOVE SEND SCHEDULER           67600022
         MVI   QCBPRI,PRILNFRE          SET PRIORITY                    68000022
         LR    R1,RQCB                  TIME QUEUE PARAMETER            68400022
         L     R15,AVTHG01              GET ADDR OF TIME DELAY ROUTINE  68800022
         BALR  R14,R15                  GO TO TIME DELAY RTN            69200022
TPOSTERB EQU   *                                                        69600022
         LA    R1,LCBERB                LOAD ADDRESS OF ERB             70000022
         BAL   R14,DSPCHAIN             EXIT                   @Y17XAMX 70400010
*********************************************************************** 70800022
HONE     DC    H'1'                     HALF WORD OF ONE                71200022
TWELVHRS DC    F'43200'                 12 HOUR DELAY           SA68666 71400005
TWEN6    EQU   26                       VALUE OF 26                     71600022
FIVE     EQU   5                        VALUE OF 5                      72000022
ONE      EQU   1                        VALUE OF 1                      72400022
THIRTY1  EQU   31                       VALUE OF 31                     72800022
CALLOPT  EQU   X'70'                    STCB PIRORITY FOR CINTVL        73000022
*********************************************************************** 73200022
         EJECT                                                          73600022
*********************************************************************** 74000022
*                              DSECTS                                 * 74400022
*********************************************************************** 74800022
         TPRIOR                                                         75200022
         SPACE 4                                                        75600022
         TAVTD                                                          76000022
         SPACE 4                                                        76400022
         TLCBD                                                          76800022
         SPACE 4                                                        77200022
         TTRMD                                                          77600022
         SPACE 4                                                        78000022
         TDISPD                                                         78400022
         SPACE 4                                                        78800022
         TQCBD                                                          79200022
         SPACE 4                                                        79300022
         TSCBD                                                          79400022
*********************************************************************** 79600022
         END                                                            80000022
