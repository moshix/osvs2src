AC01     TITLE '''IEDQAC'' - DATETIME ROUTINE'                          00300020
IEDQAC   CSECT                                                          00600020
*********************************************************************** 00900020
*A412000                                                         S22025 00970010
*C465000-471000,612000-615000,090000                             S22025 01040010
*D207000-219000                                                  S22025 01110010
*                                                                     * 01200020
*STATUS -- CHANGE LEVEL 0                                             * 01500020
*                                                                     * 01800020
*FUNCTION --                                                          * 02100020
*   INSERTS THE DATE,  THE TIME,  OR BOTH INTO THE BUFFER.            * 02400020
*                                                                     * 02700020
*   THE PREFIX SCAN POINTER (PRFSCAN) IS ACCESSED AND A LINK IS       * 03000020
*   MADE TO THE ADDRESS FINDER ROUTINE (IEDQAL) TO GET THE ADDRESS    * 03300020
*   OF THE SCAN POINTER.  INSERTION OF DATA IS TO START HERE.         * 03600020
*                                                                     * 03900020
*   IF THE TIME,  OR THE DATE AND TIME,  IS REQUESTED,  THE ROUTINE   * 04200020
*   ISSUES A TIME MACRO (SVC 11).  IF THE DATE ONLY IS REQUESTED,     * 04500020
*   IT IS ACCESSED FROM THE COMMUNICATION VECTOR TABLE (CVT).         * 04800020
*                                                                     * 05100020
*   IF THE DATE IS REQUESTED,  THE DATA RETURNED BY THE TIME MACRO    * 05400020
*   OR ACCESSED FROM THE CVT IS PLACED IN THE AVT WORK AREA           * 05700020
*   (AVTDOUBL),  WHERE IT IS ARRANGED INTO THE FORMAT YYPDDD, WHERE   * 06000020
*   YY IS THE LAST TWO BYTES OF THE YEAR,  DDD THE THREE-DIFIT        * 06300020
*   NUMBER OF THE DAY AND P A PERIOD.  THE SUBROUTINE NAMED LOOP      * 06600020
*   IS THEN ENTERED.  THIS SUBROUTINE INSERTS AN INITIAL BLANK AT     * 06900020
*   THE SCAN POINTER,  THEN LINKS REPEATEDLY TO THE BUFFER STEP       * 07200020
*   ROUTINE (IEDQAX) TO RETURN THE ADDRESS OF EACH SUCCESSIVE BYTE    * 07500020
*   AND INSERTS THE FORMATTED DATA FROM AVTDOUBL A BYTE AT A TIME     * 07800020
*   UNTIL ALL OF THE DATA HAS BEEN INSERTED.                          * 08100020
*                                                                     * 08400020
*   IF THE DATE ONLY IS REQUESTED,  THE ROUTINE RETURNS TO THE        * 08700020
*   MESSAGE HANDLER VIA THE RETURN INTERFACE ROUTINE             S22025 09000010
*   OTHERWISE THE TIME IS PROCESSED.                                  * 09300020
*                                                                     * 09600020
*   IF THE TIME IS REQUESTED,  THE TIME DATA RETURNED BY THE TIME     * 09900020
*   MACRO IS PLACED IN AVTDOUBL,  WHERE IT IS ARRANGED INTO THE       * 10200020
*   FORMAT HHPMMPSS,  WHERE HH IS THE HOUR,  MM IS THE MINUTES,  SS   * 10500020
*   ENTERED TO PLACE THE FORMATTED DATA IN THE BUFFER.  THE ROUTINE   * 10800020
*   THEN RETURNS TO THE MESSAGE HANDLER VIA THE RETURN INTERFACE      * 11100020
*   ROUTINE.                                                          * 11400020
*                                                                     * 11700020
*ENTRY POINTS --                                                      * 12000020
*       'IEDQAC01' TO PLACE THE DATE AND/OR THE TIME INTO THE         * 12300020
*   BUFFER.  CALLING SEQUENCE IS:                                     * 12600020
*                                                                     * 12900020
*        L     R12,AVTMSGS-1            GET ADDR OF VCON TABLE        * 13200020
*        IC    R15,AVTEZERO(,R1)        GET INDEX TO ROUTINE ADDR     * 13500020
*        L     R12,AVTEZERO(R12,R15)    GET ROUTINE ADDRESS           * 13800020
*        BR    R12                      EXIT TO ROUTINE               * 14100020
*                                                                     * 14400020
*INPUT --                                                             * 14700020
*   REGISTER 1 -- THE ADDRESS OF A MACRO-GENERATED PARAMETER LIST.    * 15000020
*   PARAMETER LIST FORMAT IS:                                         * 15300020
*                                                                     * 15600020
*        *********************************                            * 15900020
*        *       * PARAM *   LENGTH OF   *                            * 16200020
*        * INDEX * LIST  *     INSERT    *                            * 16500020
*        *       *  LNG  *      DATA     *                            * 16800020
*        *********************************                            * 17100020
*                                                                     * 17400020
*   REGISTER 6 -- ADDRESS OF THE BUFFER.                              * 17700020
*                                                                     * 18000020
*   REGISTER 9 -- ADDRESS OF THE AVT.                                 * 18300020
*                                                                     * 18600020
*   REGISTER 12 -- ENTRY POINT ADDRESS AND BASE REGISTER.             * 18900020
*                                                                     * 19200020
*OUTPUT --                                                            * 19500020
*   THE BUFFER CONTAINS THE FORMATTED DATE AND/OR TIME.               * 19800020
*                                                                     * 20100020
*EXTERNAL REFERENCES --                                               * 20400020
*                                                                     * 22200020
*   AVT -- ADDRESS VECTOR TABLE.                                      * 22500020
*                                                                     * 22800020
*   CVT -- COMMUNICATION VECTOR TABLE.                                * 23100020
*                                                                     * 23400020
*   BUFFER CURRENTLY BEING PROCESSED.                                 * 23700020
*                                                                     * 24000020
*EXITS,  NORMAL --                                                    * 24300020
*   THE BUFFER CONTAINS THE FORMATTED DATE AND/OR TIME.               * 24600020
*                                                                     * 24900020
*EXITS,  ERROR -- N/A.                                                * 25200020
*                                                                     * 25500020
*TABLES/WORK AREAS -- N/A.                                            * 25800020
*                                                                     * 26100020
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, ENABLED, RESIDENT,     * 26400020
*   PROBLEM PROGRAM MODE.                                             * 26700020
*                                                                     * 27000020
*NOTES -- THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL       * 27300020
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS             * 27600020
*   EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME.  THE CODING HAS      * 27900020
*   BEEN ARRANGED SO THAT REDEFINITION OF ''CHARACTER'' CONSTANTS,    * 28200020
*   BY REASSEMBLY, WILL RESULT IN A CORRECT MODULE FOR THE NEW        * 28500020
*   DEFINITIONS.                                                      * 28800020
*                                                                     * 29400020
*********************************************************************** 29700020
         EJECT                                                          30000020
********* REGISTER EQUATES *********                                    30300020
         SPACE                                                          30600020
R0       EQU   0                        TIME REGISTER                   30900020
         SPACE                                                          31200020
R1       EQU   1                        DATE REGISTER                   31500020
         SPACE                                                          31800020
RWORK2   EQU   2                        WORK REGISTER                   32100020
         SPACE                                                          32400020
RWORK3   EQU   3                        WORK REGISTER                   32700020
         SPACE                                                          33000020
RSCAN    EQU   5                        ADDRESS OF SCAN POINTER         33300020
         SPACE                                                          33600020
RPREFIX  EQU   6                        ADDRESS OF BUFFER               33900020
         SPACE                                                          34200020
RUNIT    EQU   7                                                 S22025 34500010
         SPACE                                                          34800020
RX       EQU   8                        INDEX REGISTER                  35100020
         SPACE                                                          35400020
RAVT     EQU   9                        ADDRESS OF AVT                  35700020
         SPACE                                                          36000020
RRET     EQU   10                       LOCAL RETURN REGISTER           36300020
RKEYLE   EQU   11                                                S22025 36400010
         SPACE                                                          36600020
RBASE    EQU   12                       ENTRY POINT REGISTER            36900020
*                                         AND BASE REGISTER             37200020
         SPACE                                                          37500020
R13      EQU   13                       ADDRESS OF SAVE AREA            37800020
R14      EQU   14                       RETURN REGISTER                 38100020
R15      EQU   15                       LINK REGISTER                   38400020
         SPACE                                                          38700020
********* OTHER EQUATES *********                                       39000020
         SPACE                                                          39300020
PARMLEN  EQU   1                        INSERT LENGTH                   39600020
         SPACE                                                          39900020
DATELEN  EQU   7                        LENGTH OF DATE FIELD INBFR      40200020
BOTHLEN  EQU   16                       LENGTH OF BOTH DATE & TIME      40500020
         SPACE                                                          40800020
PTLEN    EQU   4                        LENGTH OF TIME PACKED           41100020
FOUR     EQU   4                        OFFSET USED ON RETURN    S22025 41200010
PDLEN    EQU   3                        LENGTH OF DATE PACKED           41400020
UTLEN    EQU   7                        LENGTH OF TIME UNPACKED         41700020
UDLEN    EQU   5                        LENGTH OF DATE UNPACKED         42000020
         SPACE                                                          42300020
INC      EQU   1                        INCREMENT VALUE                 42600020
         SPACE                                                          42900020
INSDLEN  EQU   5                        INSERT LENGTH FOR DATE          43200020
INSTLEN  EQU   7                        INSERT LENGTH FOR TIME          43500020
         SPACE                                                          43800020
BYTELEN  EQU   8                        SHIFT FACTOR FOR ONE BYTE       44100020
         EJECT                                                          44400020
         USING IEDQAVTD,RAVT                                            44700020
         USING IEDQPRF,RPREFIX                                          45000020
         USING IEDQAC,RBASE                                             45300020
         SPACE                                                          45600020
IEDQAC01 EQU   *                                                        45900020
IEDQAC   IEDHJN DATETIME                ID AND DATE              S22025 46600010
         LR    RWORK3,R1                SET NEW PARAM REG               47400020
         LH    RSCAN,PRFSCAN            GET SCAN POINTER OFFSET         47700020
         N     RSCAN,AVTCLRHI           CLEAR TOP HALF OF REGSIETR      48000020
         IC    R15,PARMLEN(,RWORK3)     PICK UP INSERT LENGTH           48300020
         BCTR  R15,AVTEZERO             DECREMENT BY ONE                48600020
         SR    RSCAN,R15                POINT TO START OF               48900020
*                                         INSERT AREA                   49200020
         LR    R15,RSCAN               SET SCAN OFFSET           S22025 49500010
         LH    RKEYLE,AVTKEYLE         INITIALIZE FOR SCAN       S22025 49800010
         LR    RUNIT,RPREFIX           COPY BFR ADDR AS UNIT     S22025 50100010
         LR    RX,R15                   SAVE CONTENTS OF REG 15  S22025 50400010
         SPACE                                                          51000020
         CLI   PARMLEN(RWORK3),DATELEN  DATE ONLY REQUESTED             51300020
         BE    DATEONLY                 YES, GO PROCESS                 51600020
         SPACE                                                          51900020
         TIME  DEC                      NO, GET TIME AND DATE           52200020
         LR    R15,RX                   RESTORE CONT OF REG 15   S22025 52300010
         SPACE                                                          52500020
         CLI   PARMLEN(RWORK3),BOTHLEN  BOTH TIME & DATE REQUESTED      52800020
         BNE   FORMTIME                 NO, DO TIME ONLY                53100020
         BAL   RRET,PROCDATE            YES, LINK TO DO DATE            53400020
         SPACE                                                          53700020
         SPACE                                                          54600020
* THE FOLLOWING INSTRUCTIONS UNPACK THE TIME,FORMAT IT INTO THE         54900020
* BUFFER, UPDATE THE SCAN POINTER AND RETURN TO THE CALLER              55200020
         SPACE                                                          55500020
FORMTIME EQU   *                                                        55800020
         ST    R0,AVTDOUBL              STORE TIME INTO WORK AREA       56100020
         UNPK  AVTDOUBL(UTLEN),AVTDOUBL(PTLEN) UNPACK TIME              56400020
         SPACE                                                          56700020
         LH    RWORK2,AVTDOUBL+4        MOVE SECONDS                    57000020
         STH   RWORK2,AVTDOUBL+6          TO RIGHT                      57300020
         LH    RWORK2,AVTDOUBL+2        PICK UP MINUTES                 57600020
         STC   RWORK2,AVTDOUBL+4        STORE 2ND BYTE                  57900020
         SRL   RWORK2,BYTELEN           SHIFT AND                       58200020
         STC   RWORK2,AVTDOUBL+3          STORE 1ST BYTE                58500020
         MVI   AVTDOUBL+2,AVTEPER       STORE PERIODS                   58800020
         MVI   AVTDOUBL+5,AVTEPER         IN TIME                       59100020
         SPACE                                                          59400020
         LA    RWORK3,INSTLEN           SET DATA COUNT                  59700020
         BAL   RRET,LOOP                GO INSERT TIME                  60000020
         SPACE                                                          60300020
RETURN   EQU   *                                                        60600020
         SR    R15,R15                  SET GOOD RETURN CODE            60900020
         L     RBASE,AVTUI              GET RETURN ADDRESS       S22025 61200010
         B     FOUR(RBASE)              BRANCH TO RETURN ROUTINE S22025 61500010
         SPACE                                                          61800020
* THE FOLLOWING INSTRUCTIONS ACCESS THE DATE FROM THE CVT, LINK TO      62100020
* UNPACK AND FORMAT THE DATE INTO BUFFER, AND LINK TO RETURN TO THE     62400020
* CALLER.                                                               62700020
         SPACE                                                          63000020
DATEONLY EQU   *                                                        63300020
         L     R1,CVTPTR                GET ADDR OF CVT                 63600020
         USING IEDQCVTD,R1                                              63900020
         L     R1,CVTDATE               GET DATE FROM CVT               64200020
         DROP  R1                                                       64500020
         SPACE                                                          64800020
         LA    RRET,RETURN              GO PROCESS DATE                 65100020
         SPACE                                                          65400020
* THE FOLLOWING INSTRUCTIONS CONSTITUTE AN INTERNAL SUBROUTINE THAT     65700020
* UNPACKS THE DATE, FORMAT IT INTO THE BUFFER, UPDATE THE SCAN          66000020
* POINTER AND RETURN TO THE (LOCAL) CALLER.                             66300020
         SPACE                                                          66600020
PROCDATE EQU   *                                                        66900020
         ST    R1,AVTDOUBL                                              67200020
         UNPK  AVTDOUBL(UDLEN),AVTDOUBL+1(PDLEN) UNPACK DATE            67500020
         SPACE                                                          67800020
         LM    RWORK2,RWORK3,AVTDOUBL   PICK UP DATE                    68100020
         SRDL  RWORK2,BYTELEN           SHIFT RIGHT                     68400020
         ST    RWORK3,AVTDOUBL+4        STORE BACK 2ND HALF             68700020
         STH   RWORK2,AVTDOUBL+2        STORE BACK REST OF DAY          69000020
         MVI   AVTDOUBL+2,AVTEPER       INSERT PERIOD                   69300020
         SPACE                                                          69600020
         LA    RWORK3,INSDLEN           SET DATA COUNT                  69900020
         SPACE                                                          70200020
LOOP     EQU   *                                                        70500020
         SR    RX,RX                    INITIALIZE INDEX TO 0           70800020
         LA    RWORK2,INC               SET INCREMENT REG TO 1          71100020
         B     ADDR                     BRANCH TO GET ADDRESS    S22025 71130010
NXTUNIT  EQU   *                                                 S22025 71160010
         SR    R15,RKEYLE                                        S22025 71190010
         L     RUNIT,PRFTIC-IEDQPRF(RUNIT)   GET NEXT UNIT ADDR  S22025 71220010
ADDR     EQU   *                                                 S22025 71250010
         CR    R15,RKEYLE                                        S22025 71280010
         BH    NXTUNIT                  YES,GET NEXT UNIT ADDR.  S22025 71310010
         LA    RSCAN,AVTUMALN-1(R15,RUNIT)   GET DATA ADDR       S22025 71340010
         MVI   AVTEZERO(RSCAN),AVTEBLNK SET INITIAL BLANK               71400020
         SPACE                                                          72300020
LOOP1    EQU   *                                                        72600020
         IC    R14,AVTDOUBL(RX)         MOVE DATA BYTE                  73500020
         LA    R15,INC(R15)            BUMP UNIT OFFSET          S22025 73530010
         CR    R15,RKEYLE              IS IT OUT-OF-UNIT         S22025 73560010
         BNH   STC                     NO, INSERT FIRST/NXT BYTE S22025 73590010
         L     RUNIT,PRFTIC-IEDQPRF(RUNIT)   GET NEXT UNIT ADDR  S22025 73620010
         SR   R15,RKEYLE               RESET UNIT OFFSET         S22025 73650010
STC      EQU   *                                                 S22025 73680010
         LA    RSCAN,AVTUMALN-1(R15,RUNIT)   GET DATA ADDR       S22025 73710010
         STC   R14,AVTEZERO(,RSCAN)       TO BUFFER              S22025 73810010
         BXLE  RX,RWORK2,LOOP1          LOOP IF ALL DATA NOT MOVED      74100020
         LA    R15,INC(R15)            INCREMENT TO NXT BYTE     S22025 74200010
         SPACE                                                          74400020
         BR    RRET                     EXIT TO (LOCAL) CALLER          74700020
         SPACE                                                          75000020
         EJECT                                                          76500020
********* DSECTS *********                                              76800020
         SPACE                                                          77100020
          TAVTD                                                         77400020
         EJECT                                                          77700020
         TPRFD                                                          78000020
         EJECT                                                          78300020
IEDQCVTD DSECT                                                          78600020
         CVT                                                            78900020
         END                                                            79200020
