AF01     TITLE '''IEDQAF'' - INSERT DATA ROUTINE'                       00400020
IEDQAF   CSECT                                                          00800020
         SPACE 3                                                        00810010
*CHANGE ACTIVITY = AS FOLLOWS:                                          00820010
*************** MICROFICHE FLAGS **************************** SUPT CODE 00830010
*A012184-012192,174000,200400-203600,242000,448600-451000,497000 S22025 00860010
*A502000,638000-647000,664400-680800,727000,730000,892600-895300 S22025 00860110
*C012675,012738,012783,012990,013116,600000,604000,836000        S22025 00860210
*D012189,236000-248000,496000-504000,640000-648000,668000-680000 S22025 00860310
*D728000,732000                                                  S22025 00860410
*C669400-669700                                                 SA56295 00860510
         SPACE 3                                                        01200010
*********************************************************************** 01200920
*                                                                     * 01201810
*TITLE: IEDQAF - INSERT DATA ROUTINE                                  * 01201910
*                                                                     * 01202010
*MODULE NAME = IEDQAF                                                 * 01202110
*                                                                     * 01202210
*DESCRIPTIVE NAME = INSERT DATA ROUTINE                               * 01202310
*                                                                     * 01202410
*COPYRIGHT = 'NONE'                                                   * 01202510
*                                                                     * 01202610
*STATUS: CHANGE LEVEL 5                                               * 01202710
*                                                                     * 01203620
*FUNCTION -- INSERTS DATA INTO THE BUFFER OR SHIFTS DATA LEFTWARDS    * 01204520
*   WITHIN THE BUFFER.                                                * 01205420
*                                                                     * 01206320
*   THE HEART OF THE INSERT DATA ROUTINE IS AN INSERT SUBROUTINE      * 01207220
*   WHICH INSERTS DATA ACCORDING TO THE FOLLOWING PARAMETERS PASSED   * 01208120
*   TO IT: (1) THE OFFSET INTO THE BUFFER AT WHICH DATA IS TO BE      * 01209020
*   INSERTED IS INDICATED BY A PAIR OF OFFSETS PASSED IN THE RECB,    * 01209920
*   NAMELY THE PREFIX DATA OFFSET AND THE PREFIX INSERT OFFSET. THE   * 01210820
*   PREFIX DATA OFFSET MINUS THE PREFIX INSERT OFFSET SPECIFIES THE   * 01211720
*   OFFSET FROM THE BEGINNING OF THE FIRST UNIT AT WHICH DATA IS TO   * 01212620
*   BE INSERTED. (2) THE LENGTH OF THE DATA TO BE INSERTED IS         * 01213520
*   PASSED IN THE SECOND AVT PARAMETER AREA (AVTPARM3). (3) DATA TO   * 01214420
*   BE INSERTED MAY BE A STRING OF CHARACTERS WHOSE ADDRESS IS        * 01215320
*   PASSED IN AVTPARM3, OR IT MAY BE A STRING OF IDENTICAL CHARAC-    * 01216220
*   TERS (NORMALLY, IDLE CHARACTERS, HENCE CALLED IDLE CHARACTERS     * 01217120
*   HERE), IN WHICH CASE THE CHARACTER ITSELF IS PASSED IN            * 01218020
*   AVTPARM3. THIS ROUTINE CHECKS TO SEE IF THE BUFFER CONTAINS  S22025 01218422
*   EOB AND UPDATES OFFSET TO EOB APPROPRIATELY ON DELETION OR   S22025 01218822
*   INSERTION OF DATA.                                           S22025 01219222
*                                                                     * 01219820
*   IT OFTEN HAPPENS THAT THE DATA TO BE INSERTED IS ITSELF LOCATED   * 01220720
*   IN THE BUFFER. IN THIS CASE THE INSERTION CONSTITUTES A SHIFT     * 01221620
*   OF DATA LEFTWARDS IN THE BUFFER.                                  * 01222520
*                                                                     * 01223420
*   INSERT DATA SUBROUTINE:                                           * 01224320
*   THE OFFSET AT WHICH DATA IS TO BE INSERTED IS COMPUTED BY         * 01225220
*   PICKING UP THE PREFIX DATA OFFSET AND SUBTRACTING THE PREFIX      * 01226120
*   INSERT OFFSET FROM IT. THIS OFFSET IS PASSED TO THE ADDRESS       * 01227020
*   FINDER ROUTINE (IEDQAL), REQUESTING THAT THE END-OF-UNIT          * 01227920
*   ADDRESS BE RETURNED IN ADDITION TO THE INSERT ADDRESS. ON         * 01228820
*   RETURN, THE COUNT OF BYTES FROM THE INSERT ADDRESS TO THE END     * 01229720
*   OF THE UNIT IS COMPUTED, AND THE LENGTH OF DATA TO BE INSERTED    * 01230620
*   IS PICKED UP FROM AVTPARM3. THE SUBROUTINE THEN TESTS IF THE      * 01231520
*   INSERT DATA IS A STRING OF IDLES OR DATA WHOSE ADDRESS IS         * 01232420
*   PASSED.                                                           * 01233320
*                                                                     * 01234220
*   IF THE DATA TO BE INSERTED IS A CHARACTER STRING WHOSE ADDRESS    * 01235120
*   IS PASSED, THAT ADDRESS IS LOADED FROM AVTPARM3. THE SUBROUTINE   * 01236020
*   TESTS IF THE NUMBER OF BYTES TO THE END OF THE UNIT IS LESS       * 01236920
*   THAN THE TOTAL LENGTH OF DATA TO BE INSERTED.  IF IT IS NOT,      * 01237820
*   ALL THE DATA WILL FIT INTO THE CURRENT UNIT, AND IT IS MOVED      * 01238720
*   THERE. IF ALL DATA WILL NOT FIT INTO THE CURRENT UNIT, ALL THE    * 01239620
*   DATA THAT WILL FIT IS MOVED THERE. THEN THE ADDRESS OF THE NEXT   * 01240520
*   UNIT IS ACCESSED AND THE REMAINING DATA IS MOVED INTO THAT        * 01241420
*   UNIT. THE SUBROUTINE THEN EXITS TO ITS CALLER.                    * 01242320
*                                                                     * 01243220
*   IF THE DATA TO BE INSERTED IS A STRING OF IDLES, THE IDLE CHAR-   * 01244120
*   ACTER IS PICKED UP FROM AVTPARM3. THE SUBROUTINE TESTS IF THE     * 01245020
*   NUMBER OF BYTES TO THE END OF THE UNIT IS LESS THAN THE TOTAL     * 01245920
*   NUMBER OF IDLES TO BE INSERTED. IF IT IS NOT, ALL THE IDLES       * 01246820
*   WILL FIT INTO THE CURRENT UNIT. A STORE-CHARACTER LOOP INSERTS    * 01247720
*   IDLES INTO THE UNIT UNTIL THE NUMBER OF IDLES SPECIFIED IS        * 01248620
*   INSERTED. IF ALL THE IDLES WILL NOT FIT INTO THE CURRENT UNIT,    * 01249520
*   ANOTHER STORE-CHARACTER LOOP INSERTS IDLES TO THE END OF THE      * 01250420
*   CURRENT UNIT. THEN THE ADDRESS OF THE NEXT UNIT IS ACCESSED,      * 01251320
*   AND THE FIRST STORE-CHARACTER LOOP INSERTS IDLES INTO THAT UNIT   * 01252220
*   UNTIL THE NUMBER OF IDLES SPECIFIED IS INSERTED. THE SUBROUTINE   * 01253120
*   THEN EXITS TO ITS CALLER.                                         * 01254020
*                                                                     * 01254920
*   ENTRIES TO THE INSERT DATA ROUTINE:                               * 01255820
*   THE INSERT DATA ROUTINE MAY BE ENTERED TO PERFORM ANY ONE OF      * 01256720
*   FOUR FUNCTIONS: (1) TO INSERT DATA AS SPECIFIED AND RETURN        * 01257620
*   IMMEDIATELY; (2) TO INSERT DATA AS SPECIFIED, ADJUST THE PREFIX   * 01258520
*   INSERT OFFSET BY THE LENGTH OF DATA INSERTED AND RETURN; (3) TO   * 01259420
*   SHIFT DATA ACROSS SEVERAL UNITS AS SPECIFIED AND RETURN; AND      * 01260320
*   (4) TO EXPAND THE BUFFER BY SHIFTING DATA LEFTWARDS INTO THE      * 01261220
*   RESERVE CHARACTERS AREA. THE FUNCTION REQUESTED IS INDICATED      * 01262120
*   BY FLAGS SET IN THE FIRST AVT PARAMETER AREA (AVTPARM).           * 01263020
*                                                                     * 01263920
*   (1) INSERT AND RETURN FUNCTION:                                   * 01264820
*   THE INSERT DATA ROUTINE LINKS TO THE INSERT SUBROUTINE TO         * 01265720
*   PERFORM THE SPECIFIED INSERTION. ON RETURN, EXIT IS MADE TO       * 01266620
*   THE CALLER VIA THE RETURN INTERFACE ROUTINE (IEDQUI+4).      S22025 01267522
*                                                                     * 01268420
*   (2) INSERT, ADJUST AND RETURN FUNCTION:                           * 01269320
*   THE INSERT DATA ROUTINE LINKS TO THE INSERT SUBROUTINE TO         * 01270220
*   PERFORM THE SPECIFIED INSERTION. ON RETURN, THE PREFIX INSERT     * 01271120
*   OFFSET IS ACCESSED, DECREMENTED BY THE TOTAL LENGTH OF DATA       * 01272020
*   INSERTED AND STORED BACK IN THE RECB. EXIT IS MADE TO THE         * 01272920
*   CALLER VIA THE RETURN INTERFACE ROUTINE (IEDQUI+4).          S22025 01273822
*                                                                     * 01274720
*   (3) MULTIPLE-UNIT SHIFT  FUNCTION:                                * 01275620
*   THE PREFIX DATA OFFSET IS ACCESSED FROM THE RECB, AND A SHIFT-    * 01276520
*   DATA LOOP IS ENTERED. IN THIS LOOP, THE PREFIX DATA OFFSET IS     * 01277420
*   PASSED TO THE ADDRESS FINDER ROUTINE (ENTERLOP), REQUESTING THAT  * 01278322
*   THE END-OF-UNIT ADDRESS BE RETURNED IN ADDITION TO THE DATA       * 01279220
*   ADDRESS. ON RETURN, THE COUNT OF BYTES FROM THE DATA   ADDRESS    * 01280120
*   TO THE END OF THE UNIT IS COMPUTED AND COMPARED TO THE TOTAL      * 01281020
*   LENGTH OF DATA TO BE INSERTED. IF THE COUNT OF BYTES TO THE END   * 01281920
*   OF THE UNIT IS GREATER, IT IS SET IN AVTPARM3; OTHERWISE THE      * 01282820
*   TOTAL LENGTH OF DATA TO BE INSERTED IS SET IN AVTPARM3. THEN      * 01283720
*   THE DATA ADDRESS IS SET IN AVTPARM3.  A NEW DATA OFFSET IS        * 01284620
*   CALCULATED, EQUAL TO THE CURRENT DATA OFFSET PLUS THE LENGTH AS   * 01285520
*   SET IN AVTPARM3, AND THE TOTAL LENGTH OF DATA TO BE INSERTED      * 01286420
*   IS DECREMENTED BY THE VALUE SET IN AVTPARM3. (NOTE THAT THIS      * 01287320
*   MAY REDUCE IT TO ZERO.) THE ROUTINE THEN LINKS TO THE INSERT      * 01288220
*   SUBROUTINE TO SHIFT ALL THE DATA IN THE CURRENT UNIT.             * 01289120
*                                                                     * 01290020
*   ON RETURN, THE REMAINING LENGTH OF DATA IS TESTED FOR A VALUE     * 01290920
*   OF ZERO. IF IT IS NOT ZERO, DATA IN THE NEXT UNIT MUST BE         * 01291820
*   SHIFTED AS WELL. THE NEW DATA OFFSET (WHICH IS IN THIS CASE       * 01292720
*   THE OFFSET TO THE START OF DATA IN THE NEXT UNIT) IS SET AS       * 01293620
*   THE PREFIX DATA OFFSET, AND THE SHIFT-DATA LOOP IS ENTERED        * 01294520
*   AGAIN.                                                            * 01295420
*                                                                     * 01296320
*   WHEN THE REMAINING LENGTH OF DATA IS REDUCED TO ZERO, A RETURN    * 01297220
*   CODE OF ZERO IS SET IN REGISTER 15 AND RETURN IS MADE TO THE      * 01298120
*   CALLER VIA THE RETURN INTERFACE ROUTINE (IEDQUI+4).          S22025 01299022
*                                                                     * 01299920
*   NOTE: IN THE MULTIPLE-UNIT SHIFT FUNCTION, THE PREFIX INSERT      * 01300820
*   OFFSET REMAINS ZERO THROUGHOUT THE ROUTINE.                       * 01301720
*                                                                     * 01302620
*   (4) EXPAND BUFFER FUNCTION:                                       * 01303520
*   THE EXPAND LENGTH REQUESTED IS ACCESSED FROM THE PARAMETER        * 01304420
*   LIST AND SET IN THE RECB AS THE PREFIX INSERT OFFSET. THE         * 01305320
*   NUMBER OF RESERVE CHARACTERS CURRENTLY IN THE BUFFER IS           * 01306220
*   ACCESSED FROM THE LCB (LCBISZE) AND COMPARED WITH THE EXPAND      * 01307120
*   LENGTH REQUESTED. IF AN EXPAND LENGTH GREATER THAN THE NUMBER     * 01308020
*   OF RESERVE CHARACTERS IS REQUESTED, THE EXPANSION CANNOT BE       * 01308920
*   PERFORMED. A RETURN CODE OF X'04' IS SET IN REGISTER 15 AND       * 01309820
*   RETURN IS MADE TO THE CALLER VIA THE RETURN INTERFACE ROUTINE     * 01310720
*   (IEDQUI+4).                                                  S22025 01311622
*                                                                     * 01312520
*   IF THE EXPANSION CAN BE PERFORMED, THE PREFIX DATA OFFSET IS      * 01313420
*   SET TO THE SIZE OF THE PREFIX PLUS THE NUMBER OF RESERVE CHAR-    * 01314320
*   ACTERS PLUS ONE. THE LENGTH OF DATA TO BE SHIFTED IS CALCULATED   * 01315220
*   AS THE CURRENT SETTING OF THE SCAN POINTER MINUS THE PREFIX       * 01316120
*   DATA OFFSET: THIS LENGTH WILL LATER BE SET IN AVTPARM3. THE       * 01317020
*   NUMBER OF RESERVE CHARACTERS IS DECREMENTED BY THE REQUESTED      * 01317920
*   EXPAND LENGTH AND SET BACK IN LCBISZE.                            * 01318820
*                                                                     * 01319720
*   THE SCB SECONDARY DESTINATION FIELD (SCBMRFSD) IS TESTED FOR A    * 01320620
*   VALUE OF ZERO. IF IT IS NOT ZERO, MULTIPLE ROUTING HAS BEEN       * 01321520
*   REQUESTED, AND THE OFFSET IN THIS FIELD MUST BE ADJUSTED FOR      * 01322420
*   THE EXPANSION ABOUT TO BE PERFORMED. THE FIELD IS LOADED,         * 01323320
*   DECREMENTED BY THE EXPAND LENGTH AND STORED BACK.                 * 01324220
*                                                                     * 01325120
*   THE SHIFT-DATA LOOP IS THEN ENTERED, AND PROCESSING PROCEEDS      * 01326020
*   AS DESCRIBED IN THE MULTIPLE-UNIT SHIFT FUNCTION DESCRIPTION.     * 01326920
*                                                                     * 01327820
*ENTRY POINTS --                                                      * 01328720
*       'IEDQAF01' TO INSERT DATA INTO THE BUFFER OR SHIFT DATA       * 01329620
*   LEFTWARDS WITHIN THE BUFFER.  CALLING SEQUENCE FROM USER          * 01330520
*   INTERFACE IS:                                                     * 01331420
*                                                                     * 01332320
*        L     R12,AVTMSGS-1            GET ADDR OF VCON TABLE        * 01333220
*        IC    R15,AVTEZERO(,R1)        GET INDEX TO ROUTINE ADDR     * 01334120
*        L     R12,AVTEZERO(R12,R15)    GET ROUTINE ADDRESS           * 01335020
*        BR    R12                      EXIT TO ROUTINE               * 01335920
*                                                                     * 01336820
*INPUT --                                                             * 01337720
*   REGISTER 1 - THE ADDRESS OF A PARAMETER LIST GENERATED BY A       * 01338620
*   MACRO EXPANSION OR BUILT BY A CALLING ROUTINE. PARAMETER LIST     * 01339520
*   FORMAT IS:                                                        * 01340420
*                                                                     * 01341320
*        EXPAND BUFFER FUNCTION              INDEX FLAGS:             * 01342220
*        *****************                                            * 01343120
*        * INDEX *EXPAND *                   X'00' - INSERT, ADJUST   * 01344020
*        *  TO   * LENGTH*                           & RETURN         * 01344920
*        *   AF  *       *                                            * 01345820
*        *****************                   X'01' - INSERT &         * 01346720
*                                                    RETURN           * 01347620
*        OTHER FUNCTIONS (ALWAYS IN AVTPARM)                          * 01348520
*        *****************                   X'02' - MULTIPLE-UNIT    * 01349420
*        * INDEX * DATA  *                           SHIFT            * 01350320
*        *  TO   *  TYPE *                                            * 01351220
*        *   AF  *  FLAG *                   X'03' - EXPAND BUFFER    * 01352120
*        *****************                                            * 01353020
*                                                                     * 01353920
*        DATA TYPE FLAGS= X'00' - DATA ADDRESS                        * 01354820
*                         X'01' - IDLE CHARACTERS                     * 01355720
*                                                                     * 01356620
*   REGISTER 3 - ADDRESS OF THE SCB.                                  * 01357520
*                                                                     * 01358420
*   REGISTER 4 - ADDRESS OF THE LCB.                                  * 01359320
*                                                                     * 01360220
*   REGISTER 6 - ADDRESS OF THE CURRENT BUFFER.                       * 01361120
*                                                                     * 01362020
*   REGISTER 9 - ADDRESS OF THE AVT.                                  * 01362920
*                                                                     * 01363820
*   REGISTER 12 - ENTRY POINT AND BASE REGISTER FOR THIS ROUTINE.     * 01364720
*                                                                     * 01365620
*   SECOND AVT PARAMETER AREA (AVTPARM3) - ON ENTRY FOR INSERT AND    * 01366520
*   RETURN FUNCTION OR INSERT, ADJUST AND RETURN FUNCTION,            * 01367420
*   CONTAINS A PARAMETER LIST IN THIS FORMAT:                         * 01368320
*                                                                     * 01369220
*   DATA TYPE = CHARACTER STRING                                      * 01370120
*   *********************************                                 * 01371020
*   *LNG OF *        ADDRESS        *                                 * 01371920
*   * INSERT*          OF           *                                 * 01372820
*   *  DATA *   CHARACTER STRING    *                                 * 01373720
*   *********************************                                 * 01374620
*                                                                     * 01375520
*   DATA TYPE = IDLE CHARACTERS                                       * 01376420
*   *********************************                                 * 01377320
*   *LNG OF * IDLE  *               *                                 * 01378220
*   * INSERT* CHAR- *   (UNUSED)    *                                 * 01379120
*   *  DATA * ACTER *               *                                 * 01380020
*   *********************************                                 * 01380920
*                                                                     * 01381820
*   BUFFER RECB - FOR ALL BUT EXPAND BUFFER FUNCTION, CONTAINS A      * 01382720
*   PREFIX INSERT OFFSET (HALFWORD AT RECB + 4) AND A PREFIX DATA     * 01383620
*   OFFSET (HALFWORD AT RECB + 6), SUCH THAT THE DATA OFFSET MINUS    * 01384520
*   THE INSERT OFFSET INDICATES THE OFFSET FROM THE START OF DATA     * 01385420
*   IN THE FIRST BUFFER AT WHICH INSERTION OF DATA, OR TO WHICH       * 01386320
*   SHIFTING OF DATA, IS TO START.                                    * 01387220
*                                                                     * 01388120
*OUTPUT --                                                            * 01389020
*   REGISTER 15 - FOR EXPAND BUFFER FUNCTION ONLY, RETURN CODE.       * 01389920
*   VALUE IS X'00' FOR SUCCESSFUL COMPLETION AND X'04' FOR INSUFFI-   * 01390820
*   CIENT RESERVE CHARACTERS.                                         * 01391720
*                                                                     * 01392620
*EXTERNAL REFERENCES --                                               * 01393520
*   'IEDQAL' - ADDRESS FINDER ROUTINE.                                * 01394420
*                                                                     * 01395320
*   'IEDQLM' - RETURN INTERFACE ROUTINE.                              * 01396220
*                                                                     * 01397120
*   AVT - ADDRESS VECTOR TABLE.                                       * 01398020
*                                                                     * 01398920
*   BUFFER CURRENTLY BEING PROCESSED.                                 * 01399820
*                                                                     * 01400720
*   LCB - LINE CONTROL BLOCK.                                         * 01401620
*                                                                     * 01402520
*   SCB - STATION CONTROL BLOCK.                                      * 01403420
*                                                                     * 01404320
*EXITS, NORMAL -- SUCCESSFUL COMPLETION. DATA IS INSERTED IN THE      * 01405220
*   BUFFER, OR SHIFTED LEFTWARDS IN THE BUFFER, AS SPECIFIED. FOR     * 01406120
*   EXPAND BUFFER FUNCTION, REGISTER 15 CONTAINS A RETURN CODE OF     * 01407020
*   X'00'.                                                            * 01407920
*                                                                     * 01408820
*EXITS, ERROR -- FOR EXPAND BUFFER FUNCTION ONLY, INSUFFICIENT        * 01409720
*   RESERVE CHARACTERS ARE PRESENT IN THE BUFFER. REGISTER 15 CON-    * 01410620
*   TAINS A RETURN CODE OF X'04'.                                     * 01411520
*                                                                     * 01412420
*TABLES/WORK AREAS -- N/A.                                            * 01413320
*                                                                     * 01414220
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, ENABLED,               * 01415120
*   RESIDENT, PROBLEM PROGRAM MODE.                                   * 01416020
*                                                                     * 01416920
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON AN        * 01417820
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.            * 01418720
*                                                                     * 01419620
*********************************************************************** 01420520
         EJECT                                                          01438020
********* REGISTER EQUATES *********                                    01600020
         SPACE                                                          02000020
R0       EQU   0                        WORK REGISTER                   02400020
         SPACE                                                          02800020
R1       EQU   1                        PARAMETER LIST ADDR             03200020
         SPACE                                                          03600020
RWORK2   EQU   2                        WORK REGISTER                   04000020
         SPACE                                                          04400020
RSCB3    EQU   3                        SCB ADDRESS                     04800020
RFROM3   EQU   3                        INSERT DATA ADDRESS             05200020
RCHAR3   EQU   3                        IDLE CHARACTER REGISTER         05600020
         SPACE                                                          06000020
RLCB4    EQU   4                        ADDRESS OF LCB                  06400020
RUNIT4   EQU   4                        ADDRESS OF CURRENT UNIT         06800020
         SPACE                                                          07200020
RSCAN5   EQU   5                        ADDR OF INSERT LOCATION         07600020
         SPACE                                                          08000020
RPREFIX  EQU   6                        ADDR OF RECEIVING UNIT          08400020
         SPACE                                                          08800020
ROFF7    EQU   7                        DATA OFFSET REGISTER            09200020
         SPACE                                                          09600020
RLIM8    EQU   8                        TOTAL SHIFT LENGTH REGISTER     10000020
         SPACE                                                          10400020
RAVT9    EQU   9                        ADDR OF AVT                     10800020
         SPACE                                                          11200020
RLOCRTN  EQU   10                       LOCAL RETURN REGISTER           11600020
         SPACE                                                          12000020
REX11    EQU   11                       LENGTH OF INSERT DATA           12400020
         SPACE                                                          12800020
RBASE    EQU   12                       ENTRY POINT ADDRESS             13200020
*                                         AND BASE REGISTER             13600020
R13      EQU   13                       SAVE AREA ADDRESS               13800022
         SPACE                                                          14000020
R14      EQU   14                       RETURN REGISTER                 14400020
R15      EQU   15                       RETURN CODE REGISTER            14800020
         SPACE                                                          15200020
********* OTHER EQUATES *********                                       15600020
         SPACE                                                          16000020
ONE      EQU   1                        INCREMENT VALUE OF ONE          16400020
         SPACE                                                          16800020
INSOFF   EQU   4                        OFFSET TO INSERT OFFSET         17200020
FOUR     EQU   4                        OFFSET USED ON RETURN    S22025 17400022
DATOFF   EQU   6                        OFFSET TO DATA OFFSET           17600020
TWELVE   EQU   12                       OFFSET FOR +4 SAVE ADDR  S22025 17700022
SIXTEEN  EQU   16                       OFFSET FOR +4 SAVE PTR   S22025 17800022
         SPACE                                                          18000020
TESTFLAG EQU   X'03'                    ENTRY FLAGS                     18400020
SHIFUNIT EQU   X'02'                    ONE-UNIT SHIFT REQUEST          18800020
IDLEFLAG EQU   X'01'                    INSERT IDLES FLAG               19200020
PARMINDX EQU   0                        INDEX BYTE                      19600020
PARMLEN  EQU   1                        SHIFT LENGTH                    20000020
FLAGOFF  EQU   X'00'                    TURN OFF FLAG            S22025 20040022
CHARDAT  EQU   X'80'                    DATA=CHAR STRING         S22025 20080022
IDLCT    EQU   X'40'                    DATA=COUNT OF INLES      S22025 20120022
DATCONT  EQU   X'20'                    DATA=CONTRACT            S22025 20160022
CHARTO   EQU   X'10'                    TO DELIMITER = CHAR STRINS22025 20200022
OFFSTO   EQU   X'08'                    TO DELIMITER=OFFSET      S22025 20240022
EXTTO    EQU   X'04'                    TO DELIMITER=EXTENT      S22025 20280022
INCLFROM EQU   X'02'                    FROM STRING INCL IN REM  S22025 20320022
INCLTO   EQU   X'01'                    TO STRING INCL IN REMOVE S22025 20360022
         EJECT                                                          20400020
         USING IEDQPRF,RPREFIX                                          20800020
         USING IEDQAVTD,RAVT9                                           21200020
         USING IEDQLCB,RLCB4                                            21600020
         USING IEDQSCB,RSCB3                                            22000020
         USING IEDQAF,RBASE                                             22400020
         SPACE                                                          22800020
IEDQAF01 EQU   *                                                        23200020
         B     INSDATA                  BRANCH AROUND ID                23600020
IEDQAF02 EQU   *                                                 S22025 23660022
         B     SAVEIT-IEDQAF01(R15)     BRANCH AROUND ID         S22025 23720022
IEDQAF   IEDHJN ,                       MODULE ID                S22025 23800022
SAVEIT   DS    0H                                                S22025 23880022
         STM   R14,RBASE,TWELVE(R13)    SAVE CALLERS REGISTERS   S22025 23960022
         ST    R13,SIXTEEN(,R13)        STORE BACKWARD POINTER   S22025 24040022
         LA    R13,TWELVE(,R13)         ADJUST SAVE ADDRESS      S22025 24120022
         LR    RBASE,R15                BASE REG                        24200022
         SPACE 1                                                        24280022
         SPACE 1                                                 S22025 24400022
INSDATA  DS    0H                                                S22025 24700022
         TM    PARMINDX(R1),TESTFLAG    TEST ENTRY FLAGS                25200020
         SPACE                                                          25600020
         BZ    INSERTR                  INSERT DATA                     26000020
         BO    EXPNBUFF                 EXPAND BUFFER                   26400020
         SPACE                                                          26800020
         TM    PARMINDX(R1),SHIFUNIT    IS IT ONE-UNIT SHIFT            27200020
         BNO   SHIFINIT                 NO, GO DO MULT SHIFT            27600020
         SPACE                                                          28000020
         LA    RLOCRTN,EXIT             YES, SET RETURN                 28400020
         B     INSERT                   GO SHIFT ONE UNIT               28800020
         EJECT                                                          29200020
EXPNBUFF EQU   *                                                        29600020
         CLC   PRFSCAN,PRFSIZE          TEST IF RUN OUT OF BUFFER       30000020
         BNH   SETINSOF                 NO, PROCEED                     30400020
         SPACE                                                          30800020
         LA    R15,AVTECD4              YES, SET NEGATIVE               31200020
         LNR   R15,R15                    RETURN CODE IN REG 15         31600020
         B     EXIT                     RETURN IMMEDIATELY              32000020
         SPACE                                                          32400020
SETINSOF EQU   *                                                        32800020
         IC    R15,PARMLEN(,R1)         PICK UP SHIFT LENGTH            33200020
         STH   R15,INSOFF(,RPREFIX)     STORE AS INSERT OFFSET          33600020
         LR    RSCAN5,R15                                               34000020
         IC    RSCAN5,LCBISZE           PICK UP NO. OF IDLES            34400020
         LR    R14,RSCAN5               SAVE FOR LCB UPDATE             34800020
         CR    RSCAN5,R15               IS IT LESS THAN SHIFT LNG       35200020
         BNL   SETPARM                  NO, PROCEED                     35600020
         SPACE                                                          36000020
         LA    R15,AVTECD4              YES, SET ERROR RETURN CODE      36400020
         B     EXIT                     RETURN IMMEDIATELY              36800020
         SPACE                                                          37200020
SETPARM  EQU   *                                                        37600020
         LA    RSCAN5,AVTTXTSZ+1(,RSCAN5) ADD LNG OF TEXT PREFIX +1     38000020
         TM    PRFSTAT1,PRFNHDRN        IS IT HEADER PREFIX             38400020
         BO    SETDATA                  NO, STORE DATA OFFSET           38800020
         SPACE                                                          39200020
         LA    RSCAN5,AVTHDRSZ-AVTTXTSZ(,RSCAN5) YES, ADD EXTRA         39600020
         SPACE                                                          40000020
SETDATA  EQU   *                                                        40400020
         STH   RSCAN5,DATOFF(,RPREFIX)  NO, STORE AS DATA OFFSET        40800020
         SR    R14,R15                  DECR NO. IDLES BY SHIFT LNG     41200020
         STC   R14,LCBISZE              STORE BACK IN LCB               41600020
         LH    RLIM8,PRFSCAN            LOAD SCAN POINTER OFFSET        42000020
         N     RLIM8,AVTCLRHI                                           42400020
         LA    RLIM8,ONE(,RLIM8)        SET TOTAL                       42800020
         SR    RLIM8,RSCAN5               SHIFT LENGTH                  43200020
         BZ    ZERORTN                  NO SHIFT NEEDED, RETURN         43600020
         SPACE                                                          44000020
         IC    R1,AVTEZERO(,R1)         SAVE INDEX BYTE                 44400020
         STC   R1,AVTPARM                 IN AVT PARAM AREA             44800020
         TM    LCBSTAT1,LCBSENDN        IS IT SEND               S22025 44860022
         BO    NOUP                     YES                      S22025 44920022
         STH   R15,PRFXTRA+1            OBTAIN EXPAND LENGTH     S22025 45010022
         MVI   PRFXTRA,AVTEZERO         CLEAR FLAG BYTE          S22025 45050022
NOUP     EQU   *                                                 S22025 45100022
         B     SAVEINIT                 GO EXPAND BUFFER                45200020
         SPACE                                                          45600020
SHIFINIT EQU   *                                                        46000020
         LTR   RLIM8,RLIM8              TEST LIMIT REG FOR ZERO         46100020
         BZ    ZERORTN                  ZERO, DON'T SHIFT, RETURN       46200020
         SPACE                                                          46300020
         LH    RSCAN5,DATOFF(,RPREFIX)  GET INITIAL DATA OFFSET         46400020
         N     RSCAN5,AVTCLRHI                                          46800020
         SPACE                                                          47200020
SAVEINIT EQU   *                                                        47600020
         MVI   AVTPARM+1,AVTEZERO       SET DATA TYPE = CHARS           48000020
         LR    ROFF7,RSCAN5             SAVE INITIAL DATA OFFSET        48400020
         SPACE                                                          48800020
SHIFDATA EQU   *                                                        49200020
         LR    RWORK2,RPREFIX           LOAD PREFIX ADDR         S22025 49700022
         BAL   R14,ENTERLOP             GET ADDR OF SCAN POINTER S22025 50200022
         SPACE                                                          50800020
         SR    REX11,RSCAN5             GET COUNT TO END OF UNIT        51200020
         CR    REX11,RLIM8              IS IT GT TOTAL SHIFT LENGTH     51600020
         BNH   SETPARM3                 NO, SET AS SHIFT LENGTH         52000020
         SPACE                                                          52400020
         LR    REX11,RLIM8              YES, SET TOTAL AS SHIFT LNG     52800020
         SPACE                                                          53200020
SETPARM3 EQU   *                                                        53600020
         ST    RSCAN5,AVTPARM3          SET SHIFT ADDRESS               54000020
         STC   REX11,AVTPARM3           SET SHIFT LENGTH                54400020
         AR    ROFF7,REX11              SET NEW DATA OFFSET             54800020
         SR    RLIM8,REX11              DECREMENT TOTAL SHIFT LNG       55200020
         BAL   RLOCRTN,INSERT           GO SHIFT ONE UNIT OF DATA       55600020
         SPACE                                                          56000020
         STH   ROFF7,DATOFF(,RPREFIX)   STORE NEW DATA OFFSET           56400020
         LR    RSCAN5,ROFF7             PREPARE FOR NEXT SHIFT          56800020
         LTR   RLIM8,RLIM8              IS LIMIT ZERO                   57200020
         BNZ   SHIFDATA                 NO, SHIFT AGAIN                 57600020
         SPACE                                                          58000020
ZERORTN  EQU   *                                                        58400020
         SR    R15,R15                  YES, SET GOOD RETURN CODE       58800020
         SPACE                                                          59200020
EXIT     EQU   *                                                        59600020
         L     RBASE,AVTUI              GET RETURN INTERFACE ADDRS22025 60000022
         B     FOUR(RBASE)              BRANCH TO RETURN ROUTINE S22025 60400022
         EJECT                                                          60800020
INSERTR  EQU   *                                                        61200020
         LA    RLOCRTN,INSEXIT          SET IMMEDIATE RETURN            61600020
         SPACE                                                          62000020
INSERT   EQU   *                                                        62400020
         LH    RSCAN5,DATOFF(,RPREFIX)  COMPUTE TRUE                    62800020
         N     RSCAN5,AVTCLRHI           INSERT                         63200020
         SH    RSCAN5,INSOFF(,RPREFIX)    OFFSET                        63600020
         LR    R0,RSCAN5                SAVE DATA OFFSET         S22025 63800022
         LR    RWORK2,RPREFIX           LOAD PREFIX ADDR         S22025 64100022
         BAL   R14,ENTERLOP             GET ADDR OF SCAN POINTER S22025 64400022
         LR    R1,RWORK2                SAVE OFFSET              S22025 64700022
         SPACE                                                          65200020
         SR    REX11,RSCAN5             GET EOU COUNT                   65600020
         SR    RWORK2,RWORK2                                            66000020
         IC    RWORK2,AVTPARM3          GET INSERT LENGTH               66400020
         TM    LCBSTAT1,LCBSENDN        IS IT SENDING            S22025 66440022
         BO    CONTINUE                 YES CONTINUE AS USUAL    S22025 66490022
         L     RSCB3,LCBSCBA-1          OBTAIN SCBMADDRESS       S22025 66520022
         CLC   AVTADBUF+1(3),SCBDEOB+1  COMPARE FOR EOB BUFFER   S22025 66580022
         BNE   CLRSTAT                  NOT EOB BUF              S22025 66640022
         BCTR  R0,R0                    GET EXACT OFFSET         S22025 66650022
COMPARE  EQU   *                                                 S22025 66665022
         CH    R0,SCBEOB                COMP DATA OFFSET AND EOB S22025 66670022
         BNL   CLRSTAT                  BRANCH TO CLEAR STATUS   S22025 66700022
FUNCTION EQU   *                                                 S22025 66730022
         CLI   PRFXTRA,FLAGOFF          NO FUNCTION SPECIFIED    S22025 66820022
         BE    CONTINUE                 YES                      S22025 66850022
         TM    PRFXTRA,DATCONT          IS CONTRACT SPECIFIED    S22025 66880022
         BO    DELETE                   YES,GO DELETE            S22025 66910022
         CLI   PRFXTRA,IDLCT            ARE IDLES SPECIFIED     SA56295 66940010
         BE    INSRTEOB                 YES, COMPUTE            SA56295 66970010
CKREPL   EQU   *                                                        67000022
         TM    PRFXTRA,OFFSTO+EXTTO+CHARTO           REPLACE     S22025 67030022
         BZ    INSRTEOB                 NO INSERT                S22025 67060022
*    CALCULATE EOB OFFSET FOR REPLACE FUNCTION                        * 67090022
CALCREPL EQU   *                                                 S22025 67120022
         SH    RWORK2,PRFXTRA+1         INSERT - DELETE          S22025 67150022
         B     INSRTEOB                 ADJUST EOB OFFSET        S22025 67180022
**         CALCULATE EOB OFFSET FOR DELETE                           ** 67210022
DELETE   EQU   *                                                 S22025 67240022
         LH    RWORK2,PRFXTRA+1         GET DELETE LENGTH        S22025 67270022
         AR    R0,RWORK2                DATA OFFSET+DELETE LENGTHS22025 67300022
         CH    R0,SCBEOB                COMP DATA OFF+DEL LEN&EOBS22025 67330022
         BNL   DATADD                   ADJUST EOB OFFSET        S22025 67360022
**       EOB OFFSET GREATER THAN DATA OFFSET + DELETE LENGTH        **  67390022
         LNR   RWORK2,RWORK2            MAKE COUNT NEGATIVE      S22025 67420022
         B     INSRTEOB                 ADJUST EOB               S22025 67450022
**       EOB OFFSET LESS THAN OR EQUAL TO DATA OFFSET + DELETE LENGTH*  67480022
DATADD   EQU   *                                                 S22025 67510022
         SR    R0,RWORK2                GET DATA OFFSET          S22025 67520022
         STH   R0,SCBEOB                STORE EOB OFFSET         S22025 67540022
         B     CLRSTAT                  BRANCH TO CLEAR STATUS   S22025 67570022
*                    INSERT FUNCTION                                 *  67780022
INSRTEOB EQU   *                                                 S22025 67810022
         AH    RWORK2,SCBEOB            INCREMENT EOB OFFSET     S22025 67840022
         STH   RWORK2,SCBEOB            STORE EOB OFFSET         S22025 67870022
CLRSTAT  EQU   *                                                 S22025 67900022
         MVI   PRFXTRA,FLAGOFF          TURN OFF STATUS BYTE     S22025 67930022
         SR    RWORK2,RWORK2            CLEAR WORK REGISTER      S22025 67960022
         IC    RWORK2,AVTPARM3          GET INSERT LENGTH        S22025 67990022
CONTINUE EQU   *                                                 S22025 68020022
         TM    AVTPARM+1,IDLEFLAG       IDLES OR DATA                   68050022
         BO    INSRTIDL                 IDLES,GO INSERT                 68080022
         L     RFROM3,AVTPARM3          GET INSERT DATA ADDR            68400020
         SPACE                                                          68500020
DATALOOP EQU   *                                                        68600020
         CR    REX11,RWORK2             IS EOU COUNT LT DATA LNG        68800020
         BNL   MOVE2                    NO, INSERT ALL DATA             69200020
         SPACE                                                          69600020
         BCTR  REX11,AVTEZERO           DECREMENT FOR EXECUTE           70000020
         EX    REX11,MOVEX              MOVE ALL DATA THAT FITS         70400020
         LA    REX11,ONE(,REX11)        RESET DATA COUNT                70800020
         SPACE                                                          71200020
         AR    RFROM3,REX11             UPDATE DATA ADDRESS             71600020
         SR    RWORK2,REX11             DECR LENGTH BY NO. MOVED        72000020
         SPACE                                                          72400020
         L     R1,PRFTIC-IEDQPRF(,R1)   GET NEW UNIT ADDRESS     S22025 72700022
         LA    RSCAN5,AVTUMALN(,R1)     POINT TO START OF DATA   S22025 73000022
         LH    REX11,AVTKEYLE           SET EOU COUNT = UNIT LENGTH     73300020
        B     DATALOOP                  GO SHIFT/MOVE NEXT UNIT         73400020
         SPACE                                                          73600020
MOVE2    EQU   *                                                        74000020
         BCTR  RWORK2,AVTEZERO          DECREMENT FOR EXECUTE           74400020
         EX    RWORK2,MOVEX             MOVE ALL DATA                   74800020
         BR    RLOCRTN                  RETURN TO CALLER                75200020
         SPACE                                                          75600020
         SPACE 3                                                        76000020
********* EXECUTED INSTRUCTION *********                                76400020
         SPACE                                                          76800020
MOVEX    MVC   AVTEZERO(,RSCAN5),AVTEZERO(RFROM3)                       77200020
         EJECT                                                          77600020
INSRTIDL EQU   *                                                        78000020
         LH    RCHAR3,AVTPARM3          GET IDLE CHARACTER              78400020
         SPACE                                                          78800020
         BCTR  RSCAN5,AVTEZERO          ADJUST INSERT ADDR BACK         79200020
         SPACE                                                          79600020
         CR    REX11,RWORK2             IS EOU COUNT LT IDLES COUNT     80000020
         BNL   STC2                     NO, INSERT ALL IDLES            80400020
         SPACE                                                          80800020
         SR    RWORK2,REX11             SET COUNT FOR SECOND LOOP       81200020
         SPACE                                                          81600020
STC1     EQU   *                                                        82000020
         STC   RCHAR3,AVTEZERO(REX11,RSCAN5) INSERT IDLE                82400020
         BCT   REX11,STC1               LOOP TILL UNIT FULL             82800020
         SPACE                                                          83200020
         L     RSCAN5,PRFTIC-IEDQPRF(,R1) GET NEW UNIT ADDRESS   S22025 83600022
         LA    RSCAN5,AVTUMALN-1(,RSCAN5) POINT TO START OF DATA -1     84000020
         SPACE                                                          84400020
STC2     EQU   *                                                        84800020
         STC   RCHAR3,AVTEZERO(RWORK2,RSCAN5) INSERT IDLE               85200020
         BCT   RWORK2,STC2              LOOP TILL COUNT RUNS OUT        85600020
         SPACE                                                          86000020
         BR    RLOCRTN                  EXIT                            86400020
         SPACE                                                          86800020
INSEXIT  EQU   *                                                        87200020
         IC    RWORK2,AVTPARM3          PICK UP INSERT LENGTH           87600020
         LNR   RWORK2,RWORK2            FLIP FOR INVERSE SUBTRACT       88000020
         AH    RWORK2,INSOFF(,RPREFIX)  DECREMENT NO. AVAIL BYTES       88400020
         STH   RWORK2,INSOFF(,RPREFIX)  STORE BACK                      88800020
         B     ZERORTN                  RETURN TO CALLER                89200020
         SPACE 1                                                 S22025 89230022
ADDRLOOP EQU   *                                                 S22025 89260022
         L     RWORK2,PRFTIC-IEDQPRF(RWORK2) PT TO NEXT UNIT     S22025 89290022
         SH    RSCAN5,AVTKEYLE          SUBTRACT NO. BYTES PASSEDS22025 89320022
ENTERLOP EQU   *                                                 S22025 89350022
         CH    RSCAN5,AVTKEYLE          IS ITEM IN THIS UNIT     S22025 89380022
         BH    ADDRLOOP                 NO, TEST NEXT UNIT       S22025 89410022
         LA    RSCAN5,AVTUMALN-1(RSCAN5,RWORK2) YES, GET ADDR OF S22025 89440022
         LH    REX11,AVTKEYLE           GET KEY LENGTH           S22025 89470022
         LA    REX11,AVTUMALN(RWORK2,REX11) ADD RECB LNG + UNIT  S22025 89500022
         BR    R14                      RETURN TO CALLER         S22025 89530022
         EJECT                                                          89600020
********* DSECTS *********                                              90000020
         SPACE                                                          90400020
         TAVTD                                                          90800020
         EJECT                                                          91200020
         TLCBD                                                          91600020
         SPACE                                                          92000020
         TPRFD                                                          92400020
         EJECT                                                          92800020
         TSCBD                                                          93200020
         SPACE                                                          93600020
         END                                                            94000020
