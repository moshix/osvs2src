AH01     TITLE '''IEDQAH'' - INPUT SEQUENCE VERIFICATION ROUTINE'       00300020
IEDQAH   CSECT                                                          00600020
*A519000,703000-705000                                           S22025 00660022
*C042000,093000,123000                                           S22025 00720022
*C517200-518400,531000,633000,648000                             S22025 00780022
*D264000,516600-518400,519000,705000                             S22025 00840022
* $01=OZ41279  JTC2202  79.11.06 047252:                           @01A 00870012
         SPACE                                                          00900020
*********************************************************************** 01200020
*                                                                  @01A 01250012
*  $MOD(IEDQAH) COMP(M@) PROD(TCAM):                               @01A 01300012
*                                                                     * 01500020
*STATUS -- CHANGE LEVEL 0                                             * 01800020
*                                                                     * 02100020
*FUNCTION -- VERIFIES AND UPDATES AN INPUT SEQUENCE NUMBER SPECI-     * 02400020
*   FIED BY THE USER IN THE BUFFER.                                   * 02700020
*                                                                     * 03000020
*   THE ROUTINE EXAMINES THE TERMINAL-CURRENTLY-CONNECTED FIELD IN    * 03300020
*   THE LCB (LCBTTCIN).  IF IT IS ZEROES,  THE ORIGIN IS UNKNOWN.     * 03600020
*   A RETURN CODE OF X'0C' IS SET IN REGISTER 15 AND RETURN IS MADE   * 03900020
*   TO THE CALLER VIA MH INTERFACE ROUTINE (IEDQUI).             S22025 04200022
*                                                                     * 04500020
*   IF IT IS NOT ZEROES,  LCBTTCIN IS PASSED TO THE TNT CODE          * 04800020
*   (AVTRNMPT),  WHICH RETURNS THE ADDRESS OF THE TERMINAL TABLE      * 05100020
*   ENTRY FOR THE ORIGIN.                                             * 05400020
*                                                                     * 05700020
*   THE EBCDIC CHARACTERS MAKING UP THE USER-SUPPLIED INPUT           * 06000020
*   SEQUENCE NUMBER ARE PASSED IN THE AVT WORK AREA (AVTDOUBL).       * 06300020
*   THE ROUTINE CONVERTS THIS NUMBER TO A BINARY NUMBER AND           * 06600020
*   COMPARES IT TO THE ANTICIPATED INPUT SEQUENCE NUMBER LOCATED      * 06900020
*   IN THE TERMINAL TABLE ENTRY (TRMINSEQ).  IF THE NUMBER PASSED     * 07200020
*   IS HIGHER,  THE 'SEQUENCE NUMBER HIGH' ERROR FLAG IS SET IN       * 07500020
*   THE SCB,  AND A RETURN CODE OF X'08' IS SET IN REGISTER 15.  IF   * 07800020
*   THE NUMBER PASSED IS LOWER,  THE 'SEQUENCE NUMBER LOW'  ERROR     * 08100020
*   FLAG IS SET IN THE SCB,  AND A RETURN CODE OF X'04' IS SET IN     * 08400020
*   REGISTER 15.  IN BOTH CASES,  THE SEQUENCE NUMBER IN THE          * 08700020
*   TERMINAL TABLE ENTRY REMAINS UNCHANGED AND RETURN IS MADE TO      * 09000020
*   THE CALLER VIA IEDQUI                                        S22025 09300022
*                                                                     * 09600020
*   IF THE PASSED NUMBER IS EQUAL TO THE NUMBER IN THE TERMINAL       * 09900020
*   TABLE ENTRY,  THE SEQUENCE-IN FLAG IN THE SCB STATUS FIELD IS     * 10200020
*   SET (SCBSEQIN IN SCBSTATE).  IF THE CURRENT BUFFER IS A HEADER    * 10500020
*   BUFFER,  THE NUMBER IS SET IN THE PREFIX INPUT SEQUENCE NUMBER    * 10800020
*   FIELD (PRFISEQ).  THE NUMBER IS THEN INCREMENTED BY ONE.  IF      * 11100020
*   THE RESULT IS OVER 9999 (THE MAXIMUM PERMITTED SEQUENCE NUMBER)   * 11400020
*   IT IS RESET TO ONE.  THE UPDATED NUMBER IS STORED BACK IN THE     * 11700020
*   TERMINAL TABLE ENTRY,  A RETURN CODE OF X'00' IS SET IN           * 12000020
*   REGISTER 15, AND RETURN IS MADE TO THE CALLER VIA IEDQUI.    S22025 12300022
*                                                                     * 12600020
*ENTRY POINTS --                                                      * 12900020
*       'IEDQAH01' TO VERIFY AND UPDATE THE INPUT SEQUENCE NUMBER.    * 13200020
*   CALLING SEQUENCE IS:                                              * 13500020
*                                                                     * 13800020
*        L     R12,AVTMSGS-1            GET ADDR OF VCON TABLE        * 14100020
*        IC    R15,AVTEZERO(,R1)        GET INDEX TO ROUTINE ADDR     * 14400020
*        L     R12,AVTEZERO(R12,R15)    GET ROUTINE ADDRESS           * 14700020
*        BR    R12                      EXIT TO ROUTINE               * 15000020
*                                                                     * 15300020
*INPUT --                                                             * 15600020
*   REGISTER 1 - THE ADDRESS OF A MACRO-GENERATED PARAMETER LIST.     * 15900020
*   PARAMETER LIST FORMAT IS:                                         * 16200020
*                                                                     * 16500020
*   *****************                                                 * 16800020
*   * INDEX * PARM  *                                                 * 17100020
*   *  TO   *  LIST *                                                 * 17400020
*   *   AH  *  LNG  *                                                 * 17700020
*   *****************                                                 * 18000020
*                                                                     * 18300020
*   REGISTER 3 - ADDRESS OF THE SCB.                                  * 18600020
*                                                                     * 18900020
*   REGISTER 4 - ADDRESS OF THE LCB.                                  * 19200020
*                                                                     * 19500020
*   REGISTER 6 - ADDRESS OF THE CURRENT BUFFER.                       * 19800020
*                                                                     * 20100020
*   REGISTER 9 - ADDRESS OF THE AVT.                                  * 20400020
*                                                                     * 20700020
*   REGISTER 12 - ENTRY POINT AND BASE REGISTER FOR THIS ROUTINE.     * 21000020
*                                                                     * 21300020
*   FIRST AVT PARAMETER AREA (AVTPARM) - THE NUMBER OF BYTES IN THE   * 21600020
*   USER-SUPPLIED INPUT SEQUENCE NUMBER.                              * 21900020
*                                                                     * 22200020
*   AVT WORK AREA (AVTDOUBL) - THE USER-SUPPLIED INPUT SEQUENCE       * 22500020
*   NUMBER.                                                           * 22800020
*                                                                     * 23100020
*OUTPUT --                                                            * 23400020
*   REGISTER 15 - RETURN CODE.  ITS VALUE IS X'00' ON SUCCESSFUL      * 23700020
*   COMPLETION,  X'04' IF SEQUENCE NUMBER LOW,  X'08' IF SEQUENCE     * 24000020
*   NUMBER HIGH,  AND X'0C' IF ORIGIN NOT SPECIFIED.                  * 24300020
*                                                                     * 24600020
*   PREFIX INPUT SEQUENCE NUMBER FIELD (PRFISEQ) - IF A HEADER        * 24900020
*   BUFFER,  INITIALIZED TO THE INPUT SEQUENCE NUMBER ON SUCCESSFUL   * 25200020
*   COMPLETION.                                                       * 25500020
*                                                                     * 25800020
*EXTERNAL REFERENCES --                                               * 26100020
*                                                                     * 26700020
*   TERMINAL NAME TABLE CODE (AVTRNMPT).                              * 27000020
*                                                                     * 27300020
*   AVT - ADDRESS VECTOR TABLE.                                       * 27600020
*                                                                     * 27900020
*   BUFFER CURRENTLY BEING PROCESSED.                                 * 28200020
*                                                                     * 28500020
*   LCB - LINE CONTROL BLOCK.                                         * 28800020
*                                                                     * 29100020
*   SCB - STATION CONTROL BLOCK.                                      * 29400020
*                                                                     * 29700020
*EXITS,  NORMAL -- SUCCESSFUL COMPLETION.  IF A HEADER BUFFER,  THE   * 30000020
*   PREFIX INPUT SEQUENCE NUMBER FIELD IS SET.  THE INPUT SEQUENCE    * 30300020
*   NUMBER FIELD IN THE TERMINAL TABLE ENTRY (TRMINSEQ) IS UPDATED    * 30600020
*   TO THE NEXT ANTICIPATED INPUT SEQUENCE NUMBER.  THE SEQUENCE-IN   * 30900020
*   FLAG (SCBSEQIN) IS SET IN THE SCB (SCBSTATE).  REGISTER 15 CON-   * 31200020
*   TAINS A RETURN CODE OF X'00'.                                     * 31500020
*                                                                     * 31800020
*EXITS,  ERROR -- ORIGIN IS NOT SPECIFIED IN THE LCB (LCBTTCIN).      * 32100020
*   REGISTER 15 CONTAINS A RETURN CODE OF X'0C'.                      * 32400020
*                                                                     * 32700020
*   USER-SUPPLIED INPUT SEQUENCE NUMBER IS LOW.  THE 'SEQUENCE-       * 33000020
*   NUMBER-LOW' ERROR FLAG IS SET IN THE SCB.  REGISTER 15 CONTAINS   * 33300020
*   A RETURN CODE OF X'04'.                                           * 33600020
*                                                                     * 33900020
*   USER-SUPPLIED INPUT SEQUENCE NUMBER IS HIGH.  THE 'SEQUENCE-      * 34200020
*   NUMBER-HIGH' ERROR FLAG IS SET IN THE SCB.  REGISTER 15           * 34500020
*   CONTAINS A RETURN CODE OF X'08'.                                  * 34800020
*                                                                     * 35100020
*TABLES/WORK AREAS -- N/A.                                            * 35400020
*                                                                     * 35700020
*ATTRIBUTES -- SERIALLY REUSABLE,  REFRESHABLE,  ENABLED,             * 36000020
*   RESIDENT,  PROBLEM PROGRAM MODE.                                  * 36300020
*                                                                     * 36600020
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON AN        * 36900020
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.            * 37200020
*                                                                     * 37500020
*********************************************************************** 37800020
         EJECT                                                          38100020
********* REGISTER EQUATES *********                                    38400020
         SPACE                                                          38700020
RDIG0    EQU   0                        DIGIT REGISTER                  39000020
         SPACE                                                          39300020
R1       EQU   1                        INITIAL PARAMETER LIST ADDR     39600020
*                                         AND TERMINAL ENTRY ADDR       39900020
         SPACE                                                          40200020
RRET2    EQU   2                        EXIT REGISTER                   40500020
         SPACE                                                          40800020
RSCB3    EQU   3                        SCB ADDRESS                     41100020
         SPACE                                                          41400020
RLCB4    EQU   4                        LCB ADDRESS                     41700020
         SPACE                                                          42000020
RPREFIX  EQU   6                        BUFFER ADDRESS                  42300020
         SPACE                                                          42600020
RCLEAR7  EQU   7                        CLEAR MASK REGISTER             42900020
         SPACE                                                          43200020
RAVT9    EQU   9                        AVT ADDRESS                     43500020
         SPACE                                                          43800020
RCT11    EQU   11                       COUNT REGISTER                  44100020
         SPACE                                                          44400020
RBASE12  EQU   12                       BASE REGISTER                   44700020
         SPACE                                                          45000020
RINDEX14 EQU   14                       INDEX TO SUBSEQUENT DIGITS      45300020
R14      EQU   14                       RETURN REGISTER                 45600020
         SPACE                                                          45900020
RNUM15   EQU   15                       TRUE SEQ IN NO. (BFR NO.)       46200020
R15      EQU   15                       RETURN CODE REGISTER            46500020
         SPACE                                                          46800020
********* OTHER EQUATES *********                                       47100020
         SPACE                                                          47400020
NOTZERO  EQU   7                        CONDITION CODE: NOT ZERO        47700020
CLERMASK EQU   X'0F'                    MASK TO CLEAR ZONE              48000020
FOUR     EQU   4                        MAX. NO. OF DIGITS IN SEQIN     48300020
ONE      EQU   1                        INCREMENT VALUE OF 1            48600020
BLANK    EQU   X'40'                    USE TO CHK DELIM. BLK    @01A   48650012
         SPACE                                                          48900020
ZERO     EQU   8                        CONDITION CODE: ZEROES          49200020
NUMZONE  EQU   X'F0'                    ZONE FOR A NUMBER               49300020
         EJECT                                                          49500020
         USING IEDQAVTD,RAVT9                                           49800020
         USING IEDQLCB,RLCB4                                            50100020
         USING IEDQPRF,RPREFIX                                          50400020
         USING IEDQSCB,RSCB3                                            50700020
         USING IEDQAH,RBASE12                                           51000020
         SPACE                                                          51300020
IEDQAH01 EQU   *                                                        51600020
IEDQAH   IEDHJN SEQIN                   MODULE ID                S22025 51900022
         LH    R1,LCBTTCIN              PICK UP SOURCE KEY              52200020
         N     R1,AVTCLRHI              IS IT ZERO                      52500020
         LA    R15,AVTECD12             ANTICIPATE ORIGIN NOT FOUND     52800020
         BC    ZERO,EXIT                BRANCH TO RETURN IF ZERO S22025 53100022
         SPACE                                                          53400020
         L     R15,AVTRNMPT             GET TNT CODE ADDRESS            53700020
         BALR  R14,R15                  ELSE LINK TO GET TERM ENTRY     54000020
         SPACE                                                          54300020
CONVERT  EQU   *                                                        54600020
         USING IEDQTRM,R1                                               54900020
         SR    RNUM15,RNUM15            SET INITIAL VALUE               55200020
         LA    RCLEAR7,CLERMASK         SET CLEAR MASK                  55500020
         LA    RINDEX14,AVTDOUBL        POINT TO 1ST SEQ IN DIGIT       55800020
         LA    RCT11,FOUR               GET MAX COUNT OF DIGITS         56100020
         CLI   AVTPARM+1,FOUR           ARE THERE FOUR OR MORE          56400020
         BNL   CKBLK                    YES, CHK FOR DELIM. BLK  @01C   56700012
         SPACE                                                          57000020
         IC    RCT11,AVTPARM+1          NO, PICK UP NO. OF DIGITS       57300020
CKBLK    EQU   *                                                   @01A 57320012
         LR    R15,RINDEX14             PTR TO FIRST DIGIT         @01A 57340012
         AR    R15,RCT11                INCR PAST SEQ NUMBER       @01A 57360012
         CLI   0(R15),BLANK             DELIMITING BLANK?          @01A 57380012
         BNE   ERROR                    NO,SET ERROR               @01A 57400012
         SR    RNUM15,RNUM15            CLEAR REG                  @01A 57420012
         B     LOOPENT                  CONTINUE                   @01A 57440012
ERROR    EQU   *                                                   @01A 57460012
         L     R15,RC                   SET BAD RETURN CODE        @01A 57480012
         B     EXIT                     BRANCH TO RETURN           @01A 57500012
         SPACE                                                          57900020
LOOP     EQU   *                                                        58200020
         MH    RNUM15,H10               MULTIPLY NUMBER BY TEN          58500020
         LA    RINDEX14,ONE(,RINDEX14)  POINT TO NEXT DIGIT             58800020
         SPACE                                                          59100020
LOOPENT  EQU   *                                                        59400020
         TM    AVTEZERO(R14),NUMZONE    IS THIS A NUMBER                59470020
         BNO   SETLOW                   NOT A NUMBER, CONSIDER LOW      59540020
         SPACE                                                          59610020
         IC    RDIG0,AVTEZERO(,RINDEX14) PICK UP DIGIT                  59700020
         NR    RDIG0,RCLEAR7            CLEAR ZONE                      60000020
         AR    RNUM15,RDIG0             ADD IT TO ACCUMULATED           60300020
         BCT   RCT11,LOOP               LOOP TILL ALL DIGITS GOTTEN     60600020
         SPACE                                                          60900020
         CH    RNUM15,TRMINSEQ          TEST IF SEQ. NO. CORRECT        61200020
         BE    GOOD                     YES, BRANCH                     61500020
         SPACE                                                          61800020
         BL    SETLOW                   BFR NO. LOW, BRANCH             62100020
         SPACE                                                          62400020
         OI    SCBERR1,SCBSEQHN         SET 'SEQ NO. HI' IN SCB         62700020
         LA    R15,AVTECD8              SET 'SEQ NO. HI' RTN CODE       63000020
         B     EXIT                     BRANCH TO RETURN         S22025 63300022
         SPACE                                                          63600020
SETLOW   EQU   *                                                        63900020
         OI    SCBERR1,SCBSEQLN         SET 'SEQ NO. LO' IN SCB         64200020
         LA    R15,AVTECD4              SET 'SEQ NO. LO' RTN CODE       64500020
         B     EXIT                     BRANCH TO RETURN         S22025 64800022
         SPACE                                                          65100020
GOOD     EQU   *                                                        65400020
         OI    SCBSTATE,SCBSEQIN        SET FLAG IN SCB STATUS          65700020
         TM    PRFSTAT1,PRFNHDRN        IS PREFIX A HDR PREFIX          66000020
         BO    UPCOUNT                  NO, CAN'T SET FIELD, BRANCH     66300020
         SPACE                                                          66600020
         STH   RNUM15,PRFISEQ           YES, SET BFR NO. IN PREFIX      66900020
         SPACE                                                          67200020
UPCOUNT  EQU   *                                                        67500020
         LA    RNUM15,ONE(,RNUM15)      ADD ONE TO BFR NO.              67800020
         CH    RNUM15,H10K              MAX SEQ-IN NO. EXCEEDED         68100020
         BL    STORE                    NO, VALID AS IS                 68400020
         SPACE                                                          68700020
         LA    RNUM15,ONE               YES, RESET TO ONE               69000020
         SPACE                                                          69300020
STORE    EQU   *                                                        69600020
         STH   RNUM15,TRMINSEQ          SET AS NEXT EXPECTED NO.        69900020
         SR    R15,R15                  SET GOOD RETURN CODE            70200020
EXIT     EQU   *                                                 S22025 70300022
         L     RBASE12,AVTUI            GET ADDR OF RET ROUTINE  S22025 70400022
         B     FOUR(RBASE12)            RETURN TO CALLER         S22025 70500022
         SPACE 3                                                        70800020
********* CONSTANTS *********                                           71100020
         SPACE                                                          71400020
H10      DC    H'10'                    RADIX OF DECIMAL NUMBER         71700020
H10K     DC    H'10000'                 MAXIMUM SEQUENCE-IN NO.         72000020
RC       DC    F'-4'                    DELIMIT. BLK NOT FOUND    @01A  72050012
         EJECT                                                          72300020
********* DSECTS *********                                              72600020
         SPACE                                                          72900020
         TAVTD                                                          73200020
         EJECT                                                          73500020
         TLCBD                                                          73800020
         EJECT                                                          74100020
         TPRFD                                                          74400020
         EJECT                                                          74700020
         TSCBD                                                          75000020
         EJECT                                                          75300020
         TTRMD                                                          75600020
         SPACE                                                          75900020
         END                                                            76200020
