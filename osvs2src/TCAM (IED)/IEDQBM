BM01     TITLE '''IEDQBM'' - ORIGIN ROUTINE FOR SYSTEM WITH CONCENTRATE*00100022
               D MESSAGE HANDLING SUPPORT'                              00200022
         EJECT                                                          00300022
IEDQBM   CSECT                                                          00400022
*A000000-999999                                                @Z37XAMF 00420000
*A032000,265000,269000,274000,368000,370050,372000,377000,     @Y17XAXF 00440000
*A383000,384100,539000                                         @Y17XAMF 00460000
*********************************************************************** 00500022
*                                                                     * 00600022
*STATUS - CHANGE LEVEL 5                                         S22024 00700000
*                                                                     * 00800022
* FUNCTION -                                                          * 00900022
*                                                                     * 01000022
*    WITHOUT THE FORM= OPERAND SPECIFIED -                            * 01100022
*                                                                     * 01200022
*    VERIFIES THE ORIGIN OF THE MESSAGE WHEN IT IS SPECIFIED OR       * 01300022
*    INITIALIZES IT WHEN IT IS NOT FOR TERMINALS NOT IN A             * 01400022
*    CONCENTRATOR NETWORK.                                            * 01500022
*                                                                     * 01600022
*    THE BINARY SEARCH ROUTINE IS USED TO SEARCH THE TERMNAME         * 01700022
*    TABLE FOR AN ENTRY MATCHING THE NAME FOUND IN THE BUFFER.        * 01800022
*    BINARY SEARCH THEN RETURNS IN REGISTER 15 EITHER A ZERO          * 01900022
*    OR A NON-ZERO VALUE.  IF IT IS ZERO, THE NAME FROM THE           * 02000022
*    BUFFER WAS NOT FOUND IN THE TERMINAL NAME TABLE.  THE            * 02100022
*    'INVALID ORIGIN' BIT IS SET IN THE SCB ERROR BYTE (SCBERR1)      * 02200022
*    AND EXIT IS MADE TO RETURN INTERFACE WITH A RETURN CODE          * 02300022
*    X'04' IN REGISTER 15.                                            * 02400022
*                                                                     * 02500022
*    IF THE RETURN CODE IS NOT ZERO, IT IS THE KEY (ORDINAL INDEX)    * 02600022
*    TO THE TERMINAL TABLE ENTRY FOR THE NAME FOUND.  THIS KEY IS     * 02700022
*    COMPARED WITH THE KEY IN THE BUFFER PREFIX SOURCE FIELD          * 02800022
*    (PRFSRCE).  IF THEY ARE EQUAL, THE KEY IN THE PREFIX IS          * 02900022
*    CORRECT.  EXIT IS MADE TO RETURN INTERFACE WITH A RETURN CODE    * 03000022
*    OF X'00' IN REGISTER 15.                                         * 03100022
*                                                                     * 03200022
*    IF THE KEYS ARE EQUAL, A VALIEDITY CHECK IS THIS MODULES  @PI06026 03200600
*    ONLY FUNCTION.  IF THE KEYS ARE NOT EQUAL AND THE TERMINAL@PI00626 03201200
*    IS NOT A DIAL RESOURCE, AND ERROR IS RETURNED TO USER.    @PI06026 03201800
*    IF THE KEYS ARE NOT EQUAL AND THE TERMINAL IS A DIAL      @Y17XAMF 03202400
*    RESOURCE, THE FOLLOWING LOGIC THEN OCCURS:                @Y17XAMF 03203000
*                                                              @Y17XAMF 03212000
*    A NON-ZERO PRFSRCE IS VALIDATED AS FOLLOWS.    IF AN EP   @Y17XAMF 03214000
*    RESOURCE, PRFSRCE MUST REFLECT A UTERM. IF A NCP RESOURCE @Y17XAMF 03220000
*    PRFSRCE MUST REFLECT A CTERM.                             @Y17XAMF 03224000
*                                                              @Y17XAMF 03228000
*    THE ORIGIN TTE IS THEN VALIDATED BY CHECKING THAT IF IT   @Y17XAMF 03232000
*    IS AN EP RESOURCE IT MUST BE A SINGLE, NON-LGB ENTRY.     @Y17XAMF 03236000
*    IF IT IS A NCP RESOURCE, IT MUST BE A PRE-SNA DEVICE.     @Y17XAMF 03240000
*                                                              @Y17XAMF 03244000
*    AFTER VALIDATING THE TTE TYPES, THE MODULE INSURES THAT   @Y17XAMF 03248000
*    BOTH RESOURCES HAVE THE SAME DCB(LGB) ADDRESS.            @Y17XAMF 03252000
*                                                              @Y17XAMF 03256000
*    IF THE ORIGIN NAME IS AN EP RESOURCE THE PRFSRCE AND      @Y17XAMF 03260000
*    LCBTTCIN IS UPDATED WITH ITS TTCIN AND A 0 RETURN CODE IS @Y17XAMF 03264000
*    SET.  IF IT IS AN NCP RESOURCE, IEDDVAS IS                @PI06026 03266000
*    CALLED IN ORDER TO CHANGE THE CONTROL BLOCKS SO THAT ALL  @Y17XAMF 03276000
*    COMMUNICATION WILL OCCUR WITH THE DIAL ENTRY.  LCBTTCIN   @Y17XAMF 03280000
*    AND PRFSRCE ARE THEN UPDATED WITH THE DIAL TTE'S TTCIN,   @Y17XAMF 03284000
*    AND A ZERO RETURN CODE IS SET                             @Y17XAMF 03288000
*                                                              @Y17XAMF 03292000
*                                                                     * 05400022
*    WITH THE FORM= OPERAND SPECIFIED -                               * 05500022
*                                                                     * 05600022
*    DETERMINES THE SOURCE TERMINAL ATTACHED TO THE CONCENTRATOR      * 05700022
*    ENTERING THE DATA, AND EXECUTES ONLY FOR TERMINALS DEFINED       * 05800022
*    IN A CONCENTRATOR NETWORK.                                       * 05900022
*                                                                     * 06000022
*    THE DETERMINATION OF THE TERMINAL ATTACHED TO A CONCENTRATOR     * 06100022
*    IS MADE EITHER BY SPECIFYING FORM=ID ON THE ORIGIN MACRO, IN     * 06200022
*    WHICH CASE A SEARCH OF THE DEVICE ID TABLE FOR THIS CONCENTRATOR * 06300022
*    IS MADE FOR AN ID EQUAL TO THE ID ENTERED IN THE MESSAGE,        * 06400022
*    OR BY SPECIFYING FORM=NAME, IN WHICH CASE A BINARY SEARCH OF     * 06500022
*    THE TERMNAME TABLE IS MADE FOR A MATCH.  IF A MATCH IS FOUND     * 06600022
*    IN THE TERMNAME TABLE, THEN A SECOND TEST IS MADE TO INSURE      * 06700022
*    THAT THIS TERMINAL IS ATTACHED TO THE CONCENTRATOR THAT          * 06800022
*    ENTERED THE MESSAGE.                                             * 06900022
*                                                                     * 07000022
*    IF THE FORM= OPERAND IS SPECIFIED AND THE ORIGIN ENTERED IN      * 07100022
*    THE MESSAGE IS NOT A VALID NAME OR ID FOR A TERMINAL ATTACHED    * 07200022
*    TO THE CONCENTRATOR THAT ENTERED THE MESSAGE, THE FIRST ENTRY    * 07300022
*    FOR AN ATTACHED TERMINAL AFTER THE CONCENTRATOR ENTRY IS         * 07400022
*    ASSUMED TO BE THE ORIGIN.  A RETURN CODE OF X'0C' IS RETURNED    * 07500022
*    IN REGISTER 15 ON THIS CONDITION.                                * 07600022
*                                                                     * 07700022
* ENTRY POINTS -                                                      * 07800022
*         'IEDQBM01' TO VERIFY OR INITIALIZE THE ORIGIN.              * 07900022
*         ENTRY IS INITIATED BY THE ORIGIN MACRO THROUGH IEDQUI.      * 08000022
*         THE CALLING SEQUENCE IN IEDQUI IS:                          * 08100022
*                                                                     * 08200022
*         L     R12,AVTMSGS-1            GET ADDR OF VCON TABLE       * 08300022
*         IC    R15,AVTEZERO(,R1)        GET INDEX TO ROUTINE ADDR    * 08400022
*         LA    R0,ANDLOW                SET UP MASK                  * 08500022
*         NR    R15,R0                   CLEAR INDEX FLAGS            * 08600022
*         L     R12,AVTEZERO(R12,R15)    GET ROUTINE ADDR             * 08700022
*         BR    R12                      EXIT TO ROUTINE              * 08800022
*                                                                     * 08900022
* INPUT -                                                             * 09000022
*    REGISTER 1 - ADDRESS OF A MACRO GENERATED PARAMETER LIST.        * 09100022
*    PARAMETER LIST FORMAT IS:                                        * 09200022
*    REGISTER 13- ADDRESS OF SAVE AREA                         @Y17XAMF 09250000
*                                                                     * 09300022
*    *********************************                                * 09400022
*    * INDEX * PARAM * INDEX * PARAM *                                * 09500022
*    * & FORM* LIST  *  TO   * LIST  *                                * 09600022
*    * BITS  * LNGTH *  AI   * LNGTH *                                * 09700022
*    *********************************                                * 09800022
*    *       * LNGTH *                                                * 09900022
*    * ZEROS *  OF   *                                                * 10000022
*    *       * FIELD *                                                * 10100022
*    *****************                                                * 10200022
*                                                                     * 10300022
*    FORM BITS IN INDEX BYTE:                                         * 10400022
*                                                                     * 10500022
*    X'01' - FORM=NAME SPECIFIED                                      * 10600022
*    X'02' - FORM=ID SPECIFIED                                        * 10700022
*                                                                     * 10800022
*    REGISTER 3 - ADDRESS OF THE SCB.                                 * 10900022
*                                                                     * 11000022
*    REGISTER 4 - ADDRESS OF THE LCB.                                 * 11100022
*                                                                     * 11200022
*    REGISTER 6 - ADDRESS OF THE BUFFER.                              * 11300022
*                                                                     * 11400022
*    REGISTER 9 - ADDRESS OF THE AVT.                                 * 11500022
*                                                                     * 11600022
*    REGISTER 12 - ENTRY POINT ADDRESS AND BASE REGISTER.             * 11700022
*                                                                     * 11800022
*    REGISTER 14 - RETURN REGISTER.                                   * 11900022
*                                                                     * 12000022
*    REGISTER 15 - LINK REGISTER AND RETURN CODE REGISTER.            * 12100022
*                                                                     * 12200022
*    INPUT FROM AI - SCAN ROUTINE                                     * 12300022
*    AVTDOUBL - CONTAINS SOURCE NAME OR ID FOUND IN THE BUFFER.       * 12400022
*                                                                     * 12500022
*    AVTPARM+1 - CONTAINS LENGTH OF THE NAME OR ID IN THE BUFFER.     * 12600022
*                                                                     * 12700022
*    INPUT FROM A1 - BINARY SEARCH ROUTINE                            * 12800022
*    REGISTER 15 - INDEX INTO TERMNAME TABLE OR ZERO IF NO MATCH.     * 12900022
*                                                                     * 13000022
* OUTPUT -                                                            * 13100022
*    REGISTER 15 - RETURN CODES                                       * 13200022
*                                                                     * 13300022
*    X'00' - ON SUCCESSFUL COMPLETION AND FOR ZERO LENGTH BUFFERS.    * 13400022
*                                                                     * 13500022
*    X'04' - IF FORM= IS NOT SPECIFIED AND THE ORIGIN SPECIFIED       * 13600022
*    BY THE NAME IN THE BUFFER IS INVALID.                            * 13700022
*                                                                     * 13800022
*    X'08' - IF FORM= IS SPECIFIED FOR A TERMINAL NOT IN A            * 13900022
*    CONCENTRATOR NETWORK.                                            * 14000022
*                                                                     * 14100022
*    X'0C' - IF FORM= IS SPECIFIED AND THE ORIGIN SPECIFIED BY        * 14200022
*    EITHER THE ID OR THE NAME IN THE BUFFER IS NOT A TERMINAL        * 14300022
*    ATTACHED TO THE CONCENTRATOR THAT ENTERED THE MESSAGE.  IN       * 14400022
*    THIS CASE THE ORIGIN IS SET TO THE FIRST ATTACHED TERMINAL       * 14500022
*    ENTRY AFTER THE CONCENTRATOR ENTRY.                              * 14600022
*                                                                     * 14700022
*    BUFFER PREFIX SOURCE FIELD (PRFSRCE) - SET TO THE KEY TO THE     * 14800022
*    CORRECT TERMINAL ON SUCCESSFUL COMPLETION OR SET TO THE KEY      * 14900022
*    TO THE FIRST ENTRY AFTER THE CONCENTRATOR ENTRY IF FORM= WAS     * 15000022
*    SPECIFIED AND THE NAME OR ID IN THE BUFFER WAS INVALID FOR       * 15100022
*    THIS CONCENTRATOR.                                               * 15200022
*                                                                     * 15300022
*                                                                     * 15400022
*    SCB ERROR BYTE (SCBERR1) - IF RETURN FROM BINARY SEARCH          * 15500022
*    ROUTINE IS ZERO, IF PRFSRCE CONTAINS A KEY DIFFERENT FROM THE    * 15600022
*    KEY RETURNED BY BINARY SEARCH, OR IF THE TERMINAL INDICATED BY   * 15700022
*    THE KEY RETURNED BY BINARY SEARCH IS NOT IN THE DCB INDICATED    * 15800022
*    BY THE LCB FROM THE BUFFER PREFIX, AND FORM= WAS NOT SPECIFIED,  * 15900022
*    THE 'INVALID ORIGIN' BIT IS SET ON.                              * 16000022
*                                                                     * 16100022
*    OUTPUT TO AI - SCAN ROUTINE                                      * 16200022
*    REGISTER 1 - POINTS TO AI'S PARAMETER LIST.                      * 16300022
*                                                                     * 16400022
*    OUTPUT TO A1 - BINARY SEARCH                                     * 16500022
*    REGISTER 0 - LENGTH OF NAME FIELD FOUND IN THE BUFFER.           * 16600022
*                                                                     * 16700022
*    REGISTER 1 - ADDRESS OF AVTDOUBL WHICH CONTAINS THE NAME         * 16800022
*    OR ID FOUND IN THE BUFFER.                                       * 16900022
*                                                                     * 17000022
* EXTERNAL REFERENCES -                                               * 17100022
*                                                                     * 17200022
*         IEDQUI - INTERFACE TO SCAN ROUTINE AND TO MH                * 17300022
*                                                                     * 17330000
*         IEDDVAS- DEVICE ASSOCATION ROUTINE(DIAL SUPPORT)            * 17360000
*                                                                     * 17400022
*         IEDQA1 - BINARY SEARCH                                      * 17500022
*                                                                     * 17600022
*         TERMNAME TABLE CODE (AVTRNMPT)                              * 17700022
*                                                                     * 17800022
* EXITS,  NORMAL -                                                    * 17900022
*                                                                     * 18000022
*         BUFFER IS A ZERO LENGTH BUFFER.  REGISTER 15 CONTAINS A     * 18100022
*         RETURN CODE OF X'00'.                                       * 18200022
*                                                                     * 18300022
*         WITHOUT FORM= OPERAND SPECIFIED -                           * 18400022
*                                                                     * 18500022
*         THE KEY IN THE BUFFER PREFIX SOURCE FIELD (PRFSRCE)         * 18600022
*         MATCHES THE KEY FOR THE NAME FOUND IN THE BUFFER.           * 18700022
*         REGISTER 15 CONTAINS A RETURN CODE OF X'00'.                * 18800022
*                                                                     * 18900022
*         PRFSRCE, ZEROS ON ENTRY, IS INITIALIZED TO THE KEY FOR      * 19000022
*         THE NAME FOUND IN THE BUFFER.  THE LCB TERMINAL-CURRENTLY-  * 19100022
*         CONNECTED FIELD (LCBTTCIN) IS INITIALIZED TO THE SAME       * 19200022
*         KEY.  REGISTER 15 CONTAINS A RETURN CODE OF X'00'.          * 19300022
*                                                                     * 19400022
*         WITH FORM= OPERAND SPECIFIED -                              * 19500022
*                                                                     * 19600022
*         PRFSRCE AND LCBTTCIN ARE SUCCESSFULLY INITIALIZED TO THE    * 19700022
*         KEY FOR THE PROPER TERMINAL ATTACHED TO THE CONCENTRATOR.   * 19800022
*         REGISTER 15 CONTAINS A RETURN CODE OF X'00'.                * 19900022
*                                                                     * 20000022
*         THE NAME FOUND IN THE BUFFER IS NOT FOUND IN THE TERMNAME   * 20100022
*         TABLE, OR IF IT IS FOUND, IT IS NOT THE NAME OF A TERMINAL  * 20200022
*         ATTACHED TO THE CONCENTRATOR THAT ENTERED THE MESSAGE;      * 20300022
*         OR THE ID FOUND IN THE BUFFER IS NOT FOUND IN THE           * 20400022
*         CONCENTRATOR'S DEVICE ID TABLE.  PRFSRCE AND LCBTTCIN       * 20500022
*         ARE INITIALIZED TO THE KEY FOR THE FIRST ATTACHED TERMINAL  * 20600022
*         ENTRY AFTER THE CONCENTRATOR ENTRY.  REGISTER 15 CONTAINS   * 20700022
*         A RETURN CODE OF X'0C'.                                     * 20800022
*                                                                     * 20900022
* EXITS,  ERROR -                                                     * 21000022
*                                                                     * 21100022
*         WITHOUT FORM= OPERAND SPECIFIED -                           * 21200022
*                                                                     * 21300022
*         THE NAME FOUND IN THE BUFFER IS NOT FOUND IN THE TERMINAL   * 21400022
*         NAME TABLE.  THE 'INVALID ORIGIN' BIT IN SCBERR1 IS SET     * 21500022
*         ON.  REGISTER 15 CONTAINS A RETURN CODE OF X'04'.           * 21600022
*                                                                     * 21700022
*         THE KEY FOR THE NAME FOUND IN THE BUFFER DOES NOT MATCH     * 21800022
*         THE KEY IN PRFSRCE.  THE 'INVALID ORIGIN' BIT IS SET ON.    * 21900022
*         REGISTER 15 CONTAINS A RETURN CODE OF X'04'.                * 22000022
*                                                                     * 22100022
*         THE TERMINAL FOR THE NAME FOUND IN THE BUFFER IS NOT        * 22200022
*         ASSOCIATED WITH THE SAME DCB AS THE LCB WHOSE ADDRESS       * 22300022
*         IS IN THE BUFFER (PRFLCB).  THE 'INVALID ORIGIN' BIT IS     * 22400022
*         SET.  REGISTER 15 CONTAINS A RETURN CODE OF X'04'.          * 22500022
*                                                                     * 22600022
*         WITH FORM= OPERAND SPECIFIED -                              * 22700022
*                                                                     * 22800022
*         THE FORM= OPERAND WAS SPECIFIED FOR A TERMINAL NOT IN       * 22900022
*         A CONCENTRATOR NETWORK.  REGISTER 15 CONTAINS A RETURN      * 23000022
*         CODE OF X'08'.                                              * 23100022
*                                                                     * 23200022
* TABLES/WORK AREAS -                                                 * 23300022
*                                                                     * 23400022
*    AVT                                                              * 23500022
*    LCB                                                              * 23600022
*    SCB                                                              * 23700022
*    QCB                                                              * 23800022
*    QCB EXTENSION                                                    * 23900022
*    DEVICE ID TABLE                                                  * 24000022
*    TERMINAL TABLE                                                   * 24100022
*    BUFFER PREFIX                                                    * 24200022
*                                                                     * 24300022
* ATTRIBUTES -                                                        * 24400022
*    SERIALLY REUSABLE, REFRESHABLE, ENABLED, RESIDENT,               * 24500022
*    PROBLEM PROGRAM MODE.                                            * 24600022
*                                                                     * 24700022
* CHARACTER CODE DEPENDENCY -                                         * 24800022
*    THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL            * 24900022
*    REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS            * 25000022
*    EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME.  THE CODING HAS     * 25100022
*    BEEN ARRANGED SO THAT REDEFINITION OF ''CHARACTER'' CONSTANTS,   * 25200022
*    BY REASSEMBLY, WILL RESULT IN A CORRECT MODULE FOR THE NEW       * 25300022
*    DEFINITIONS.                                                     * 25400022
*                                                                     * 25500022
* NOTES -NONE                                                         * 25600022
*                                                                     * 25700022
*********************************************************************** 25800022
         EJECT                                                          25900022
********* REGISTER EQUATES *********                                    26000022
         SPACE                                                          26100022
R0       EQU   0                        WORK REGISTER                   26200022
R1       EQU   1                        PARAMETER LIST ADDRESS          26300022
RWORK2   EQU   2                        WORK REGISTER                   26400022
RSCB     EQU   3                        SCB ADDRESS                     26500022
R3       EQU   3                        WORK REGISTER          @Y17XAMF 26550000
RLCB     EQU   4                        LCB ADDRESS                     26600022
RLEN     EQU   5                        LNG PASSED TO IDSRCH            26700022
RQCB2    EQU   5                        SECOND QCB BASE          X03039 26750007
RPREFIX  EQU   6                        BUFFER ADDRESS                  26800022
RQCB     EQU   7                        QCB ADDRESS                     26900022
RTERM2   EQU   8                        SECOND TERMINAL ENTRY  @YM09033 26950000
RWORK8   EQU   8                        WORK REGISTER                   27000022
RAVT     EQU   9                        AVT ADDRESS                     27100022
RWORK10  EQU   10                       WORK REGISTER                   27200022
RPARM    EQU   11                       PARM LIST ADDR SAVE REG         27300022
RBASE    EQU   12                       BASE REGISTER                   27400022
R13      EQU   13                       SAVE AREA ADDRESS      @Y17XAMF 27450000
R14      EQU   14                       RETURN ADDRESS                  27600022
R15      EQU   15                       LINK ADDRESS AND                27700022
*                                       RETURN CODE REGISTER            27800022
         SPACE 3                                                        27900022
********* OTHER EQUATES *********                                       28000022
         SPACE                                                          28100022
ONE      EQU   1                        USED AS DISP AND FOR SLL.       28200022
TWO      EQU   2                        USED AS DISPLACEMENT            28300022
THREE    EQU   3                        USED TO BUMP PTR 3 BYTES.       28400022
ADLEN    EQU   3                        LENGTH OF A 3-BYTE ADDRESS      28500022
FOUR     EQU   4                        OFFSET FOR BRANCH               28570022
EIGHT    EQU   8                        TO REACH CONC'S DEV DEP FLD     28640022
*                                       AND OFFSET FOR A1 ADDR          28710022
D15      EQU   15                       TO SHIFT OUT HI HALF OF REG     28800022
FORMBITS EQU   X'03'                    STATUS BITS FOR FORM=           29000022
FORMNAME EQU   X'01'                    FORM=NAME SPECIFIED             29100022
FORMID   EQU   X'02'                    FORM=ID SPECIFIED               29200022
XFF      EQU   X'FF'                    TEST FOR INVALD ID OF X'FF'     29300022
         EJECT                                                          29400022
         USING IEDQSCB,RSCB             ADDRESSABILITY FOR SCB          29500022
         USING IEDQLCB,RLCB             ADDRESSABILITY FOR LCB          29600022
         USING IEDQPRF,RPREFIX          ADDRESSABILITY FOR PREFIX       29700022
         USING IEDQQCB,RQCB             ADDRESSABILITY FOR QCB          29800022
         USING IEDQAVTD,RAVT            ADDRESSABILITY FOR AVT          29900022
         USING IEDQBM01,RBASE           ADDRESSABILITY FOR ROUTINE      30000022
         SPACE                                                          30100022
IEDQBM01 EQU   *                                                        30200022
IEDQBM   IEDHJN SKIP                    MODULE ID              @Y17XAMF 30250000
         LH    R15,PRFSIZE              GET PRFSIZE                     30800022
         LTR   R15,R15                  ZERO LENGTH BUFFER?             30900022
         BZ    RETURN                   YES RETURN TO CALLER            31000022
         SPACE                                                          31100022
         LR    RPARM,R1                 NO, SAVE PARM LIST ADDR         31200022
         TM    AVTEZERO(RPARM),FORMBITS FORM= SPEC'D IN ORGN MAC?       31300022
         BZ    GOSCAN                   NO, NON-CONC ASSUMED            31400022
         SPACE                                                          31500022
         LH    R1,LCBTTCIN              PASS KEY FOR SPEC'D NAME        31600022
         LA    R15,AVTECD8              ASSUME DIAL LINE                31700022
         LTR   R1,R1                    LCBTTCIN ZERO?                  31800022
         BZ    RETURN                   YES RETURN                      31900022
         SPACE                                                          32000022
         N     R1,AVTCLRHI              CLEAR HI-ORDER TWO BYTES        32100022
         L     R15,AVTRNMPT             GET ADDR OF TNT CODE            32200022
         BALR  R14,R15                  LINK TO GET TERM ENTRY ADDR     32300022
         SPACE                                                          32400022
         USING IEDQTRM,R1               ADDRABILITY FOR TERM ENT        32500022
         LA    R15,AVTECD8              ASSUME NOT IN CONC NETWORK      32600022
         TM    TRMDEVFL+1,TRMCONC       TERM IN CONC NETWORK?           32700022
         BZ    RETURN                   NO, GO RETURN                   32800022
         SPACE                                                          32900022
         DROP  R1                                                       33000022
         SPACE                                                          33100022
         LR    RWORK8,R1                SAVE ADDR OF TERM ENTRY         33200022
         LR    R1,RPARM                 RESTORE PARM LIST ADDR          33300022
         SPACE                                                          33400022
GOSCAN   EQU   *                                                        33500022
         LA    R1,TWO(R1)               INDEX TO SCANS PARM LIST        33600022
         L     R15,AVTUI                GET ADDR OF INTERFACE RTN       33700022
         BALR  R14,R15                  BRANCH TO SCAN ROUTINE          33800022
         SPACE                                                          33900022
         LTR   R15,R15                  CHECK FOR RUNOUT                33920022
         BM    RETURN                   YES, RETURN TO CALLER           33940022
         SPACE                                                          33960022
         LA    RWORK2,AVTDOUBL          BUFFER ADDR TO BE PASSED        34000022
         SR    RLEN,RLEN                CLEAR LENGTH REGISTER           34100022
         IC    RLEN,AVTPARM+1           GET LNG OF FLD IN BUFFER        34200022
         TM    AVTEZERO(RPARM),FORMID   FORM=ID SPECIFIED?              34300022
         BO    IDSRCH                   YES, GO TO DVCID SRCH           34400022
         SPACE                                                          34500022
         LR    R1,RWORK2                NO, THEN SET UP REGS FOR        34560022
         LR    R0,RLEN                  BINARY SEARCH ROUTINE.          34620022
         L     R15,AVTMSGS-1            GET VCON TABLE ADDR             34680022
         L     R15,EIGHT(R15)           LOAD ADDR OF A1 ROUTINE         34740022
         BAL   R14,FOUR(R15)            LINK TO BINARY SEARCH ROUTINE   34800022
         SPACE                                                          34900022
PROCESS  EQU   *                                                        35000022
         LTR   R15,R15                  IS KEY ZERO?                    35100022
         BZ    SETBIT3                  YES, ERROR, BRANCH              35200022
         SPACE                                                          35300022
         LR    RWORK2,R15               NO, SAVE KEY                    35400022
         TM    AVTEZERO(RPARM),FORMNAME FORM=NAME SPECIFIED?            35500022
         BO    RITECONC                 YES, GO INSURE THIS TERM        35600022
*                                       IS ATT. TO CORRECT CONC         35700022
         SPACE                                                          35800022
         SR    R15,R15                  ASSUME GOOD RETURN              35900022
         CH    RWORK2,PRFSRCE           KEY FOUND=PREFIX KEY?           36000022
         BE    RETURN                   YES, GO RETURN                  36100022
         TM    LCBSTAT2,LCBDIAL         IF NOT A DIAL RESOURCE,@PI06026 36130000
         BZ    SETBIT3                   SET ERROR CONDITION   @PI06026 36160000
         SPACE                                                          36200022
         LH    R1,PRFSRCE               LOAD KEY FROM PREFIX            36300022
         LTR   R1,R1                    IS IT ZERO                      36400022
         BZ    PASSKEY                  YES, GET ENTRY FOR KEY          36500022
         SPACE                                                          36600022
         L     R15,AVTRNMPT             NO, GET ADDR OF TNT CODE        36700022
         BALR  R14,R15                  LINK TO GET TERM ENTRY ADDR     36800022
         SH    R1,TPREFIX               GET NEG PREF ADDRESS   @Y17XAMF 36850000
         SPACE                                                          36900022
         USING IEDNTRM,R1               ADDRABILITY FOR TERM ENT        36950000
         LR    RTERM2,R1                SAVE ORIGINAL TERMENTRY  X03039 37005007
         TM    TRMSTATE,TRMPREF         DEVICE ATTACHED TO 3705@Y17XAMF 37006000
         BO    CHKCTERM                 YES, AVOID LINE CHECK  @Y17XAMF 37007000
         TM    TRMSTATE,TRMLINE         IS THIS A LINE           X03039 37110007
         BNO   SETBIT3                  NO, ERROR, BRANCH               37200022
         B     PASSKEY                  BRANCH                 @Y17XAMF 37220000
CHKCTERM DS    0H                                              @Y17XAMF 37240000
         CLI   TRMTYPE,TRMCTERM         IF 3705, PRFSRCE MUST  @Y17XAMF 37260000
         BNE   SETBIT3                   BE A CTERM            @Y17XAMF 37280000
         SPACE                                                          37300022
PASSKEY  EQU   *                                                        37400022
         LR    R1,RWORK2                PASS KEY FOR SPECIFIED NAME     37500022
         L     R15,AVTRNMPT             GET ADDR OF TNT CODE            37600022
         BALR  R14,R15                  LINK TO GET TERM ENTRY ADDR     37700022
         SH    R1,TPREFIX               GET NEG PREFIX ADDRESS @Y17XAMF 37750000
         SPACE                                                          37800022
         L     RQCB,TRMDESTQ-1          GET QCB ADDR FROM TRM ENTRY     37900022
*                                       DO LCB AND QCB POINT            38000022
*                                       TO THE SAME DCB                 38100022
         CLC   LCBDCBPT+1(ADLEN),QCBDCBAD                               38200022
         BNE   SETBIT3                  NO, ERROR, BRANCH               38300022
         SPACE                                                          38400022
         TM    TRMSTATE,TRMPREF         IS DEVICE ON 3705      @Y17XAMF 38400100
         BO    IS3705                   BRANCH ON YES          @Y17XAMF 38400200
         TM    TRMSTATE,TRMSNGLE        ORIGIN MUST BE SINGLE  @Y17XAMF 38400300
         BNZ   SETBIT3                   ENTRY                 @Y17XAMF 38400400
         TM    TRMDSORG,TRMLGB          ORIGIN MUST NOT BE     @Y17XAMF 38400500
         BO    SETBIT3                   A LGB                 @Y17XAMF 38400600
         B     SETKEY2                  IS GOOD 270X DIAL CALL @Y17XAMF 38400700
IS3705   DS    0H                                              @Y17XAMF 38400800
         NC    TRMCOHRT(L'TRMCOHRT),TRMCOHRT IF FIRST ORIGIN   @Y17XAMF 38400900
         BZ    FSTORG                    BRANCH                @Y17XAMF 38401000
         ST    R1,SAVREG                SAVE R1                @Y17XAMF 38401100
         LH    R1,TRMCOHRT              SEE IF ORIGIN IS ATTCH @Y17XAMF 38401200
         N     R1,AVTCLRHI              MAKE POSITIVE          @Y17XAMF 38401300
         L     R15,AVTRNMPT             GET CONV ROUTINE ADDR  @Y17XAMF 38401400
         BALR  R14,R15                  GO GET TTE ADDRESS     @Y17XAMF 38401500
         SH    R1,TPREFIX               CALCULATE NEG PREF ADDR@Y17XAMF 38401600
         CH    RWORK2,TRMSUBST          IF ORIGIN IS ALREADY   @Y17XAMF 38401700
         BE    SETBIT3                   CONNECTED BRANCH      @Y17XAMF 38401800
         L     R1,SAVREG                RELOAD ORIGIN TTE ADDR @Y17XAMF 38401900
FSTORG   DS    0H                                              @Y17XAMF 38402000
         CLI   TRMTYPE,TRMPSNA          IF 3705, ORIGIN MUST   @Y17XAMF 38402100
         BNE   SETBIT3                   BE PRE-SNA            @Y17XAMF 38402200
         TM    TRMPRE1,TRMCONT          IS CONTACT OUTSTANDING @Y17XAMF 38415100
         BO    SETBIT3                  YES, BRANCH            @Y17XAMF 38415200
         LR    R1,RWORK2                GET NEW ORIGIN INDEX   @YM09050 38416010
         L     R15,AVTSAVTP             SET SAVT BASE          @YM09050 38416110
         USING IEDNSVTD,R15                                    @YM09050 38421110
         L     R15,SAVTTNTX             ADDR TNT INDEX TO NET  @YM09050 38426110
*                                       ADDR CONVERSION        @YM09050 38431110
         BALR  R14,R15                  GET NETWORK ADDRESS OF @YM09050 38436110
*                                       NEW ORIGIN             @YM09050 38441110
         LTR   R15,R15                  INVALID NETWORK ADDR   @YM09050 38446110
         BZ    SETBIT3                  BR YES                 @YM09050 38451110
         LR    R1,RWORK2                R1 = ORIGIN TTCIN      @Y17XAMF 38456110
         LH    R0,PRFSRCE               R0 = CTERM TTCIN       @Y17XAMF 38461110
         N     R0,AVTCLRHI              MAKE TTCIN POSTIVE     @Y17XAMF 38466110
         L     R15,AVTSAVTP             GET SAVT ADDRESSABILTY @YM09033 38471110
         L     R15,SAVTDVAS             GET IEDDVAS ADDRESS    @Y17XAMF 38476110
         BALR  R14,R15                  GO TO IEDDVAS          @Y17XAMF 38481110
         DROP  R15                                             @YM09033 38486110
SETKEY2  EQU   *                                                        38500022
         SR    R15,R15                  SET GOOD RETURN CODE            38600022
SETKEY   EQU   *                                                        38700022
         STH   RWORK2,LCBTTCIN          INITIALIZE LCB WITH KEY         38800022
         STH   RWORK2,PRFSRCE           SET KEY IN BUFFER PREFIX        38900022
         B     RETURN                   RETURN TO CALLER                39000022
         SPACE                                                          39100022
SETBIT3  EQU   *                                                        39200022
         TM    AVTEZERO(RPARM),FORMNAME FORM=NAME SPECIFIED?            39300022
         BO    IDSRCH                   YES, THEN GO GET KEY FOR        39400022
*                                       FIRST ENTRY AFTER CONC          39500022
         SPACE                                                          39600022
         OI    SCBERR1,SCBORIGN         SET 'INVALID ORIGIN' IN SCB     39700022
         LA    R15,AVTECD4              SET ERROR RETURN CODE           39800022
         SPACE                                                          39900022
RETURN   EQU   *                                                        40000022
         L     RBASE,AVTUI              GET ADDRESS OF RETURN ROUTINE   40100022
         B     FOUR(RBASE)              RETURN TO CALLER                40200022
         EJECT                                                          40300022
RITECONC EQU   *                                                        40400022
         LR    R1,RWORK2                PASS KEY FOR NAME IN BFR        40500022
         L     R15,AVTRNMPT             TNT CODE                        40600022
         BALR  R14,R15                  GO GET TERM ENT ADDR            40700022
         SPACE                                                          40800022
         SR    R15,R15                  CLEAR FOR IDSRCH                40900022
         USING IEDQTRM,R1               ADDRABILITY FOR TERM ENT        41000022
         TM    TRMDEVFL+1,TRMCONC       TERM IN CONC NETWORK?           41100022
         BZ    IDSRCH                   NO, GO SET TO FIRST             41200022
*                                       ENTRY AFTER THE CONC.           41300022
         SPACE                                                          41400022
         L     RQCB,TRMDESTQ-1          ADDR OF QCB                     41500022
         SPACE                                                          41600022
         DROP  R1                                                       41700022
         SPACE                                                          41800022
         TM    QCBDSFLG,QCBDRQQ         IS IT A DRQ?                    41900022
         BO    IDSRCH                   YES, THEN NAME IS A CONC        42000022
*                                       NAME. GO GET KEY FOR 1ST        42100022
*                                       ENTRY AFTER PROPER CONC.        42200022
         SPACE                                                          42300022
         LH    R1,QCBEXTO               NOT DRQ, GET QCB EXT OFF        42400022
         AR    R1,RQCB                  COMPUTE PTR TO QCBEXTENT        42500022
         SPACE                                                          42600022
         USING IEDQQCBE,R1              ADDRABILITY FOR QCBE            42700022
         L     R15,QCBECONC-1           PTR TO CONC TERM ENT            42800022
         LA    R15,AVTEZERO(,R15)       CLEAR HI ORDER BYTE             42900022
         SPACE                                                          43000022
IDSRCH   EQU   *                                                        43100022
         USING IEDQTRM,RWORK8           RWORK8 HAS ADDR OF TRM ENT      43200022
         L     RQCB,TRMDESTQ-1          GET ADDR OF QCB                 43300022
         TM    QCBDSFLG,QCBDRQQ         IS IT DRQ?                      43400022
         BO    NAMETEST                 YES, THEN TERM ENT IS A         43500022
*                                       CONC ENTRY. GO TEST FOR         43600022
*                                       FORM=NAME.                      43700022
         SPACE                                                          43800022
         LH    R1,QCBEXTO               NOT DRQ. GET QCB EXT OFFSET     43900022
         AR    R1,RQCB                  QCBEXTO+QCBSTART=PTR QCBEXT     44000022
         L     RWORK8,QCBECONC-1        GET PTR TO CONC TRM ENT         44100022
         SPACE                                                          44200022
NAMETEST EQU   *                                                        44300022
         TM    AVTEZERO(RPARM),FORMNAME FORM=NAME SPEC'D                44400022
         BZ    GETIDTAB                 NO, GO GET DVCID TABLE          44500022
         SPACE                                                          44600022
         LTR   R15,R15                  INVALID TERM NAME?              44700022
         BZ    GETIDTAB                 YES, GO GET DVCID TABLE         44800022
         SPACE                                                          44900022
         LA    RWORK8,AVTEZERO(,RWORK8) CLEAR HI ORDER BYTE             45000022
         CR    RWORK8,R15               IS TERM ATTACHED TO THE         45100022
*                                       SAME CONC THAT IS               45200022
*                                       ENTERING THE MESSAGE?           45300022
         SPACE                                                          45400022
         BE    SETKEY2                  YES GO SET TTCIN & SRCE         45500022
         SPACE                                                          45600022
GETIDTAB EQU   *                                                        45700022
         SR    RWORK10,RWORK10          CLEAR WORK REG.                 45800022
         LA    R1,TRMOPNO               PTR TO DEV FLD IF NO OPTFLD     45900022
         TM    TRMSTATE,TRMOPTFN        OPTION FIELDS USED?             46000022
         BZ    NOOPT                    NO, GO FIND DEV DEPEND FLD      46100022
         SPACE                                                          46200022
         IC    RWORK10,TRMOPNO          GET NUMBER OPTION ENTRIES       46300022
         LA    R1,THREE(R1,RWORK10)     BUMP DEV DEPND FLD PTR PAST     46400022
*                                       OPTION OFFSETS.                 46500022
NOOPT    EQU   *                                                        46600022
         LH    R0,TRMDEVFL              GET DEV DEPND FIELD FLAGS.      46700022
         SLL   R0,D15                   SHIFT FLAGS TO LEFT IN R0.      46800022
         LA    RWORK8,EIGHT             SET CTR TO TEST 8 DEVICE        46900022
*                                       DEPENDENT FIELD FLAGS.          47000022
LOOP     EQU   *                                                        47100022
         SLL   R0,ONE                   SHIFT TO TEST FOR PRESENCE      47200022
*                                       OF NEXT DEVICE DEPN'D FLD.      47300022
         LTR   R0,R0                    THIS FIELD PRESENT?             47400022
         BNM   BCTIT                    NO, DON'T BUMP PTR.             47500022
         SPACE                                                          47600022
         IC    RWORK10,AVTEZERO(R1)     GET LNG OF THIS FIELD.          47700022
         LA    R1,ONE(R1,RWORK10)       BUMP PTR PAST THIS FIELD.       47800022
         SPACE                                                          47900022
BCTIT    EQU   *                                                        48000022
         BCT   RWORK8,LOOP              REACHED DVCID CHAR TAB PTR?     48100022
*                                       IF NOT, GO BACK                 48200022
         MVC   AVTPARM+1(ADLEN),TWO(R1) YES, MOVE TAB ADDR TO WORK      48300022
         L     R1,AVTPARM               AREA FOR LOAD INTO REG.         48400022
         USING IEDQDVCT,R1              ADDRABILITY FOR DVCID TAB       48500022
         IC    RWORK10,DVCENLTH         GET TABLE ENTRY WIDTH           48600022
         TM    AVTEZERO(RPARM),FORMNAME FORM=NAME SPECIFIED?            48700022
         BO    FIRSTENT                 YES THEN DON'T SEARCH TAB,      48800022
*                                       JUST GET KEY FOR FIRST          48900022
*                                       ENTRY AFTER CONC.               49000022
         SPACE                                                          49100022
         LR    RWORK8,RLEN              GET LNG FOR X'FF' TEST          49200022
         BCT   RWORK8,CONTINU           IF LNG PASSED IS ONE,           49300022
*                                       THEN TEST FOR X'FF'             49400022
         SPACE                                                          49500022
         CLI   AVTEZERO(RWORK2),XFF     ID PASSED = X'FF' ?             49600022
         BE    FIRSTENT                 YES, GO SET KEY TO FIRST        49700022
*                                       ENTRY AFTER CONC.               49800022
         SPACE                                                          49900022
CONTINU  EQU   *                                                        50000022
         LA    R1,DVCECW                BUMP BASE PTR TO 1ST ENT        50100022
         SPACE                                                          50200022
SRCHLOOP EQU   *                                                        50300022
         AR    R1,RWORK10               BUMP TO NEXT ENTRY.BYPASSNG     50400022
*                                       CONC ENTRY FIRST TIME.          50500022
         CLI   DVCIDLTH,DVCEND          REACHED END OF TABLE?           50600022
         BE    NOTFOUND                 YES GO GET KEY FOR FIRST        50700022
*                                       ENTRY AFTER CONC.               50800022
         SPACE                                                          50900022
         IC    RWORK8,DVCIDLTH          PICK UP DVCID LENGTH            51000022
         CR    RWORK8,RLEN              EQUAL TO LNG PASSED?            51100022
         BNE   SRCHLOOP                 NO, GO GET NEXT ENTRY           51200022
         SPACE                                                          51300022
         BCTR  RWORK8,AVTEZERO          DECREMENT FOR COMPARE           51400022
         EX    RWORK8,COMP              TABLE DVCID = ID PASSED?        51500022
         BNE   SRCHLOOP                 NO, GO GET NEXT ENTRY           51600022
         SPACE                                                          51700022
         SR    R15,R15                  YES, SET GOOD RTN CODE          51800022
         B     GETINDEX                 GO GET INDEX INTO TNT           51900022
         SPACE                                                          52000022
NOTFOUND EQU   *                                                        52100022
         L     R1,AVTPARM               RESET PTR TO BGN OF TAB         52200022
FIRSTENT EQU   *                                                        52300022
         LA    R1,DVCECW(RWORK10)       PNT TO 1ST ENT AFTER CONC       52400022
         IC    RWORK8,DVCIDLTH          PICK UP ID LNG                  52500022
         BCTR  RWORK8,AVTEZERO          DECR FOR COMMON LA INST         52600022
         LA    R15,AVTECD12             SET X'0C' RETURN CODE           52700022
         SPACE                                                          52800022
GETINDEX EQU   *                                                        52900022
         LA    R1,TWO(RWORK8,R1)        POINT R1 TO TNT OFFSET          53000022
         MVC   AVTPARM+2(TWO),AVTEZERO(R1)                              53100022
*                                       MOVE OFFSET TO WORK AREA        53200022
         L     RWORK2,AVTPARM           FOR LOAD INTO RWORK2.           53300022
         N     RWORK2,AVTCLRHI          CLEAR HI ORDER HALF             53400022
         B     SETKEY                   GO SET LCBTTCIN                 53500022
*                                       AND PRFSRCE.                    53600022
         SPACE                                                          53700022
*                                       CLC ID IN TAB TO ID PASSED      53800022
COMP     CLC   AVTEZERO(1,RWORK2),DVCCHAR COMPARE                S22024 53900000
SAVREG   DS    A                        SAVE TTE ADDR AREA     @Y17XAMF 53970000
TPREFIX  DS    0H                                              @Y17XAMF 54040000
         DC    AL2(TRMPRFSZ)            TTE PREFIX SIZE        @Y17XAMF 54110000
         EJECT                                                          54200022
********* DSECTS *********                                              54300022
         SPACE                                                          54400022
         TAVTD                                                          54500022
         EJECT                                                          54600022
         TDVCIDTD                                                       54700022
         EJECT                                                          54800022
         TLCBD                                                          54900022
         EJECT                                                          55000022
         TPRFD                                                          55100022
         EJECT                                                          55200022
         TQCBD                                                          55300022
         EJECT                                                          55400022
         TQCBED                                                         55500022
         EJECT                                                          55600022
         TSCBD                                                          55700022
         EJECT                                                          55800022
         TTRMD                                                          55900022
         END                                                            56000022
