         TITLE 'IEDQBV  -  COMMBUF ROUTINE'                             00300022
         GBLB  &IEDQZA(64)                                     @Y17XAMG 00400000
&IEDQZA(6) SETB 1                                              @Y17XAMG 00500000
IEDQBV   CSECT                                                          00600022
*A005000                                                       @Y17XAMG 00700000
*C468000                                                       @Y17XAMG 00800000
*********************************************************************** 00900022
**************** CHANGE ACTIVITY FOLLOWS ****************************** 00950010
*THIS IS A DUMMY APAR TO CORRECT FICHE FLAGGING PROBLEM        @OZ19773 01000010
*                                                                     * 01200022
* STATUS -- CHANGE LEVEL 0                                            * 01500022
*                                                                     * 01800022
* FUNCTION -- THIS ROUTINE IS ENTERED WHEN A COMMBUF MACRO HAS BEEN   * 02100022
*    CODED IN THE INHDR SUBGROUP. THE CURRENT BUFFER WILL BE MOVED    * 02400022
*    TO THE NEXT AVAILABLE COMMON BUFFER DATA AREA, THE NAMED TLIST   * 02700022
*    WILL BE ACCESSED, AND A COMMBUF STCB WILL BE INSERTED INTO THE   * 03000022
*    STCB CHAIN OF THE APPROPRIATE LCB'S IF THE MAXDEEP OPERAND HAS   * 03300022
*    NOT BEEN EXCEEDED AND THE LINE IS ACTIVE.                        * 03600022
*                                                                     * 03900022
* ENTRY POINTS --                                                     * 04200022
*                                                                     * 04500022
*         FIRST EXECUTABLE INSTRUCTION.                               * 04800022
*                                                                     * 05100022
* INPUT --                                                            * 05400022
*                                                                     * 05700022
*    R1 HAS THE ADDRESS OF THE COMMBUF MACRO PARAMETER LIST.          * 06000022
*                                                                     * 06300022
*    R12 HAS THE ENTRY POINT ADDRESS.                                 * 06600022
*                                                                     * 06900022
* OUTPUT --                                                           * 07200022
*                                                                     * 07500022
*    A COMMBUF STCB WILL BE INSERTED INTO THE STCB CHAIN OF THE       * 07800022
*    APPROPRIATE LCB'S.                                               * 08100022
*                                                                     * 08400022
* EXTERNAL REFERENCES --                                              * 08700022
*                                                                     * 09000022
*         .IEDQTNT -- TO FIND THE TERMINAL ENTRY                      * 09300022
*                                                                     * 09600022
*         .IGG019RB OR IGG019RO -- TCAM DISPATCHER                    * 09900022
*                                                                     * 10200022
* EXITS,NORMAL --                                                     * 10500022
*                                                                     * 10800022
*         COMMBUF STCB HAS BEEN INSERTED INTO THE STCB CHAIN OF THE   * 11100022
*         APPROPRIATE LCB'S. REGISTER 15 CONTAINS A RETURN CODE OF    * 11400022
*         X'00'.                                                      * 11700022
*                                                                     * 12000022
* EXITS,ERROR --                                                      * 12300022
*                                                                     * 12600022
*         MHGET DID NOT SUCCESSFULLY EXECUTE. REGISTER 15 CONTAINS A  * 12900022
*         RETURN CODE OF X'04'.                                       * 13200022
*                                                                     * 13500022
*         NO COMMBUF STCB WAS AVAILABLE. REGISTER 15 CONTAINS A       * 13800022
*         RETURN CODE OF X'08'.                                       * 14100022
*                                                                     * 14400022
* TABLES/WORK AREAS --                                                * 14700022
*                                                                     * 15000022
*    STCB                                                             * 15300022
*    AVT                                                              * 15600022
*    QCB                                                              * 15900022
*    LCB                                                              * 16200022
*    DCB                                                              * 16500022
*    TERMINAL TABLE                                                   * 16800022
*    COMMON BUFFER MASTER QCB                                         * 17100022
*    COMMON BUFFER DATA AREA PREFIX                                   * 17200022
*                                                                     * 17400022
* ATTRIBUTES --                                                       * 17700022
*                                                                     * 18000022
*    REUSABLE,ENABLED,RESIDENT                                        * 18300022
*                                                                     * 18600022
* CHARACTER CODE DEPENDENCY --                                        * 18900022
*                                                                     * 19200022
*    THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A PARTICULAR   * 19500022
*    INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.           * 19800022
*                                                                     * 20100022
* NOTES --                                                            * 20400022
*                                                                     * 20700022
*    STANDARD TCAM LINKAGES AND DEVIATIONS FROM STANDARDS.            * 21000022
*                                                                     * 21300022
*********************************************************************** 21600022
R0       EQU   0                        WORK REGISTER                   21900022
R1       EQU   1                        PARAMETER LIST ADDRESS          22200022
R2       EQU   2                        WORK REGISTER                   22500022
R3       EQU   3                        WORK REGISTER                   22800022
RSTCB    EQU   3                        STCB BASE                       23100022
RLCB     EQU   4                        LCB BASE                        23400022
R5       EQU   5                        WORK REGISTER                   23700022
R6       EQU   6                        WORK REGISTER                   24000022
R7       EQU   7                        WORK REGISTER                   24300022
RQCB     EQU   7                        QCB ADDRESS                     24600022
R8       EQU   8                        WORK REGISTER                   24900022
RCMB     EQU   8                        COMMON BUFFER DSECT ADDRESS     25200022
RAVT     EQU   9                        AVT ADDRESS                     25500022
RDCB     EQU   10                       DCB BASE                        25800022
RDISP    EQU   11                       WORK REGISTER                   26100022
RBASE    EQU   12                       BASE REGISTER                   26400022
R13      EQU   13                       SAVE AREA ADDRESS               26700022
R14      EQU   14                       RETURN ADDRESS                  27000022
R15      EQU   15                       LINK ADDRESS                    27300022
*********************************************************************** 27600022
*                                                                     * 27900022
         USING IEDCBDA,R14              COMMON BUFFER DATA AREA PREFIX  28200022
         USING IEDCMB,RCMB              COMMON BUFFER MASTER QCB        28500022
         USING IEDQSTCB,RSTCB           STCB                            28800022
         USING IEDQLCB,RLCB             LCB                             29100022
         USING IEDQQCB,RQCB             QCB                             29400022
         USING IHADCB,RDCB              DCB                             29700022
         USING IEDQAVTD,RAVT            AVT                             30000022
         USING IEDQTRM,R1               TERMINAL ENTRY                  30300022
         USING IEDQDISP,RDISP           DISPATCHER                      30600022
         USING *,RBASE                  CSECT BASE                      30900022
*                                                                     * 31200022
*********************************************************************** 31500022
IEDQBV   IEDHJN IEDQBV01                                                31800022
         L     RCMB,AVTCBQCB            GET ADDRESS OF COMMON BUFR QCB  33000022
         L     R14,CMBNEXT-ONE          GET ADDR OF DATA AREA PREFIX    33300022
         CLC   CBDAUSCT(TWO),AVTFZERO   IS USE COUNT ZERO               33600022
         BE    USCTOK                   BRANCH IF YES                   33680022
         LH    R6,LOSTCNTR              GET NO LOST CNTR                33760022
         LA    R6,ONE(R6)               INCR CNTR                       33840022
         STH   R6,LOSTCNTR              SET NEW VALUE                   33920022
         B     EXIT2                    EXIT                            34000022
USCTOK   EQU   *                                                        34080022
         L     R6,CBDAADDR-ONE          GET ADDRESS OF DATA AREA        34200022
         LR    R5,R1                    SAVE PARM LIST ADDRESS          34500022
         MVC   TWO(TWO,R6),AVTFZERO     ZERO OUT DATA LENGTH IN DATA    34800022
*                                         AREA                          35100022
*                                                                       35400022
         SPACE 1                                                        35450022
*********************************************************************** 35500022
*        MOVE DATA FROM THE BUFFER TO THE DATA AREA                   * 35550022
*********************************************************************** 35600022
         SPACE 1                                                        35650022
         MHGET WORK=(6)                 MOVE DATA FROM BUFFER TO AREA   35700022
         LTR   R15,R15                  GOOD RETURN CODE                36000022
         BNZ   EXIT1                    EXIT IF NOT                     36300022
         SPACE 1                                                        36350022
*********************************************************************** 36400022
*        FIND THE TLIST ENTRY                                         * 36450022
*********************************************************************** 36500022
         SPACE 1                                                        36550022
         L     R1,FOUR(R5)              GET ADDR OF TLIST CHAR STRING   36600022
         SR    R0,R0                    CLEAR REGISTER                  36900022
         IC    R0,FOUR(R5)              GET LENGTH OF TLIST NAME        37200022
         MVI   AVTDOUBL,AVTEBLNK        CLEAR AVT WORK AREA             37230022
         MVC   AVTDOUBL+ONE(SEVEN),AVTDOUBL                             37260022
*                                       TO BLANKS                       37290022
         LR    R15,R0                   LENGTH FOR EXECUTE              37320022
         BCTR  R15,AVTEZERO             DECR FOR EXECUTE                37350022
         EX    R15,MOVIT                MOVE NAME                       37380022
         LA    R1,AVTDOUBL              ADDR FOR NAME                   37410022
         L     R15,AVTMSGS-ONE          GET VCON TABLE ADDRESS          37500022
         L     R15,EIGHT(R15)           LOAD ADDR OF A1 ROUTINE         37800022
         BAL   R14,FOUR(R15)            LINK TO BINARY SEARCH ROUTINE   38100022
         LR    R1,R15                   ASSUME OFFSET FOUND             38200022
         LTR   R15,R15                  WAS THE TLIST ENTRY FOUND       38400022
         LA    R15,NTLIST               ASSUME LIST NOT FOUND           38500022
         BZ    EXIT1                    EXIT IF NOT                     38700022
         L     R15,AVTRNMPT             GET ADDR OF TNT CODE            39300022
         BALR  R14,R15                  LINK TO GET TERM ENTRY ADDR     39600022
         SR    R2,R2                    CLEAR REGISTER                  39900022
         ST    R5,SAVE                  SAVE ADDR OF PARAMETER LIST     40200022
         LR    R5,R1                    GET ADDR OF TERMINAL ENTRY      40500022
         TM    TRMSTATE,TRMLIST         IS THIS A LIST ENTRY            40570022
         LA    R15,NTLIST               SET RETURN CODE                 40640022
         BZ    EXIT1                    BRANCH IF NO                    40710022
         LH    R6,FOUR(R5)              GET NO. OF ENTRIES IN TLIST     40800022
LOOP     EQU   *                                                        41100022
         LTR   R6,R6                    FINISHED                        41400022
         BZ    EXIT                     BRANCH IF YES                   42400022
         SPACE 1                                                        43850022
*********************************************************************** 43900022
*        GET THE TLIST ENTRY                                          * 43950022
*********************************************************************** 44000022
         SPACE 1                                                        44050022
NOTFINI  EQU   *                                                        44100022
         BCTR  R6,R0                    SUBTRACT ONE FROM NO. ENTRIES   44400022
         SR    R1,R1                    CLEAR REGISTER                  44700022
         LH    R1,SIX(R2,R5)            GET TLIST ENTRY                 45000022
         L     R15,AVTRNMPT             GET ADDR OF TERMNAME TABLE      45300022
         BALR  R14,R15                  AND POINT TO TERMINAL ENTRY     45600022
         SR    R3,R3                    CLEAR REGISTER                  45900022
         IC    R3,TRMCHCIN              GET CHARACTERISTICS INDEX       46200022
         BCTR  R3,R0                    DECREMENT BY ONE                46500022
         MH    R3,AVTDCTLN              MULTIPLY BY DCT LEN    @Y17XAMG 46800000
         A     R3,AVTCSTCS              DCT BASE                        47100022
         TM    TWO(R3),CBRDCAST         IS THIS A BROADCAST TERMINAL    47400022
         BZ    OFFSET                   IF NOT, GET NEXT ENTRY          47700022
         L     RQCB,TRMDESTQ-ONE        GET QCB ADDRESS                 48000022
         L     RDCB,QCBDCBAD-ONE        GET DCB ADDRESS                 48300022
         TM    DCBOFLGS,OPEN            IS DCB OPEN                     48600022
         BNZ   GETLCB                   BRANCH IF YES                   48900022
OFFSET   EQU   *                                                        49200022
         LA    R2,TWO(R2)               IF NOT,PREPARE TO PICK UP NEXT  49500022
*                                         OFFSET                        49800022
         B     LOOP                     GO TO GET NEXT ENTRY            50100022
         SPACE 1                                                        50150022
*********************************************************************** 50200022
*        GET THE LCB                                                  * 50250022
*********************************************************************** 50300022
         SPACE 1                                                        50350022
GETLCB   EQU   *                                                        50400022
         SR    R15,R15                  CLEAR REGISTER                  50700022
         IC    R15,QCBRELLN             GET QCB'S STARTING RLN          51000022
         SR    R1,R1                    CLEAR REGISTER                  51300022
         IC    R1,DCBEIOBX              GET LCB SIZE                    51600022
         MR    R14,R1                   GET STARTING LCB OFFSET         51900022
         AL    R15,DCBIOBAD             POINT TO IOB OF LCB             52200022
         LR    RLCB,R15                 GET IOB ADDRESS                 52500022
         LA    R15,LCBFLAG1-IEDQLCB     GET OFFSET TO BEGINNING OF LCB  52800022
         SR    RLCB,R15                 GET ADDRESS OF LCB              53100022
         TM    LCBSTAT1,LCBRECVN+LCBSENDN IS THE LINE STOPPED           53400022
         BNM   OFFSET                   BRANCH IF YES                   53700022
         TM    LCBSTAT1,LCBOCNI         STOPLINE IN PROGRESS            54000022
         BO    OFFSET                   BRANCH IF YES                   54300022
         TM    QCBSTAT,QCBTRMHO         IS TERMINAL HELD                54600022
         BO    OFFSET                   BRANCH IF SO                    54900022
         L     RSTCB,LCBSTCBA-ONE       GET FIRST STCB OFF LCB          55200022
CHKSTCB  EQU   *                                                        55500022
         CLI   STCBPRI-IEDQSTCB(RSTCB),CMBVTO IS THIS COMMBUF STCB      55800022
         BNE   STCBSUB                  IF NOT,GO GET COMMBUF STCB      56100022
CBSTCB   EQU   *                                                        56400022
         SR    R15,R15                  CLEAR REGISTER FIFTEEN          56700022
         LH    R15,STCBCNTR             GET NO. COMMBUF STCB'S          57000022
         LA    R15,ONE(R15)             ADD ONE TO COUNT                57300022
         STH   R15,STCBCNTR             STORE UPDATED COUNT             57600022
         L     R1,SAVE                  GET ADDR OF PARM LIST           57900022
         LH    R15,TWO(R1)              GET MAXDEEP VALUE               58200022
         CH    R15,STCBCNTR             IS MAXDEEP EXCEEDED             58500022
         BH    GETSTCB                  BRANCH IF NO                    58800022
         LH    R15,LOSTCNTR             GET NO. LOST VALUE              59100022
         LA    R15,ONE(R15)             ADD ONE TO NO. LOST COUNTER     59400022
         STH   R15,LOSTCNTR             STORE NEW VALUE                 59700022
         XC    STCBCNTR,STCBCNTR        CLEAR CNTR                      59900022
         B     OFFSET                   GET NEXT ENTRY                  60100022
GETSTCB  EQU   *                                                        60300022
         L     RSTCB,STCBLINK-ONE-IEDQSTCB(RSTCB) GET NEXT STCB         60600022
         B     CHKSTCB                  CHECK FOR COMMBUF STCB          60900022
         SPACE 1                                                        60950022
*********************************************************************** 61000022
*        GET A COMMBUF STCB AND CHAIN INTO THE LCB                    * 61050022
*********************************************************************** 61100022
         SPACE 1                                                        61150022
STCBSUB  EQU   *                                                        61200022
         SR    R0,R0                    CLEAR REG                       61400022
         LH    R0,CMBASTCB              GET NO AVAIL STCBS              61600022
         LTR   R0,R0                    IS STCB AVAIL                   61800022
         LA    R15,NOSTCB               ASSUME NONE                     62000022
         BZ    EXIT1                    BRANCH IF NO                    62200022
         BCTR  R0,ZERO                  DECR NO AVAIL STCB              62400022
         STH   R0,CMBASTCB              SET NEW AVAIL COUNT             62600022
         L     R15,CMBSTCB-ONE          GET ADDRESS OF STCB             63300022
         MVI   FOUR(R15),CMBPRITY       PLACE PRIORITY IN STCB          63600022
         LH    R1,SIX(R2,R5)            GET TTCIN                       63900022
         STH   R1,TWO(R15)              STORE IN STCB                   64200022
         MVI   0(R15),CMBVTO            MOVE IN MCPL FIELD              64500022
         SR    R0,R0                    CLEAR REGISTER ZERO             64800022
         IC    R0,CMBNINDX              GET INDEX TO DATA AREA          65100022
         STC   R0,ONE(R15)              PLACE INDEX IN STCB             65400022
         LR    R0,R3                    SAVE STCB ADDRESS               65700022
         LR    R7,RCMB                  GET ADDR OF MASTER QCB          66000022
         SH    R7,HEIGHT                SUBTRACT EIGHT FROM ADDR OF QCB 66300022
         LR    R3,RLCB                  CHAIN INTO LCB                  66600022
         ST    R13,AVTPARM              SAVE REGISTER 13                66900022
         LA    R13,AVTSAVE2             GET ADDRESS OF SAVE AREA 2      67200022
         L     RDISP,AVTEA              GET DISPATCHER ADDRESS          67500022
         BAL   R14,DSPUNAVR             BRANCH TO DISPATCHER            67800022
         L     R13,AVTPARM              RESTORE REGISTER 13             68100022
         SPACE 1                                                        68750022
*********************************************************************** 68800022
*        POST THE LCB                                                 * 68850022
*********************************************************************** 68900022
         SPACE 1                                                        68950022
FREELCB  EQU   *                                                        69000022
         L     R14,CMBNEXT-ONE          GET ADDR OF DATA AREA PREFIX    69300022
         LH    R3,CBDAUSCT              GET DATA AREA USE COUNT         69600022
         LA    R3,ONE(R3)               UPDATE USE COUNT                69900022
         STH   R3,CBDAUSCT              STORE NEW USE COUNT IN PREFIX   70200022
         L     R3,CBDAADDR-ONE          POINT TO DATA AREA              70500022
         MVC   CBDALEN(TWO),TWO(R3)     MOVE IN DATA AREA LENGTH        70700022
         OI    LCBSTAT2,LCBSNDPR        SET SEND PRIORITY               70900022
         TM    LCBSTAT1,LCBFREEN        IS LCB FREE                     71100022
         BZ    TESTPOLL                 BRANCH IF NOT                   71400022
         MVI   LCBPRI,PRILNFRE          POST PRIORITY                   71700022
         OI    LCBSTAT1,LCBSENDN        SET SEND STATE                  71800022
         ST    RLCB,LCBQCBA-ONE         LCB IS QCB                      72000022
         LR    R1,RLCB                  PARAMETER FOR POST              72300022
         ST    R13,AVTPARM              SAVE REGISTER 13                72600022
         LA    R13,AVTSAVE2             GET ADDRESS OF SAVE AREA 2      72900022
         BAL   R14,DSPPOSTR             BRANCH TO DISPATCHER            73200022
         L     R13,AVTPARM              RESTORE REGISTER 13             73500022
         B     OFFSET                   GET NEXT TLIST ENTRY            73800022
         SPACE 1                                                        73850022
*********************************************************************** 73900022
*        CHECK TO SEE IF THE LINE IS BEING AUTOPOLLED                 * 73950022
*********************************************************************** 74000022
         SPACE 1                                                        74050022
TESTPOLL EQU   *                                                        74100022
         CLI   LCBCPA+TWENTY4,AUTOPOLL  IS LINE BEING AUTOPOLLED        74400022
         BNE   OFFSET                   BRANCH IF NOT                   74700022
         MVI   LCBCPA+THIRTY2,NOP       NOP TICS                        75000022
         MVI   LCBCPA+FIFTY6,NOP        AFTER POLLS                     75300022
         B     OFFSET                   GET NEXT TLIST ENTRY            75600022
         SPACE 1                                                        75640022
*********************************************************************** 75680022
*        GET THE ADDRESS OF THE NEXT DATA AREA PREFIX AND STORE       * 75720022
*        IT IN THE COMMBUF MASTER QCB                                 * 75760022
*********************************************************************** 75800022
         SPACE 1                                                        75840022
EXIT     EQU   *                                                        75900022
         L     R14,CMBNEXT-ONE          GET ADDR OF DATA AREA PREFIX    76200022
         SR    R3,R3                    CLEAR REGISTER                  76500022
         SR    R7,R7                    CLEAR REGISTER                  76800022
         IC    R3,CBDAINDX              GET INDEX BYTE                  77100022
         LH    R7,CMBDAREA              GET NO. DATA AREAS              77400022
         BCTR  R7,R0                    SUBTRACT ONE                    77700022
         CR    R3,R7                    COMPARE VALUES                  78000022
         BE    BEGIN                    GET ADDR OF FIRST PREFIX        78300022
         LA    R15,CBDPRFSZ(R14)        POINT TO NEXT PREFIX            78600022
         B     NEXT                     STORE ADDRESS                   78900022
BEGIN    EQU   *                                                        79200022
         L     R15,CMBFIRST-ONE         GET ADDR OF FIRST PREFIX        79500022
NEXT     EQU   *                                                        79800022
         SR    R0,R0                    CLEAR REGISTER                  80100022
         IC    R0,0(R15)                GET INDEX BYTE                  80400022
         ST    R15,CMBNEXT-ONE          STORE ADDR OF NEXT AREA         80700022
         STC   R0,CMBNINDX              STORE INDEX BYTE                81000022
         DROP  R14                                                      81300022
EXIT2    EQU   *                                                        81340022
         SR    R15,R15                  CLEAR REG                       81380022
         LH    R1,LOSTCNTR              GET NO LOST                     81420022
         LTR   R1,R1                    ANY LOST                        81460022
         BZ    EXIT1                    BRANCH IF NO                    81500022
         LA    R15,EIGHT                SET TOO DEEP RETURN CODE        81540022
         LA    R6,LOSTCNTR              GET ADDRESS OF LOST COUNTER     81600022
         MVC   ZERO(FOUR,R6),AVTFZERO   ZERO OUT LOST AND STCB CNTRS    81900022
EXIT1    EQU   *                                                        82200022
         L     RBASE,AVTUI              GET RETURN INTERFACE ADDRESS    82500022
         B     FOUR(RBASE)              RETURN TO CALLER                82800022
*********************************************************************** 83100022
         SPACE 1                                                        83130022
MOVIT    MVC   AVTDOUBL(1),ZERO(R1)     EXECUTE FOR MOVE                83160022
         DS    0F                                                       83200022
SAVE     DC    F'0'                     ADDR OF PARM LIST               83400022
LOSTCNTR DC    H'0'                     NO. LOST COUNTER                83700022
STCBCNTR DC    H'0'                     STCB COUNTER                    84000022
HEIGHT   DC    H'8'                     HALF WORD OF EIGHT              84300022
ZERO     EQU   0                        ZERO                            84370022
ONE      EQU   1                        ONE                             84440022
TWO      EQU   2                        TWO                             84510022
FOUR     EQU   4                        CONSTANT FOUR                   84600022
FIVE     EQU   5                        FIVE                            84700022
SIX      EQU   6                        SIX                             84900022
SEVEN    EQU   7                        SEVEN                           84930022
EIGHT    EQU   8                        EIGHT                           84960022
TWENTY4  EQU   24                       TWENTY FOUR                     85020022
THIRTY2  EQU   32                       THIRTY TWO                      85080022
FIFTY6   EQU   56                       FIFTY SIX                       85140022
NOP      EQU   X'03'                    NOP COMMAND                     85200022
AUTOPOLL EQU   X'09'                    AUTOPOLL COMMAND                85500022
OPEN     EQU   X'10'                    DCB OPEN FLAG                   85800022
CMBVTO   EQU   X'26'                    COMMON BUFFER MCPL              86100022
CBRDCAST EQU   X'40'                    BROADCAST TERMINAL              86400022
CMBPRITY EQU   X'90'                    COMMON BUFFER PRIORITY          86700022
NTLIST   EQU   X'0C'                    RETURN CODE FOR NOT TLIST       86800022
NOSTCB   EQU   X'10'                    NO STCB AVAIL                   86900022
         LTORG                                                 @OZ19773 86950010
*********************************************************************** 87000022
         SPACE 4                                                        87300022
         TAVTD                                                          87600022
         SPACE 4                                                        87900022
         TLCBD                                                          88200022
         SPACE 4                                                        88500022
         TTRMD                                                          88800022
         SPACE 4                                                        89100022
         TDISPD                                                         89400022
         SPACE 4                                                        89700022
         TQCBD                                                          90000022
         SPACE 4                                                        90300022
         DCBD  DSORG=TX                                                 90600022
         SPACE 4                                                        90900022
         TPRIOR                                                         91200022
         SPACE 4                                                        91500022
         TSTCBD                                                         91800022
         SPACE 4                                                        92100022
         SPACE 4                                                        92700022
         IEDCBDA                                                        93000022
         SPACE 4                                                        93300022
         IEDCMB                                                         93600022
*********************************************************************** 93900022
         END                                                            94200022
