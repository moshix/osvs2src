DQBX TITLE '''IEDQBX''   LOG SEGMENT'                                   00500020
IEDQBX   CSECT                                                          01000020
*C210000,235000,535000-550000,557800,605000,690000,841000,847000 S22025 01500022
*C710000                                                        SA56630 01700022
*A590000,745000,800000                                         @SA72459 01700181
*A655000                                                       @XA11336 01700210
*                                                                     * 02000020
*********************************************************************** 02500020
*                                                                     * 03000020
*TITLE 'IEDQBX'  LOG SEGMENT                                          * 03500020
*                                                                     * 04000020
*STATUS: CHANGE LEVEL 5                                               * 04500081
*                                                                     * 05000020
*FUNCTION -- THIS ROUTINE WILL WRITE A MESSAGE SEGMENT ONTO THE       * 05500020
*   LOGGING MEDIUM SPECIFIED BY THE USED.  THIS IS DONE AS IN LINE    * 06000020
*   CODE IN THE MH.  A WRITE WILL BE ISSUED FOR EACH UNIT OF THE      * 06500020
*   BUFFER, THEN A CHECK WILL BE ISSUED FOR EACH WRITE.  NP OTHER     * 07000020
*   PROCESSING WILL BE DONE BY TCAM WHILE THE LOG TAKES PLACE         * 07500020
*                                                                     * 08000020
*ENTRY POINTS -- 'IEDQBX', TO LOG A MESSAGE SEGMENT                   * 08500020
*   CALLING SEQUENCE  --  LA  R1,PARLIST                              * 09000020
*                         LA   R12,IEDQBX                             * 09500020
*                         BR   R12                                    * 10000020
*                                                                     * 10500020
*INPUT -- THE LOG MACRO WHEN ISSUED IN THE INHDR, OUTHDR, INBUF, OR   * 11000020
*   OUT BUF SUBGROUP, WILL EXPAND A 1 WORD PARAMETER LIST.  IT HAS IN * 11500020
*   THE HIGH ORDER BYTE AN OFFSET INTO IEDQMSGS TO THE VCON OF        * 12000020
*   IEDQBX WHICH WILL ALLOW IEDQUI TO CALL THE ROUTINE.  THE LOW      * 12500020
*   ORDER 3 BYTES WILL CONTAIN A 3 BYTE ADDRESS OF THE DCB OT BE      * 13000020
*   USED TO LOG THE SEGMENT.                                          * 13500020
*   IEDQUI WILL PASS THE PARAMETER LIST ADDRESS IN R1 AND THE         * 14000020
*   ADDRESS OF THE BUFFER TO BE WRITTEN IN R6.                        * 14500020
*                                                                     * 15000020
*OUTPUT -- IEDQBX WILL ATTEMPT TO LOG THE SEGMENT ON THE LOGGING      * 15500020
*   MEDIUM SPECIFIED IN THE DCB.  ON RETURN TO MH, R15 WILL           * 16000020
*   CONTAIN THE FOLLOWING RETURN CODES -                              * 16500020
*   / -- SUCCESSFULLY WRITTEN                                         * 17000020
*   4 -- DCB NOT OPEN                                                 * 17500020
*   IF AN ERROR OCCURRED IN WRITING THE BUFFER, BSAM WILL GIVE        * 18000020
*   CONTROL TO THE USER'S SYNAD EXIT IN THE DCB.  THIS EXIT MUST      * 18500020
*   RETURN TO THE INSTRUCTION FOLLOWING THE CHECK MACRO.              * 19000020
*                                                                     * 19500020
*EXTERNAL ROUTINES -- IEDQUI - LOG MACRO WILL BALR TO IEDQUI AND      * 20000020
*   UI WILL CALL IEDQBX.                                              * 20500020
*   IEDQUI+4 - IEDQBX WILL EXIT TO IEDQUI+4 TO RESTORE THE       S22025 21000022
*   USER'S REGISTERS AND RETURN TO THE NEXT INSTRUCTION.              * 21500020
*   BSAM WRITE - TO WRITE THE UNITS OF THE BUFFER                     * 22000020
*   BSAM CHECK - TO CHECK THE WRITES                                  * 22500020
*                                                                     * 23000020
*EXITS-NORMAL -- IEDQBX WILL EXIT TO IEDQUI+4 WHICH RETURNS TO   S22025 23500022
*   THE INSTRUCTION AFTER THE LOG MACRO, R15 WILL CONTAIN 0.          * 24000020
*                                                                     * 24500020
*EXITS-ERROR -- IF THE DCB IS NOT OPEN, IEDQBX WILL EXIT AS ABOVE     * 25000020
*   WITH R15 CONTAINING 4.                                            * 25500020
*   IF A WRITE WAS NOT SUCCESSFUL, THE CHECK MACRO WILL GO TO THE     * 26000020
*   SYNAD EXIT OF THE DCB.                                            * 26500020
*                                                                     * 27000020
*TABLES/WORK AREAS -- DCBD, AVTD                                      * 27500020
*   AVTFIELDS - AVTLM                                                 * 28000020
*                                                                     * 28500020
*ATTRIBUTES -- REUSABLE,REFRESHABLE, RESIDENT, ENABLED                * 29000020
*                                                                     * 29500020
*NOTES -- THE OPERATION OF THIS MADULE DOES NOT DEPEND UPON A         * 30000020
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      * 30500020
*   SET.                                                              * 31000020
*                                                                     * 31500020
*********************************************************************** 32000020
*                                                                     * 32500020
*NOTES -- THE DCB OPENED MUST BE A BSAM DCB.                          * 33000020
*   OPERANDS ARE --                                                   * 33500020
*   DSORG=PS                                                          * 34000020
*   BFALN=  OMITTED                                                   * 34500020
*   BLKSIZE=  (KEYLENGTH)  ALL UNITS WILL BE WRITTEN WITHOUT THE      * 35000020
*        12 BYTE UNIT MANAGEMENT AREA AND ALL ARE THE SAME SIZE       * 35500020
*   BUFCB=  OMITTED                                                   * 36000020
*   BUFL=  OMITTED                                                    * 36500020
*   DDNAME=  (USED OPTION)                                            * 37000020
*   NCP=  SHOULD BE THE MAXIMUM NUMBER OF UNITS THAT CAN APPEAR IN    * 37500020
*        A BUFFER.  THIS IS A USER OPTION - IT CAN BE ANYTHING.       * 38000020
*   RECFM=F                                                           * 38500020
*   MACRF=W                                                           * 39000020
*   SYNAD=  (USER SPECIFIED - SHOULD CONFORM TO STANDARDS FOR         * 39500020
*        SYNAD EXITS - ERRORS THAT MAY HAVE BEEN ENCOUNTERED WILL     * 40000020
*        IGNORED BY LOG.)                                             * 40500020
*                                                                     * 41000020
*   A GET MAIN WILL PROVIDE AS MANY CONTROL BLOCKS AS SPECIFIED IN    * 41500020
*   THE NCP OPERAND OF THE DCB.  THEY WILL BE IN A CONTIGUOUS         * 42000020
*   AREA.  THERE WILL BE A CONTROL AREA FOR EACH DCB USED FOR LOG.    * 42500020
*                                                                     * 43000020
*                                                                     * 43500020
*                         REGISTER EQUATES                            * 44000020
R0       EQU   0                        GET MAIN PAR LIST               44500020
RPARM    EQU   1                        ADDR OF THE PAR LIST PASSED     45000020
RDECB    EQU   2                        ADDR OF WRITE CNTRL BLK         45500020
RAREA    EQU   3                        ADDR OF AREA TO BE WRITTEN      46000020
R4       EQU   4                                                        46500020
RNCP     EQU   5                        NO. OF WRITE CONTROL BLKS       47000020
RPREFIX  EQU   6                        ADDR OF BUFFER TO WRITE         47500020
RNB      EQU   7                        NO. OF UNITS IN BFR             48000020
RUNIT    EQU   8                        ADDR OF UNIT TO WRITE           48500020
RAVT     EQU   9                        ADDR OF AVT                     49000020
RDCB     EQU   10                       DCB ADDRESS FRO LOG             49500020
R11      EQU   11                                                       50000020
RBASE    EQU   12                       ENTRY POINT ADDRESS             50500020
R13      EQU   13                                                       51000020
RETURN   EQU   14                       RETURN ADDRESS                  51500020
R15      EQU   15                                                       52000020
FOUR     EQU   4                        OFFSET USED ON RETURN    S22025 52200022
*                                                                       52500020
*                         USING STATMENTS                             * 53000020
         USING IEDQAVTD,RAVT            AVT  BASE                S22025 53500022
         USING IHADCB,RDCB              DCB  BASE                S22025 54000022
         USING IEDQPRF,RPREFIX          PREFIX BASE              S22025 54500022
         USING *,RBASE                  BASE FOR ROUTINE         S22025 55000022
*                                                                     * 55500020
IEDQBX01 EQU   *                                                 S22025 55580022
IEDQBX   IEDHJN ENTRY                                            S22025 55780022
         NC    PRFSIZE(2),PRFSIZE       ZERO LENGTH BFRS                56000020
         LA    R15,8                    SHOULD NOT BE LOGGED            56500020
         BZ    EXIT                      IF 0 EXIT WITH 8 RTN CODE      57000020
         L     RDCB,0(RPARM)            DCB ADDRESS                     57500020
         TM   DCBOFLGS,OPEN             HAS THE DCB BEEN OPENED         58500020
         BO    OPENED                   BR YES                          59000020
ERROR    EQU   *                                               @SA72459 59200081
         LA    R15,4                    SET RETURN CODE                 59500020
         B     EXIT                     RETURN WITH 4 ERROR CODE S22025 60500022
OPENED   EQU   *                                                        62000020
         TM    DCBEODAD+3,E01           HAS THE GETMAIN BEEN DONE       62500020
*              THE EODAD FIELD HAS A VALUE OF 1 IF NO EODAD IS          63000020
*              SPECIFIED.  THE EODAD EXIT WILL NEVER BE USED SINCE IT   63500020
*              APPLIES ONLY TO READ                                     64000020
         BNO   OK                       BR YES                          64500020
         SR    RNCP,RNCP                CLEAR REG                       65000020
         IC    RNCP,DCBNCP              NO. OF DECB'S NEEDED            65500020
         LTR   RNCP,RNCP                ANY WRITES REQD        @XA11336 65600010
         BZ    EXIT                     BR NO, EXIT            @XA11336 65700010
         LA    R4,DECBSZ                SIZE OF A DECB                  66000020
         MR    R4,R4                    SIZE OF TOTAL REA IF R5         66500020
         LR    R0,RNCP                  SET FOR GETMAIN                 67000020
         GETMAIN  R,LV=(0)                                              67500020
         IC    R0,DCBEODAD              SAVE HI ORDER BYTE              68000020
         ST    RPARM,DCBEODAD           ADDR OF DECBS IN DCB            68500020
         STC   R0,DCBEODAD              RESTORE HI-ORDER BYTE    S22025 69000022
OK       EQU   *                                                        69500020
         SR    RNB,RNB                  CLEAR REGS                      70000020
         SR    RNCP,RNCP                                                70500020
         EJECT                                                          71000022
*              THE FOLLOWING CODE WILL DETERMINE THE NUMBER OF  SA56630 71030022
*              BUFFER UNITS THAT CONTAIN VALID DATA AND SHOULD  SA56630 71060022
*              BE LOGGED                                        SA56630 71090022
         XC    AVTDOUBL+4(4),AVTDOUBL+4      ZERO WORK AREA     SA56630 71120022
         MVC   AVTDOUBL+6(2),AVTKEYLE        MSG Q LENGTH       SA56630 71150022
         SR    RDECB,RDECB                   CLEAR WORK REG     SA56630 71180022
         LH    RAREA,PRFSIZE                 LOAD DATA LENGTH   SA56630 71210022
         N     RAREA,AVTCLRHI                CLEAR HI ORDR BYTE SA56630 71240022
         D     RDECB,AVTDOUBL+4              CALC NO UNITS      SA56630 71270022
         LR    RNB,RAREA                     NO. UNITS TO WRITE SA56630 71300022
         LTR   RDECB,RDECB                   EVEN MULTIPLE      SA56630 71330022
         BNP   NOADD                         BRANCH IF YES      SA56630 71360022
         LA    RNB,E01(RNB)                  INCREMENT COUNT    SA56630 71390022
NOADD    EQU   *                                                        71420022
         EJECT                                                          71450022
         LR    RUNIT,RPREFIX            ADDR FIRST UNIT OF BFR          71500020
WRITE1   EQU   *                                                        72000020
         L     RDECB,DCBEODAD           ADDR FIRST DECB                 72500020
WRITE    EQU   *                                                        73000020
        LA    RNCP,1(RNCP)              ADD TO NO OF WRITES             73500020
         LR    RPARM,RDECB              ADDR OF DECB FOR WRITE          74000020
         LA    RAREA,AVTUMALN(RUNIT)    ADD 12 BYTES FR ADDR            74500020
         TM    DCBOFLGS,OPEN            DCB OPENED?            @SA72459 74600081
         BNO   ERROR                    BRANCH IF NO           @SA72459 74700081
         WRITE   (1),SF,(10),(3),MF=E                                   75000020
         BCT   RNB,NEXTUNIT             DECR NO. OF UNITS               75500020
         B    ALLBFR                    HERE IF ALL WRITTEN             76000020
NEXTUNIT EQU   *                                                        76500020
         L     RUNIT,PRFTIC-IEDQPRF(RUNIT)                              77000020
         LA    RDECB,DECBSZ(RDECB)      NEXT DECB ADDR                  77500020
         EX    RNCP,CLI                 IS THIS ALL THE WRITES THAT     78000020
         BH    WRITE                    CAN BE DONE - BR YES            78500020
ALLBFR   EQU   *                                                        79000020
         L     RDECB,DCBEODAD           ADDR FIRST DECB TO CHECK        79500020
CHECK    EQU   *                                                        80000020
         TM    DCBOFLGS,OPEN            DCB OPENED?            @SA72459 80100081
         BNO   ERROR                    BRANCH IF NO           @SA72459 80200081
         CHECK   (2)                                                    80500020
         LA    RDECB,DECBSZ(RDECB)      NEXT DECB ADDR                  81000020
         BCT   RNCP,CHECK               DECRMENT NO. UNITS              81500020
         LTR   RNB,RNB                  ANY UNITS LEFT                  82000020
         BNZ   WRITE1                   BR YES                          82500020
         SR    R15,R15                  SET RETURN CODE                 83000020
EXIT     EQU   *                                                        83500020
         L     RBASE,AVTUI              GET RET INTERFACE ADDR   S22025 84100022
         B     FOUR(RBASE)              BRANCH TO RETURN ROUTINE S22025 84700022
OPEN     EQU   X'10'                                                    85500020
E01      EQU   X'01'                                                    86000020
DECBSZ   EQU   20                                                       86500020
CLI      CLI   DCBNCP,X'00'             NO. OF WRITE CONTROL BLKSS22025 87000022
         TPRFD                                                          87500020
         DCBD  DSORG=BS                                                 88000020
         TAVTD                                                          88500020
         END                                                            89000020
