 TITLE 'IEDQFE10 STCB TRACE DUMP'                                       00100020
IEDQFE10 CSECT                                                          00200020
*C445000,471000,297000                                         @SA69974 00200110
*D309000-318000                                                @SA69974 00200210
*********************************************************************** 00300020
*                                                                     * 00400020
*MODULE NAME - IEDQFE10 STCB TRACE DUMP                               * 00500020
*                                                                     * 00600020
*STATUS--CHANGE LEVEL 1                                               * 00700020
*                                                                     * 00800020
*FUNCTION-- THIS MODULE WILL WRITE THE STCB TRACE TABLE OUT TO A      * 01500020
*   SEQUENTIAL DATA SET USING COMWRITE (IEDQFW). ADDRESSABILITY IS    * 01800020
*   ESTABLISHED AND A TEST IS MADE FOR A LOAD OR DELETE FUNCTION.     * 02100020
*   ON A LOAD OPERATION IF AT LEAST FOUR ENTRIES ARE NOT FOUND IN THE * 02400020
*   TRACE TABLE AN ERROR MESSAGE IS PREPARED AND CONTROL IS RETURNED  * 02700020
*   TO THE CALLING ROUTINE. THE TABLE IS THAN DIVIDED IN HALF WITH    * 03000020
*   SEPARATE CURRENT, FIRST AND LAST POINTERS FOR EACH HALF.          * 03300020
*   UPON ENTRY FROM IEDQC6 THIS ROUTINE CHECKS TO SEE IF THIS IS A    * 03600020
*   LOAD OR DELETE FUNCTION.IF A LOAD IS REQUESTED THE FLAGS AT       * 03900020
*   AVTAFE10 ARE TESTED. IF THEY ARE OFF,THIS ROUTINE STORES ITS ENTRY* 04200020
*   POINT ADDRESS INTO AVTAFE10 AND SETS THE FLAGS ON,INDICATING THAT * 04500020
*   THE ROUTINE IS ACTIVE.THE ROUTINE THEN RETURNS TO IEDQC6 WITH A   * 04800020
*   RETURN CODE OF ZERO. IF THE FLAGS ARE ALREADY ON RETURN IS TO     * 05100020
*   IEDQC6 WITH A RETURN CODE OF FOUR.IF A DELETE FUNCTION IS         * 05400020
*   REQUESTED THE FLAGS IN AVTAFE10 ARE SET OFF INDICATING THAT THE   * 05700020
*   ROUTINE IS NOT ACTIVE.                                            * 06000020
*   THIS ROUTINE IS ENTERED FROM THE DISPATCHER AT SYMBOL 'DISPENT'   * 06300020
*   UPON ENTRY AT THIS POINT THE TWO TRACE TABLES ARE SET UP WITH IDEN* 06600020
*   TIFING HEADERS. ALL SUBSEQUENT ENTRIES TO THIS ROUTINE FROM THE   * 06900020
*   DISPATCHER WILL ENTER AT SYMBOL 'BYPASS'. AT THIS ENTRY A COUNT OF* 07200020
*   THE NUMBER OF TABLES FILLED IS UPDATED. A TEST IS MADE TO         * 07500020
*   SEE IF THE PREVIOUS TABLE HAS BEEN WRITTEN. IF NOT, RETURN IS MADE* 07800020
*   TO THE DISPATCHER AND THE CURRENT TABLE WILL BE USED AGAIN.       * 08100020
*   A TEST IS THEN MADE TO SEE IF THE PARAMETER POINTERS IN THE       * 08400020
*   AVT ARE FREE. IF ONE IS FOUND AVAILABLE HEADER INFORMATION        * 08700020
*   IS COMPLETED AT THE BEGINNING OF THE CURRENT TABLE.               * 09000020
*   THE PARAMETER LIST FOR COMWRITE IS COMPLETED TO PRINT THE CURRENT * 09300020
*   TABLE AND CONTINUE TRACING ENTRIES IN THE AVAILABLE TRACE TABLE.  * 09600020
*   WHEN NO PARAMETER POINTER IS AVAILABLE CONTROL IS RETURNED TO     * 09900020
*   THE DISPATCHER AND THE CURRENT TABLE IS REUSED.                   * 10200020
*   ON A DELETE OPERATION THE DISPATCHER INSTRUCTIONS AND TRACE TABLE * 10800020
*   POINTERS ARE RESTORED TO THEIR ORIGINAL CONDITION AND CONTROL IS  * 11100020
*   RETURNED TO THE CALLING ROUTINE.                                  * 11400020
*                                                                     * 11700020
*ENTRY POINT--'IEDQFE10' TO INITIALIZE A LOAD OPERATION OR DEACTIVATE * 12000020
*                  ON A DELETE OPERATION.                             * 12300020
*        AT SYMBOL 'DISPENT' FROM DISPATCHER TO COMPLETE              * 12600020
*        INITIALIZATION                                               * 12900020
*        AT SYMBOL 'BYPASS' FROM DISPATCHER TO WRITE CURRENT TRACE    * 13200020
*                                                                     * 13300020
*INPUT                                                                * 13400020
*       FROM IEDQC6 REGISTER 4 - ADDRESS OF AVT.                      * 13500020
*                   REGISTER 13 - SAVE AREA                           * 13800020
*                   REGISTER 0 - ZERO FOR LOAD OPERATION              * 14100020
*                                NON-ZERO FOR DELETE OPERATION        * 14400020
*        FROM IGG019RO                                                * 14700020
*             REGISTER 2 - BASE ADDRESS                               * 15000020
*             REGISTER 5 - ADDRESS OF TRACE TABLE POINTERS            * 15300020
*             REGISTER 13 - ADDRESS OF AVT + HEX 48 (AVTSAVE2)        * 15600020
*             REGISTER 14 - RETURN ADDRESS.                           * 15800020
*                                                                     * 16000020
*OUTPUT-- TO IEDQC6 REGISTER 15 - RETURN CODE                         * 16200020
*EXTERNAL ROUTINES-- SVC 102 TO POST ECB FOR COMWRITE.                * 16600020
*                                                                     * 17000020
*EXITS-NORMAL--A RETURN CODE OR MESSAGE IS RETURNED TO THE CALLING    * 17400020
*        ROUTINE.                                                     * 17600020
*                                                                     * 17800020
*EXITS-ERROR--                                                        * 18000020
*        RETURN CODE OF 4 IN REGISTER 15.                             * 18200020
*        MESSAGES-                                                    * 18400020
*             IED110I  LESS THAN 4 ENTRIES                            * 18600020
*             IED111I  NO TRACE TABLE                                 * 18800020
*                                                                     * 19000020
*TABLES/WORK AREAS--PARMLIST USED BY AQCTL (SVC 102). PARMLIST USED   * 19200020
*        BY COMWRITE(IEDQFW). DSECTS OF AVT.                          * 19400020
*                                                                     * 19600020
* NOTE-  THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL        * 19800020
*        REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS        * 20100020
*        EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME. REDEFINITION OF * 20400020
*        CHARACTER CONSTANTS BY REASSEMBLY WILL RESULT IN A CORRECT   * 20700020
*        MODULE FOR THE NEW DEFINITIONS. THIS MODULE IS ALSO          * 21000020
*        DEPENDENT ON DISPLACEMENTS IN SOME OS CONTROL BLOCKS. THESE  * 21300020
*        DISPLACEMENTS ARE DEFINED IN THE EQUATES PRECEEDING THE      * 21600020
*        CODING. REDEFINITION OF THESE EQUATES & REASSEMBLY WILL      * 21900020
*        RESULT IN A CORRECT MODULE FOR THE NEW DISPLACEMENTS.        * 22200020
*********************************************************************** 22500020
 EJECT                                                                  22700020
R0       EQU   0                                                        22900020
R1       EQU   1                                                        23100020
R2       EQU   2                                                        23400020
R3       EQU   3                                                        23700020
R4       EQU   4                                                        24000020
R5       EQU   5                                                        24300020
R6       EQU   6                                                        24600020
R7       EQU   7                                                        24900020
R8       EQU   8                                                        25200020
R9       EQU   9                                                        25500020
R10      EQU   10                                                       25800020
R11      EQU   11                                                       26100020
R12      EQU   12                                                       26400020
R13      EQU   13                                                       26700020
R14      EQU   14                                                       27000020
R15      EQU   15                                                       27300020
         EJECT                                                          27600020
*              R4 AVT ADDRESS                                           27900020
*              ENTER FROM ROUTER                                        28200020
         USING IEDQAVTD,R4                                              28500020
         STM   R14,R12,12(R13)     SAVE                                 28800020
         BALR  R2,0                REGS                                 29100020
         USING *,2                                                      29400020
         ST R13,SAVER+4                                                 29700020
IEDQFE10 IEDHJN ENTER                                          @SA69974 29800010
         LA    R8,SAVER                                                 30000020
         ST    R8,8(R13)                                                30300020
         LR    R13,R8                                                   30600020
         LTR   R0,R0               LOAD OF DELETE FUNCTION              32100020
         BNZ   DELETE              BRANCH DELETE                        32400020
* THIS IS TO TEST FOR FOUR OR MORE TRACE ENTRIES                        32700020
         L     R5,AVTDISTR         R5 NOW POINTS TO POINTERS OF TRACE   33000020
         LTR   R5,R5               IS THERE A TRACE TABLE               33300020
         BZ    NOTRACE             NO                                   33600020
         L     R8,4(R5)            LOAD FIRST ENTRY                     33900020
         L     R9,8(R5)            LOAD LAST ENTRY                      34200020
         SR    R9,R8               SUBTRACT TO FIND LENGTH              34500020
         SRL   R9,5                BOUNDARY ALIGNMENT                   34800020
         SLL   R9,1                                                     35100020
         CL    R9,FOUR             AT LEAST FIVE ENTRIES                35400020
         BNL   FOURORMR            YES                                  35700020
         LA    R15,LESFOUR         RETURN AND WRITE MESSAGE             36000020
MSGRTN   LCR   R15,R15             LOAD COMPLIMENT                      36300020
RETURN   L     R13,SAVER+4         RESTORE                              36600020
         L     R14,12(R13)                                              36900020
         LM    R0,R12,20(R13)                                           37200020
         BR    R14                 RETURN TO ROUTER                     37500020
NOTRACE  LA    R15,NOTRMSG         LOAD MESSAGE ADDRESS                 37800020
         B     MSGRTN                                                   38100020
FOURORMR SLL   R9,4                THE LNG OF THE TABLE IS NOW BOUNDRY  38400020
         SRL   R9,1                DIVIDE FOR LTH OF EACH SEP TABLE     38700020
         LR    R5,R9               SAVE LENGTH                          39000020
         STH   R9,PARMLN           STORE LTH IN PARM COM WRITE          39300020
         AR    R9,R8               ADD FOR ADDRESS OF LAST ENTRY        39600020
         LA    R7,16                                                    39900020
         SR    R9,R7               SUB FOR LAST ENTRY OF FIRST TABLE    40200020
         ST    R9,LASONE           SAVE LAST ENTRY FOR 1ST TABLE        40500020
         ST    R8,FIRONE           SAVE ENTRY FOR 1ST TABLE             40800020
         ST    R8,CURONE           SAVE CURRENT FOR 1ST TABLE           41100020
         LA    R9,16(R9)                                                41400020
         ST    R9,FIRTWO           1ST ENTRY 2ND TABLE                  41700020
         LA    R8,0(R9,R5)         SET UP FOR ADDRESS OF LAST ENTRY     42000020
         SR    R8,R7                                                    42300020
         ST    R8,LASTWO           LAST ENTRY OF 2ND TABLE              42600020
         ST    R9,CURTWO           STORE 2ND CURRENT POINTER            42900020
         L     R9,AVTDISTR         LOAD OLD TRACE POINTER               43200020
         MVC   OLDPTR(12),0(R9)    SAVE TRACE POINTERS                  43800020
         OC    AVTAFE10,AVTAFE10        TEST FOR PREV LOAD.    @SA69974 44500010
         BNZ   ERROR              YES                                   44600020
         MVC   0(12,R9),CURONE     SET UP NEW POINTERS                  44800020
         L     R10,AVTCWTCB                                             44860020
         ST    R10,COMWRTCB                                             44920020
         LA    R10,DISPENT         ADDRESS OF ENTRY POINT               45000020
         ST    R10,AVTAFE10        STORE ENTRY ADDRESS IN AVT           45900020
         MVI   AVTAFE10,X'FF'      SET DUMP REQUESTED                   46200020
         SR    R15,R15             SET GOOD RETURN CODE                 46500020
         B     RETURN                                                   46800020
DELETE   XC    AVTAFE10,AVTAFE10        CLEAR ADDRESS SLOT.    @SA69974 47100010
         L     R15,AVTDISTR        GET TRACE POINTER ADDRESS            47200020
         MVC   0(12,R15),OLDPTR    RESTORE TRACE POINTERS               47300020
         SR    R15,R15                                                  47400020
         B     RETURN                                                   47700020
ERROR    LA    R15,4               LOAD BAD RETURN CODE                 48000020
         B     RETURN                                                   48300020
         EJECT                                                          48600020
* INPUT        R2  BASE ADDRESS                                         48900020
*              R5   ADDRESS OF TRACE TABLE POINTERS                     49200020
*              R13  ADDRESS OF AVT PLUS HEX 48 (AVTSAVE2)               49500020
*              R14  RETURN ADDRESS                                      49800020
*              R15  ENTRY POINT OF SUBTASK                              50100020
         DROP  R2                                                       50400020
         DROP  R4                                                       50700020
         USING AVTSAVE2,R13                                             51000020
         USING *,R2                                                     51300020
*                                  THE FIRST TIME THE 'DISPATCHER'      51600020
*                                  ENTERS THIS MODULE IT WILL           51900020
*                                  BE AT THIS POINT                     52200020
DISPENT  STM   R0,R15,SAVE                                              52500020
         L     R8,FIRONE           LOAD FIRST ENTRY                     52800020
         LA    R8,16(R8)           UPDATE FOR 16 BYTES OF SPACE         53100020
         ST    R8,CURONE           STORE IN FIRST TABLE                 53400020
         L     R8,FIRTWO           LOAD FIRST ENTRY                     53700020
         LA    R8,16(R8)           UPDATE FOR 16 BYTES OF SPACE         54000020
         ST    R8,CURTWO           STORE IN SECOND TABLE                54300020
         L     R3,FIRONE           LOAD 1ST ENTRY 1ST TABLE             55500020
         L     R4,FIRTWO           LOAD 1ST ENTRY 2ND TABLE             55800020
         MVC   13(3,R3),AVTBASE+1       MOVE AVT ADDR INTO TABLE 1      56100020
         MVC   13(3,R4),AVTBASE+1       MOVE AVT ADDR INTO TABLE 2      56400020
         MVC   0(12,R5),CURONE     SET UP TO USE TABLE 1                56700020
         LA    R10,BYPASS                                               57000020
         ST    R10,AVTAFE10        CHANGE AVT POINTER TO BYPASS         57300020
         MVI   AVTAFE10,X'FF'      RESTORE FLAG                         57600020
         LM    R0,R15,SAVE                                              57900020
         BALR  R2,0                SET UP BASE REGISTER TO WRITE OUT    58200020
*                                  THE FIRST TABLE                      58500020
         EJECT                                                          58800020
*  SET UP IS COMPLETE AT THIS POINT THE DISPATCHER WILL ENTER AT        59100020
*   BYPASS FROM NOW ON                                                  59400020
* INPUT        R2                  BASE ADDRESS                         59700020
*              R5                  ADDRESS OF TRACE TABLE POINTERS      60000020
*              R13                 ADDRESS OF AVT + X'48'  (AVTSAVE2)   60300020
*              R14                 RETURN ADDRESS                       60600020
*              R15                 ENTRY POINT OF SUBTASK               60900020
         DROP  R2                                                       61200020
         USING *,R2                                                     61500020
BYPASS   EQU   *                                                        61800020
         TM    AVTOPECA,X'40'      HAS OPERATOR CONTROL ABENDED         62100020
         BO    RESTORE             YES BRANCH                           62400020
         STM   R0,R15,SAVE         SAVE REGS                            62700020
         IC    R1,TRACECNT              GET TRACE COUNTER               63000020
         LA    R1,1(,R1)                UPDATE COUNT                    63300020
         STC   R1,TRACECNT              STORE COUNTER BACK              63600020
         L     R3,4(R5)            LOAD 1ST ENTRY                       63900020
         TM    PARMFLAG,X'40'      HAS COMWRITE WRITTEN TABLE           64200020
         BC    8,RAP1              NO BRANCH TO USE TABLE AGAIN         64500020
         STC   R1,12(,R3)               PLACE COUNT IN TRACE BUFFER     64800020
         TS    AVTCWTS1            PARM POINTER AVAILABLE               65100020
         BC    4,CKTS2             NO BRANCH                            65400020
         LA    R1,PARMAREA         LOAD ADDRESS OF COMWRITE PARMS       65700020
         ST    R1,AVTCWPM1         STORE ADDRESS IN AVT PARM POINTER    66000020
         LA    R1,AVTCWEC1         GET COMWRITE ECB ADDRESS             66300020
         B     STAQC               GO SET UP TO WRITE                   66600020
CKTS2    TS    AVTCWTS2            PARM POINTER AVAILABLE               66900020
         BC    4,RAP1              NO BRANCH                            67200020
         LA    R1,PARMAREA         LOAD ADDRESS OF COMWRITE PARMS       67500020
         ST    R1,AVTCWPM2         STORE ADDRESS IN AVT PARM POINTER    67800020
         LA    R1,AVTCWEC2         GET COMWRITE ECB ADDRESS             68100020
STAQC    ST    R1,AQPARM           STORE IN AQCTL  PARM                 68400020
         MVI   AQPARM,X'20'        SET NORMAL FLAGS                     68700020
         MVC   PARMAREA(4),4(R5)        MOVE IN START OF AREA TO OUTPUT 69000020
         MVI   PARMFLAG,X'00'      CLEAR FLAG                           69300020
         LA    R1,AQPARM                                                69600020
         AQCTL                     ISSUE POST FOR COMWRITE              69900020
         CLC   4(8,R5),FIRONE      COMPARE TO SEE WHICH TABLE           70200020
*                                  IS BEING USED NOW                    70500020
         BE    SECOND              USE SECOND TABLE                     70800020
         MVC   0(12,R5),CURONE     MOVE POINTERS TO                     71100020
*                                  USE FIRST TABLE                      71400020
         B     RETURN2                                                  71700020
SECOND   MVC   0(12,R5),CURTWO     SET UP TO USE TABLE 2                72000020
         B     RETURN2                                                  72300020
RAP1     L     R3,4(R5)            LOAD 1ST ENTRY                       72600020
         LA    R3,16(R3)           UPDATE FOR SPACE IN FRONT OF TABLE   72900020
         ST    R3,0(R5)                 STORE 1ST TRACE ADDRESS         73200020
*                                       TO BE USED IN TABLE             73500020
RETURN2  LM    R0,R15,SAVE                                              73800020
         BR    R14                      RETURN TO DISPATCHER            74100020
RESTORE  MVC   0(12,R5),OLDPTR     RESTORE TRACE POINTERS               75100020
         BR    R14                 RETURN                               76400020
SAVE     DS    18F                                                      79100020
SAVER    DS    18F                                                      79600020
OLDPTR   DC    3F'0'               TRACE POINTER SAVE AREA              80100020
LESFOUR  DC    CL29'IED110I  LESS THAN 4 ENTRIES    '                   80700020
NOTRMSG  DC    CL29'IED111I  NO TRACE TABLE      '                      81000020
TRACECNT DC    AL1(0)                   TRACE SEQUENCE COUNTER          81300020
CURONE   DC    F'0'                CURRENT FIRST TABLE                  81600020
FIRONE   DC    F'0'                FIRST   FIRST TABLE                  81900020
LASONE   DC    F'0'                LAST   FIRST TABLE                   82200020
CURTWO   DC    F'0'                CURRENT SECOND TABLE                 82800020
FIRTWO   DC    F'0'                FIRST   SECOND TABLE                 83100020
LASTWO   DC    F'0'                LAST   SECOND TABLE                  83400020
AQPARM   DC    X'20',AL3(0)        PARMLIST USED BY SVC AQCTL           84000020
         DC    X'80'                                                    84300020
         DC    AL3(TJID)                                                84600020
COMWRTCB DC    F'0'                                                     84700020
TJID     DC    X'0000'                                                  84900020
         DS    0F                                                       85200020
PARMAREA DC    F'0'                PARM LIST PASSED TO                  85500020
PARMTS   DC    X'00'               COMWRITE                             85800020
PARMFLAG DC    X'40'               COMPLETE FLAG ON                     86100020
PARMLN   DC    X'0000'                                                  86400020
PARMCT   DC    F'0'                                                     86700020
PARMID   DC    CL4'STCB'           ID FOR STCB TRACE                    87000020
FOUR     DC    X'00000004'                                              87300020
         TAVTD                                                          87900020
         END                                                            88200020
