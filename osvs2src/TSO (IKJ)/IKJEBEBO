BEBO     TITLE 'EDIT SUBCOMMAND - BOTTOM - IKJEBEBO'                    00500020
*********************************************************************** 01000020
* STATUS-  VERSION NO. 01, OS/360 RELEASE NO. 20                      * 01500020
*                                                                     * 02000020
* FUNCTION-  IKJEBEBO IS AN EDIT SUBCOMMAND WHICH IS USED TO MOVE THE * 02200020
*    POSITION OF THE CURRENT LINE POINTER '*' TO THE LAST LINE OF THE * 02400020
*    DATA SET BEING EDITED.                                           * 02600020
*                                                                     * 04000020
* ENTRY POINTS-  IKJEBEBO IS THE MAIN AND ONLY ENTRY POINT.           * 04500020
*                                                                     * 05000020
* INPUT-  REGISTER 1 CONTAINS A POINTER TO EDIT'S COMMUNICATION AREA. * 05300020
*    THE LAST RECORD OF THE UTILITY DATA SET IS READ.                 * 05600020
*                                                                     * 06500020
* OUTPUT-  A RETURN CODE IN REGISTER 15. POSSIBLE RETURN CODES ARE-   * 06700020
*    0 - OPERATION WAS SUCCESSFUL                                     * 06900020
*    12 - I/O ERROR                                                   * 07100020
*                                                                     * 08200020
* EXTERNAL REFERENCES-                                                * 08280020
*         IKJEBEMS     -ISSUES APPROPRIATE MESSAGES TO TERMINAL USER  * 08360020
*         IKJEBEUT     -READS UTILITY DATA SET                        * 08440020
*    MACROS USED-                                                     * 08520020
*         IKJEBESH     -PROVIDES LINKAGE FOR EDIT SERVICE ROUTINES    * 08600020
*         IKJEBECA     -LAYOUT AND DESCRIPTION OF COMMUNICATION AREA  * 08680020
*         IKJEBESV     -STANDARD ENTRY LINKAGE AND ADDRESSABILITY     * 08760020
*         IKJEBERT     -STANDARD EXIT LINKAGE                         * 08840020
*         IKJEBEML     -STANDARD MESSAGE TO TERMINAL USER             * 08920020
*                                                                     * 14500020
* EXITS,NORMAL-  STANDARD EXIT TO CONTROLLER WITH RETURN CODE = 0 IN  * 14800020
*    REGISTER 15                                                      * 15100020
*                                                                     * 16000020
* EXITS,ERROR- STANDARD EXIT TO CONTROLLER WITH RETURN CODE = 12 IN   * 16300020
*    REGISTER 15                                                      * 16600020
*                                                                     * 17500020
* TABLES/WORK AREAS-  THE EDIT SUBCOMMAND WORK AREA IN THE COMMUNICA- * 17700020
*    TION AREA IS USED AND THE LAST RECORD OF THE UIILITY DATA SET IS * 17900020
*    READ IN TO THE TEMPORARY BUFFER POOL IN THE COMMUNICATION AREA   * 18100020
*    STARTING AT LOCATION CATEMPBF+12.                                * 18300020
*                                                                     * 20000020
* ATTRIBUTES-  REFRESHABLE, ENABLED, NON-PRIVILEGED                   * 20500020
*                                                                     * 21000020
* CHARACTER CODE DEPENDENCY-  NONE                                    * 21100020
*                                                                     * 21300020
* NOTES-  STANDARD LINKAGE CONVENTIONS USED                           * 21400020
*********************************************************************** 21500020
         EJECT                                                          22000020
IKJEBEBO CSECT                                                          22500020
         SPACE 2                                                        23000020
*********************************************************************** 23500020
*    EQUATES OF SYMBOLIC REGISTERS                                      24000020
*********************************************************************** 24500020
PARMREG0 EQU   0                       PARAMETER POINTER                25000020
PARMREG1 EQU   1                       PARAMETER POINTER                25500020
TESTREG  EQU   2                       WORK REGISTER                    26000020
BINREG   EQU   3                       WORK REGISTER                    26500020
WORKREG  EQU   5                       WORK REGISTER                    27000020
UTREG    EQU   8                       WORK REGISTER                    27500020
COMMREG  EQU   9                       COMMUNICATION AREA ADDRESSING    28000020
BASEREG  EQU   11                      BASE REGISTER                    28500020
DATAREG  EQU   12                      WORK REGISTER                    29000020
SAVEREG  EQU   13                      SAVE AREA POINTER                29500020
RETREG   EQU   14                      RETURN TO CALLER                 30000020
RETCDREG EQU   15                      RETURN CODE                      30500020
         SPACE 2                                                        31000020
*********************************************************************** 31500020
*    EQUATES OF SYMBOLIC CONSTANTS                                      32000020
*********************************************************************** 32500020
BM0      EQU   0                       CONSTANT OF 0                    33000020
BM3      EQU   3                       CONSTANT OF 3                    33500020
BM4      EQU   4                       CONSTANT OF 4                    34000020
BM8      EQU   8                       CONSTANT OF 8                    34500020
BM9      EQU   9                       CONSTANT OF 9                    35000020
BM12     EQU   12                      CONSTANT OF 12                   35500020
IOERROR  EQU   12                      IOERROR RET. CODE                36000020
LAST     EQU   X'05'                   READ LAST RECORD CODE            36500020
END      EQU   X'80'                   END OF PARAMETER LIST CODE       37000020
         SPACE 2                                                        37500020
*********************************************************************** 38000020
*    MESSAGE REFERENCES                                                 38500020
*********************************************************************** 39000020
         IKJEBEMI (501)                                                 39500020
         SPACE 2                                                        40000020
*********************************************************************** 40500020
*    STANDARD ENTRY LINKAGE AND ESTABLISHMENT OF ADDRESSABILITY IN      41000020
*    THIS ROUTINE AND IN THE COMMUNICATION AREA                         41500020
*********************************************************************** 42000020
         IKJEBESV (14,12),T,*          SAVE CALLER'S REGISTERS AND      42500020
*                                      ESTABLISH ADDRESSABILITY         43000020
         USING IKJEBECA,COMMREG        ADDRESSABILITY IN COMM. AREA     43500020
         SPACE 2                                                        44000020
*********************************************************************** 44500020
*    CALL IKJEBEUT THROUGH IKJEBESH TO READ LAST RECORD OF UTILITY      45000020
*    DATA SET                                                           45500020
*********************************************************************** 46000020
         MVC   COWORD1(BM4),CAPTCDCB   PUT ADDRESS OF CURRENT UTILITY   46500020
*                                      DCB IN THE 3 LOW ORDER BYTES OF  47000020
*                                      1ST PARAM WORD                   47500020
         MVI   COWORD1,LAST            PUT READ LAST RECORD CODE IN     48000020
*                                      THE HIGH ORDER BYTE OF 1ST       48500020
*                                      PARAM WORD                       49000020
         XC    COWORD2(BM4),COWORD2    2ND PARAM WORD DOES NOT CONTAIN  49500020
*                                      ANY POINTER                      50000020
         LA    TESTREG,CATEMPBF+BM12                                    50500020
         ST    TESTREG,COWORD3         PUT POINTER TO TEMP. BUFFER      51000020
*                                      POOL IN COMM. AREA WHERE THE     51500020
*                                      RECORD IS TO BE PLACED IN 3      52000020
*                                      LOW ORDER BYTES OF 3RD PARAM.    52500020
*                                      WORD                             53000020
         MVI   COWORD3,END             PUT END OF PARAMETER LIST        53500020
*                                      CODE IN HIGH ORDER BYTE OF       54000020
*                                      3RD PARAM. WORD                  54500020
         LA    UTREG,COWORD1           LET UTREG POINT TO PARMLIST      55000020
         IKJEBESH (COMMREG),IKJEBEUT,PARAM=((COMMREG),(UTREG)),MF=(E,A) 55500020
         SPACE 2                                                        56000020
*********************************************************************** 56500020
*    CHECK RETURN CODE FROM IKJEBEUT AFTER READING LAST RECORD          57000020
*********************************************************************** 57500020
         LTR   RETCDREG,RETCDREG       TEST RET. CODES FROM IKJEBEUT    58000020
         BZ    SET                     BRANCH IF ZERO TO SET            58500020
         C     RETCDREG,TESTEMP        COMPARE ERETURN CODE WITH 4      59000020
         BZ    EMPTY                   RC=4 THEN GO TO EMPTY            59500020
         B     IOERR                   RC=8 THEN GO TO IOERR            60000020
         SPACE 2                                                        60500020
*********************************************************************** 61000020
*    '*' VALUE IS SET TO KEY OF LAST RECORD AND 'THE LINE TO BE         61500020
*    VERIFIED SWITCH' IS TURNED ON                                      62000020
*********************************************************************** 62500020
SET      MVC   CACURNUM(BM4),CATEMPBF+BM12  PUT KEY OF LAST RECORD IN   63000020
*                                      CURRENT LINE NUMBER '*'          63500020
         OI    CACFLAG1,CALNTOVF       TURN 'THE LINE TO BE VERIFIED    64000020
*                                      SWITCH' ON                       64500020
         SPACE 2                                                        65000020
*********************************************************************** 65500020
*    STANDARD EXIT LINKAGE TO CONTROLLER                                66000020
*********************************************************************** 66500020
RETURN   SR    RETCDREG,RETCDREG       SET RETURN CODE TO 0             67000020
EXIT     EQU   *                                                        67500020
         IKJEBERT (14,12),T,RC=(15)    RELOAD CALLER'S REGISTERS AND    68000020
*                                      RETURN CONTROL TO CALLER         68500020
         SPACE 2                                                        69000020
*********************************************************************** 69500020
*    UTILITY DATA SET IS EMPTY                                          70000020
*********************************************************************** 70500020
EMPTY    MVC   B(BM12),INLIST                                           71000020
         SPACE 2                                                        71500020
*********************************************************************** 72000020
*    MESSAGE- 'NO LINES IN DATA SET' IS ISSUED HERE                     72500020
*********************************************************************** 73000020
ISSUE    IKJEBEML M501,MF=(E,B)                                         73500020
NORMAL   XC    CACURNUM(BM4),CACURNUM  SET CURRENT LINE VALUE '*' TO 0  74000020
         B     RETURN                  BRANCH UNCOND. TO RETURN         74500020
         SPACE 2                                                        75000020
*********************************************************************** 75500020
*    I/O ERROR IN READING UTILITY DATA SET                              76000020
*********************************************************************** 76500020
IOERR    LA    RETCDREG,IOERROR                                         77000020
         B     EXIT                    BRANCH UNCOND. TO EXIT TO        77500020
*                                      CONTROLLER                       78000020
         SPACE 3                                                        78500020
*********************************************************************** 79000020
*    CONSTANT AREA FOR IKJEBEBO                                         79500020
*********************************************************************** 80000020
TESTEMP  DC    F'4'                    FULL WORD OF 4                   80500020
INLIST   IKJEBEML M501,MF=L                                             81000020
         SPACE 2                                                        81500020
         IKJEBECA                                                       82000020
         SPACE 2                                                        82500020
         ORG   CASCWKA                 EDIT SUBCOMMAND WORK AREA        83000020
A        DS    5F                      5 FULLWORDS FOR IKJEBESH MACRO   83500020
B        DS    10F                     10 FULLWORDS FOR IKJEBEML MACRO  84000020
COWORD1  DS    F                       1ST PARAM WORD FOR UT            84500020
COWORD2  DS    F                       2ND PARAM WORD FOR UT            85000020
COWORD3  DS    F                       3RD PARAM WORD FOR UT            85500020
         END                                                            86000020
