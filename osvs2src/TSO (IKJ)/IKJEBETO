BETO     TITLE 'EDIT SUBCOMMAND - TOP - IKJEBETO'                       00400020
*********************************************************************** 00800020
* STATUS-  VERSION NO. 01, OS/360 RELEASE NO. 20                      * 01200020
*                                                                     * 01600020
* FUNCTION-  IKJEBETO IS AN EDIT SUBCOMMAND WHICH IS USED TO MOVE THE * 01700020
*    POSITION OF THE CURRENT LINE POINTER '*' TO THE VERY BEGINNING OF* 01800020
*    THE DATA SET.  THAT IS, '*' TAKES ON A POSITION BEFORE THE FIRST * 01900020
*    RECORD OF A NON-NUMBERED DATA SET AND TAKES ON THE VALUE OF LINE * 02000020
*    NUMBER ZERO FOR A LINE-NUMBERED DATA SET.                        * 02100020
*                                                                     * 04000020
* ENTRY POINTS-  IKJEBETO IS THE MAIN AND ONLY ENTRY POINT            * 04400020
*                                                                     * 04800020
* INPUT-  REGISTER 1 CONTAINS A POINTER TO EDIT'S COMMUNICATION AREA. * 05000020
*    THE FIRST RECORD OF THE UTILITY DATA SET IS READ.                * 05200020
*                                                                     * 06000020
* OUTPUT-  A RETURN CODE IN REGISTER 15. POSSIBLE RETURN CODES ARE-   * 06200020
*    0 - OPERATION WAS SUCCESSFUL                                     * 06400020
*    12 - I/O ERROR                                                   * 06600020
*                                                                     * 07400020
* EXTERNAL REFERENCES-                                                * 07460020
*         IKJEBEMS     -ISSUES APPROPRIATE MESSAGES TO TERMINAL USER  * 07520020
*         IKJEBEUT     -READS UTILITY DATA SET                        * 07580020
*    MACROS USED-                                                     * 07640020
*         IKJEBESH     -PROVIDES LINKAGE FOR EDIT SERVICE ROUTINES    * 07700020
*         IKJEBECA     -LAYOUT AND DESCRIPTION OF COMMUNICATION AREA  * 07760020
*         IKJEBESV     -STANDARD ENTRY LINKAGE AND ADDRESSABILITY     * 07820020
*         IKJEBERT     -STANDARD EXIT LINKAGE                         * 07880020
*         IKJEBEML     -STANDARD MESSAGE TO TERMINAL USER             * 07940020
*                                                                     * 12400020
* EXITS,NORMAL-  STANDARD EXIT TO CONTROLLER WITH RETURN CODE = 0 IN  * 12600020
*    REGISTER 15                                                      * 12800020
*                                                                     * 13600020
* EXITS,ERROR-  STANDARD EXIT TO CONTROLLER WITH RETURN CODE = 12 IN  * 13800020
*    REGISTER 15                                                      * 14000020
*                                                                     * 14800020
* TABLES/WORK AREAS-  THE EDIT SUBCOMMAND WORK AREA IN THE EDIT       * 14900020
*    COMMUNICATION AREA IS USED AND THE FIRST RECORD OF THE UTILITY   * 15000020
*    DATA SET IS READ INTO THE TEMPORARY BUFFER POOL IN THE COMMUNICA-* 15100020
*    TION AREA STARTING AT LOCATION CATEMPBF+12.                      * 15200020
*                                                                     * 16800020
* ATTRIBUTES-  REFRESHABLE, ENABLED, NON-PRIVILEGED.                  * 17200020
*                                                                     * 17600020
* CHARACTER CODE DEPENDENCY-  THIS SUBCOMMAND IS CHARACTER CODE       * 17660020
*    DEPENDENT AND HAS TO BE REASSEMBLED WHEN CHARACTER CODE CHANGES  * 17720020
*                                                                     * 17860020
* NOTES-  STANDARD LINKAGE CONVENTIONS USED                           * 17930020
*********************************************************************** 18000020
         EJECT                                                          18400020
IKJEBETO CSECT                                                          18800020
*                                                                       18801001
*A512500-513500                                                 YA00043 18802001
*C320000                                                        YA00043 18803001
*D516000-612000,652000-680000,776000-780000,788000-792000       YA00043 18804001
         SPACE 2                                                        19200020
*********************************************************************** 19600020
*    EQUATES OF SYMBOLIC REGISTERS                                      20000020
*********************************************************************** 20400020
PARMREG0 EQU   0                       PARAMETER POINTER                20800020
PARMREG1 EQU   1                       PARAMETER POINTER                21200020
TESTREG  EQU   2                       WORK REGISTER                    21600020
BINREG   EQU   3                       WORK REGISTER                    22000020
WORKREG  EQU   5                       WORK REGISTER                    22400020
UTREG    EQU   8                       WORK REGISTER                    22800020
COMMREG  EQU   9                       COMMUNICATION AREA ADDRESSING    23200020
BASEREG  EQU   11                      BASE REGISTER                    23600020
DATAREG  EQU   12                      WORK REGISTER                    24000020
SAVEREG  EQU   13                      SAVE AREA POINTER                24400020
RETREG   EQU   14                      RETURN TO CALLER                 24800020
RETCDREG EQU   15                      RETURN CODE                      25200020
         SPACE 2                                                        25600020
*********************************************************************** 26000020
*    EQUATES OF SYMBOLIC CONSTANTS                                      26400020
*********************************************************************** 26800020
TM0      EQU   0                       CONSTANT OF ZERO                 27200020
TM3      EQU   3                       CONSTANT OF 3                    27600020
TM4      EQU   4                       CONSTANT OF 4                    28000020
TM8      EQU   8                       CONSTANT OF 8                    28400020
TM12     EQU   12                      CONSTANT OF 12                   28800020
FIRST    EQU   X'04'                   READ 1ST RECORD CODE             29200020
END      EQU   X'80'                   END OF PARMLIST CODE             29600020
IOERROR  EQU   12                      IOERROR RET. CODE                30000020
         SPACE 2                                                        30400020
*********************************************************************** 30800020
*    MESSAGE REFERENCES                                                 31200020
*********************************************************************** 31600020
         IKJEBEMI (501)                                         YA00043 32000001
         SPACE 2                                                        32400020
*********************************************************************** 32800020
*    STANDARD ENTRY LINKAGE AND ESTABLISHMENT OF ADDRESSABILITY IN      33200020
*    THIS ROUTINE AND IN THE COMMUNICATION AREA                         33600020
*********************************************************************** 34000020
         IKJEBESV (14,12),T,*          SAVE CALLER'S REGS. AND          34400020
*                                      ESTABLISH ADDRESSABILITY         34800020
         USING IKJEBECA,COMMREG        ADDRESSABILITY IN COMM. AREA     35200020
         SPACE 2                                                        35600020
*********************************************************************** 36000020
*    CALL IKJEBEUT THROUGH IKJEBESH TO READ FIRST RECORD OF UTILITY     36400020
*    DATA SET                                                           36800020
*********************************************************************** 37200020
         MVC   COWORD1(TM4),CAPTCDCB   PUT ADDRESS OF CURRENT UTILITY   37600020
*                                      DCB IN THE 3 LOW ORDER BYTES OF  38000020
*                                      1ST PARAM. WORD                  38400020
         MVI   COWORD1,FIRST           PUT X'04' IN THE HIGH ORDER      38800020
*                                      BYTE OF 1ST PARAM. WORD          39200020
         XC    COWORD2(TM4),COWORD2    2ND PARAM. WORD DOES NOT         39600020
*                                      CONTAIN ANY POINTER              40000020
         LA    TESTREG,CATEMPBF+TM12                                    40400020
         ST    TESTREG,COWORD3         PUT POINTER TO TEMP. BUFFER      40800020
*                                      POOL IN COMM. AREA WHERE THE     41200020
*                                      RECORD IS TO BE PLACED IN 3      41600020
*                                      LOW ORDER BYTES OF 3RD PARAM.    42000020
*                                      WORD                             42400020
         MVI   COWORD3,END             PUT END OF PARAMETER LIST        42800020
*                                      CODE IN HIGH ORDER BYTE OF       43200020
*                                      3RD PARAM. WORD                  43600020
         LA    UTREG,COWORD1           LET UTREG PT.TO PARMLIST         44000020
         IKJEBESH (COMMREG),IKJEBEUT,PARAM=((COMMREG),(UTREG)),MF=(E,A) 44400020
*                                  SERVICE RTN. HANDLER MACRO           44800020
         SPACE 2                                                        45200020
*********************************************************************** 45600020
*    CHECK RETURN CODE FROM IKJEBEUT AFTER READING FIRST RECORD         46000020
*********************************************************************** 46400020
         LTR   RETCDREG,RETCDREG       TEST RET. CODES FROM IKJEBEUT    46800020
         BZ    SET                     BRANCH IF ZERO TO SET            47200020
         C     RETCDREG,TESTEMP        COMPARE RETURN CODE WITH 4       47600020
         BZ    EMPTY                   RC=4 THEN GO TO EMPTY            48000020
         B     IOERR                   RC=8 THEN GO TO IOERR            48400020
         SPACE 2                                                        48800020
*********************************************************************** 49200020
*    '*' VALUE IS SET TO ZERO                                           49600020
*********************************************************************** 50000020
SET      XC    CACURNUM(TM4),CACURNUM  SET CURRENT LINE VALUE '*' TO 0  50400020
         SR    BINREG,BINREG                                            50800020
         C     BINREG,CATEMPBF+TM12    TEST WHETHER THERE IS RECORD 0   51200020
         BNE   RETURN                  NO, VERIFICATION         YA00043 51250001
*                                      UNNECESSARY              YA00043 51260001
         OI    CACFLAG1,CALNTOVF       YES, SPECIFY LINE TO BE  YA00043 51300001
*                                      VERIFIED                 YA00043 51350001
         SPACE 2                                                        61600020
*********************************************************************** 62000020
*    STANDARD EXIT LINKAGE TO CONTROLLER                                62400020
*********************************************************************** 62800020
RETURN   SR    RETCDREG,RETCDREG       SET RETURN CODE TO 0             63200020
EXIT     EQU   *                                                        63600020
         IKJEBERT (14,12),T,RC=(15)    RELOAD CALLER'S REGISTERS AND    64000020
*                                      RETURN CONTROL TO CALLER         64400020
         SPACE 2                                                        64800020
*********************************************************************** 68400020
*    UTILITY DATA SET IS EMPTY                                          68800020
*********************************************************************** 69200020
EMPTY    MVC   B(TM12),INLIST                                           69600020
         SPACE 2                                                        70000020
*********************************************************************** 70400020
*    MESSAGE- 'NO LINES IN DATA SET' IS ISSUED HERE                     70800020
*********************************************************************** 71200020
ISSUE    IKJEBEML M501,MF=(E,B)                                         71600020
NORMAL   XC    CACURNUM(TM4),CACURNUM  SET CURRENT LINE VALUE '*' TO 0  72000020
         B     RETURN                  BRANCH UNCOND. TO RETURN         72400020
         SPACE 2                                                        72800020
*********************************************************************** 73200020
*    I/O ERROR IN READING UTILITY DATA SET                              73600020
*********************************************************************** 74000020
IOERR    LA    RETCDREG,IOERROR        SET RET. CODE TO 12              74400020
         B     EXIT                    BRANCH UNCOND. TO EXIT TO        74800020
*                                      CONTROLLER                       75200020
         SPACE 3                                                        75600020
*********************************************************************** 76000020
*    CONSTANT AREA FOR IKJEBETO                                         76400020
*********************************************************************** 76800020
TESTEMP  DC    F'4'                    FULL WORD OF 4                   77200020
INLIST   IKJEBEML M501,MF=L                                             78400020
         SPACE 2                                                        79600020
         IKJEBECA                                                       80000020
         SPACE 2                                                        80400020
         ORG   CASCWKA                 EDIT SUBCOMMAND WORK AREA        80800020
A        DS    5F                      5 FULLWORDS FOR IKJEBESH MACRO   81200020
B        DS    10F                     10 FULLWORDS FOR IKJEBEML MACRO  81600020
COWORD1  DS    F                       1ST PARAM WORD FOR UT            82000020
COWORD2  DS    F                       2ND PARAM WORD FOR UT            82400020
COWORD3  DS    F                       3RD PARAM WORD FOR UT            82800020
         END                                                            83200020
