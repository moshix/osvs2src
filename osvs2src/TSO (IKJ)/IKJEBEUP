BEUP     TITLE 'EDIT SUBCOMMAND - UP - IKJEBEUP'                        00200020
*********************************************************************** 00400020
* STATUS-  VERSION NO. 01, OS/360 RELEASE NO. 20                      * 00600020
*                                                                     * 00800020
* FUNCTION-  IKJEBEUP IS AN EDIT SUBCOMMAND WHICH IS USED TO CAUSE THE* 00830020
*    CURRENT LINE POINTER '*' TO BE MOVED TOWARD THE BEGINNING OF THE * 00860020
*    DATA SET.  IF THE OPERATION OF THIS SUBCOMMAND CAUSES THE        * 00890020
*    CURRENT LINE POINTER TO REACH THE FIRST RECORD OF THE DATA SET   * 00920020
*    OR IF THIS SUBCOMMAND IS ISSUED WITH THE '*' VALUE ALREADY       * 00950020
*    POINTING AT THE FIRST LINE OF THE DATA SET, THE USER WILL BE     * 00980020
*    NOTIFIED. IF DURING THE PROCESS OF READING THE UTILITY DATA SET, * 01010020
*    ATTENTION IS ISSUED BY THE TERMINAL USER, CONTROL WILL BE        * 01040020
*    RETURNED TO THE CONTROLLER IMMEDIATELY WITH A RETURN CODE OF     * 01070020
*    ZERO IN REGISTER 15.                                             * 01100020
*                                                                     * 03000020
* ENTRY POINTS-  IKJEBEUP IS THE MAIN AND ONLY ENTRY POINT            * 03060020
*                                                                     * 03120020
* INPUT-  REGISTER 1 CONTAINS A POINTER TO EDIT'S COMMUNICATION AREA. * 03210020
*    A PART OF OR THE WHOLE UTILITY DATA SET IS READ AS REQUIRED.     * 03300020
*                                                                     * 03600020
* OUTPUT-  A RETURN CODE IN REGISTER 15. POSSIBLE RETURN CODES ARE-   * 03700020
*    0 - OPERATION WAS SUCCESSFUL.                                    * 03800020
*    8 - STACK NEEDS FLUSHING                                         * 03850020
*    12 - I/O ERROR                                                   * 03900020
*                                                                     * 04400020
* EXTERNAL REFERENCES-                                                * 04410020
*         IKJPARS      -SCANS OPERAND                                 * 04420020
*         IKJEBEUT     -READS UTILITY DATA SET                        * 04430020
*         IKJEBEMS     -MESSAGES TO USER AND LINE VERIFICATION        * 04440020
*    EDIT MACROS USED-                                                * 04450020
*         IKJEBESH     -PROVIDES LINKAGE TO EDIT SERVICE ROUTINES     * 04460020
*         IKJEBEML     -STANDARD MESSAGE TO USER                      * 04470020
*         IKJEBEMG     -MESSAGE INSERTION                             * 04480020
*         IKJEBECA     -LAYOUT AND DESCRIPTION OF COMMUNICATION AREA  * 04490020
*         IKJEBESV     -STANDARD ENTRY LINKAGE AND ADDRESSABILITY     * 04500020
*         IKJEBERT     -STANDARD EXIT LINKAGE                         * 04510020
*    SYSTEM MACROS USED-                                              * 04520020
*         LINK         -CALLS IKJPARS                                 * 04530020
*         IKJPARM      -GENERATE PCL CSECT                            * 04540020
*         IKJIDENT     -GENERATE COUNT FIELD IN PCL                   * 04550020
*         IKJENDP      -CLOSE PCL CSECT                               * 04560020
*         IKJRLSA      -RELEASE PDL CORE                              * 04570020
*         SAVE         -SAVE PARSE'S REGISTERS                        * 04580020
*         RETURN       -RETURN TO PARSE                               * 04590020
*         IKJPPL       -PARSE PARAMETER LIST                          * 04600020
*                                                                     * 06800020
* EXITS,NORMAL-  STANDARD EXIT TO THE CONTROLLER WITH RETURN CODE = 0 * 06900020
*    IN REGISTER 15.                                                  * 07000020
*                                                                     * 07400020
* EXITS,ERROR-  STANDARD EXIT TO THE CONTROLLER WITH RETURN CODE = 12 * 07500020
*    OR 8 IN REGISTER 15.                                             * 07600020
*                                                                     * 08000020
* TABLES/WORK AREAS-  THE EDIT SUBCOMMAND WORK AREA IN THE COMMUNCA-  * 08100020
*    TION AREA IS USED                                                * 08200020
*                                                                     * 08600020
* ATTRIBUTES-  REFRESHABLE, ENABLED, NON-PRIVILEGED                   * 08800020
*                                                                     * 09000020
* CHARACTER CODE DEPENDENCY-  THIS SUBCOMMAND IS CHARACTER CODE       * 09100020
*    DEPENDENT AND HAS TO BE REASSEMBLED WHEN CHARACTER SET CODE      * 09200020
*    CHANGES.                                                         * 09300020
*                                                                     * 09800020
* NOTES-  STANDARD LINKAGE CONVENTIONS ARE USED                       * 09900020
*********************************************************************** 10000020
IKJEBEUP CSECT                                                          10200020
         SPACE 2                                                        10400020
*********************************************************************** 10600020
*    EQUATES OF SYMBOLIC REGISTERS                                      10800020
*********************************************************************** 11000020
PARMREG0 EQU   0                       PARAMETER POINTER                11200020
PARMREG1 EQU   1                       PARAMETER POINTER                11400020
TESTREG  EQU   2                       WORK REGISTER                    11600020
BINREG   EQU   3                       WORK REGISTER                    11800020
ADDREG   EQU   4                       WORK REGISTER                    12000020
DCBREG   EQU   5                       WORK REGISTER                    12200020
WORKREG  EQU   6                       WORK REGISTER                    12400020
PDLREG   EQU   7                       POINTER TO PDL                   12600020
UTREG    EQU   8                       PARAMETERS FOR IKJEBEUT POINTER  12800020
COMMREG  EQU   9                       COMMUNICATION AREA ADDRESSING    13000020
TEMPREG  EQU   10                      WORK REGISTER                    13200020
BASEREG  EQU   11                      BASE REGISTER                    13400020
DATAREG  EQU   12                      WORK REGISTER                    13600020
SAVEREG  EQU   13                      SAVE AREA REGISTER               13800020
RETREG   EQU   14                      RETURN TO CALLER                 14000020
RETCDREG EQU   15                      RETURN CODE                      14200020
         SPACE 2                                                        14400020
*********************************************************************** 14600020
*    EQUATES OF SYMBOLIC CONSTANTS                                      14800020
*********************************************************************** 15000020
UP0      EQU   0                       CONSTANT OF 0                    15200020
UP1      EQU   1                       CONSTANT OF 1                    15400020
UP3      EQU   3                       CONSTANT OF 3                    15600020
UP4      EQU   4                       CONSTANT OF 4                    15800020
UP8      EQU   8                       CONSTANT OF 8                    16000020
UP12     EQU   12                      CONSTANT OF 12                   16200020
UP16     EQU   16                      CONSTANT OF 16                   16400020
END      EQU   X'80'                   END OF PARAMETER LIST CODE       16600020
FIRST    EQU   X'04'                   READ 1ST RECORD CODE             16800020
PREV     EQU   X'01'                   READ PREVIOUS RECORD CODE        17000020
SW1      EQU   X'80'                                                    17200020
ALL      EQU   X'FF'                                                    17400020
         SPACE 2                                                        17600020
*********************************************************************** 17800020
*    MESSAGE REFERENCES                                                 18000020
*********************************************************************** 18200020
         IKJEBEMI (312,313,501,505)                                     18400020
         EJECT                                                          18600020
*********************************************************************** 18800020
*    STANDARD ENTRY LINKAGE AND ESTABLISHMENT OF ADDRESSABILITY BOTH    19000020
*    IN THIS ROUTINE AND IN THE COMMUNICATION AREA                      19200020
*********************************************************************** 19400020
         IKJEBESV (14,12),T,*                                           19600020
         LR    COMMREG,PARMREG1                                         19800020
         USING IKJEBECA,COMMREG                                         20000020
         MVI   SW,UP0                  CLEAR SWITCH                     20100020
         SPACE 2                                                        20200020
*********************************************************************** 20400020
*    OPERAND TEST                                                       20600020
*********************************************************************** 20800020
         TM    CAPTIBFR,CAOPERND       TEST WHETHER THERE OPERANDS      21000020
         BNO   DEFAULT                 NO OPERAND, THEN GO TO DEFAULT   21200020
         SPACE 2                                                        21400020
*********************************************************************** 21600020
*    CALL IKJPARSE TO SCAN OPERAND                                      21800020
*********************************************************************** 22000020
OPERAND  LA    PARMREG1,CATMPLST       LET REG. 1 PT. TO TMP SERVICE    22200020
*                                      RTN. PARAMETER LIST              22400020
         MVC   CATMPLST+PPLPCL-PPL(UP4),ADUPPCL  PUT POINTER TO PCL IN  22600020
*                                                PPLPCL                 22800020
         LA    TESTREG,CAPTPRSD                                         23000020
         ST    TESTREG,CATMPLST+PPLANS-PPL  PUT POINTER TO ANS. PLACE   23200020
*                                           IN PPLANS                   23400020
         MVC   CATMPLST+PPLCBUF-PPL(UP4),CAPTIBFR  PUT ADDRESS OF       23600020
*                                      INPUT BUFFER IN PPLCBUF          23800020
         ST    COMMREG,CATMPLST+PPLUWA-PPL  PUT POINTER TO COMM. AREA   24000020
*                                           IN PPLVWA                   24200020
         LINK  EP=IKJPARS                                               24400020
         SPACE 2                                                        24600020
*********************************************************************** 24800020
*    CHECK RETURN CODES FROM PARSE SERVICE ROUTINE AND ESTABLISH        25000020
*    ADDRESSABILITY IN PDL DSECT                                        25200020
*********************************************************************** 25400020
         L     PDLREG,CAPTPRSD         PUT PTR. TO PDL IN PDLREG        25600020
         USING UPPDL,PDLREG            ADDRESSABILITY IN PDL            25800020
         LTR   RETCDREG,RETCDREG       CHECK RET. CODE FROM PARSE       26000020
         BZ    CONV                    IF SUCCESSFUL COMPLETION THEN GO 26200020
*                                      TO CONV                          26400020
         C     RETCDREG,UPDC4          TEST WHETHER R.C.=4              26600020
         BE    FLUSH                   R.C.=4 THEN RET. TO FLUSH        26800020
         C     RETCDREG,UPDC8          TEST WHETHER R.C.=8              27000020
         BE    NORMAL                  R.C.=8 THEN RET. TO CONTROLLER   27200020
         C     RETCDREG,UPDC12         TEST WHETHER R.C.=12             27400020
         BE    MSG5                    R.C.=12 THEN GO TO ISSUE MSG5    27600020
         C     RETCDREG,UPDC16         TEST WHETHER R.C.=16             27800020
         BE    MESSAGE3                R.C.=16 THEN GO TO ISSUE MSG3    28000020
         SPACE 2                                                        28200020
*********************************************************************** 28400020
*    CONVERSION OF OPERAND 'COUNT' TO BINARY                            28600020
*********************************************************************** 28800020
CONV     L     TESTREG,COUNT           LET TESTREG CONT. ADDRESS OF     29000020
*                                      OPERAND                          29200020
         LH    ADDREG,COUNT+UP4        PUT LENGTH OF OPERAND IN 2       29400020
*                                      LOW ORDER BYTES OF ADDREG        29600020
         BCTR  ADDREG,PARMREG0         LENGTH OF OPERAND - 1            29800020
         EX    ADDREG,PACKED           CONVERT OPERAND FROM ZONED       29900020
*                                      DECIMAL TO PACKED DECIMAL        30000020
         CVB   BINREG,COPACKED                                          30200020
         SPACE 2                                                        30400020
*********************************************************************** 30600020
*    CALL IKJEBEUT THROUGH IKJEBESH TO READ 1ST RECORD OF UTILITY       30800020
*    DATA SET                                                           31000020
*********************************************************************** 31200020
READFRST MVC   COWORD1(UP4),CAPTCDCB   PUT ADDRESS OF UTILITY DCB IN 3  31400020
*                                      LOW ORDER BYTES OF 1ST           31600020
*                                      PARAM WORD                       31800020
         MVI   COWORD1,FIRST           PUT READ 1ST RECORD CODE IN      32000020
*                                      HIGH ORDER BYTE OF 1ST PARAM     32200020
*                                      WORD                             32400020
         XC    COWORD2(UP4),COWORD2    NO POINTER IS PUT IN 2ND PARAM   32600020
*                                      WORD                             32800020
         LA    TESTREG,CATEMPBF+UP12                                    33000020
         ST    TESTREG,COWORD3         PUT ADDRESS OF BUFFER POOL IN    33200020
*                                      3RD PARAM WORD                   33400020
         MVI   COWORD3,END             PUT END OF PARAMETER LIST        33600020
*                                      CODE IN HIGH ORDER BYTE OF       33800020
*                                      3RD PARAM WORD                   34000020
         LA    UTREG,COWORD1           LET UTREG PT. TO PARMLIST        34200020
         IKJEBESH (COMMREG),IKJEBEUT,PARAM=((COMMREG),(UTREG)),MF=(E,A) 34400020
*                                      SERVICE RTN. HANDLER MACRO       34600020
         SPACE 2                                                        34800020
*********************************************************************** 35000020
*    CHECK RETURN CODE FROM IKJEBEUT AFTER READING 1ST RECORD           35200020
*********************************************************************** 35400020
         LTR   RETCDREG,RETCDREG       TEST RETURN CODE FROM IKJEBEUT   35600020
         BZ    CONT1                   BRANCH IF ZERO TO CONT1          35800020
         C     RETCDREG,UPDC4          TEST WHETHER R.C.=4              36000020
         BE    EMPTY                   BRANCH IF EQUAL TO EMPTY         36200020
IOERR    LA    RETCDREG,UP12           SET R.C. TO 12                   36400020
         SPACE 2                                                        36600020
*********************************************************************** 36800020
*    STANDARD EXIT LINKAGE TO CONTROLLER                                37000020
*********************************************************************** 37200020
EXIT     TM    CAPRSPDL,CAFREEDL       TEST WHETHER PARSE PDL REQUIRES  37300020
*                                      FREEMAIN                         37400020
         BNZ   RET                     PDL NOT EXIST THEN RETURN TO     37600020
*                                      CONTROLLER                       37800020
         IKJRLSA CAPRSPDL                                               37900020
         OI    CAPRSPDL,CAFREEDL       TURN ON THE BIT TO INDICATE PDL  38000020
*                                      WAS FREED                        38100020
RET      EQU   *                                                        38200020
         IKJEBERT (14,12),T,RC=(15)                                     38400020
         SPACE 2                                                        38600020
*********************************************************************** 38800020
*    IF NO OPERAND IS SPECIFIED IN THE SUBCOMMAND, THEN IT IS DEFAULTED 39000020
*    TO 1                                                               39200020
*********************************************************************** 39400020
DEFAULT  LA    BINREG,UP1                                               39600020
         B     READFRST                BRANCH TO READ 1ST RECORD OF     39800020
*                                      UTILITY DATA SET                 40000020
         SPACE 2                                                        40200020
*********************************************************************** 40400020
*    DATA SET IS EMPTY                                                  40600020
*********************************************************************** 40800020
EMPTY    MVC   B(UP12),INLIST1                                          41000020
         IKJEBEML M501,MF=(E,B)        ISSUE MESSAGE1- 'NO LINES IN     41200020
*                                      DATA SET'                        41400020
         XC    CACURNUM(UP4),CACURNUM  SET CURRENT LINE NUMBER TO ZERO  41600020
NORMAL   SR    RETCDREG,RETCDREG       SET RETURN CODE TO ZERO          41800020
         B     EXIT                    BRANCH UNCOND. TO CHECK WHETHER  42000020
*                                      PARSE WAS CALLED                 42200020
FLUSH    LA    RETCDREG,UP8            INDICATE STACK NEEDS FLUSHING    42250020
         B     EXIT                    BRANCH TO DO SOME CHECKS         42300020
         SPACE 2                                                        42400020
*********************************************************************** 42600020
*    READ PREVIOUS RECORD OF DATA SET STARTING WITH THE RECORD PREVIOUS 42800020
*   TO THE CURRENT ONE                                                  43000020
*********************************************************************** 43200020
CONT1    MVC   COKEYA(UP4),CACURNUM    PUT '*' VALUE TO COKEYA IN       43400020
*                                      SUBCMD. WORKAREA                 43600020
         LR    WORKREG,BINREG          PUT OPERAND 'COUNT' IN WORKREG   43800020
READ     MVI   COWORD1,PREV            PUT READ PREVIOUS RECORD CODE    44000020
*                                      IN HIGH ORDER BYTE OF 1ST        44200020
*                                      PARAM WORD                       44400020
         LA    TESTREG,COKEYA          LET 2ND PARAM WORD POINT TO      44600020
         ST    TESTREG,COWORD2         COKEYA                           44800020
         IKJEBESH (COMMREG),IKJEBEUT,PARAM=((COMMREG),(UTREG)),MF=(E,A) 45000020
*                                      SERVICE RTN. HANDLER MACRO       45200020
         SPACE 2                                                        45400020
*********************************************************************** 45600020
*    CHECK RETURN CODE FROM IKJEBEUT AFTER READING PREVIOUS RECORD      45800020
*********************************************************************** 46000020
         LTR   RETCDREG,RETCDREG       TEST RETURN CODE FROM IKJEBEUT   46200020
         BZ    CONT2                   BRANCH IF ZERO TO CONT2          46400020
         C     RETCDREG,UPDC4          TEST WHETHER R.C.=4              46600020
         BE    TOP                     BRANCH ON EQUAL TO TOP           46800020
         B     IOERR                   R.C.=8 THEN GO TO IOERR          47000020
         SPACE 2                                                        47200020
*********************************************************************** 47400020
*    ISSUE MESSAGE2 'TOP OF DATA SET' WHENEVER FIRST RECORD OF DATA SET 47600020
*    IS REACHED                                                         47800020
*********************************************************************** 48000020
TOP      OI    SW,SW1                  TURN SW1 ON                      48200020
ISSUE1   MVC   B(UP12),INLIST2         MESSAGE2- 'TOP OF DATA SET'      48400020
         IKJEBEML M505,MF=(E,B)                                         48600020
         B     SET                     GO TO SET * VALUE                48800020
CONT2    BCT   WORKREG,LOOP            SUB 1 FROM WORKREG AND GO        49000020
*                                      TO LOOP IF CONTENT OF            49200020
*                                      WORKREG NOT =0                   49400020
         SPACE 2                                                        49600020
*********************************************************************** 49800020
*    '*' VALUE IS SET TO LAST LINE READ                                 50000020
*********************************************************************** 50200020
SET      MVC   CACURNUM(UP4),CATEMPBF+UP12  SET '*' VALUE TO KEY OF     50400020
*                                      LAST LINE REFERENCED             50600020
VERIFY   OI    CACFLAG1,CALNTOVF       TURN THE LINE TO BE VERIFIED     50800020
*                                      SWITCH ON                        51000020
         SPACE 2                                                        51200020
*********************************************************************** 51400020
*    IF 'TOP OF DATA SET' HAS NOT BEEN ISSUED, READ A RECORD PREVIOUS   51600020
*    TO THE ONE WITH THE '*' VALUE AGAIN                                51800020
*********************************************************************** 52000020
TEST     TM    SW,SW1                  TEST WHETHER SW1 IS ON           52200020
         BO    TURNOFF1                SW1 IS ON THEN GO TO TURNOFF1    52400020
         MVC   COKEYA(UP4),CATEMPBF+UP12                                52600020
         MVI   COWORD1,PREV                                             52800020
         LA    TESTREG,COKEYA                                           53000020
         ST    TESTREG,COWORD2                                          53200020
         MVI   COWORD2,END                                              53400020
         IKJEBESH (COMMREG),IKJEBEUT,PARAM=((COMMREG),(UTREG)),MF=(E,A) 53600020
         SPACE 2                                                        53800020
*********************************************************************** 54000020
*    CHECK RETURN CODE FROM IKJEBEUT AFTER READING ONE MORE RECORD      54200020
*********************************************************************** 54400020
         LTR   RETCDREG,RETCDREG                                        54600020
         BZ    NORMAL                  R.C.=0 THEN GO TO NORMAL         54800020
         C     RETCDREG,UPDC4                                           55000020
         BNE   IOERR                   R.C. NOT =0,4 THEN GO TO IOERR   55200020
ISSUE2   MVC   B(UP12),INLIST2                                          55400020
         IKJEBEML M505,MF=(E,B)                                         55600020
         B     NORMAL                  BRANCH UNCOND. TO NORMAL         55800020
*                                      RETURN TO CONTROLLER             56000020
TURNOFF1 NI    SW,ALL-SW1                                               56200020
         B     NORMAL                  BRANCH UNCOND. TO NORMAL RET.    56400020
*                                      TO CONTROLLER                    56600020
LOOP     TM    CAATTN,CAATTNIS         TEST WHETHER ATTN IS ISSUED      56800020
         BO    NORMAL                  BRANCH ON ATTN ISSUED TO         57000020
*                                      NORMAL RETURN TO CONTROLLER      57200020
NO       MVC   COKEYA(UP4),CATEMPBF+UP12  SET VALUE OF COKEYA TO KEY    57400020
*                                      OF LAST LINE READ                57600020
         B     READ                    BRANCH TO READ ANOTHER RECORD    57800020
         SPACE 2                                                        58000020
*********************************************************************** 58200020
*    MESSAGE3- 'NOT ENOUGH MAIN STORAGE TO EXECUTE UP' IS               58400020
*    ISSUED HERE                                                        58600020
*********************************************************************** 58800020
MESSAGE3 MVC   B(UP12),INLIST3                                          59000020
         IKJEBEML M312,,SUBCMD,,MF=(E,B)                                59200020
         B     FLUSH                   BRANCH TO SET R.C. TO 8 AND      59400020
*                                      RETURN CONTROL TO CONTROLLER     59600020
MSG5     EQU   *                                                        59800020
         SPACE 2                                                        60000020
*********************************************************************** 60200020
*    MSG5- 'COMMAND SYSTEM ERROR+  IKJPARS ERROR CODE 12' IS ISSUED     60400020
*    HERE                                                               60600020
*********************************************************************** 60800020
         MVC   B(UP12),INLIST5                                          61000020
         IKJEBEML M313,M3131,,INS12,MF=(E,B)                            61200020
         B     FLUSH                   BRANCH TO SET R.C. TO 8 AND      61400020
*                                      RETURN TO CONTROLLER             61600020
         SPACE 2                                                        61800020
*********************************************************************** 62000020
*    VALIDITY CHECKING ROUTINE FOR UP OPERAND                           62200020
*********************************************************************** 62400020
UVALID   SAVE  (14,12),T,*             SAVE PARSE'S REGISTERS           62600020
         BALR  TEMPREG,UP0             ESTABLISH ADDRESSABILITY         62800020
         USING *,TEMPREG               ADDRESSABILITY IN VALIDITY       63000020
*                                      CHECKING ROUTINE                 63200020
         L     TESTREG,UP0(PARMREG1)   PUT POINTER TO COUNT IN TESTREG  63400020
         L     BINREG,UP0(TESTREG)     PUT COUNT IN BINREG              63600020
         LH    UTREG,UP4(TESTREG)      PUT LENGTH OF COUNT IN 2 LOW     63800020
*                                      ORDER BYTE OF UTREG              64000020
         BCTR  UTREG,PARMREG0          LENGTH OF COUNT - 1              64200020
         EX    UTREG,COMPARE           TEST WHETHER COUNT=0             64400020
         BE    EXIT1                   EQUAL TO ZERO THEN GO TO EXIT1   64600020
         SR    RETCDREG,RETCDREG       PUT R.C.=0 IN REG. 15            64800020
EXIT2    RETURN (14,12),T,RC=(15)      RETURN TO PARSE                  65000020
EXIT1    LA    RETCDREG,UP4            PUT R.C.=4 IN REG. 15            65200020
         B     EXIT2                   BRANCH TO RET. TO PARSE          65400020
         SPACE 3                                                        65600020
*********************************************************************** 65800020
*    CONSTANT AREA FOR IKJEBEUP                                         66000020
*********************************************************************** 66200020
UPDC4    DC    F'4'                    FULL WORD OF 4                   66400020
UPDC8    DC    F'8'                    FULLWORD OF 8                    66600020
UPDC12   DC    F'12'                   FULLWORD OF 12                   66800020
UPDC16   DC    F'16'                   FULLWORD OF 16                   67000020
ADUPPCL  DC    A(IKJEBUP0)             ADCON OF PCL                     67200020
ZERO     DC    8C'0'                   CONSTANT OF ZERO                 67400020
PACKED   PACK  COPACKED(UP8),UP0(UP0,TESTREG)                           67600020
COMPARE  CLC   UP0(UP0,BINREG),ZERO                                     67800020
         SPACE 2                                                        68000020
*********************************************************************** 68200020
*    MESSAGE INSERTION MACRO - IKJEBEMG IS ISSUED HERE                  68400020
*********************************************************************** 68600020
SUBCMD   IKJEBEMG ,M312IN1,'UP'                                         68800020
INS12    IKJEBEMG CODE12,M3131IN1,'IKJPARS'                             69000020
CODE12   IKJEBEMG 0,M3131IN2,'12'                                       69200020
         SPACE 2                                                        69400020
*********************************************************************** 69600020
*    LIST FORMS OF IKJEBEML MACRO                                       69800020
*********************************************************************** 70000020
INLIST1  IKJEBEML M501,MF=L                                             70200020
INLIST2  IKJEBEML M505,MF=L                                             70400020
INLIST3  IKJEBEML M312,,SUBCMD,,MF=L                                    70600020
INLIST5  IKJEBEML M313,M3131,,INS12,MF=L                                70800020
         SPACE 2                                                        71000020
*********************************************************************** 71200020
*    MACROS OF PARSE                                                    71400020
*********************************************************************** 71600020
         DS    0F                                                       71800020
IKJEBUP0 IKJPARM DSECT=UPPDL                                            72000020
COUNT    IKJIDENT 'UP OPERAND',MAXLNTH=8,FIRST=NUMERIC,OTHER=NUMERIC,  *72200020
               PROMPT='UP OPERAND',VALIDCK=UVALID                       72400020
         IKJENDP                                                        72600020
         SPACE 2                                                        72800020
         IKJPPL                                                         73000020
         SPACE 2                                                        73200020
         IKJEBECA                                                       73400020
         ORG   CASCWKA                 EDIT SUBCOMMAND WORKAREA         73600020
COPACKED DS    D                       DOUBLE WORD FOR PACKED DECIMAL   73800020
*                                      OF 'COUNT'                       74000020
COWORD1  DS    F                       1ST PARAM WORD FOR UT            74200020
COWORD2  DS    F                       2ND PARAM WORD FOR UT            74400020
COWORD3  DS    F                       3RD PARAM WORD FOR UT            74600020
COKEYA   DS    F                       CURRENT REFERENCED VALUE FOR UT  74800020
A        DS    5F                      5 FULLWORDS FOR IKJEBESH MACRO   75200020
B        DS    10F                     10 FULLWORDS FOR IKJEBEML MACRO  75400020
SW       DS    X                       SWITCH FOR DET. WHETHER ONE MORE 75600020
*                                      RECORD SHOULD BE READ            75800020
         END                                                            76000020
