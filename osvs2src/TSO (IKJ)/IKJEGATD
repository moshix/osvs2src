ATD      TITLE 'IKJEGATD - THE SECOND LOAD MODULE OF THE AT SUBCOMMAND' 00010002
         COPY  IKJEGSIO                                                 00010402
IKJEGATD CSECT                                                          00093602
         EJECT                                                          00094002
*****************************  PROLOGUE  ****************************** 00094402
*                                                                     * 00095202
* STATUS                                                              * 00096002
*    CHANGE LEVEL 00, VERSION NUMBER 02, VS2     03/02/73             * 00096802
*                                                                     * 00097602
* FUNCTION/OPERATION                                                  * 00098402
*    THIS MODULE CONSTRUCTS A DEFER QUEUE FOR SAVING REQUESTS FOR     * 00099202
*    ESTABLISHING BREAKPOINTS UNTIL THE CONCERNED MODULE IS BROUGHT   * 00100002
*    INTO CORE.                                                       * 00110002
*                                                                     * 00120002
* ENTRY POINTS                                                        * 00130002
*    IKJEGATD - ENTERED FROM IKJEGAT, THE FIRST LOAD MODULE OF        * 00140002
*    THE AT SUBCOMMAND.  IT FIRST CHECKS TO MAKE CERTAIN ALL          * 00150002
*    ADDRESSES FOR THE DEFER REQUEST ARE FULLY QUALIFIED              * 00160002
*    AND SPECIFY THE SAME LOADNAME. IT THEN BUILDS THE                * 00170002
*    DEFERRED BREAK ELEMENT AND PUTS IT ON THE DEFERRED QUEUE.        * 00180002
*                                                                     * 00190002
* INPUT                                                               * 00200002
*    REGISTER 1 CONTAINS THE ADDRESS OF TCOMTAB. THE FIELD WORKAREA   * 00210002
*    IN TCOMTAB CONTAINS THE ADDRESS OF TSTCWORK. ATD'S WORK AREA     * 00220002
*    IS PART OF TSTCWORK. THE FIELD PDEADDR IN ATD'S WORK AREA        * 00230002
*    CONTAINS THE ADDRESS OF THE FIRST ADDRESS PDE RETURNED BY        * 00240002
*    PARSE. THE FIELD TSTANSPL IN TCOMTAB POINTS TO THE PDL           * 00250002
*    WHICH WAS BUILT BY PARSE.                                        * 00260002
*                                                                     * 00270002
* OUTPUT                                                              * 00280002
*    OUTPUT CONSISTS OF THE FOLLOWING                                 * 00290002
*    1.  DIAGNOSTIC MESSAGES                                          * 00300002
*    2.  A DEFER QUEUE POINTED TO BY DEFERTAB IN TCOMTAB IN THE       * 00310002
*    FORMAT DESCRIBED UNDER THE SYMBOL NEWQ.                          * 00320002
*    3.  RETURN CODES AS LISTED BELOW-                                * 00330002
*                                                                     * 00340002
*    0 - MAINLINE MAY REISSUE THE TEST MODE MESSAGE AND GET ANOTHER   * 00350002
*    SUBCOMMAND. IF AN ERROR OCCURRED, TSTFLUSH IN TCOMTAB IS ON.     * 00360002
*    16 - ATTENTION HAS BEEN SCHEDULED                                * 00370002
*    20 - INDICATES RETURN FROM THE RETRY ROUTINE                     * 00380002
*    24 - INDICATES AN ESTAE MACRO FAILURE                            * 00382002
*                                                                     * 00390002
* EXTERNAL REFERENCES                                                 * 00400002
*    IKJEGIO OUTPUTS ERROR MESSAGES VIA IKJEGSIO MACRO                * 00410002
*                                                                     * 00430002
* EXITS, NORMAL                                                       * 00440002
*    RETURN VIA REGISTER 14 TO THE CALLER                             * 00450002
*                                                                     * 00460002
* EXITS, ERROR                                                        * 00462002
*    RETURN VIA REGISTER 14 TO THE CALLER                             * 00464002
*                                                                     * 00466002
* TABLES/WORK AREAS                                                   * 00470002
*    THIS MODULE USES TWO WORK AREAS                                  * 00480002
*    1.  SUBCOMMAND WORK AREA DESCRIBED BY TSTCWORK AND WORKSP DSECTS * 00490002
*    2.  TEST COMMUNICATION WORK AREA DESCRIBED BY TCOMTAB DSECT      * 00500002
*                                                                     * 00510002
* ATTRIBUTES                                                          * 00520002
*    REENTRANT, REFRESHABLE                                           * 00530002
*                                                                     * 00540002
* CHARACTER CODE DEPENDENCY                                           * 00550002
*    THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL REPRESEN-  * 00560002
*    TATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT TO THE  * 00570002
*    ONE USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED SO THAT * 00580002
*    REDEFINITION OF 'CHARACTER' CONSTANTS, BY REASSEMBLY, WILL RESULT* 00590002
*    IN A CORRECT MODULE FOR THE NEW DEFINITIONS.                     * 00600002
*                                                                     * 00610002
* NOTES                                                               * 00620002
*    NONE.                                                            * 00630002
*                                                                     * 00640002
*********************************************************************** 00650002
         EJECT                                                          00660002
*********************************************************************** 00670002
*                                                                     * 00680002
*               EQUATES                                               * 00690002
*                                                                     * 00700002
*********************************************************************** 00710002
         SPACE                                                          00720002
R0       EQU   0                        REGISTER 0 - USED ONLY FOR      00730002
*                                       INTERFACING WITH OS             00740002
R1       EQU   1                        REGISTER 1 - PARAMETER AND WORK 00750002
*                                       REGISTER                        00760002
R2       EQU   2                        REGISTER 2 - WORKREG - ALSO     00770002
*                                       USED AS BASE FOR DME            00780002
R3       EQU   3                        REGISTER 3 - RESERVED FOR       00790002
*                                       MAINTENANCE                     00800002
R4       EQU   4                        REGISTER 4 - WORKREG            00810002
R5       EQU   5                        REGISTER 5 - WORKREG            00820002
R6       EQU   6                        REGISTER 6 - WORKREG            00830002
R7       EQU   7                        REGISTER 7 - SWITCH FOR         00840002
*                                       SETTING UP LOAD NAME, AND BASE  00850002
*                                       FOR DBE                         00860002
R8       EQU   8                        REGISTER 8 - WORKREG FOR STAE   00870002
R9       EQU   9                        REGISTER 9 - BASE FOR TCOMTAB   00880002
R10      EQU   10                       REGISTER 10 - BASE FOR TSTCWORK 00890002
R11      EQU   11                       REGISTER 11 - BASE FOR ADDRESS  00900002
*                                       PDES                            00910002
R12      EQU   12                       REGISTER 12 - PROGRAM BASE      00920002
*                                       REGISTER                        00930002
R13      EQU   13                       REGISTER 13 - SAVE AREA POINTER 00940002
R14      EQU   14                       REGISTER 14 - RETURN ADDRESS    00950002
R15      EQU   15                       REGISTER 15 - RETURN CODES,     00960002
*                                       ENTRY ADDRESSES                 00970002
         SPACE                                                          00980002
BLANK    EQU   C' '                     BLANK                           00990002
HEXFF    EQU   X'FF'                    BYTE WITH ALL BITS ON           01000002
         SPACE                                                          01010002
ZERO     EQU   0                        NUMBER 0                        01020002
ONE      EQU   1                        NUMBER 1                        01030002
TWELVE   EQU   12                       NUMBER 12                       01040002
SIXTEEN  EQU   16                       NUMBER 16                       01050002
TWENTY   EQU   20                       NUMBER 20                       01060002
TWENTY8  EQU   28                       NUMBER 28                       01070002
         SPACE                                                          01080002
L0       EQU   0                        LENGTH OF 0                     01090002
L3       EQU   3                        LENGTH OF 3                     01100002
L7       EQU   7                        LENGTH OF 7                     01110002
         SPACE                                                          01120002
D0       EQU   0                        DISPLACEMENT OF 0               01130002
D1       EQU   1                        DISPLACEMENT OF 1               01140002
D4       EQU   4                        DISPLACEMENT OF 4               01150002
         SPACE                                                          01160002
CVTADDR  EQU   X'10'                    ADDRESS OF CVT                  01190002
RC24     EQU   24            RETURN CODE 24                             01192002
         SPACE                                                          01200002
         EJECT                                                          01250002
*********************************************************************** 01260002
*                                                                     * 01270002
*              ATD PROCESSING STARTS HERE                             * 01280002
*                                                                     * 01290002
*********************************************************************** 01300002
         SAVE  (14,12),,IKJEGATD        SAVE CALLER'S REGISTERS         01310002
         LR    R12,R15                  ESTABLISH ADDRESSABILITY FOR    01320002
         USING IKJEGATD,R12             THIS CSECT                      01330002
         LR    R9,R1                    PUT ADDRESS OF TCOMTAB IN R9    01340002
         USING TCOMTAB,R9               ESTABLISH ADDRESSABILITY TO     01350002
*                                       COMMUNICATION WORK AREA         01360002
         L     R5,REGSAVE2              R5=ADDR OF AT'S SAVE AREA       01370002
         ST    R5,SAVENEXT-SAVEAREA(R13)  SAVE FORWARD POINTER          01380002
         ST    R13,SAVELAST-SAVEAREA(R5)  SAVE BACK POINTER             01390002
         LR    R13,R5                   R13=ADDR OF AT'S SAVE AREA      01400002
         SPACE                                                          01410002
*********************************************************************** 01420002
*        TURN OFF XCTL SWITCH IN TCOMTAB. GET ADDRESSABILITY TO       * 01430002
*        WORKAREA, AND ISSUE ESTAE TO INTERCEPT ABENDS.               * 01440002
*********************************************************************** 01450002
         NI    TSTFLGS2,HEXFF-TSTXCTL   TURN OFF XCTL SWITCH            01460002
         STM   R14,R12,RETURNPT-SAVEAREA(R13)  SAVE AT'S REGISTERS      01470002
*                                       IN CASE OF AN ABEND             01480002
         L     R10,WORKAREA             R10=ADDRESS OF COMMON WORK AREA 01490002
         USING TSTCWORK,R10             ESTABLISH ADDRESSABILITY TO IT  01500002
         XC    STAEL,STAEL   CLEAR ESTAE PARM LIST                      01550002
         L     R15,TSTSTAE   R15=ADDR OF ESTAE EXIT ROUTINE             01560002
         ESTAE (R15),CT,PARAM=ATDSPL,XCTL=NO,RECORD=YES,               *01600002
               MF=(E,STAEL)  ESTABLISH ESTAE EXIT                       01602002
         LTR   R15,R15       WAS ESTAE SUCCESSFUL?                      01604002
         BZ    ATD1          YES,CONTINUE                               01606002
         STC   R15,TSTESTRC  SAVE ESTAE RETURN CODE                     01608002
         LA    R15,RC24      SET RETURN CODE TO 24                      01608402
         B     RETMAIN       RETURN TO IKJEGMNL                         01608802
         SPACE                                                          01610002
*********************************************************************** 01620002
*        GET ADDRESS OF FIRST ADDRESS PDE AND SET UP FOR PROCESSING.  * 01630002
*********************************************************************** 01640002
ATD1     EQU   *                                                        01642002
         L     R11,PDEADDR              R11=POINTER TO FIRST ADDRESS    01650002
*                                       PDE                             01660002
         USING IKJPARMA,R11             ESTABLISH ADDRESSABILITY TO PDE 01670002
         XR    R7,R7                    R7=0 INDICATES FIRST VALID      01680002
*                                       PDE YET TO BE ENCOUNTERED       01690002
         SPACE                                                          01700002
*********************************************************************** 01710002
*        CHECK FOR FULLY QUALIFIED ADDRESS. IF A RANGE IS GIVEN,      * 01720002
*        BE SURE BOTH ADDRESSES ARE IN THE SAME LOAD.                 * 01730002
*********************************************************************** 01740002
ISITCONV DS    0H                       CHECK FOR CONVERTED PDE         01750002
         TM    PDEUSER,PDECONVD         HAS PDE BEEN CONVERTED - IF IT  01760002
*                                       HAS NOT, THEN ADDRESS IS        01770002
*                                       FULLY QUALIFIED                 01780002
         BO    DEFERMSG                 PUT OUT MESSAGE FOR DEFER MUST  01790002
*                                       BE USED WITH FULLY QUALIFIED    01800002
*                                       ADDRESS                         01810002
         TM    PDE2FLG1,LDNAMFLG        WAS A RANGE GIVEN               01820002
         BZ    ASKFIRST                 NO, TEST FOR FIRST UNRESOLVED   01830002
*                                       PDE                             01840002
         CLC   PDELEN1,PDE2LEN1         DO LOADNAMES HAVE THE SAME      01850002
*                                       NUMBER OF CHARACTERS            01860002
         BNE   BADLDNM                  NO, PUT OUT ERROR MSG           01870002
         L     R2,PDELDNAM              R2=ADDRESS OF FIRST LOADNAME    01880002
         L     R6,PDE2LDNA              R6=ADDRESS OF SECOND LOADNAME   01890002
         LH    R4,PDELEN1               R4=LENGTH OF LOADNAMES          01900002
         BCTR  R4,ZERO                  DECREMENT FOR EXECUTE INSTR.    01910002
         EX    R4,COMPARE               DO LOADNAMES MATCH              01920002
         BNE   BADLDNM                  LOADNAMES DON'T MATCH - PUT OUT 01930002
*                                       ERROR MESSAGE                   01940002
         SPACE                                                          01950002
*********************************************************************** 01960002
*        IF THIS IS THE FIRST UNCONVERTED PDE, MOVE THE LOAD          * 01970002
*        NAME TO WORK AREA FOR COMPARISON WITH LATER PDE LOAD         * 01980002
*        NAMES. OTHERWISE, CHECK THAT LOAD NAME IS THE SAME           * 01990002
*        AS THAT OF PRECEEDING PDES.                                  * 02000002
*********************************************************************** 02010002
ASKFIRST DS    0H                       IS THIS FIRST UNRESOLVED PDE    02020002
         LTR   R7,R7                    IS THE SWITCH ZERO              02030002
         BZ    MOVELOAD                 YES, MOVE LOADNAME TO WORKAREA  02040002
         LH    R4,PDELEN1               R4=LENGTH OF NEW LOADNAME       02050002
         CLR   R5,R4                    IS LENGTH EQUAL TO LENGTH OF    02060002
*                                       FIRST                           02070002
         BNE   BADLDNM                  NO, PUT OUT MESSAGE - LOADNAMES 02080002
*                                       FOR A DEFER REQUEST MUST        02090002
*                                       BE THE SAME                     02100002
         LA    R6,LOADNAME              R6=POINTER TO LOADNAME IN       02110002
*                                       WORKAREA                        02120002
         L     R2,PDELDNAM              R2=POINTER TO NEW LOADNAME      02130002
         BCTR  R4,ZERO                  DECREMENT FOR EXECUTE INSTR     02140002
         EX    R4,COMPARE               COMPARE LOADNAMES               02150002
         BNE   BADLDNM                  LOADNAMES DON'T MATCH - PUT OUT 02160002
*                                       ERROR MESSAGE                   02170002
         SPACE                                                          02180002
*********************************************************************** 02190002
*        GET CHAIN POINTER TO NEXT PDE. IF NONE, GO INSERT ELEMENT    * 02200002
*        IN DEFERRED QUEUE FOR THIS REQUEST. IF MORE PDES, GO         * 02210002
*        CHECK THEM.                                                  * 02220002
*********************************************************************** 02230002
NAMESOK  DS    0H                       THE LOADNAMES MATCH             02240002
         L     R11,PDE2CHAI             R11=PDE CHAIN                   02250002
         LA    R11,D0(R11)              CLEAR HIGH ORDER BYTE           02260002
         LTR   R11,R11                  IS CHAIN EMPTY                  02270002
         BZ    SEARCHQ                  YES, CHECK FOR DEFER QUEUE      02280002
         B     ISITCONV                 CHECK FOR PDE CONVERTED         02290002
         SPACE                                                          02300002
*********************************************************************** 02310002
*        INSTRUCTION TO BE EXECUTED FOR COMPARISON OF LOAD            * 02320002
*        NAMES FOR PDES.                                              * 02330002
*********************************************************************** 02340002
COMPARE  CLC   D0(L0,R6),D0(R2)         COMPARE INSTRUCTION             02350002
         SPACE                                                          02360002
*********************************************************************** 02370002
*        A FULLY QUALIFIED ADDRESS WAS NOT GIVEN. SEND AN ERROR       * 02380002
*        MESSAGE TO THE TERMINAL, AND RETURN.                         * 02390002
*********************************************************************** 02400002
DEFERMSG DS    0H                       PREPARE TO OUTPUT MESSAGE FOR   02410002
*                                       FULLY QUALIFIED ADDRESS MUST    02420002
*                                       BE GIVEN WITH DEFER             02430002
         LA    R1,MSG2       R1=ADDR OF MESSAGE NUMBERS                 02440002
         B     PUTMSG                   PUT OUT MESSAGE AND RETURN      02460002
         SPACE                                                          02470002
*********************************************************************** 02480002
*        ALL LOADNAMES WERE NOT THE SAME. SEND AN ERROR MESSAGE TO    * 02490002
*        THE TERMINAL AND RETURN.                                     * 02500002
*********************************************************************** 02510002
BADLDNM  DS    0H                       PREPARE TO OUTPUT MESSAGE FOR   02520002
*                                       ALL LOADNAMES ON A DEFER        02530002
*                                       REQUEST MUST BE SAME            02540002
         LA    R1,MSG3       R1=ADDR OF MESSAGE NUMBERS                 02550002
         B     PUTMSG                   PUT OUT MESSAGE AND RETURN      02570002
         SPACE                                                          02580002
*********************************************************************** 02590002
*        WHEN THE FIRST UNCONVERTED PDE IS FOUND, AND IS VALID,       * 02600002
*        THIS CODE IS EXECUTED TO MOVE THE LOADNAME FROM THE PDE      * 02610002
*        TO ATD'S WORKAREA FOR COMPARISON WITH LATER PDES.            * 02620002
*********************************************************************** 02630002
MOVELOAD DS    0H                       FIRST UNRESOLVED PDE FOUND      02640002
         LA    R7,D1(R7)                SET SWITCH FOR FIRST UNRESOLVED 02650002
*                                       PDE                             02660002
         MVI   LOADNAME,BLANK           BLANK OUT LOADNAME FIELD IN     02670002
         MVC   LOADNAME+D1(L7),LOADNAME WORKAREA                        02680002
         L     R6,PDELDNAM              R6=ADDRESS OF LOADNAME          02690002
         LA    R4,LOADNAME              R4=ADDRESS FOR LOADNAME IN      02700002
*                                       WORKAREA                        02710002
         LH    R5,PDELEN1               R5=LENGTH OF LOADNAME STRING    02720002
         BCTR  R5,ZERO                  DECREMENT FOR MOVE INST         02730002
         EX    R5,LOADMVC               MOVE LOADNAME TO WORKAREA       02740002
         LA    R5,D1(R5)                R5=LENGTH OF LOADNAME           02750002
         B     NAMESOK                  CHECK FOR MORE PDE'S            02760002
         SPACE                                                          02770002
*********************************************************************** 02780002
*        THIS INSTRUCTION IS EXECUTED TO MOVE THE LOADNAME FROM       * 02790002
*        THE PDE TO ATD'S WORKAREA.                                   * 02800002
*********************************************************************** 02810002
LOADMVC  MVC   D0(L0,R4),D0(R6)         MOVE LOADNAME TO WORKAREA       02820002
         SPACE                                                          02830002
*********************************************************************** 02840002
*        ALL PDES HAVE BEEN CHECKED AS MUCH AS POSSIBLE AND ARE       * 02850002
*        OK. IF THE DEFERRED QUEUE IS EMPTY, GO BUILD A DEFERRED      * 02860002
*        QUEUE. IF QUEUE IS NOT EMPTY, GET ADDRESS OF FIRST DME ON    * 02870002
*        DEFERRED QUEUE.                                              * 02880002
*********************************************************************** 02890002
SEARCHQ  DS    0H                       ARE ANY DME'S ON THE DEFER      02900002
*                                       QUEUE                           02910002
         OC    DEFERTAB,DEFERTAB        IS THE DEFER QUEUE EMPTY ?      02920002
         BZ    NEWQ                     YES, ESTABLISH THE QUEUE        02930002
         L     R2,DEFERTAB              R2=ADDRESS OF FIRST DME ON THE  02940002
*                                       QUEUE                           02950002
         USING DME,R2                   R2 IS USED FOR DME              02960002
*                                       ADDRESSABILITY                  02970002
         SPACE                                                          02980002
*********************************************************************** 02990002
*        CHECK THE LOADNAME IN THE DME. IF IT IS NOT THE SAME AS      * 03000002
*        THE LOADNAME IN THE PDES, GO TRY THE NEXT DME. IF IT         * 03010002
*        IS THE SAME, GO TO END OF ASSOCIATED DBE CHAIN, AND BUILD    * 03020002
*        A NEW DBE FOR THIS REQUEST.                                  * 03030002
*********************************************************************** 03040002
TESTNAME DS    0H                       COMPARE LOADNAMES               03050002
         CLC   LOADNAME,DMELOAD         IS NEW LOADNAME EQUAL TO ONE    03060002
*                                       IN DME                          03070002
         BNE   NEXTDME                  NO, ARE THERE MORE DME'S        03080002
         L     R7,DMEDBE                R7=POINTER TO FIRST DBE         03090002
         USING DBE,R7                   R7 IS USED FOR DBE              03100002
*                                       ADDRESSABILITY                  03110002
         SPACE                                                          03120002
TRYDBE   DS    0H                       IS THIS LAST DBE FOR DME        03130002
         OC    DBEDBE,DBEDBE            IS DBE LINK FIELD ZERO          03140002
         BZ    GETDBE                   YES, GET CORE FOR NEXT DBE      03150002
         L     R7,DBEDBE                R7=POINTER TO NEXT DBE          03160002
         B     TRYDBE                   IS THIS LAST DBE FROM DME       03170002
         SPACE                                                          03180002
*********************************************************************** 03190002
*        LOADNAME IN DME DID NOT AGREE WITH LOADNAME IN PDES.         * 03200002
*        IF THERE ARE MORE DMES, GET THE ADDRESS OF THE NEXT ONE, AND * 03210002
*        GO CHECK IT. IF NO MORE, GO BUILD ONE FOR THIS REQUEST.      * 03220002
*********************************************************************** 03230002
NEXTDME  DS    0H                       ARE MORE DME'S ON THE QUEUE     03240002
         OC    DMEDME,DMEDME            IS LINK FIELD EMPTY             03250002
         BZ    NEWDME                   YES, GET CORE FOR ANOTHER DME   03260002
         L     R2,DMEDME                R2=POINTER TO NEXT DME          03270002
         B     TESTNAME                 BRANCH TO COMPARE LOADNAMES     03280002
         SPACE                                                          03290002
*********************************************************************** 03300002
*        THE DEFERRED QUEUE WAS EMPTY. BUILD A DME AND DBE FOR        * 03310002
*        THIS REQUEST AND CHAIN THEM TO THE TCOMTAB.                  * 03320002
*********************************************************************** 03330002
NEWQ     DS    0H                                                       03340002
         SPACE                                                          03350002
         MVC   GETCORE(GETLEN),GETL     MOVE LIST FORM OF GETMAIN TO    03360002
*                                          WORKAREA                     03370002
         LA    R1,GETCORE               R1=ADDRESS OF LIST FORM         03380002
         GETMAIN EC,LV=TWENTY8,A=ADDRCORE,SP=ONE,MF=(E,(R1)) GET CORE   03390002
*                                          FOR FIRST DME AND DBE        03400002
         LTR   R15,R15                  WAS CORE GOTTEN                 03410002
SMTGET01 BNZ   NOCORE                   NO. PRINT'INSUFFICIENT CORE'    03420002
         L     R2,ADDRCORE              R2=ADDRESS OF GOTTEN CORE       03430002
         ST    R2,DEFERTAB              SET POINTER IN TCOMTAB          03440002
         SPACE                                                          03450002
BLDELEMS DS    0H                       ACTUAL BUILDING OF ELEMENTS IS  03460002
*                                       DONE HERE                       03470002
         MVC   DMELOAD,LOADNAME         PUT LOADNAME IN DME             03480002
         LA    R7,DME+DMELNH            R7=ADDRESS OF FIRST DBE FROM    03490002
*                                       DME                             03500002
         ST    R7,DMEDBE                PUT ADDRESS OF FIRST DBE INTO   03510002
*                                       DME                             03520002
         XC    DMEDME,DMEDME            ZERO DME LINK FIELD             03530002
BLDDBE   DS    0H                                                       03540002
         MVC   DBEPDL,TSTANSPL          SAVE ADDRESS OF PDL IN DBE      03550002
         XC    TSTANSPL,TSTANSPL        CLEAR POINTER TO PDL            03560002
         MVC   DBEINBUF,INBUF           SAVE INBUF PTR IN DBE           03570002
         XC    INBUF,INBUF              CLEAR INBUF IN TCOMTAB          03580002
         XC    DBEDBE,DBEDBE            ZERO DBE LINK FIELD             03590002
         B     CODE0                    RETURN TO MAINLINE WITH DEFER   03600002
*                                       QUEUE SUCCESSFULLY BUILT        03610002
         SPACE                                                          03620002
*********************************************************************** 03630002
*        NO DME EXISTS FOR LOADNAME. BUILD A DME AND DBE AND          * 03640002
*        CHAIN THEM TO EXISTING QUEUE.                                * 03650002
*********************************************************************** 03660002
NEWDME   DS    0H                       A NEW DME MUST BE ESTABLISHED   03670002
         MVC   GETCORE(GETLEN),GETL     MOVE LIST FORM OF GETMAIN TO    03680002
*                                       WORKAREA                        03690002
         LA    R1,GETCORE               R1=ADDRESS OF LIST FORM         03700002
         GETMAIN EC,LV=TWENTY8,A=ADDRCORE,SP=ONE,MF=(E,(R1)) GET CORE   03710002
*                                       FOR NEW DME                     03720002
         LTR   R15,R15                  WAS CORE GOTTEN                 03730002
SMTGET02 BNZ   NOCORE                   NO. PRINT'INSUFFICIENT CORE'    03740002
         MVC   DMEDME,ADDRCORE          LINK NEW DME TO LAST DME        03750002
         L     R2,ADDRCORE              R2=ADDRESS OF NEW DME           03760002
         B     BLDELEMS                 BRANCH TO FILL IN FIELDS OF     03770002
*                                       DME AND DBE                     03780002
         SPACE                                                          03790002
*********************************************************************** 03800002
*        A DME EXISTS ON THE DEFERRED QUEUE FOR LOADNAME. BUILD A     * 03810002
*        DBE FOR THIS REQUEST AND CHAIN IT TO DBE QUEUE FOR DME.      * 03820002
*********************************************************************** 03830002
GETDBE   DS    0H                       A NEW DEFER BREAK ELEMENT MUST  03840002
*                                       BE ESTABLISHED                  03850002
         MVC   GETCORE(GETLEN),GETL     MOVE LIST FORM OF GETMAIN TO    03860002
*                                       WORK AREA                       03870002
         LA    R1,GETCORE               R1=ADDRESS OF L FORM OF MACRO   03880002
         GETMAIN EC,LV=TWELVE,A=ADDRCORE,SP=ONE,MF=(E,(R1)) GET CORE    03890002
*                                       FOR NEW DBE                     03900002
         LTR   R15,R15                  WAS CORE GOTTEN                 03910002
SMTGET03 BNZ   NOCORE                   NO. PRINT'INSUFFICIENT CORE'    03920002
         MVC   DBEDBE,ADDRCORE          CHAIN NEW DBE TO LAST DBE       03930002
         L     R7,ADDRCORE              PUT ADDRESS OF NEW DBE IN R7    03940002
         B     BLDDBE                   GO BUILD A NEW DBE              03950002
         SPACE                                                          03960002
*********************************************************************** 03970002
*    THERE WAS NOT ENOUGH CORE FOR GETMAIN. ISSUE MESSAGE AND QUIT.   * 03980002
*********************************************************************** 03990002
NOCORE   DS    0H                       THERE IS INSUFFICIENT CORE TO   04000002
*                                       SET UP DEFER ELEMENT            04010002
         LA    R1,MSG1       R1=ADDR OF MESSAGE NUMBERS                 04020002
         SPACE                                                          04040002
PUTMSG   DS    0H                       HAVE IKJEGIO PUT OUT MESSAGE    04050002
         OI    TSTFLGS4,TSTFLUSH        TURN ON TSTFLUSH SO MNL WILL    04053002
*                                       FLUSH THE INPUT STACK           04056002
         XC    TSTIOPRM(L'TSTIOPRM),TSTIOPRM CLEAR IO PARM LIST         04106002
         LM    R2,R3,D0(R1)  LOAD R2-3 WITH MESSAGE NUMBERS             04156002
         IKJEGSIO MSG,FIRST=(R2),SECOND=(R3),ID=AT004,MF=(E,TSTIOPRM)   04166002
         C     R15,ATTNCODE  IS RETURN CODE 16,20 OR 24?                04176002
         BNL   RETMAIN       YES, PASS IT BACK TO IKJEGMNL              04178002
         SPACE                                                          04180002
*********************************************************************** 04190002
*        SET RETURN CODE TO ZERO AND RETURN.                          * 04200002
*********************************************************************** 04210002
CODE0    DS    0H                       SET RETURN CODE TO ZERO         04220002
         XR    R15,R15                  R15=0                           04230002
         B     RETMAIN                  RETURN TO MAINLINE              04240002
         EJECT                                                          04250002
*********************************************************************** 04260002
*        THIS IS THE ESTAE EXIT FOR ATD.                              * 04270002
*********************************************************************** 04300002
RETRY2   DS    0H                       RETRY ROUTINE IN CASE OF        04310002
*                                       AN ABEND                        04320002
         L     R5,CVTPTR                R5=ADDRESS OF CVT               04330002
         USING CVT,R5                   ESTABLISH ADDRESSABILITY TO IT  04340002
         L     R5,CVTTCBP               R5=POINTER TO DOUBLE WORD OF    04350002
*                                       POINTERS TO NEXT TO BE DIS-     04360002
*                                       PATCHED AND CURRENT TCB'S       04370002
         DROP  R5                                                       04380002
         L     R5,D4(R5)                R5=POINTER TO CURRENT TCB       04390002
*                                       (TEST'S TCB)                    04400002
         USING TCB,R5                   ESTABLISH ADDRESSABILITY TO IT  04410002
         L     R9,TCBTRN                R5=ADDRESS OF TCOMTAB           04420002
         DROP  R5                                                       04430002
         L     R13,REGSAVE2             R13=ADDRESS OF AT'S SAVE AREA   04440002
         L     R12,GR12-SAVEAREA(R13)   RESTORE BASE REGISTER           04450002
         LA    R15,TWENTY               R15=20 - INDICATES RETURN FROM  04530002
*                                       RETRY ROUTINE                   04540002
         SPACE                                                          04550002
RETMAIN  DS    0H                       RETURN TO MAINLINE              04560002
         LR    R2,R15        SAVE RETURN CODE                           04562002
         ESTAE 0             REMOVE ESTAE                               04564002
         LR    R15,R2        RESET RETURN CODE                          04566002
         NI    TSTFLGS4,HEXFF-TSTRERTN TURN OFF RETRY BIT               04568002
         L     R13,SAVELAST-SAVEAREA(R13)  RESTORE R13                  04570002
         RETURN (14,12),,RC=(15)        RETURN                          04580002
         EJECT                                                          04590002
*********************************************************************** 04600002
*                                                                     * 04610002
*              CONSTANTS AND MACROS USED IN ATD.                      * 04620002
*                                                                     * 04630002
*********************************************************************** 04640002
GETL     GETMAIN EC,SP=ONE,MF=L         LIST FORM OF GETMAIN            04650002
GETLEN   EQU   *-GETL                   LENGTH OF LIST FORM             04660002
         SPACE                                                          04700002
* CONSTANTS                                                             04710002
         DS    0F                                                       04720002
DUMMYABT DC    X'FF'         DUMMY ABEND TABLE                          04730002
ATTNCODE DC    F'16'         RC FOR ATTENTION                           04742002
*********************************************************************** 04750002
*              MESSAGE POINTERS                                       * 04752002
*********************************************************************** 04754002
MSG1     DC    F'39'         UNABLE TO ESTABLISH BREAKPOINT             04756002
         DC    F'260'        INSUFFICIENT STORAGE                       04758002
MSG2     DC    F'39'         UNABLE TO ESTABLISH BREAKPOINT             04758402
         DC    F'245'        FULLY QUALIFIED ADDRESS MUST BE GIVEN      04758802
MSG3     DC    F'39'         UNABLE TO ESTABLISH BREAKPOINT             04759202
         DC    F'246'        ALL LOADNAMES MUST BE THE SAME             04759602
         SPACE                                                          04759702
ATDSPL   IKJEGSPL RTRY=RETRY2,ABNTB=DUMMYABT,MODNM=IKJEGATD,TNM=ATD     04809702
         EJECT                                                          05290002
         IKJPARMA                       PDE DSECT                       05300002
         EJECT                                                          05310002
         IKJPPL                                                         05320002
PPLEND   EQU   *                        END OF PPL                      05330002
         EJECT                                                          05340002
SAVEAREA DSECT                                                          05352002
SAVEUSER DS    F                        UNUSED                          05354002
SAVELAST DS    F                        PTR TO LAST SAVEAREA (CALLER'S) 05356002
SAVENEXT DS    F                        PTR TO NEXT SAVEAREA (SUBRTN)   05358002
RETURNPT DS    F                        PTR TO CALLER'S RESUME ADDRESS  05358402
ENTRYPT  DS    F                        PTR TO MY ENTRY POINT           05358802
GR0      DS    F                        GENERAL REGISTER 0              05359202
GR1      DS    F                        GENERAL REGISTER 1              05359602
GR2      DS    F                        GENERAL REGISTER 2              05359702
GR3      DS    F                        GENERAL REGISTER 3              05359802
GR4      DS    F                        GENERAL REGISTER 4              05359902
GR5      DS    F                        GENERAL REGISTER 5              05363202
GR6      DS    F                        GENERAL REGISTER 6              05365202
GR7      DS    F                        GENERAL REGISTER 7              05365602
GR8      DS    F                        GENERAL REGISTER 8              05366002
GR9      DS    F                        GENERAL REGISTER 9              05366402
GR10     DS    F                        GENERAL REGISTER 10             05366502
GR11     DS    F                        GENERAL REGISTER 11             05366602
GR12     DS    F                        GENERAL REGISTER 12             05369902
LSAVE    EQU   *-SAVEAREA               LENGTH OF SAVEAREA              05371902
         EJECT                                                          05373402
         IKJEGDME                                                       05376702
         SPACE 2                                                        05380002
         IKJEGDBE                                                       05390002
         EJECT                                                          05400002
TCB      IKJTCB                         EXPANSION OF TCB                05410002
         EJECT                                                          05420002
CVT      DSECT                                                          05430002
         CVT                                                            05440002
         EJECT                                                          05450002
         TCOMTAB                        COMMUNICATIONS TABLE DSECT      05460002
         EJECT                                                          05470002
         TSTCWORK                       TEST WORK AREA DSECT            05480002
         ORG   CWORKCMD                 RESET TO COMMAND WORK AREA      05490002
WORKSP   DS    0H                                                       05502002
         SPACE                                                          05504002
*********************************************************************** 05506002
*                                                                     * 05508002
*        THIS IS A MAPPING OF THE AREA CALLED CWORKCMD IN TSTCWORK.   * 05508402
*        WHEN AT IS ENTERED TO ACTIVATE DEFERED BREAKPOINTS, THE      * 05508802
*        FIRST 8 CHARACTERS OF THE WORK AREA CONTAIN THE NAME OF THE  * 05509202
*        NEWLY LOADED MODULE. THE NINTH BYTE CONTAINS A FLAG WHICH    * 05509602
*        INDICATES WHETHER OR NOT THE MODULE IS IN OVERLAY. THE       * 05509702
*        LAST 64 BYTES OF THE WORK AREA ARE RESERVED FOR A REGISTER   * 05509802
*        SAVE AREA FOR IKJEGLDF IN THIS CASE.                         * 05509902
*        IF ANY CHANGES ARE MADE TO THE WORK AREA, MAKE THE SAME      * 05519902
*        CHANGES IN IKJEGAT. BOTH MODULES USE THE SAME FORMAT         * 05529902
*        OF WORK AREA.                                                * 05539902
*                                                                     * 05559902
*********************************************************************** 05569902
PDLADDR  DS    A -                      ADDRESS OF PDL BEING            05579902
*                                       PROCESSED BY AT WHEN            05589902
*                                       ACTIVATING DEFERED BREAKPOINTS  05599902
PARSECD  EQU   PDLADDR -                NONZERO SERVICE ROUTINE         05601902
*                                       RETURN CODES SENT TO VALIDITY   05603902
*                                       CHECK ROUTINES ARE STORED HERE  05605902
BYTENUMB DS    F -                      SAVE AREA FOR NUMBER OF BYTES   05607902
*                                       LAST GOTTEN                     05608302
SAVEPTR  DS    A -                      POINTER TO PREVIOUS BREAK       05608702
*                                       ELEMENT LINK FIELD              05609102
CHNADDR  DS    A -                      ADDRESS OF SUBCOMMAND CHAIN     05609502
*                                       FOR BREAKPOINT(S) BEING BUILT   05609602
STAEL    DS    CL16          PARM LIST AREA FOR ESTAE                   05659802
STRNGADR DS    A -                      ADDRESS OF ADDRESS STRING       05669802
*                                       FOR RANGE BEING PROCESSED       05679802
         DS    0F                                                       05689802
PARMS    DS    (PPLEND-PPL)X -          PPL IS BUILT HERE               05699802
         ORG   PARMS                                                    05701802
BRKSAVE  DS    18F -                    SAVE AREA FOR BREAKRTN          05703802
SAVE2345 DS    4F            WORK SAVE AREA FOR R2-5                    05704202
         ORG   STRNGADR                                                 05705802
PDEADDR  DS    A -                      ADDRESS OF FIRST ADDRESS PDE    05707802
*                                       IN PDL - PASSED AS PARM         05708202
         ORG   WORKSP                                                   05708602
LOADNAME DS    CL8 -                    LOAD NAME OF NEWLY LOADED MOD.  05709002
*                                       FOR WHICH DEFERED BREAK-        05709402
*                                       POINTS MAY BE ACTIVATED, OR     05709502
*                                       LOAD NAME FOR WHICH DEFERED     05709602
*                                       BREAKPOINTS ARE TO BE BUILT     05709702
LOADATTR EQU   * -                      ONE BYTE FLAG INDICATING        05759702
*                                       OVERLAY STATUS FOR NEWLY        05769702
*                                       LOADED MODULE                   05779702
GETCORE  GETMAIN   EC,SP=1,MF=L -       LIST FORM OF GETMAIN            05789702
ADDRCORE EQU   GETCORE+4 -              SECOND WORD OF LIST FORM        05799702
*                                       CONTAINS ADDRESS OF CORE        05801702
         ORG   WORKSP                                                   05803702
XCTLL    XCTL  EP=IKJEGATD,SF=L -       LIST FORM OF XCTL               05805702
         ORG   STAEL                    OVERLAY STAE CONTROL BLOCK      05807702
SRHPARMS DS    0H                       BRKELEM REMOVAL ROUTINE'S       05808102
*                                       PARM LIST                       05808502
SRHFLAGS DS    F'0'                     FLAGS - 0 TO REMOVE BRKELEM     05808902
SRHADDR1 DS    A                        ADDRESS OF BRKPOINT FOR         05809302
*                                       BRKELEM TO BE REMOVED           05809402
SRHADDR2 DS    A                        SECOND ADDRESS OF RANGE OR 0    05809502
SRHPRMLH EQU   *-SRHPARMS               LENGTH OF SRH PARM LIST         05809602
         EJECT                                                          05859602
IKJEGATD CSECT                                                          05869602
         DS    0F            FORCE FULLWORD ALIGNMENT                   05879602
ATDPATCH DC    50C'Z'        PATCH AREA                                 05889602
         END                                                            05909602
