         TITLE  'IKJEGCPY -- COPY SUBCOMMAND FOR TSO/TEST'              00100002
* A 326000,366000                                            @ZA10462   00150000
*********************************************************************** 00200002
*                                                                       00300002
* STATUS --                                                             00400002
*    CHANGE LEVEL 000, VERSION 02, OS/VS2                               00500002
*                                                                       00600002
* FUNCTION --                                                           00700002
*    THIS SUBCOMMAND OF TEST RECEIVES CONTROL FROM TEST'S MAINLINE      00800002
*    MODULE(IKJEGMNL) VIA A LINK.                                       00900002
*    IT COPIES FROM STORAGE TO STORAGE , STORAGE TO REGISTERS,          01000002
*    REGISTERS TO STORAGE, AND ALSO FROM REGISTERS TO REGISTERS.        01100002
*    IT HAS AN ADDITIONAL FUNCTION OF OBTAINING POINTERS.               01200002
*    COPY HAS 2 POSITIONAL OPERANDS AND 3 KEYWORDS(LENGTH,POINTER,      01300002
*    AND NOPOINT).                                                      01400002
*    THE POINTER KEYWORD CHANGES THE MEANING OF THE FROM POSITIONAL.    01500002
*    EITHER SPECIFY --                                                  01600002
*    COPY  ADDRESS1  ADDRESS2                                           01700002
*    COPY  ADDRESS1  ADDRESS2  LENGTH(DECIMAL)                          01800002
*    COPY  ADDRESS1  ADDRESS2  POINTER                                  01900002
*    COPY  ADDRESS1  ADDRESS2  LENGTH(DECIMAL)  POINTER                 02000002
*    ADDRESS1 MAY BE AN ADDRESS OR GENERAL REGISTER.                    02100002
*    BUT, IF POINTER IS SPECIFIED IT CANNOT BE GENERAL REGISTER.        02200002
*    ADDRESS2 MAY BE AN ADDRESS OR GENERAL REGISTER.                    02300002
*    EXAMPLES OF COPY FOLLOW --                                         02400002
*    COPY  10.%%%+88%  A06A0.  L(512)                                   02500002
*    RELOCATING A TCB TO KEYED(OR GOTTEN) CORE.                         02600002
*    COPY  80680.  80681.  L(79)                                        02700002
*    PROPIGATION OF 1 BYTE THRU A BUFFER.                               02800002
*    COPY  80680.  80682.  L(44)                                        02900002
*    MOVING SOME INSTRUCTIONS AROUND. THESE FIELDS OVERLAP BUT          03000002
*    THE COPY IS DONE BACKWARDS TO AVOID PROPIGATION.                   03100002
*    COPY  0R  A0200.  L(64)                                            03200002
*    SAVING ALL REGISTERS FOR A LATER LOAD. ANY LENGTH ABOVE            03300002
*    64 WILL DEFAULT TO 64.                                             03400002
*    COPY  13R%+12N  14R  L(60)                                         03500002
*    EQUIVALENT TO A LOAD MULTIPLE FROM A SAVE AREA.                    03600002
*    COPY  15R  0R  L(8)                                                03700002
*    CONTENTS OF 15 AND 0 TO 0 AND 1                                    03800002
*    COPY  A  3R                                                        03900002
*    COPY FROM LOCATION A TO REGISTER 3 WITH A DEFAULT LENGTH OF 4      04000002
*    COPY  10.%  3R  P                                                  04100002
*    PLACES THE CVT ADDRESS INTO REGISTER 3 ZEROING THE HIGH BYTE.      04200002
*    COPY  10.  3R                                                      04300002
*    THIS WILL DO THE SAME AS ABOVE WITHOUT THE HIGH BYTE ZEROED.       04400002
*    COPY  80.  1R  L(1)  P                                             04500002
*    THIS WILL PUT A X'80' INTO THE HIGH BYTE OF R1,LOW 3 REMAIN.       04600002
*    COPY  40.  A  L(1)  P                                              04700002
*    THIS WILL PUT A BLANK AT LOCATION 'A'                              04800002
*    COPY  A  A+1  L(79)                                                04900002
*    THIS WILL PROPIGATE THE ABOVE BLANK THRU A BUFFER.                 05000002
*    COPY  IEFBR14.IEFBR14  15R  P                                      05100002
*    THIS WILL PLACE THE ENTRY POINT OF IEFBR14 INTO R15.               05200002
*    COPY  6B000.  A+1  L(3)  P                                         05300002
*    A+1 WILL NOW CONTAIN THE 3 BYTE ADDRESS X'06B000'                  05400002
*    COPY  0.  0R  L(8)                                                 05500002
*    THIS WILL NOT ZERO BOTH 0R AND 1R , THE POINTER KEYWORD DEFAULTS   05600002
*    TO A MAXIMUM OF 4.  ONLY REG 0 WAS CLEARED.                        05700002
*                                                                       05800002
* ENTRY POINTS --                                                       05900002
*    IKJEGFM  -- FROM PARSE FOR 'FROM' POSITIONAL.                      06000002
*    IKJEGTO  -- FROM PARSE FOR 'TO' POSITIONAL.                        06100002
*    IKJEGLN  -- FROM PARSE FOR 'LENGTH' KEYWORD.                       06200002
*    STAERTRY -- AFTER AN ABEND                                         06300002
*                                                                       06400002
* INPUT --                                                              06500002
*    A LINE OF INPUT.                                                   06600002
*                                                                       06700002
* OUTPUT --                                                             06800002
*    DIAGNOSTIC MESSAGES UPON ERROR.                                    06900002
*                                                                       07000002
* EXTERNAL REFERENCES --                                                07100002
*    IKJPARS(LINK)                                                      07200002
*    IKJEGCVT(BALR) TSTCONVT IN TCOMTAB.                                07300002
*    IKJEGBLD(BALR) TSTADDR IN TCOMTAB.                                 07400002
*    IKJEGIO(BALR) USING THE IKJEGSIO MACRO.                            07500002
*                                                                       07600002
* EXITS, NORMAL --                                                      07700002
*    RETURN TO MAINLINE(IKJEGMNL) RC OF 0-NORMAL,16-ATTENTION.          07800002
*                                                                       07900002
* EXITS, ERROR --                                                       08000002
*    RETURN TO MAINLINE(IKJEGMNL) RC OF 20-ABEND,24-ESTAE COULDNT BE    08100002
*    ISSUED.                                                            08160002
*                                                                       08200002
* TABLES/WORK AREAS --                                                  08300002
*    TCOMTAB-TEST'S COMMUNICATION TABLE.                                08400002
*    WA- COPY'S WORKAREA.                                               08500002
*                                                                       08600002
* ATTRIBUTES --                                                         08700002
*    REENTRANT-REFRESHABLE.                                             08800002
*                                                                       08900002
* CHARACTER CODE DEPENDENCY --                                          09000002
*    REASSEMBLE IS REQUIRED FOR CHANGE OF CODE.                         09100002
*                                                                       09200002
* NOTES --                                                              09300002
*    REGISTER CONVENTIONS                                               09400002
*    R0, R1, R3, R4, R5 ARE QUICKY WORK REGS.                           09500002
*    R2 PDL ADDRESS(MAIN ROUTINE), PDE ADDRESS(VALIDITY CK EXIT)        09600002
*    R6 USED AS WORK AREA IN ADDRESS VALIDITY CHECK ROUTINE.            09700002
*    R7 IS PARAMETER REG FOR ADDRRESS VALIDITY CHECK ROUTINE.           09800002
*    R8 IS THE VALIDITY LIST PASSED FROM IKJPARS.                       09900002
*    R9 IS TCOMTAB ADDRESS.                                             10000002
*    R10 IS WA ADDRESS. (COPY'S WORK AREA)                              10100002
*    R11 IS USED AS AN INTERNAL LINK REGISTER ONLY.                     10200002
*    R12 IS THE BASE REGISTER.                                          10300002
*    THE HIGH BYTE IN REGISTER 12 INDICATES WHICH VALIDITY EXIT IS      10400002
*    EXECUTING.                                                         10500002
*    PLM = SY35-0004                                                    10550002
*    *     TSO COMMAND PROCESSOR LOGIC                                  10560002
*    *     VOLUME III - TEST                                            10570002
* CHANGE-ACTIVITY -                                                     10580000
*                 APAR OZ10462 FIXES FETCHING AND STORING OF            10590000
*                 REGISTERS IN THE INCORRECT RB WHEN AN                 10600000
*                 ASYNCHRONOUS EXIT CAUSES AN IRB TO BE QUEUED          10610000
*                 ON THE TCB/RB CHAIN.  THE FIX IS AROUND LABELS        10620000
*                 SKIP2 AND CHECKTO.  THE SVC 97 SVRB ADDRESS           10630000
*                 IS OBTAINED FROM ECBPP+1.                             10640000
*********************************************************************** 10700002
         SPACE                                                          10800002
         EJECT                                                          10900002
         COPY  IKJEGSIO                                                 10950002
*        EQUATES                                                        11000002
         SPACE                                                          11100002
R0       EQU   0              REG.  0                                   11200002
R1       EQU   1              REG.  1                                   11300002
R2       EQU   2              REG.  2                                   11400002
R3       EQU   3              REG.  3                                   11500002
R4       EQU   4              REG.  4                                   11600002
R5       EQU   5              REG.  5                                   11700002
R6       EQU   6              REG.  6                                   11800002
R7       EQU   7              REG.  7                                   11900002
R8       EQU   8              REG.  8                                   12000002
R9       EQU   9              REG.  9                                   12100002
R10      EQU   10             REG. 10                                   12200002
R11      EQU   11             REG. 11                                   12300002
R12      EQU   12             REG. 12                                   12400002
R13      EQU   13             REG. 13                                   12500002
R14      EQU   14             REG. 14                                   12600002
R15      EQU   15             REG. 15                                   12700002
         SPACE                                                          12800002
X0       EQU   0              NO.  0                                    12900002
X1       EQU   1              NO.  1                                    13000002
X2       EQU   2              NO.  2                                    13100002
X3       EQU   3              NO.  3                                    13200002
X4       EQU   4              NO.  4                                    13300002
X5       EQU   5              NO.  5                                    13400002
X6       EQU   6              NO.  6                                    13500002
X7       EQU   7              NO.  7                                    13600002
X8       EQU   8              NO.  8                                    13700002
X9       EQU   9              NO.  9                                    13800002
N10      EQU   10             NO. 10                                    13900002
N11      EQU   11             NO. 11                                    14000002
N12      EQU   12             NO. 12                                    14100002
N13      EQU   13             NO. 13                                    14200002
N14      EQU   14             NO. 14                                    14300002
N15      EQU   15             NO. 15                                    14400002
N16      EQU   16             NO. 16                                    14500002
N24      EQU   24             NO. 24                                    14600002
N28      EQU   28             NO. 28                                    14700002
N64      EQU   64             NO. 64                                    14800002
N80      EQU   80             NO. 80                                    14900002
N256     EQU   256            NO. 256                                   15000002
RC8      EQU   8              RETURN CODE 08                            15100002
RC12     EQU   12             RETURN CODE 12                            15200002
RC16     EQU   16             RETURN CODE 16                            15300002
RC20     EQU   20             RETURN CODE 20                            15400002
CVTPTR   EQU   16             LOCATION OF CVT POINTER                   15500002
CPYCODE  EQU   0              COPY CODE                                 15600002
FMCODE   EQU   1              FM CODE                                   15700002
TOCODE   EQU   FMCODE+X1      TO CODE                                   15800002
LNCODE   EQU   TOCODE+X1      LN CODE                                   15900002
PTRKEY   EQU   1              PARSE CODE FOR POINTER KEYWORD.           16000002
BLANK    EQU   C' '           BLANK                                     16100002
CHARF    EQU   C'F'           F                                         16200002
CHART    EQU   C'T'           T                                         16300002
PLUS     EQU   C'+'           PLUS SIGN.                                16400002
TESTSVC  EQU   97             TEST SVC NUMBER                           16500002
X80      EQU   X'80'          HEXIDECIMAL 80                            16600002
XFF      EQU   X'FF'          X'FF'                                     16700002
* FIRST LEVEL MESSAGE EQUATES                                           16750002
M0064    EQU   64             INVALID LENGTH                            16760002
M0066    EQU   66             INVALID FROM ADDRESS                      16770002
M0072    EQU   72             INVALID TO ADDRESS                        16780002
M0101    EQU   101            INVALID ADDRESS ...                       16790002
M0103    EQU   103            NOT ENOUGH MAIN STORAGE                   16792002
M0104    EQU   104            COPY FAILED                               16794002
* SECOND LEVEL MESSAGE EQUATES                                          16796002
M0266    EQU   266            LEN MAY NOT EXCEED 64                     16798002
M0267    EQU   267            REG NOT. MUST BE GEN REG                  16798402
M0268    EQU   268            REG NOT. MUST BE INDIRECT                 16798802
M0277    EQU   277            PARSE ERROR CODE 12                       16799202
M0279    EQU   279            ADDRESS IN WRITE PROT. CORE               16799302
M0283    EQU   283            SVC 97 RET CODE NONZERO                   16799602
M0284    EQU   284            LEN MUST BE BTWN 0 AND 65535              16799702
* CONSTANT MESSAGE INSERT EQUATES                                       16799902
I0030    EQU   30             COPY                                      16833202
I0040    EQU   40             IKJPARS                                   16843202
I0099    EQU   99             BLOW CODE IN ABENDTABLE                   16853202
I0097    EQU   97             BLOW CODE IN ABENDTABLE                   16863202
         EJECT                                                          16866702
*        DSECTS                                                         16900002
         SPACE                                                          17000002
WA       DSECT                WORK AREA DSECT                           17100002
WALIST   DS    0A             PARSE PARAMETER LIST.                     17200002
WAUPT    DS    1A             UPT POINTER.                              17300002
WAECT    DS    1A             ECT POINTER                               17400002
WAECBPTR DS    1A             ATTENTION ECB POINTER.                    17500002
WAPRM    DS    1A             PARM BLOCK POINTER.                       17600002
WANSWER  DS    1A             PTR TO TSTANSPL                           17700002
WACODE   DS    0X             VALIDITY KEYWORD CODE.                    17800002
WAINBUF  DS    1A             INPUT BUFFER TO BE PARSED.                17900002
WASAVERC DS    0X             ONE BYTE RETURN CODE FORM CVT,SYM OR IO   18000002
WAPASS   DS    1A             PARM TO BE PASSED TO VALIDITY EXIT.       18100002
WAFROM   DS    1A             FROM ADDRESS OR FROM REGISTER.            18200002
WATO     DS    1A             TO ADDRESS OR TO REGISTER.                18300002
WALENGTH DS    1A             DEFAULT OR LENGTH SPECIFIED BY THE USER.  18400002
WATEMP   DS    3F             TEMPORARY W.A. FOR SVC 97 AND SEARCH RTN  18450002
* THREE WORD AREA USED BY SRH RTN TO PASS PARAMETERS                    18460002
         ORG   WATEMP         ORG TO NAME EACH WORD                     18500002
WATEMP1  DS    F              FIRST WORD                                18550002
WATEMP2  DS    F              SECOND WORD                               18600002
WATEMP3  DS    F              THIRD WORD                                18650002
WAFMREG  DS    16A            SAVE AREA FOR FROM REGISTER(S)            18700002
WATOREG  DS    16A            SAVE AREA FOR NEW REGISTERS               18800002
WAEND    EQU   *-WA           LENGTH OF WORKAREA.                       18900002
         SPACE                                                          19000002
VAL      DSECT                                                          19100002
VALPDE   DS    1A             PDE PASSED FROM PARSE.                    19200002
VALPASS  DS    1A             PARM PASSED BY MAIN ROUTINE.              19300002
VAL2LVL  DS    1A             2ND LEVEL MESSAGE PTR FOR PARSE.          19400002
         SPACE 2                                                        19500002
RB       DSECT                                                          19600002
RBGRSAVE EQU   RB+32                                                    19700002
         EJECT                                                          19800002
IKJEGCPY CSECT                                                          19900002
*        MAIN PROCESSOR CODE                                            20000002
         SPACE                                                          20100002
         USING *,R12          ADDRESSABILITY                            20200002
         USING TCOMTAB,R9     ADDRESSABILITY                            20300002
         USING WA,R10         ADDRESSABILITY                            20400002
         USING IKJPARMD,R2    ADDRESSABILITY                            20450002
         USING BRKELEM,R6     ADDRESSABILITY                            20500002
         SAVE  (14,12),,*     SAVE MAINLINES REGISTERS.                 20600002
         LR    R12,R15        SETUP BASE REG                            20700002
         L     R3,REGSAVE2    SET FOWARD AND BACKWD POINTERS            20800002
         ST    R13,X4(R3)     DITTO                                     20900002
         ST    R3,X8(R13)     DITTO                                     21000002
         LR    R13,R3         SET UP MY SAVE                            21100002
         L     R4,WORKAREA    GET GENERAL WORK AREA PTR.                21200002
         LA    R10,CWORKCMD-TSTCWORK(R4)  SET UP CPY'S W.A. PTR         21300002
         L     R3,TSTSTAE     GET ADDRESS OF TEST STAE EXIT (STA)       21700002
         STM   R14,R12,N12(R13)  SAVE REGS IF ABEND OCCURS.             21800002
         LR    R1,R10         STAE LIST PTR FOR STAE SVC                21900002
         MVC   X0(STAELSTL,R10),STAELIST  MOVE IN LIST                  22000002
         ESTAE (R3),PARAM=LISTESTA,RECORD=YES,MF=(E,(R1))  ISSUE ESTAE  22150002
         LTR   R15,R15        Q.GOT AN S.C.B.                           22200002
         BZ    ESTAEOK        RC=0 FROM ESTAE, ALL OK                   22210002
         STC   R15,TSTESTRC   STORE RET CODE                            22220002
         LA    R15,N24        SEND RC=24 TO MAINLINE                    22230002
         B     MNLRETS        BRANCH TO RETURN                          22240002
         SPACE                                                          22400002
*        WALENGTH IS INITIALIZED.                                       22500002
*        WALENGTH = X'00000004'                                         22600002
*        WHEN THE LENGTH IS NOT SPECIFIED, A DEFAULT OF 4 IS TAKEN.     22700002
*        COPY  A1680.  ADDR2 -- WILL CAUSE WAFROM TO BE X'000A1680'     22800002
*        COPY  3R      ADDR2 -- WILL CAUSE WAFROM TO BE X'80000003'     22900002
*        WATO IS HANDLED THE SAME AS WAFROM.                            23000002
*        IF A REGISTER IS SPECIFIED IN EITHER THE 'FROM' OR             23100002
*        THE 'TO' POITIONALS, A MAXIMUM LENGTH OF 64 MAY                23200002
*        BE DEFAULTED TO.                                               23300002
*        ANYTIME POINTER IS SPECIFIED, A MAXIMUM LENGTH OF 4 MAY        23400002
*        BE DEFAULTED TO.                                               23500002
         SPACE                                                          23600002
ESTAEOK  LA    R0,X4          SET DEFAULT LENGTH OF 4                   23700002
         ST    R0,WALENGTH    DITTO                                     23800002
PARSE    EQU   *              GOING TO PARSE.                           23900002
         LR    R1,R10         SET PARM REGISTER.                        24000002
         MVC   WALIST(N12),PARMLIST  SETUP LIST.                        24100002
         MVC   WAPRM(X4),PRMAD  DITTO                                   24200002
         LA    R5,TSTANSPL    DITTO                                     24300002
         ST    R5,WANSWER     DITTO                                     24400002
         MVC   WAINBUF(X4),INBUF  DITTO                                 24500002
         ST    R13,WAPASS     PARM TO BE PASSED TO VALIDITY EXIT.       24600002
         LINK  EP=IKJPARS,ERRET=LINKERR     GO TO PARSE.                24700002
BLOWPARS EQU   *              IF LINK FAILS ABEND WILL OCCUR HERE.      24800002
FROMPARS EQU   *              COMING FROM PARSE.                        24900002
         L     R2,TSTANSPL    GET THE PDL ADDRESS.                      25000002
         B     TABLE1(R15)    BRANCH TABLE.                             25100002
TABLE1   B     AOK            ALL O.K. MAYBE.                           25200002
         B     NOPROMPT       NOPROMPT MODE. QUIT.                      25300002
         B     ATTN           RET TO MNL WITH A CODE OF 16              25400002
         B     BADPARMS       PRINT 'PARSE ERROR CODE 12' RETURN.       25500002
         B     NOCORE         PRINT 'NOT ENOUGH CORE' RETURN.           25600002
         B     CHECKRC        A RET. CODE HAS BEEN PASSED BY EXIT RTN.  25700002
         SPACE                                                          25800002
         SPACE                                                          25900002
CHECKRC  EQU   *              OBTAIN THE PASSED RET. CODE FROM VAL EXIT 26000002
         IC    R15,WASAVERC   R15= RET. CODE.                           26100002
         CH    R15,ATTNCODE   COMPARE TO CODE 16                        26150002
         BL    NOPROMPT       IF < 16, SET TO 0 WITH TSTFLUSH           26200002
         B     MNLRET         ELSE RETURN WITH CODE PASSED (16 OR 20)   26250002
NOCORE   EQU   *              PRINT 'NOT ENOUGH CORE' MSG               26300002
         LA    R2,M0103       MSG - NOT ENOUGH CORE                     26350002
         LA    R3,X0          NO SECOND LEVEL MSG                       26400002
         BAL   R11,MSG1       GO OUTPUT MESSAGE                         26450002
         B     MNLRET0        RETURN TO MNL                             26600002
BADPARMS EQU   *              PRINT 1ST AND 2ND LEVEL MESSAGES          26700002
         LA    R2,M0104       MSG - COPY FAILED                         26800002
         LA    R3,M0277       MSG - PARSE ERROR                         26850002
         BAL   R11,MSG2       GO OUTPUT MESSAGE                         26900002
         B     MNLRET0        RETURN TO MNL.                            27000002
ATTN     EQU   *              RETURN TO MNL WITH CODE OF 16.            27100002
         LA    R15,RC16       DITTO                                     27200002
         B     MNLRET         RETURN.                                   27300002
NOPROMPT EQU   *                                                        27400002
         OI    TSTFLGS4,TSTFLUSH  SET THE FLUSH BIT                     27500002
         B     MNLRET0        RETURN TO MAINLINE.                       27600002
AOK      EQU   *              ALL O.K. MAYBE.                           27700002
         L     R5,WALENGTH    CHECK LENGTH FOR ZERO.                    27800002
         LTR   R5,R5          Q. WAS ZERO ENTERED BY USER.              27900002
         BZ    MNLRET0        A. YES USERS WISH IS GRANTED.             28000002
         CLI   PRMP+X1,PTRKEY  Q.IS POINTER SPECIFIED.                  28100002
         BNE   NOPOINT        NO.                                       28200002
         TM    WAFROM,X80     Q.IS REGISTER SPECIFIED.                  28300002
         BZ    POINT          NO. THIS IS LEGAL.                        28400002
         LA    R2,M0066       MSG - INVAL FROM ADDR.                    28500002
         LA    R3,M0268       MSG - INDIRECT GEN REG                    28550002
         BAL   R11,MSG1       GO OUTPUT MESSAGES                        28600002
         B     NOPROMPT       SET FLUSH BIT AND RETURN.                 28700002
* IF POINTER IS SPECIFIED, THE VALUE SPECIFIED AS ADDRESS1 IS MOVED     28800002
* INTO 'WAFMREG'(USED AS A WORK AREA) AND THE ADDRESS OF WAFMREG IS     28900002
* PLACED IN WAFROM.  A MAXIMUM LENGTH OF 4 MAY BE SPECIFIED. IF THE     29000002
* LENGTH IS GREATER THAN 4, A LENGTH OF 4 IS ASSUMED. IF THE LENGTH     29100002
* IS LESS THAN 4 (=N), THEN ONLY THE 1ST N BYTES OF THE 4 BYTE VALUE    29200002
* SPECIFIED WILL BE USED. THEY WILL BE RIGHT JUSTIFIED IN THE FIRST     29300002
* 4 BYTES OF WAFMREG.                                                   29400002
POINT    EQU   *              POINTER WAS SPECIFIED                     29500002
         LA    R0,X4          Q. LENGTH HIGHER THAN DEFAULT.            29600002
         CR    R0,R5          *                                         29700002
         BH    SKIP1          NO. DONT DEFAULT.                         29800002
         ST    R0,WALENGTH    DEFAULT TO LENGTH OF 4                    29900002
         LR    R5,R0          NEW LENGTH TO R5                          30000002
SKIP1    EQU   *                                                        30100002
         LA    R1,WAFROM      GET POINTER TO ADDRESS.                   30200002
         BCTR  R1,X0          INITIALIZE FOR BUMP.                      30300002
BUMPONE  EQU   *                                                        30400002
         LA    R1,X1(R1)      BUMP ONE.                                 30500002
         CR    R0,R5          Q.MOVE NOW OR BUMP.                       30600002
         LA    R5,X1(R5)      ADD ONE TO THE LENGTH.                    30700002
         BNE   BUMPONE        BUMP UNTIL COUNT REACHES FOUR.            30800002
         MVI   WAFROM,X0      CLEAR THE HIGH BYTE.                      30900002
         MVC   WAFMREG(X4),X0(R1)   MOVE POINTER ADDRESS.               31000002
         LA    R1,WAFMREG     PLACE ADDRESS OF SLOT 0 TO WAFROM         31100002
         ST    R1,WAFROM      DITTO.                                    31200002
         B     CHECKTO        CHECK IF 'TO' WAS ENTERED.                31300002
NOPOINT  EQU   *                                                        31400002
         TM    WAFROM,X80     Q.IS IT A REGISTER.                       31500002
         BZ    VALCHKFM       NO. IT IS AN ADDRESS.                     31600002
REGISTER EQU   *                                                        31700002
         LA    R0,N64         REGISTER WAS ENTERED.                     31800002
         CR    R0,R5          Q.LENGTH GREATER THAN 64.                 31900002
         BH    SKIP2          A. NO. DO NOT DEFAULT.                    32000002
         ST    R0,WALENGTH    DEFAULT TO 64.                            32100002
SKIP2    EQU   *                                                        32200002
         L     R1,WAFROM      OBTAIN FROM REGISTER NUMBER.              32300002
         SLL   R1,X2          LOSE HIGH BIT. AND MULT BY 4.             32400002
         SR    R0,R1          R0=64-(REG NUMX4)                         32500002
         L     R3,ECBPP       ADDRESSIBILITY TO SVRB REG SA  @ZA10462   32600000
         LA    R3,RBGRSAVE-RB(R3)  DITTO.                               32800002
         LA    R4,WAFMREG     ADDRESSIBILITY TO FM REG SAVE AREA.       32900002
         AR    R4,R0          POINTER TO SLOT WHERE REGS WILL BE MOVED  33000002
         LTR   R5,R1          Q. WAS R0 SPECIFIED.                      33100002
         BZ    MOVE2          BYPASS THIS MOVE. ONLY 1 MOVE REQD.       33200002
         SPACE                                                          33300002
*        AT THIS POINT, ASSUME  COPY  3R  ADDR2 L(64),WAS SPECIFIED.    33400002
*        REGS 0,1,2 ARE MOVED FROM SVRB REG SAVE TO THE LAST 3 SLOTS    33500002
*        OF WAFMREG SAVE AREA.                                          33600002
         SPACE                                                          33700002
         BCTR  R5,X0          MOVE THE FIRST PORTION OF REGISTERS.      33800002
         EX    R5,MVC         MOVE THEM.                                33900002
MOVE2    EQU   *              2ND MOVE UNLESS 0R WAS SPECIFIED.         34000002
         AR    R3,R1          POINTER TO REG SLOT IN SVRB.              34100002
         LA    R4,WAFMREG     SET POINTER TO BEGINNING OF FM REG AREA.  34200002
         LR    R5,R0          LENGTH OF 2ND PORTION TO BE MOVED.        34300002
         SPACE                                                          34400002
*        AT THIS POINT, ASSUME  COPY  3R  ADDR2 L(64),WAS SPECIFIED.    34500002
*        REGS 3,4,5,6,7,8,9,10,11,12,13,14,15 ARE MOVED FROM            34600002
*        SVRB REG SAVE TO THE FIRST 13 SLOTS OF WAFMREG SAVE AREA.      34700002
         SPACE                                                          34800002
         BCTR  R5,X0          DECREMENT FOR MOVE.                       34900002
         EX    R5,MVC         MOVE THE 2ND PORTION.                     35000002
         ST    R4,WAFROM      WAFROM= POINTER TO WAFMREG                35100002
         B     CHECKTO        CHECK 'TO' ADDRESS                        35150002
VALCHKFM EQU   *                                                        35200002
         SPACE                                                          35300002
         L     R3,WAFROM      OBTAIN FROM ADDRESS.                      35400002
         L     R5,WALENGTH    OBTAIN THE LENGTH.                        35500002
         LR    R7,R3          PUT ADDRESS TO BE CHECKED IN R7.          35600002
         BAL   R11,ADDRCHK    VALIDITY CHECK ROUTINE                    35700002
         LR    R15,R8         RET CODE TO REG 15                        35750002
         LA    R8,X8          REG 8 IS 8                                35752002
         CR    R15,R8         COMPARE RET CODE TO 8                     35754002
         BL    CHECKTO        0 OR 4 ARE OK                             35756002
         BH    MNLRET         HAVE RET CODE GREATER THAN 8              35758002
         LA    R2,M0066       MSG - INVALID FROM ADDR                   35900002
         LA    R3,M0279       MSG - ADDR IN PROT. CORE                  35950002
         BAL   R11,MSG1       GO OUTPUT MESSAGES                        36000002
         B     NOPROMPT       SET FLUSH BIT AND QUIT.                   36100002
CHECKTO  EQU   *                                                        36200002
         TM    WATO,X80       Q. IS IT ADDRESS OR REGISTER.             36300002
         BZ    COPY2          A. IT'S AN ADDRESS . COPY IT.             36400002
*  COPY IS TO REGISTERS                                                 36500002
         L     R3,ECBPP        ADDRESSABILITY TO SVRB REG SA  @ZA10462  36600000
         LA    R3,RBGRSAVE-RB(R3)  DITTO.                               36800002
         LA    R4,WATOREG     ADDRESSABILITY TO 'TO' REG SAVE AREA.     36900002
         LA    R5,N64-X1      MOVE ALL REGS TO 'TO' SAVE AREA.          37000002
         EX    R5,MVC         MOVE THEM.                                37100002
         LA    R0,N64         CHECK FOR LENGTH LARGER THAN 64.          37200002
         C     R0,WALENGTH    Q. LARGER THAN 64.                        37300002
         BH    SKIP5          NO. BYPASS DEFAULT.                       37400002
         ST    R0,WALENGTH    DEFAULT TO 64.                            37500002
SKIP5    EQU   *                                                        37600002
         L     R1,WATO        GET REGISTER SPECIFIED.                   37700002
         SLL   R1,X2          DROP HIGH BIT. AND MULT X 4.              37800002
         SR    R0,R1          64-(REG NUMX4)= LNG 2ND PORTION           37900002
         L     R3,WAFROM      OBTAIN FROM ADDRESS.                      38000002
         SPACE                                                          38100002
*        AT THIS POINT , WAFROM WILL ALWAYS CONTAIN AN ADDRESS.         38200002
         SPACE                                                          38300002
         LA    R4,WATOREG     ADDRESSIBILITY TO 'TO' REG SAVE AREA.     38400002
         AR    R4,R1          POINT TO REG SLOT OF SPECIFIED REG.       38500002
         L     R5,WALENGTH    OBTAIN LENGTH TO BE MOVED.                38600002
         CR    R5,R0          Q. IS LENGTH GREATER THAN 2ND PORTION OF  38700002
*                             'TO' SAVE AREA.                           38800002
         BH    MORE           YES. TWO MOVES REQUIRED.                  38900002
         BCTR  R5,X0          ONE MOVE. LENGTH SPECIFIED.               39000002
BLOW1A   EQU   *              PROTECTION CHECK HERE, IF ABEND.          39050002
         EX    R5,MVC         MOVE IT.                                  39100002
BLOW1    EQU   *              PROTECTION CHECK HERE, IF ABEND.          39110002
         BAL   R8,SRH0        CALL SEARCH ROUTINE                       39150002
         B     COPYR          CHANGE REGS. ALL OF THEM.                 39200002
MORE     EQU   *              2 MOVES REQUIRED.                         39300002
         LR    R5,R0          OBTAIN LENGTH OF SECOND PORTION(1ST MOVE) 39400002
         BCTR  R5,X0          DECREMENT FOR EXECUTE.                    39500002
BLOW2A   EQU   *              PROTECTION CHECK HERE, IF ABEND.          39550002
         EX    R5,MVC         MOVE IT.                                  39600002
BLOW2    EQU   *              PROTECTION CHECK HERE, IF ABEND.          39610002
         BAL   R8,SRH0        CALL SEARCH ROUTINE                       39650002
         AR    R3,R0          UPDATE FROM ADDRESS FOR 2ND MOVE.         39700002
         LA    R4,WATOREG     2ND MOVE GOES TO BEGINNING OF WATOREG     39800002
         L     R5,WALENGTH    OBTAIN LENGTH OF 2ND MOVE.                39900002
         SR    R5,R0          DITTO.                                    40000002
         BCTR  R5,X0          DECREMENT FOR MOVE.                       40100002
BLOW3A   EQU   *              PROTECTION CHECK HERE, IF ABEND.          40150002
         EX    R5,MVC         MOVE IT.                                  40200002
BLOW3    EQU   *              PROTECTION CHECK HERE, IF ABEND.          40210002
         BAL   R8,SRH0        CALL SEARCH ROUTINE                       40250002
* INVOKE SVC 97 TO MOVE REGS FROM WORK AREA (WATOREG) TO PP TCB.        40300002
COPYR    EQU   *              CHANGE ALL REGISTERS.                     40400002
         L     R3,PPTCB       ADDR OF TCB                               40450002
         LA    R4,WATOREG     POINTER TO NEW REGS.                      40500002
         IKJEGS9G AGPR,TCBADDR=(R3),VALUE=(R4),MF=(E,WATEMP)            40550002
         LTR   R15,R15        IF CHANGE WAS SUCCESSFUL,                 41000002
         BZ    MNLRET0        RETURN TO MAINLINE.                       41100002
         LA    R2,N12         REG 2 = 12 FOR COMPARISON                 41150002
         CR    R15,R2         COMPARE RET CODE TO 12                    41160002
         BH    MNLRET         GREATER THAN 12, RETURN TO MNL            41180002
         LA    R2,M0104       MSG - COPY FAILED                         41200002
         LA    R3,M0283       MSG - SVC 97 RET CODE NONZERO             41250002
         BAL   R11,MSG2       GO OUTPUT MESSAGES                        41300002
         B     NOPROMPT       RETURN TO MAINLINE.                       41400002
         SPACE                                                          41500002
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *       41600002
*   - THE 'TO' ADDRESS IS NOT A REGISTER -                              41700002
*        CHECK FOR OVERLAPPING OF FIELDS                                41800002
*        THE ONLY PERMISSABLE OVERLAP IS                                41900002
*        IF THE 'TO' ADDRESS IS ONE HIGHER THAN THE 'FROM' ADDRESS      42000002
*        IF FIELDS DO OVERLAP THEN THE MOVE IS DONE BACKWARDS.          42100002
         SPACE                                                          42200002
COPY2    EQU   *              SHOULD COPY BE BACKWARDS OR FORWARDS.     42300002
         L     R3,WAFROM      OBTAIN FROM ADDRESS.                      42400002
         L     R4,WATO        OBTAIN TO ADDRESS.                        42500002
         L     R5,WALENGTH    OBTAIN LENGTH.                            42600002
         SPACE                                                          42700002
*        R3= 'FROM' ADDRESS                                             42800002
*        R4= 'TO' ADDRESS                                               42900002
*        R5= 'LENGTH'                                                   43000002
         SPACE                                                          43100002
         LR    R7,R4          PUT ADDRESS TO BE CHECKED IN R7.          43200002
         BAL   R11,ADDRCHK    ADDRESS VALIDITY CHECK ROUTINE            43300002
         LTR   R15,R8         TEST RETURN CODE                          43350002
         BZ    GOODTO         IF ZERO, ALL OK SO FAR                    43360002
         LA    R8,N12         REG 8 IS 12 FOR COMPARISON                43410002
         CR    R15,R8         COMPARE RET CODE TO REG 8                 43460002
         BH    MNLRET         GREATER THAN 12, BACK TO MNL              43470002
COPY2B   LA    R2,M0072       MSG - INVALID TO ADDR                     43500002
         LA    R3,M0279       MSG - ADDR IN PROT. CORE                  43550002
         BAL   R11,MSG1       GO OUTPUT MESSAGES                        43600002
         B     NOPROMPT       QUIT. AND RETURN TO MNL.                  43700002
GOODTO   EQU   *                                                        43800002
* REMOVE BREAK POINTS FROM 'TO' AREA BEFORE COPY.                       43850002
         LA    R1,X8          SEARCH AND REMOVE WITH BACKUP             43860002
         ST    R1,WATEMP1     1ST WORD - OPTION CODE                    43870002
         ST    R4,WATEMP2     2ND WORD - START OF RANGE                 43880002
         LA    R1,X0(R4,R5)   START PLUS LENGTH OF RANGE                43890002
         BCTR  R1,X0          BACK UP ONE                               43892002
         ST    R1,WATEMP3     3RD WORD - END OF RANGE                   43894002
         LA    R1,WATEMP      ADDRESS OF PARM LIST                      43896002
         L     R15,TSTSRHRT   ADDRESS OF SEARCH ROUTINE                 43898002
         BALR  R14,R15        CALL SEARCH                               43898402
         CH    R15,H8         CHECK RETURN CODE                         43898802
         BH    NOFREE         IF ERROR, RETURN                          43899202
         SPACE                                                          43900002
         CR    R3,R4          Q. IS FROM LOWER THAN TO ADDRESS.         44000002
         BNL   COPY           NO. CANT OVERLAP.                         44100002
         LA    R3,X1(R3)      ADD ONE TO FROM ADDRESS.                  44200002
         CR    R3,R4          Q.IS TO ADDRESS ONLY ONE HIGHER THAN FROM 44300002
         BCTR  R3,X0          RESET FROM ADDRESS.                       44500002
         BE    COPY0          YES. CHECK FOR BREAKPOINT                 44550002
         AR    R3,R5          GET LAST BYTE OF FROM.                    44600002
         CR    R3,R4          Q. IS THIS BYTE MORE THAN TO ADDR.        44700002
         BNH   COPY           NO. COPY FOWARD.                          44800002
         SPACE                                                          44900002
*        BACKWARD COPY NOW IN PROGRESS.                                 45000002
*        AT THIS POINT THERE ARE OVERLAPPING FIELDS AND THE 'TO'        45100002
*        ADDRESS IS MORE THAN 1 GREATER THAN THE 'FROM' ADDRESS.        45200002
*        THE MOVE BACKWARDS LOOP IS KEPT TIGHT FOR SPEED.               45300002
*        CONSIDERATION HAS BEEN GIVEN TO SMALL LENGTHS RATHER THAN      45400002
*        LARGE.                                                         45500002
         SPACE                                                          45600002
         AR    R4,R5          OBTAIN LAST BYTE OF TO ADDRESS.           45700002
COPYLEFT EQU   *              COPY BACKWARDS.                           45800002
         BCTR  R3,X0          R3=ADDR-1                                 45900002
         BCTR  R4,X0          R4=ADDR-1                                 46000002
BLOW4A   EQU   *              PROTECTION CHECK HERE, IF ABEND.          46050002
         MVC   X0(X1,R4),X0(R3) MOVE ONE BYTE AT A TIME.                46100002
BLOW4    EQU   *              PROTECTION CHECK HERE, IF ABEND.          46200002
         BCT   R5,COPYLEFT    LOOP UNTIL LENGTH IS EXHAUSTED.           46300002
         B     COPY1B         COPY COMPLETE. CALL SEARCH RTN.           46400002
         SPACE 1                                                        46450002
* IF THERE IS A ONE BYTE OVERLAP, THE SEARCH ROUTINE IS CALLED          46460002
* BEFORE THE COPY IS DONE.  IF THE 1ST BYTE OF THE 'FROM' RANGE         46470002
* CONTAINS A BREAKPOINT, THE ORIGINAL VALUE OF THAT BYTE IS             46480002
* PLACED IN THE 1ST BYTE OF THE 'TO' RANGE.  THE 'TO' ADDRESS           46490002
* AND 'FROM' ADDRESS ARE BUMPED BY ONE AND THE LENGTH IS DECRE-         46492002
* MENTED BY ONE.                                                        46494002
COPY0    LA    R1,X4          CODE = SEARCH + LOCATE, NO BACKUP         46498002
         ST    R1,WATEMP1     * 1ST WORD OF PARM LIST                   46498402
         ST    R3,WATEMP3     * 3RD WORD OF PARM LIST (END RANGE)       46499202
         BCTR  R3,X0          BACK UP ONE                               46499302
         ST    R3,WATEMP2     * 2ND WORD OF PARM LIST (START RANGE)     46499602
         LA    R1,WATEMP      ADDRESS OF PARM LIST                      46499702
         L     R15,TSTSRHRT   ADDRESS OF SEARCH ROUTINE                 46499802
         BALR  R14,R15        CALL SEARCH ROUTINE                       46499902
         CH    R15,H4         IF ERROR OCCURRED                         46533202
         BH    NOFREE         RETURN TO MAINLINE                        46543202
         BE    COPY           IF NOT FOUNT GO DO COPY                   46553202
         L     R6,X4(R1)      GET ADDR OF BREAK ELEMENT                 46563202
         L     R7,BRKADDR     GET ADDR OF ORIGINAL INST.                46565202
         CR    R7,R3          DOES INST START IN RANGE                  46565602
         BNE   COPY0B         YES - MOVE 1ST BYTE                       46566002
         LA    R6,X1(R6)      NO - MOVE 2ND BYTE                        46566402
BLOW5A   EQU   *              PROTECTION CHECK HERE, IF ABEND.          46569102
COPY0B   MVC   X0(X1,R4),BRKINST MOVE CHARACTER                         46571902
BLOW5    EQU   *              PROTECTION CHECK HERE, IF ABEND.          46573902
         BCTR  R5,X0          DECREMENT R5 BY ONE                       46574602
         LTR   R5,R5          IF LENGTH IS NOW 0                        46577302
         BZ    MNLRET0        DONE. RETURN TO MAINLINE                  46580002
         LR    R3,R4          INCREMENT R3 BY ONE                       46582702
         LA    R4,X1(R4)      INCREMENT R4 BY ONE                       46585402
         STM   R3,R5,WAFROM   RESET VALUES IN WORK AREA                 46588102
*        B     COPY                                                     46588202
         SPACE 1                                                        46588502
* COPY FORWARD USED WHEN                                                46588902
*    (1) FIELDS DO NOT OVERLAP,                                         46600002
*    (2) THE 'FROM' ADDRESS IS GREATER THAN THE 'TO' ADDRESS,           46700002
*    (3) THE 'TO' ADDRESS IS 1 GREATER THAN THE 'FROM' ADDRESS          46800002
*        (THE FIELD WILL BE SET TO THE SAME CHARACTER AS THE            46900002
*        'FROM' FIELD BEGAN WITH).                                      47000002
COPY     EQU   *              COPY AN ADDRESS TO AN ADDRESS FOWARD.     47100002
         L     R3,WAFROM      GET FROM ADDRESS.                         47200002
         L     R4,WATO        GET TO ADDRESS                            47300002
         LA    R0,N256        MOVE 256 AT A TIME.                       47400002
         L     R1,WALENGTH    GET THE LENGTH.                           47500002
         LA    R5,N256-X1     LENGTH OF 256-1                           47600002
LOOP     EQU   *              COPY LOOP.                                47700002
         CR    R1,R0          IS LENGTH NOW LESS THAN 256               47800002
         BNH   COPY1          YES. FINAL COPY.                          47900002
BLOW6A   EQU   *              PROTECTION CHECK HERE, IF ABEND.          47950002
         EX    R5,MVC         COPY 256                                  48000002
BLOW6    EQU   *              PROTECTION CHECK HERE, IF ABEND.          48100002
         ALR   R3,R0          UPDATE FROM ADDRESS.                      48200002
         ALR   R4,R0          UPDATE TO ADDRESS.                        48300002
         SR    R1,R0          OBTAIN LENGTH OF WHAT'S LEFT.             48400002
         B     LOOP           KEEP GOING.                               48500002
COPY1    EQU   *              FINAL COPY.                               48600002
         LR    R5,R1          GET FINAL LENGTH.                         48700002
         BCTR  R5,X0          DECREMENT FOR EXECUTE.                    48800002
BLOW7A   EQU   *              PROTECTION CHECK HERE, IF ABEND.          48850002
         EX    R5,MVC         MOVE IT.                                  48900002
BLOW7    EQU   *              PROTECTION CHECK HERE, IF ABEND.          49000002
         SPACE 1                                                        49010002
COPY1B   L     R3,WAFROM      OBTAIN FROM ADDRESS                       49050002
         L     R4,WATO        OBTAIN TO ADDRESS                         49060002
         L     R5,WALENGTH    OBTAIN LENGTH                             49070002
         BCTR  R4,X0          DECREMENT 'TO' ADDRESS                    49080002
         CR    R3,R4          IS 'TO' 1 GREATER THAN 'FROM'?            49090002
         LA    R4,X1(R4)      RESTORE 'TO' ADDRESS                      49092002
         BE    COPY1C         SEARCH DONE ( 1 BYTE PROPAGATED )         49094002
         BAL   R8,SRH         CALL SRH                                  49096002
COPY1C   EQU   *                                                        49098002
MNLRET0  SR    R15,R15        RET. TO MNL WITH A CODE OF ZERO.          49100002
MNLRET   LR    R2,R15         SAVE RET CODE OVER ESTAE                  49102002
         ESTAE 0              REMOVE ESTAE REQUEST                      49110002
         LR    R15,R2         RESTORE RETURN CODE                       49112002
         NI    TSTFLGS4,XFF-TSTRERTN  SHUT OFF TCOMTAB FLAG             49120002
MNLRETS  L     R13,X4(R13)    RESTORE THE REGISTERS.                    49200002
         RETURN  (14,12),T,RC=(15)  RETURN.                             49300002
         SPACE                                                          49400002
         SPACE                                                          49500002
MVC      MVC   X0(X0,R4),X0(R3)  ALL MOVES ARE DONE WITH THIS MVC.      49600002
         SPACE                                                          49700002
         EJECT                                                          50500002
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         50510002
*  FUNCTION - CALLS THE SEARCH ROUTINE TO LOCATE BREAKPOINTS  *         50550002
*        IN THE RANGE SPECIFIED.  FOR EVERY BREAKPOINT FOUND  *         50560002
*        IN THE 'FROM' RANGE, IT REPLACES THE INSTRUCTION IN  *         50570002
*        THE 'TO' RANGE.                                      *         50580002
*  INPUT -                                                    *         50582002
*        R3 = 'FROM' ADDRESS                                  *         50584002
*        R4 = 'TO' ADDRESS                                    *         50586002
*        R5 = LENGTH ( OR LENGTH-1 IF ENTERED AT SRH0 )       *         50588002
*        R8 = RETURN ADDRESS                                  *         50588402
*  REGISTER USAGE                                             *         50588802
*        R1 = WORK REG                                        *         50588902
*           = PARM REG                                        *         50589002
*           = ADDR OF INST IN 'TO' RANGE                      *         50589102
*        R6 = ADDRESS OF BREAK ELEMENT                        *         50607502
*        R7 = ADDR OF INST IN 'FROM' RANGE                    *         50626402
*  REGISTERS AT OUTPUT -                                      *         50636402
*        R1, R6, R7, R14, R15 DESTROYED                       *         50638402
*        R5 = LENGTH                                          *         50640402
*        OTHERS ARE UNCHANGED                                 *         50642402
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         50657102
SRH      BCTR  R5,X0          LENGTH-1                                  50659102
SRH0     AR    R5,R3          END OF RANGE                              50661102
         LR    R7,R3          INIT START ADDRESS                        50663102
         BCTR  R7,X0          BACK UP ONE                               50669202
SRHLOOP  LA    R1,X4          CODE = SEARCH + LOCATE, NO BACKUP         50677502
         ST    R1,WATEMP1     * PARMLIST: CODE = 4                      50679502
         ST    R7,WATEMP2     *           START OF RANGE                50681502
         ST    R5,WATEMP3     *           END OF RANGE                  50687602
         LA    R1,WATEMP      ADDRESS OF PARM LIST                      50689602
         L     R15,TSTSRHRT   ADDRESS OF SEARCH ROUTINE                 50691602
         BALR  R14,R15        CALL SEARCH ROUTINE                       50693602
         B     SRHRC(R15)     CHECK RETURN CODE                         50693702
SRHRC    B     SRHFOUND       R.C. 0 = FOUND                            50703702
         B     SRHDONE        R.C. 4 = NOT FOUND                        50720302
         B     NOPROMPT       R.C. 8 = INVALID PARAMETERS               50720702
         B     NOFREE         R.C.12 = STAE                             50722302
SRHFOUND L     R6,WATEMP2     GET ADDR OF BREAK ELEMENT                 50730302
         L     R7,BRKADDR     GET ADDR OF INST                          50740302
         LR    R1,R7          * DISPLACEMENT INTO                       50742302
         SR    R1,R3          * 'FROM' AREA                             50744302
         AR    R1,R4          INST IN 'TO' AREA                         50746302
         CR    R7,R3          DOES INST START BEFORE RANGE?             50746702
         BL    SRHMVC2        YES. MOVE ONLY 1ST BYTE                   50746802
SRHMVC1  MVC   X0(X1,R1),BRKINST REPLACE 1ST BYTE                       50746902
         CR    R7,R5          IS THIS LAST BYTE IN RANGE?               50755702
         BE    SRHDONE        YES. DONE                                 50757702
SRHMVC2  MVC   X1(X1,R1),BRKINST+X1 REPLACE 2ND BYTE                    50759702
         LA    R7,X2(R7)      START RANGE AFTER BREAKPOINT              50761702
         CR    R7,R5          IS NEW START AFTER END?                   50762102
         BNH   SRHLOOP        NO. SEARCH FOR MORE BREAKPOINTS           50762502
SRHDONE  SR    R5,R3          LENGTH - 1                                50764102
         LA    R5,X1(R5)      LENGTH                                    50764502
         BR    R8             RETURN                                    50764602
         EJECT                                                          50773402
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         50782402
* INPUT:  R7 = ADDRESS TO BE CHECKED                          *         50791202
*         R5 = LENGTH OF FIELD                                *         50800002
* RETURNS TO 0(R11) IF ADDRESS IS READ/WRITE                  *         50900002
*         TO 4(R11) IF ADDRESS IS NOT READ ONLY               *         51050002
*         TO 8(R11) IF ADDRESS IS INVALID                     *         51060002
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         51100002
ADDRCHK  EQU   *                                                        51200002
         L     R6,CVTPTR      ADDRESS CVT                               51300002
         L     R6,X0(R6)      ADDRESS NEW/OLD                           51400002
         L     R6,X4(R6)      ADDRESS OF TCB                            51500002
         IKJEGS9G       ADDRCHK,TCBADDR=(R6),VALUE=(R7),MF=(E,WATEMP)   51700002
         LR    R8,R15         SAVE RETURN CODE                          51850002
         AR    R7,R5          GET END OF FIELD.                         52100002
         BCTR  R7,X0          - LAST BYTE.                              52200002
         IKJEGS9G       ADDRCHK,TCBADDR=(R6),VALUE=(R7),MF=(E,WATEMP)   52300002
         CR    R8,R15         IS OLD R.C. > NEW R.C.?                   52450002
         BH    ADDRCHK3       YES. USE OLD R.C.                         52500002
         LR    R8,R15         NO. USE NEW R.C.                          52550002
ADDRCHK3 EQU   *                                                        52600002
         BR    R11            RETURN                                    52700002
         EJECT                                                          52800002
*        VALIDITY CHECK EXIT                                            52900002
         DROP  R2                                                       53000002
         SPACE                                                          53100002
         USING  IKJPARMA,R2   ADDRESSIBILITY                            53200002
         USING  VAL,R8        ADDRESSIBILITY                            53300002
         SPACE                                                          53400002
IKJEGFM  DS    0D             'FROM' EXIT.                              53500002
         STM   R14,R12,N12(R13)  SAVE PARSE'S REGISTERS.                53600002
         L     R12,FMBASE-IKJEGFM(R15)  GET FROM KEYWORDS BASE REG.     53700002
         B     VALIDITY       GO TO COMMON EXIT.                        53800002
         SPACE                                                          53900002
         SPACE                                                          54000002
IKJEGTO  DS    0D             'TO' EXIT.                                54100002
         STM   R14,R12,N12(R13)  SAVE PARSE'S REGISTERS.                54200002
         L     R12,TOBASE-IKJEGTO(R15)  GET TO'S BASE REGISTER          54300002
         B     VALIDITY       GO TO COMMON VALIDITY EXIT                54400002
         SPACE                                                          54500002
IKJEGLN  DS    0D             LENGTH'S KEYWORD EXIT                     54600002
         STM   R14,R12,N12(R13)  SAVE PARSE'S REGISTERS.                54700002
         L     R12,LNBASE-IKJEGLN(R15)  GET LENGTH'S BASE REG.          54800002
         SPACE                                                          54900002
VALIDITY EQU   *              COMMON VALIDITY EXIT.                     55000002
         L     R14,VALPASS-VAL(R1)  PARM PASSED IS REGSAVE2 PTR.        55100002
         LM    R2,R11,N28(R14)  GET ALL OF MAIN ROUTINE'S REGS.         55200002
         LR    R8,R1          SAVE PARSE'S PARM LIST PTR.               55300002
         LR    R0,R12         MOVE CODE TO REG 0                        55400002
         SRL   R0,N24         RIGHT ADJUST IT.                          55500002
         STC   R0,WACODE      SAVE IT.                                  55600002
         L     R3,REGSAVE3    MY REG SAVE AREA.                         55700002
         ST    R3,X8(R13)     FOWARD POINTER.                           55800002
         ST    R13,X4(R3)     BACKWARD POINTER.                         55900002
         LR    R13,R3         SAVE AREA POINTER TO 13                   56000002
         SR    R14,R14        CLEAR REG 14                              56050002
         L     R4,OUTBUF      ACCESS POINTER TO OUTBUF                  56060002
         ST    R14,X0(R4)     CLEAR 1ST WORD OF OUTBUF                  56070002
         OI    TSTFLGS4,TSTVALCK INDICATE VALIDITY ROUTINE              56080002
         L     R2,VALPDE      ADDRESSABILITY TO P.D.E.                  56100002
         CLI   WACODE,LNCODE  IS THIS AN EXIT FOR LENGTH Q.Q.Q.         56200002
         BE    LENGTHCK       YES. CONVERT IT.                          56300002
         LNR   R1,R2          CONVERT THE FROM OR TO ADDRESS.           56400002
         LPR   R0,R2          PASS THE PDE TO IKJEGCVT.                 56500002
         L     R15,TSTCONVT   GO TO CONVERT.                            56600002
         BALR  R14,R15        GO.                                       56700002
FROMCVT  B     TABLE2(R15)    COMING FROM CONVERT.                      56800002
TABLE2   B     QAOK           ALL SEEMS TO BE O.K.                      56900002
         B     PROMPT1        CVT SAYS PROMPT.                          57000002
         B     QUIT           MUST QUIT.                                57100002
         B     QUIT           MUST QUIT.                                57200002
         B     QUIT           MUST QUIT.                                57300002
         B     QUIT           MUST QUIT.                                57400002
QUIT     EQU   *              QUIT HERE.                                57500002
         STC   R15,WASAVERC   SAVE TELLTALE RET. CODE.                  57600002
         LA    R15,RC12       RET TO PARSE .                            57700002
         B     PARSRET        RETURN.                                   57800002
PROMPT1  EQU   *                                                        58300002
         LA    R15,RC8        RETURN TO PARSE WITH 8.                   58400002
         B     PARSRET        RETURN.                                   58500002
QAOK     EQU   *              IS ALL REALLY O.K.                        58600002
         L     R3,PDEUSER     GET CONVERT'S ADDRESS.                    58700002
         LTR   R3,R3          IS IT CONVERT'S OR SYM'S                  58800002
         BNM   SKIP3          IT REALY IS CONVERTS                      58900002
         L     R3,X0(R3)      NO. IT'S REALLY SYMS.                     59000002
SKIP3    EQU   *              IN EITHER CASE , HERE WE ARE.             59100002
         LA    R3,X0(R3)      CLEAR FLAGS FROM TOP BYTE                 59150002
         LH    R4,PDEINDCT    CHECK FOR INDIRECT COUNT.                 59200002
         LTR   R4,R4          IS COUNT ZERO Q.Q.Q.                      59300002
         BNZ   SAVEADDR       NO. HAS TO BE AN ADDRESS.                 59400002
         TM    PDEFLG4,GENR   IS IT A GENERAL REGISTER Q.Q.Q.           59500002
         BZ    SKIP4          NO. CHECK FOR FLOATING POINT.             59600002
         O     R3,REGCODE     YES IT'S GENERAL REGISTER. SET HIGH BIT.  59700002
SKIP4    EQU   *                                                        59800002
         TM    PDEFLG4,LFPR+SFPR  Q. IS IT A FLOATING POINT REG.        59900002
         BZ    SAVEADDR       NO. IT HAS TO BE AN ADDRESS.              60000002
PRINT    EQU   *                                                        60200002
         LA    R4,M0101       MSG - INVALID ADDRESS                     60250002
         LA    R3,M0267       MSG - MUST BE GEN REG                     60300002
         BAL   R11,MSG4       GO OUTPUT MESSAGES                        60350002
         CH    R15,ATTNCODE   WAS ATTENTION HIT                         60400002
         BNL   QUIT           YES, RETURN                               60450002
         B     PROMPT1        PROMPT 'REENTER'                          60500002
SAVEADDR EQU   *              HAVE AN ADDRESS. OR GENERAL REG.          60600002
         CLI   WACODE,TOCODE  CHECK WHAT KEYWORD.                       60700002
         BE    SAVETO         'TO'                                      60800002
SAVEFROM ST    R3,WAFROM      'FROM'                                    60900002
         B     PARSRET0       RETURN TO PARSE WITH AOK CODE.            61000002
SAVETO   ST    R3,WATO        SAVE THE 'TO' ADDR OR REG.                61100002
         B     PARSRET0       AOK BACK TO PARSE.                        61200002
LENGTHCK EQU   *              CONVERT THE LENGTH.                       61300002
         SR    R0,R0          CLEAR REG 0                               61400002
         LPR   R1,R2          PDE ADDR TO R1 AND INSURE POITIVE.        61500002
         MVI   PDERSV1,DECIMAL  INDICATE CONVERT TO DECIMAL.            61600002
DECIMAL  EQU   X'10'          CONVERT TO DECIMAL INDICATOR.             61700002
         L     R15,TSTCONVT   CONVERT'S BRANCH ENTRY.                   61800002
         BALR  R14,R15        GO TO CONVERT.                            61900002
FMCVT    LTR   R15,R15        BACK FROM CONVERT.                        62000002
         BNZ   FROMCVT        NOT ZERO. GO TO BRANCH TABLE.             62100002
         L     R3,CONAREA     GET CONVERTED LENGTH.                     62200002
         L     R4,X1(R3)      DITTO.                                    62300002
         ST    R4,WALENGTH    SAVE IT FOR THE MAIN ROUTINE.             62400002
         L     R5,MAXIMUM     Q.GREATER THAN 65,535                     62500002
         CR    R4,R5          Q. IS IT.                                 62600002
         BNH   REGLNCHK       NO. CHECK FOR REGISTERS SPECIFIED.        62700002
         LA    R4,M0064       MSG - INVALID LENGTH                      62750002
         LA    R3,M0284       MSG - LEN BTWN 0 AND 65535                62800002
FMCVT2   BAL   R11,MSG3       GO OUTPUT MESSAGES                        62850002
         CH    R15,ATTNCODE   WAS ATTENTION HIT                         62900002
         BNL   QUIT           YES, RETURN                               62950002
         B     PROMPT1        PROMPT 'REENTER'                          63100002
         SPACE                                                          63200002
REGLNCHK TM    WAFROM,X80     IS REG SPECIFIED AS 'FROM' ADDR ?         63210002
         BO    MAX64          YES. CHECK MAX LENGTH OF 64.              63220002
         TM    WATO,X80       IS REG SPECIFIED AS 'TO' ADDR ?           63230002
         BZ    PARSRET0       NO. RETURN NORMALLY.                      63240002
MAX64    LA    R5,N64         TOTAL SIZE OF REGS IS 64.                 63242002
         CR    R4,R5          DOES LENGTH EXCEED 64?                    63244002
         BNH   PARSRET0       NO. RETURN NORMALLY TO PARSE.             63246002
         LA    R4,M0064       MSG - INVALID LENGTH                      63248002
         LA    R3,M0266       MSG - LEN 64 WHEN REGS SPEC.              63248102
         B     FMCVT2         BUILD AND PRINT MESSAGES.                 63248402
         SPACE                                                          63250002
PARSRET0 SR    R15,R15        RET. CODE OF ZERO.                        63300002
PARSRET  EQU   *              RETURN TO PARSE                           63350002
         NI    TSTFLGS4,XFF-TSTVALCK SHUT OFF VALIDITY FLAG             63360002
         L     R4,OUTBUF      ACCESS POINTER TO OUTBUF                  63370002
         L     R4,X0(R4)      ACCESS 1ST WORD OF OUTBUF                 63380002
         LTR   R4,R4          IS THERE A SECOND LEVEL MESSAGE           63390002
         BZ    PARSRETN       NO, RETURN TO PARSE                       63392002
         MVC   VAL2LVL(X4),OUTBUF  YES, GIVE IT TO PARSE                63394002
PARSRETN L     R13,X4(R13)    GET PARSE'S R13                           63400002
         RETURN  (14,12),T,RC=(15)  RETURN.                             63500002
         SPACE                                                          63600002
         EJECT                                                          67800002
*        STAE RETRY ROUTINE.                                            67900002
         SPACE                                                          68000002
STAERTRY EQU   *              ABEND PROCESSING HERE.                    68100002
         L     R3,CVTPTR      GET CVT PTR.                              68200002
         L     R4,X0(R3)      GET DOUBLE WORD PTR(NEW , CURRENT)        68300002
         L     R5,X4(R4)      GET CURRENT TCB PTR.                      68400002
         L     R9,TCBTRN-TCB(R5)  GET TCOMTAB ADDRESS.                  68500002
         L     R13,REGSAVE2   GET THE MAIN ROUTINE'S REGISTERS.         68600002
         L     R12,CPYBASE-STAERTRY(R15)  DITTO.                        68700002
         LM    R2,R11,N28(R13)  DITTO.                                  68800002
NOFREE   EQU   *                                                        68850002
         LA    R15,RC20       RETURN TO MAINLINE(IKJEGMNL) WITH 20      69300002
         B     MNLRET         GO.                                       69400002
         EJECT                                                          70600002
* THE FOLLOWING ARE MESSAGE OUTPUTTING ROUTINES.                        70650002
MSG1     XC    TSTIOPRM,TSTIOPRM  CLEAR PARAMETER AREA                  70660002
         IKJEGSIO MSG,FIRST=(R2),SECOND=(R3),ID=CPY08,                 *70670002
               MF=(E,TSTIOPRM) CALL IO                                  70680002
         CH    R15,ATTNCODE   WAS RET CODE GREATER OR = 16              70692402
         BNL   MNLRET         YES, RETURN                               70694002
         BR    R11            NO, BRANCH AND CONTINUE                   70696002
         SPACE                                                          70696402
MSG2     XC    TSTIOPRM,TSTIOPRM CLEAR PARAMETER AREA                   70698002
         IKJEGSIO MSG,FIRST=(R2),INST11=(NUM,I0030),SECOND=(R3),       *70698402
               ID=CPY08,MF=(E,TSTIOPRM) CALL IO                         70698802
         CH    R15,ATTNCODE   WAS RET CODE GREATER OR = 16              70699302
         BNL   MNLRET         YES, RETURN                               70699602
         BR    R11            NO, BRANCH AND CONTINUE                   70699702
         SPACE                                                          70699802
MSG3     XC    TSTIOPRM,TSTIOPRM CLEAR PARAMETER AREA                   70699902
         IKJEGSIO MSG,FIRST=(R4),INST11=(VPDE,(R2)),SECOND=(R3),       *70700002
               ID=CPY08,MF=(E,TSTIOPRM) CALL IO                         70733202
         BR    R11            CONTINUE PROCESSING                       70743202
         SPACE                                                          70745202
MSG4     XC    TSTIOPRM,TSTIOPRM CLEAR PARAMETER AREA                   70753202
         IKJEGSIO MSG,FIRST=(R4),INST11=(APDE,(R2)),SECOND=(R3),       *70763202
               ID=CPY08,MF=(E,TSTIOPRM) CALL IO                         70765202
         BR    R11            CONTINUE PROCESSING                       70765602
         SPACE                                                          70766002
LINKERR  LR    R3,R1          REG 3 = ABEND CODE                        70766402
         LR    R4,R15         REG 4 = RETURN CODE                       70766502
         XC    TSTIOPRM,TSTIOPRM CLEAR PARAMETER AREA                   70766602
         IKJEGSIO SVCERR,SVC=6,ABENDRG=(R3),RC=(R4),                   *70777702
               INST11=(NUM,I0040),ID=CPY08,MF=(E,TSTIOPRM) CALL IO      70787702
         CH    R15,ATTNCODE   IS RET CODE LESS THAN 16                  70788202
         BNL   MNLRET         NO, RETURN TO MAINLINE                    70788302
         B     MNLRET0        YES, SET RC=0, RETURN                     70788402
         SPACE                                                          70788502
LISTESTA IKJEGSPL RTRY=STAERTRY,ABNTB=ABTAB,MODNM=IKJEGCPY,            *70790802
               TNM=COPY       SET UP PARAMETER LIST FOR ESTAE           70791202
         EJECT                                                          70792502
*        LIST FORMS                                                     70796302
         SPACE                                                          70800002
****ESTAE CHANGES***                                                    70850002
****STAELIST ESTAE MF=L                                                 70860002
STAELIST ESTAE MF=L           ESTAE LIST FORM.                          70900002
STAELSTL EQU   *-STAELIST                                               71000002
         EJECT                                                          71100002
*        CONSTANTS                                                      71200002
         SPACE                                                          71300002
STAEADDR DC    AL3(STAERTRY)  ADDRESS OF STAE RETRY ADDRESS.            71400002
ONE      DC    H'1'           DECIMAL ONE                               71500002
SIX      DC    H'6'           DECIMAL 6                                 71600002
ATTNCODE DC    H'16'          DECIMAL 16                                71700002
H4       DC    H'4'           CONSTANT FOR R.C. COMPARE                 71750002
H8       DC    H'8'           CONSTANT FOR R.C. COMPARE                 71760002
         SPACE 1                                                        71800002
ABTAB    DS    0F             I.C. TABLE AND CODES FOR IKJEGSTA.        71900002
         DC    AL1(I0097),AL3(BLOW1A) PROTECTION CHECK                  72000002
         DC    AL1(I0097),AL3(BLOW1)  PROTECTION CHECK                  72100002
         DC    AL1(I0097),AL3(BLOW2A) PROTECTION CHECK                  72150002
         DC    AL1(I0097),AL3(BLOW2)  PROTECTION CHECK                  72200002
         DC    AL1(I0097),AL3(BLOW3A) PROTECTION CHECK                  72250002
         DC    AL1(I0097),AL3(BLOW3)  PROTECTION CHECK                  72260002
         DC    AL1(I0097),AL3(BLOW4A) PROTECTION CHECK                  72270002
         DC    AL1(I0097),AL3(BLOW4)  PROTECTION CHECK                  72280002
         DC    AL1(I0097),AL3(BLOW5A) PROTECTION CHECK                  72290002
         DC    AL1(I0097),AL3(BLOW5)  PROTECTION CHECK                  72292002
         DC    AL1(I0097),AL3(BLOW6A) PROTECTION CHECK                  72294002
         DC    AL1(I0097),AL3(BLOW6)  PROTECTION CHECK                  72296002
         DC    AL1(I0097),AL3(BLOW7A) PROTECTION CHECK                  72298002
         DC    AL1(I0097),AL3(BLOW7)  PROTECTION CHECK                  72298402
         DC    AL1(I0099),AL3(BLOWPARS)   LINK TO PARSE FAILED.         72300002
         DC    AL1(255),AL3(0)  END OF TABLE INDICATOR.                 72400002
         SPACE 1                                                        72500002
         DS    0F                                                       72600002
CPYBASE  DC    AL1(CPYCODE),AL3(IKJEGCPY)  BASE FOR MAIN ROUTINE.       72700002
FMBASE   DC    AL1(FMCODE),AL3(IKJEGCPY)  BASE FOR 'FROM' KEYWORD.      72800002
TOBASE   DC    AL1(TOCODE),AL3(IKJEGCPY)  BASE FOR 'TO' KEYWORD.        72900002
LNBASE   DC    AL1(LNCODE),AL3(IKJEGCPY)  BASE FOR 'LENGTH' KEYWORD.    73000002
MAXIMUM  DC    A(65535)       MAXIMUM LENGTH.                           73100002
PRMAD    DC    A(IKJEGPRM)    POINTER TO PARSE CSECT.                   73200002
CHNGREGS DC    X'04000000'    CODE FOR SVC 97.                          73300002
CHNGALL  DC    X'FF000000'    CODE TO CHANGE ALL REGISTERS.             73400002
REGCODE  DC    X'80000000'    HIGH BIT INDICATES GENERAL REGISTER.      73500002
CONSTANT DC    60C'Z'         PATCH AREA                                73550002
         EJECT                                                          73600002
         SPACE                                                          73700002
*        PARSE CSECT/DSECT                                              73800002
         SPACE                                                          73900002
IKJEGPRM IKJPARM                                                        74000002
PRMF     IKJPOSIT  ADDRESS,VALIDCK=IKJEGFM,PROMPT='FROM ADDRESS'        74100002
PRMT     IKJPOSIT  ADDRESS,VALIDCK=IKJEGTO,PROMPT='TO ADDRESS'          74200002
PRMP     IKJKEYWD                                                       74300002
         IKJNAME  'POINTER'                                             74400002
         IKJNAME  'NOPOINT'                                             74500002
PRMLNKEY IKJKEYWD                                                       74600002
         IKJNAME  'LENGTH',SUBFLD=PRMLNSUB                              74700002
PRMLNSUB IKJSUBF                                                        74800002
PRML     IKJIDENT   'LENGTH',FIRST=NUMERIC,OTHER=NUMERIC,MAXLNTH=5,    *74900002
               VALIDCK=IKJEGLN,PROMPT='LENGTH'                          75000002
         IKJENDP                                                        75100002
         EJECT                                                          75200002
         BRKELEM              BREAK ELEMENT DSECT                       75250002
         EJECT                                                          79300002
         IKJTCB                                                         79400002
         EJECT                                                          79500002
         TCOMTAB                                                        79600002
         EJECT                                                          79700002
         TSTCWORK                                                       79800002
         SPACE 5                                                        79900002
         IKJPARMA                                                       80000002
         END                                                            80100002
