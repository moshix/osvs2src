         TITLE 'TEST LOADER       IKJEGLDR'                             00050002
         COPY  IKJEGSIO                                                 00054002
IKJEGLDR CSECT                                                          00060002
         EJECT                                                          00100002
*********************************************************************** 00150002
*                                                                       00200002
* STATUS --                                                             00250002
*    CHANGE LEVEL 00, VERSION NO. 02, VS2   02/27/73                    00300002
*                                                                       00350002
* FUNCTION -- IKJEGLDR LOADS THE PROGRAM TO BE TESTED                   00400002
*                                                                       00450002
*    IKJEGLDR IS ATTACHED BY THE TEST INITIALIZATION                    00500002
*    ROUTINE, IKJEGINT.  IT IS PASSED AS AN INPUT PARAMETER THE         00550002
*    ADDRESS OF THE TCOMTAB IN REGISTER ONE.  AFTER SAVING              00600002
*    REGISTERS AND SETTING PROGRAM ADDRESSABILITY, IT PICKS UP          00650002
*    FROM THE TCOMTAB THE ADDRESS OF THE PARSE DESCRIPTOR LIST          00700002
*    FOR THE TEST COMMAND.  IT THEN CHECKS TO SEE WHETHER THE           00750002
*    INPUT IS A LOAD MODULE OR AN OBJECT MODULE.  WHEN THE INPUT        00800002
*    IS A LOAD MODULE, THE PROCESSING BY THE OS/VS2 LOADER AS           00850002
*    DESCRIBED IN THE FOLLOWING PARAGRAPH IS SKIPPED.                   00900002
*                                                                       00950002
*    IF THE INPUT IS AN OBJECT MODULE, IKJEGLDR GETS CORE               01000002
*    AND BUILDS A PARAMETER LIST FOR THE OS/VS2 LOADER (HEWLOAD).YM1030 01050002
*    IKJEGLDR THEN LOADS THE LOADER AND BRANCHES TO IT.  IF THERE       01100002
*    IS AN ERROR DURING LOADER PROCESSING, THEN CONTROL IS              01150002
*    RETURNED TO OS/VS2.  IF THERE ARE NO ERRORS, THEN THE LOADER       01200002
*    IS DELETED AND THE PARAMETER LIST CORE FREED.                      01250002
*                                                                       01300002
*    ONCE HEWLOAD HAS COMPLETED, IF REQUIRED, IKJEGLDR           YM1030 01350002
*    CHECKS TO SEE IF THE PROBLEM PROGRAM IS A COMMAND PROCESSOR,       01400002
*    AS DESCRIBED BY THE PDE FOR THE CP OPERAND OF THE TEST             01450002
*    COMMAND.  IF SO, IKJEGIO IS CALLED IN ORDER TO GET A COMMAND       01500002
*    FROM THE USER.  ONCE THE COMMAND IS RECEIVED, CORE IS GOTTEN       01550002
*    FOR A COMMAND PROCESSOR PARAMETER LIST (CPPL) AND THE LIST         01600002
*    IS BUILT.  NEXT, THE COMMAND SCAN SERVICE ROUTINE (IKJSCAN)        01650002
*    IS CALLED TO PROCESS THE VERB FIELD OF THE COMMAND IN ORDER        01700002
*    TO SIMULATE THE CONDITIONS THAT WOULD HAVE OCCURRED IF THE         01750002
*    COMMAND HAD BEEN RECEIVED BY THE TMP.  IKJEGLDR THEN               01800002
*    BRANCHES TO THE LOCATION AT WHICH A PSEUDO BREAK POINT IS          01850002
*    INSERTED INTO THE PROBLEM PROGRAM, AS DESCRIBED BELOW.             01900002
*                                                                       01950002
*    IF THE PROBLEM PROGRAM IS NOT A COMMAND PROCESSOR, THEN            02000002
*    INSTEAD OF GETTING A COMMAND FROM THE USER, IKJEGLDR BUILDS        02050002
*    A PARAMETER LIST IN THE STANDARD OS/VS2 FORMAT.  IF THE USER       02100002
*    SPECIFIES PARAMETER DATA ON THE TEST COMMAND, THEN THIS DATA       02150002
*    IS PLACED INTO THE LIST.  IF NO DATA IS FURNISHED, A               02200002
*    PARAMETER LIST WITH A LENGTH OF FOUR CONTAINING ALL ZEROES         02250002
*    IS CONSTRUCTED.                                                    02300002
*                                                                       02350002
*    ONCE THE PARAMETER LIST HAS BEEN CONSTRUCTED, WHETHER              02400002
*    IT IS A COMMAND PROCESSOR OR STANDARD OS/VS2 LIST, IKJEGLDR        02450002
*    CALLS IKJEGS9G IN ORDER TO PLACE THE ADDRESS OF THE TCOMTAB        02500002
*    INTO THE PROBLEM PROGRAM TCB AND TO TURN ON THE TCBTCP BIT         02550002
*    IN THE TCB.  UPON RETURN FROM IKJEGS9G, IKJEGLDR FINDS THE         02600002
*    ENTRY POINT OF THE PROBLEM PROGRAM BY SEARCHING THE CONTENTS       02650002
*    DIRECTORY ENTRIES.  ONCE THE ENTRY POINT HAS BEEN FOUND, THE       02700002
*    FIRST TWO BYTES OF USER CODE ARE SAVED IN THE TSTGO FIELD OF       02750002
*    THE TCOMTAB AND A SVC 97 IS PLACED INTO THE PROBLEM                02800002
*    PROGRAM.  THIS IS DONE SO THAT WHEN IKJEGLDR XCTLS TO THE          02850002
*    PROGRAM, SVC 97 WILL GET CONTROL AND POST THE INITIALIZATION       02900002
*    ROUTINE.  THUS, THE PROBLEM PROGRAM WILL THEN BE INITIALIZED       02950002
*    WITH THE STANDARD TEST FORMAT, WHICH IS A PRB FOR THE              03000002
*    PROBLEM PROGRAM CHAINED TO AN SVRB FOR SVC 97.  ONCE THE           03050002
*    PSEUDO BREAK POINT IS ESTABLISHED, IKJEGLDR XCTLS TO THE           03100002
*    PROBLEM PROGRAM.                                                   03150002
*                                                                       03200002
* ENTRY POINTS --                                                       03250002
*    IKJEGLDR                                                           03300002
*                                                                       03350002
* INPUT --                                                              03400002
*    TEST COMMUNICATION TABLE POINTED TO BY REGISTER 1.                 03450002
*                                                                       03500002
*    TPL POINTED TO BY TPLPTR IN TCOMTAB                                03550002
*                                                                       03600002
*    THE DCB OPENED BY IKJEGINT IS HUNG FROM TSTDCB IN TCOMTAB          03650002
*                                                                       03700002
*    THE BLDL ENTRY GOTTEN BY IKJEGINT IS LOCATED AT BLDLAREA           03750002
*    BLDLAREA IS EQUATED TO CONAREA IN TCOMTAB                          03800002
*                                                                       03850002
*    THE PDL(PARAMETER DISCRIPTOR LIST) IS PASSED AT TSTANSPL           03900002
*    IN TCOMTAB                                                         03950002
*                                                                       04000002
* OUTPUT --                                                             04050002
*    REGISTER 1 POINTS TO THE PARAMETER LIST BUILT FOR THE PROGRAM      04100002
*    TO BE TESTED.                                                      04150002
*    REGISTER 15 CONTAINS A RETURN CODE.                                04200002
*                                                                       04250002
* EXTERNAL REFERENCES --                                                04300002
*         IKJEGIO- TO WRITE DIAGNOSTIC MESSAGES TO THE TERMINAL IN      04350002
*         THE CASE OF ERROR.                                            04400002
*                                                                       04450002
* EXITS, NORMAL --                                                      04500002
*         XCTL TO THE EPA OF THE PROGRAM TO BE TESTED.                  04550002
*                                                                       04600002
* EXITS, ERROR --                                                       04650002
*         RETURN VIA REGISTER 14.                                       04700002
*           RC=0 ERROR OCCURRED                                         04710002
*           RC=4 ATTENTION OCCURRED                                     04720002
*           RC=20 ESTAE ERROR EXIT ENTERED                              04730002
*           RC=24 ESTAE MACRO ERROR                                     04740002
*                                                                       04750002
* TABLES/WORKAREAS --                                                   04800002
*    TCOMTAB POINTED TO BY REG 9, IS TEST'S COMMUNICATION TABLE         04850002
*                                                                       04900002
*    TPL POINTED TO BY REGISTER 10 IS THE TEST PARAMETER LIST           04950002
*                                                                       05000002
*    PDL POINTED TO BY TSTANSPL IN TCOMTAB                              05050002
*                                                                       05100002
*    WORKAREA POINTED TO BY TCOMTAB IS THE GENERAL W.A. FOR ALL OF TEST 05150002
*                                                                       05200002
* ATTRIBUTES --                                                         05250002
*    RE-ENTRANT                                                         05300002
*    REFRESHABLE                                                        05350002
*                                                                       05400002
* CHARACTER CODE DEPENDENCY --                                          05450002
*    NONE                                                               05500002
*                                                                       05550002
* NOTES --                                                              05600002
*    REGISTER CONVENTIONS FOR LOADER ARE AS FOLLOWS                     05650002
*    REG 9  TCOMTAB POINTER                                             05700002
*    REG 10 TPL POINTER                                                 05750002
*    REG 11 ONLY USED FOR LINK REGISTER                                 05800002
*    REG 12 IS THE BASE REGISTER                                        05850002
*    REG 6  IS FOR MAINTENANCE                                          05900002
*                                                                       05950002
**************************CHANGE ACTIVITY****************************** 05960000
*    @ZA15329 - NEAR LABEL SETPSUED TCBTCP FLAG ZEROED WHEN INVALID.  * 05970000
*                                                                     * 05980000
*********************************************************************** 06000002
         EJECT                                                          06050002
*        EQUATES AND CONSTANTS                                          06100002
         SPACE                                                          06150002
LOAD     EQU   1                                                        06200002
DCBLEN   EQU   96                                                       06250002
CP       EQU   1                                                        06300002
X0       EQU   0              HEXIDECIMAL 0                             06350002
X1       EQU   1              HEXIDECIMAL 1                             06400002
X2       EQU   2              HEXIDECIMAL 2                             06450002
X3       EQU   3              HEXIDECIMAL 3                             06500002
X4       EQU   4              HEXIDECIMAL 4                             06550002
X5       EQU   5              HEXIDECIMAL 5                             06600002
X6       EQU   6              HEXIDECIMAL 6                             06650002
X7       EQU   7              HEXIDECIMAL 7                             06700002
X8       EQU   8              HEXIDECIMAL 8                             06750002
X9       EQU   9              HEXIDECIMAL 9                             06800002
N12      EQU   12             TWELVE.                                   06850002
N15      EQU   15             FIFTEEN.                                  06900002
BLANK    EQU   X'40'          BLANK.                                    06950002
X40      EQU   X'40'          HEXIDECIMAL '40'                          07000002
XFF      EQU   X'FF'          HEXIDECIMAL 'FF'                          07050002
N22      EQU   22             TWENTY TWO.                               07100002
N24      EQU   24             TWENTY FOUR.                              07150002
N25      EQU   25             TWENTY FIVE.                              07200002
N28      EQU   28             TWENTY EIGHT.                             07250002
N44      EQU   44             FORTY FOUR.                               07300002
N48      EQU   48             FORTY EIGHT.                              07350002
N50      EQU   50             FIFTY.                                    07400002
N96      EQU   96             NINETY SIX.                               07450002
N98      EQU   98             NINETY EIGHT.                             07500002
MINORBIT EQU   X'04'          MINOR BIT IN CDE ATTR 1.                  07550002
NOSYNTAX EQU   X'80'         NO SYNTAX CHECK                            07560002
ATTNBIT  EQU   B'01000000'    TO TEST IF ATTENTION HAS BEEN ENTERED     07600002
ZERO     EQU   0              USED IN CONJUNCTION WITH ATTN TEST        07610002
R0       EQU   0              REGISTER 0                                07650002
R1       EQU   1              REGISTER 1                                07700002
R2       EQU   2              REGISTER 2                                07750002
R3       EQU   3              REGISTER 3                                07800002
R4       EQU   4              REGISTER 4                                07850002
R5       EQU   5              REGISTER 5                                07900002
R6       EQU   6              REGISTER 6                                07950002
R7       EQU   7              REGISTER 7                                08000002
R8       EQU   8              REGISTER 8                                08050002
R9       EQU   9              REGISTER 9                                08100002
R10      EQU   10             REGISTER 10                               08150002
R11      EQU   11             REGISTER 11                               08200002
R12      EQU   12             REGISTER 12                               08250002
R13      EQU   13             REGISTER 13                               08300002
R14      EQU   14             REGISTER 14                               08350002
R15      EQU   15             REGISTER 15                               08400002
RC20     EQU   20            RETURN CODE 20                             08410002
RC24     EQU   24            RETURN CODE 24                             08420002
         EJECT                                                          08450002
         SAVE  (14,12),,*                                               08700002
         LR    R12,R15        LOAD R12 AS BASE REGISTER                 08750002
         USING IKJEGLDR,R12   ESTABLISH ADDRESSABILITY                  08800002
         LR    R9,R1          R9=TCOMTAB ADDRESS.                       08850002
         USING TCOMTAB,R9     TCOMTAB ADDRESSABILITY.                   08900002
         LR    R4,R13         R4=BACK POINTER.                          08950002
         L     R13,REGSAVE2   R13=NEW REGSAVE AREA ADDR.                09000002
         ST    R4,X4(R13)     STORE BACK POINTER IN NEW AREA.           09050002
         ST    R13,X8(R4)     STORE FORE POINTER IN OLD AREA.           09100002
         L     R2,WORKAREA   R2=ADDR OF WORK AREA                       09110002
         USING TSTCWORK,R2   ESTABLISH ADDRESSABILITY                   09120002
         XC    LDRESTAE,LDRESTAE CLEAR ESTAE PARM LIST                  09130002
         L     R15,TSTSTAE   R15=ADDR OF ESTAE EXIT ROUTINE             09132002
         ESTAE (R15),CT,PARAM=LDRSPL,XCTL=NO,RECORD=YES,               *09140002
               MF=(E,LDRESTAE) ESTABLISH ESTAE EXIT                     09142002
         DROP  R2                                                       09144002
         LTR   R15,R15       WAS ESTAE SUCCESSFUL?                      09146002
         BZ    LDR1          YES,CONTINUE                               09148002
         STC   R15,TSTESTRC  NO,STORE RETURN CODE                       09148402
         LA    R15,RC24      RC=24 IS ESTAE FAILURE                     09148802
         B     RETURN        RETURN TO INITIALIZATION                   09149202
LDR1     EQU   *                                                        09149602
         USING ECT,R2         ECT ADDRESSABILITY.                       09150002
         L     R8,TSTANSPL    R8=ADDR OF PASSED PDL.                    09200002
         USING IKJPARMD,R8    *                                         09250002
         L     R3,CVTPTR      GET CVT ADDRESS                           09300002
         USING CVT,R3                                                   09350002
         L     R4,CVTTCBP     GET TCB WORDS ADDRESS                     09400002
         L     R7,X4(R4)      GET CURRENT TCB ADDRESS                   09450002
         ST    R7,PPTCB       INITIALIZE THE PPTCB IN TCOMTAB           09500002
         L     R10,TPLPTR     GET TPL ADDRESS                           09550002
         USING TPL,R10         TPL ADDRESSABILITY                       09600002
         ST    R7,TPLCTCB     INITIALIZE TPL ATTACHED TCB SLOT          09650002
         L     R2,TPLECT      ECT POINTER.                              09700002
         XC    ECTRCDF(X2),ECTRCDF  ZERO RTN CD                         09750002
         SPACE                                                          09800002
*        IF LOAD KEYWORD SPECIFIED, THEN FETCH WILL LOAD.               09850002
*        ELSE, IT WAS OBJECT WHICH WAS SPECIFIED, IN WHICH CASE         09900002
*        THE OSLOADER IS USED TO DO THE LOAD.                           09950002
         SPACE                                                          10000002
         CLI   LOADOBJ+X1,LOAD Q.IS 'LOAD' SPECIFIED.                   10050002
         BNE   OSLOAD         NO,USE OS LOADER.                         10100002
*                             YES,USE PROGRAM FETCH.                    10150002
         L     R4,TSTDCB      GET THE DCB ADDRESS - 4                   10200002
         LA    R4,X4(R4)      GET THE DCB ADDRESS.                      10250002
         L     R5,BLDLAREA    GET THE BLDL ENTRY ADDRESS.               10300002
         L     R2,TPLECT      ECT POINTER.                              10350002
         MVC   ECTPCMD(L'ECTPCMD),X4(R5)  PUT MEMBER NAME IN ECT.       10400002
         SPACE                                                          10450002
*        WAS CP SPECIFIED, IF YES THEN PROMPT FOR THE COMMAND.          10500002
*        IF NO, THEN PROCEED.                                           10550002
         SPACE                                                          10600002
PARMSET  EQU   *                                                        10650002
         CLI   CPNOCP+X1,CP    Q.IS 'CP' SPECIFIED.                     10700002
         BE    CMDPRMPT       YES,REQUEST A COMMAND.                    10750002
         SPACE                                                          10800002
*        IF A PARM STRING WERE ENTERED, GET A BUFFER AND FILL IT.       10850002
*        OTHERWISE , PROCEED                                            10900002
         SPACE                                                          10950002
         NC    STRING+X1(X3),STRING+X1  PARM STRING SPECIFIED Q.        11000002
         BNZ   PARMSTRG       YES,SET UP PARMS.                         11050002
         GETMAIN RC,LV=8      GET STORAGE FOR PARMLIST.                 11100002
         LTR   R15,R15        WAS GETMAIN SUCCESSFUL                    11120002
         BNZ   GETMERR        NO. ISSUE MESSAGES AND TERMINATE          11130002
         LA    R3,X4(R1)      PARMS ADDR.                               11150002
         SR    R4,R4          EMPTY LIST.                               11200002
         O     R3,HIGHBIT     INDICATE LAST ELEMENT OF LIST.            11250002
         STM   R3,R4,X0(R1)   INITIALIZE EMPTY LIST                     11300002
         LR    R7,R1          SAVE THE PARAMETER LIST ADDRESS.          11350002
GOPP     EQU   *                                                        11400002
         BAL   R11,SETTRN     TCBTCP BIT=1 TCBTRNB=TCOMTAB ADDRESS.     11450002
         L     R2,TPLECT      ECT POINTER                               11500002
         LA    R3,ECTPCMD     POINTER TO THE MEMBER NAME.               11550002
         L     R4,TSTTCB      THIS IS THE JOB STEP TCB.                 11600002
         SPACE                                                          11650002
*        IF THE MODULE IS ALREADY IN CORE , SET A PSUEDO BREAKPT.       11700002
*        IF IT IS NOT IN CORE , WHICH IS THE 99.9999 PERCENT CASE,      11750002
*        THE BREAK POINT WILL BE SET BY THE IKJEGINT MODULE             11800002
*        AFTER BEING POSTED BY SVC 61.                                  11850002
         SPACE                                                          11900002
         XR    R1,R1                                                    11950002
         L     R1,TCBJPQ-TCB(R4)  FIRST CDE.                            12000002
         USING CDENTRY,R1                                               12050002
CDELOOP  EQU   *                                                        12100002
         LA    R1,X0(R1)      CLEAR HIGH ORDER BYTE                     12150002
         LR    R2,R1          COPY CDE ADDRESS INTO REG TWO             12160002
         LTR   R1,R1          IS IT THE END OF CDE'S                    12200002
         BZ    LLSLOOK        LOOK ON THE LOAD LIST...                  12250002
         CLC   CDNAME(L'CDNAME),X0(R3)  CHECK THE NAME.                 12300002
         BE    SETPSEUD          FOUND A MATCH.                         12350002
         L     R1,CDCHAIN     GET THE NEXT CDE ADDRESS                  12400002
         B     CDELOOP        LOOP.......                               12450002
LLSLOOK  EQU   *                                                        12500002
         L     R1,TCBLLS-TCB(R4)  GET LOAD LIST BEGIN.                  12550002
         USING LLE,R1                                                   12600002
         USING CDENTRY,R2      ESTABLISH ADDRESSABILITY FOR CDE         12650002
LLSLOOP  EQU   *                                                        12700002
         LTR   R1,R1          CHECK FOR END OF LOAD LIST.               12750002
         BZ    OUT            NOFIND GET OUT.                           12800002
         LM    R1,R2,LLECHN   R1=NEXT ENTRY...R2=CDE PTR.               12850002
         CLC   CDNAME(L'CDNAME),X0(R3)  SEE IF NAME IS IN CORE.         12900002
         BNE   LLSLOOP        NO FIND LOOP.....                         12950002
SETPSEUD EQU   *              *                                         13000002
         L     R1,CDENTPT        GET THE ENTRY POINT.                   13050002
         MVC   TSTGOPSW(X2),X0(R1)  SAVE THE INSTR.                     13100002
         MVC   X0(X2,R1),TSTSVC  SET THE PSEUDO BREAK POINT.            13150002
         MVI   DEFERTAB,X0    SET TRN TO ZERO              @ZA15329     13170000
         DROP  R2             FREE BASE REGISTER USED FOR CDE           13200002
         USING ECT,R2         ESTABLISH ADDRESSABILITY FOR ECT          13250002
OUT      EQU   *                                                        13300002
         L     R15,WORKAREA   ADDR OF XCTL LIST FORM FOR XCTL.          13350002
         MVC   X0(XCTLLEN,R15),XCTLFORM  *                              13400002
         OI    TSTFLGS3,TSTGOSW    SET SPECIAL PSEUDO BP SWITCH.        13450002
         CLI   LOADOBJ+X1,LOAD  LOAD OR OBJECT...                       13500002
         LR    R1,R7          PARMAMETER LIST TO REGISTER ONE...        13550002
         L     R13,X4(R13)    FIX UP REGISTER 13 FOR PP                 13600002
         BNE   XCTLOBJ        OBJECT...                                 13650002
         BAL   R14,ATNCHK     GO SEE IF AN ATTENTION WAS POSTED         13660002
         L     R4,TSTDCB      GET THE DCB ADDRESS - 4                   13700002
         LA    R4,X4(R4)      GET THE DCB ADDRESS.                      13750002
         L     R5,BLDLAREA    GET THE BLDL ENTRY ADDRESS.               13800002
         LA    R5,X4(R5)           OFFSET INTO BLDL ENTRY.              13850002
         XC    N28(N44,R13),N28(R13)  ZERO REGS FOR PROTECTION.         13900002
         XCTL  (2,12),DCB=(R4),DE=(R5),SF=(E,(15))                      13950002
         EJECT                                                          14000002
XCTLOBJ  EQU   *                                                        14050002
         BAL   R14,ATNCHK     GO SEE IF AN ATTENTION WAS POSTED         14060002
         XC    N28(N44,R13),N28(R13)  ZERO REGS FOR PROTECTION.         14100002
         XCTL  (2,12),EPLOC=(3),SF=(E,(15))                             14150002
         SPACE                                                          14200002
         SPACE                                                          14250002
CMDPRMPT EQU   *                                                        14300002
*        REQUEST A COMMAND FOR THE PROGRAM BEING TESTED.                14350002
         SPACE                                                          14400002
         XC    TSTIOPRM(L'TSTIOPRM),TSTIOPRM CLEAR IO PARM LIST         14450002
* ISSUE 'ENTER COMMAND FOR CP' MESSAGE AND GET INPUT                    14500002
         IKJEGSIO PTGT,FIRST=M0070,ID=LDR18,MF=(E,TSTIOPRM)             14510002
         LTR   R15,R15        TEST FOR ZERO RETURN CODE                 14520002
         BZ    PRMPGETM       NORMAL RETURN CONTINUE                    14530002
         C     R15,ATTNCODE   TEST FOR ATTENTION IN EFFECT              14540002
         BE    RET4           ATTN HIT, EXIT WITH RETURN CODE=4         14542002
         B     RET0           CANNOT CONTINUE DUE TO IO ERRORS          14544002
         SPACE                                                          14550002
*        BUILD PARMLIST FOR COMMAND SCAN SERVICE ROUTINE AND SCAN THE   14600002
*        VERB FIELD OF THE COMMAND RECEIVED FROM THE TERMINAL.          14650002
PRMPGETM EQU   *                                                        14660002
         GETMAIN RC,LV=16      GET CORE FOR CPPL                        14700002
         LTR   R15,R15        WAS GETMAIN SUCCESSFUL                    14720002
         BNZ   GETMERR        NO. ISSUE MESSAGES AND TERMINATE          14730002
         LR    R7,R1          SAVE ADDRESS.                             14750002
         USING CPPL,R7        *                                         14800002
         SPACE                                                          14850002
         L     R1,WORKAREA    R1=ADDR OF PARM LIST.                     14900002
         MVC   X0(N12,R1),PARMLIST MOVE IN STANDARD PARMS.              14950002
         LA    R2,N24(,R1)    R2=FLAG WORD ADDR.                        15000002
         LA    R3,N28(,R1)    R3=OUTPUT AREA ADDR.                      15050002
         L     R4,INBUF       CPPL COMMAND BUFFER =IN BUFFER.           15100002
         ST    R4,CPPLCBUF     INITIALIZE CPPL.                         15150002
         MVC   CPPLUPT(N12),TPLUPT  DITTO.                              15200002
         SR    R5,R5          FLAG WORD.                                15250002
         STM   R2,R5,N12(R1)   MOVE IN FLAG & OUTPUT AREA ADDRS.        15300002
         ST    R5,INBUF       CLEAR TEST'S INPUT BUFFER POINTER.        15350002
         OI    X0(R2),NOSYNTAX INDICATE 'NO SYNTAX CHECK'.              15400002
         LINK  EP=IKJSCAN,ERRET=SCANERR     SCAN VERB FIELD OF COMMAND. 15550002
         L     R2,TPLECT      ECT POINTER.                              15600002
         NI    ECTSWS,XFF-ECTNOPD  TURN OFF PARM SW.                    15650002
         TM    X6(R3),X40     Q.COMMAND NAME W/NO FIELDS.               15700002
         BZ    SKIP           SWITCH OFF.                               15750002
         OI    ECTSWS,ECTNOPD  TURN SWITCH ON.                          15800002
SKIP     EQU   *              *                                         15850002
         B     GOPP           PLACE PSEUDO-BREAK & ENTER PP.            15900002
ATNCHK   EQU   *                                                        15910002
         L     R4,ECBTMPA     GET ADDRESS OF ATTENTION ECB.             15920002
         TM    ZERO(R4),ATTNBIT HAS AN ATTENTION BEEN POSTED            15930002
         BO    RET4           IF SO, LET'S GET OUT OF HERE              15940002
         BR    R14            OTHERWISE, RETURN TO CALLER               15942002
         SPACE                                                          15950002
PARMSTRG EQU   *                                                        16000002
*        SET UP SPECIFIED PARM STRING AS POINTED TO FROM A STANDARD     16050002
*        VARIABLE LIST.                                                 16100002
         SPACE                                                          16150002
         L     R4,STRING      R4=ADDR OF CHARACTER STRING.              16200002
         LH    R2,STRING+X4    R2=LENGTH OF STRING.                     16250002
         CL    R2,AHUNDRED    DOES IT EXCEED MAXIMUM Q.                 16300002
         BNH   PARMST10       STRING NOT GTR THAN 100,CONTINUE          16350002
         XC    TSTIOPRM(L'TSTIOPRM),TSTIOPRM  CLEAR IO PARM LIST        16360002
* ISSUE 'PARAMETER STRING LENGTH EXCEEDED' MESSAGE                      16370002
* ISSUE 'MAXIMUM LENGTH IS 100' MESSAGE                                 16380002
         IKJEGSIO MSG,FIRST=M0069,SECOND=M0271,ID=LDR18,               *16390002
               MF=(E,TSTIOPRM)                                          16392002
         BAL   R11,IORETCK    TEST IO RETURN CODES                      16394002
         B     RET0           RETURN WITH RC-0-ERROR                    16396002
PARMST10 EQU   *                                                        16400002
         LA    R0,X6(,R2)     R0=LENGTH OF STRING & POINTER.            16450002
         GETMAIN RC,LV=(0)    GET STORAGE FOR LIST AND STRING.          16500002
         LTR   R15,R15        WAS GETMAIN SUCCESSFUL                    16520002
         BNZ   GETMERR        NO. ISSUE MESSAGES AND TERMINATE          16530002
         LA    R3,X4(,R1)     R3=ADDR FOR PASSED PARM STRING.           16550002
         STH   R2,X4(R1)      MOVE LENGTH TO PARMS.                     16600002
        LTR   R2,R2          IS NULL PARM STRING PASSED ZZZZZZZZ        16610000
        BZ    NULPARMS       YES, SKIP EXECUTE INST     ZZZZZZZZ        16620000
         BCTR  R2,X0          DECREMENT FOR EXECUTE.                    16650002
         EX    R2,STRMVC      MOVE THE STRING.                          16700002
NULPARMS O     R3,HIGHBIT     INDICATE LAST LIST ELEMENT. ZZZZZZZZ      16720000
         ST    R3,X0(,R1)     STORE PARM POINTER IN LIST.               16800002
         LR    R7,R1          SAVE THE PARAMETER LIST ADDRESS.          16850002
         B     GOPP           PLACE PSEUDO-BREAK, ENTER PP.             16900002
         SPACE                                                          16950002
STRMVC   MVC   X6(X0,R1),X0(R4)         *                               17000002
         SPACE                                                          17050002
OSLOAD   EQU   *                                                        17100002
*        SET UP PARM LIST AND DDNAME LIST FOR OS LOADER.LOAD THE        17150002
*        PROCESSING PORTION OF THE LOADER,BRANCH TO IT AND DELETE       17200002
*        IT UPON ITS RETURN.                                            17250002
         SPACE                                                          17300002
         GETMAIN  RC,LV=OBJCORE  GET CORE FOR OSLOAD PARMS              17350002
         LTR   R15,R15        WAS GETMAIN SUCCESSFUL                    17370002
         BNZ   GETMERR        NO. ISSUE MESSAGES AND TERMINATE          17380002
         LA    R2,X8(R1)      OPTION LIST ADDRESS.                      17400002
         LA    R3,OPTCORE(R2)  POINT BEYOND OPTIONS                     17450002
         O     R3,HIGHBIT     INDICATE LAST ELEMENT.                    17500002
         MVC   X0(OPTLN,R2),OPTIONS MOVE IN OPTIONS.                    17550002
         XC    X0(N98,R3),X0(R3) CLEAR DDNAME LIST.                     17600002
         MVC   N98-X8(X8,R3),TERMDD  PLACE DDNAME INTO LIST             17650002
         MVI   X1(R3),N96     DDNAME LIST LENGTH.                       17700002
         MVC   X2(X8,R3),INTSTDDN MOVE DDNAME TO SYSLIN SLOT IN LIST.   17750002
         STM   R2,R3,X0(R1)   INITIALIZE LIST.                          17800002
         LR    R2,R1          SAVE LIST ADDR.                           17850002
         LOAD  EP=HEWLOAD,ERRET=LOADERR     LOAD THE VS LOADER.         17900002
         LR    R15,R0         R15=VS LOADER ENTRY POINT.                17950002
         LR    R1,R2          R1=LIST ADDR.                             18000002
         BALR  R14,R15        ENTER LDR TO LOAD TESTED PROGRAM          18050002
         C     R15,FOUR       Q. IS PROGRAM SUCCESSFULLY LOADED.        18150002
         BH    RET0           NO. DEPART                                18200002
         LR    R3,R1          SAVE NAME POINTER IN R3.                  18250002
         DELETE EP=HEWLOAD     DELETE THE VS LOADER.                    18300002
         LR    R1,R2          R1=ADDR OF STORAGE GOTTEN FOR LOADER.     18350002
         FREEMAIN  RC,LV=OBJCORE,A=(1)  FREE THE PARM CORE              18400002
         LTR   R15,R15        WAS FREEMAIN SUCCESSFUL                   18420002
         BZ    OSLOAD1        YES. CONTINUE                             18430002
         XC    TSTIOPRM(L'TSTIOPRM),TSTIOPRM  CLEAR IO PARM LIST        18440002
* ISSUE 'FREEMAIN SVC ERROR' MESSAGE                                    18444002
         IKJEGSIO SVCERR,SVC=120,RC=(R15),ID=LDR18,MF=(E,TSTIOPRM)      18446002
         BAL   R11,IORETCK    TEST IO RETURN CODES                      18448002
* CONTINUE AFTER ERROR                                                  18448402
         SPACE                                                          18450002
*        ISSUE A LOAD FOR THE PROGRAM BROUGHT IN BY THE VS LOADER       18500002
*        IN ORDER TO GET IT ON THE LOAD LIST.SAVE THE PROGRAM NAME      18550002
*        IN TSTCURLD OF TCOMTAB AND SET UP PARAMETERS REQUIRED BY       18600002
*        THE PROGRAM.                                                   18650002
         SPACE                                                          18700002
OSLOAD1  EQU   *                                                        18710002
         L     R2,TPLECT      ECT POINTER.                              18750002
         MVC   ECTPCMD(X8),X0(R3)  PUT MEMBER NAME IN ECT.              18800002
         B     PARMSET        SET UP PARAMETERS FOR THE PROGRAM.        18850002
SETTRN   EQU   *                                                        18900002
         BAL   R14,ATNCHK     GO SEE IF AN ATTENTION WAS POSTED         18910002
         L     R2,WORKAREA    GET POINTER TO WORKING CORE               18950002
         USING TSTCWORK,R2    ESTABLISH ADDRESSABILITY                  19000002
         L     R4,PPTCB       OUR TCB ADDRESS ...                       19050002
         IKJEGS9G TRN,TCBADDR=(R4),VALUE=(R9),MF=(E,LDRSVCL) SET TCBTRN 19100002
         LTR   R15,R15        CHECK RETURN CODE FROM SVC                19150002
         BNZ   PUTSVCMG       BRANCH IF OTHER THAN ZERO                 19200002
         SPACE                                                          19250002
         IKJEGS9G TCPON,TCBADDR=(R4),MF=(E,LDRSVCL) TCBTCP              19300002
         LTR   R15,R15        CHECK RETURN CODE FROM SVC                19350002
         BNZ   PUTSVCMG       BRANCH IF OTHER THAN ZERO                 19400002
         BR    R11            RETURN                                    19450002
RET4     LA    R15,X4         RC=4                                      19500002
         B     RETURN         RETURN TO CALLER                          19510002
RET0     SR    R15,R15        ZERO RETURN CODE.                         19520002
RETURN   EQU   *              RETURN.                                   19550002
         LR    R2,R15        SAVE RETURN CODE                           19552002
         ESTAE 0             REMOVE ESTAE                               19560002
         LR    R15,R2        RESET RETURN CODE                          19562002
         NI    TSTFLGS4,XFF-TSTRERTN TURN OFF RETRY BIT                 19570002
         L     R13,X4(R13)     ADDRESS OF REGSAVE AREA                  19600002
         RETURN (14,12),RC=(15)  RETURN.                                19650002
PUTSVCMG EQU   *                                                        19700002
         XC    TSTIOPRM(L'TSTIOPRM),TSTIOPRM  CLEAR IO PARM LIST        19750002
* ISSUE 'LOADER FAILED' MESSAGE                                         19800002
* ISSUE ' SVC 97 RETURN CODE NONZERO' MESSAGE                           19850002
         IKJEGSIO MSG,FIRST=M0058,INST11=(NUM,I0031),SECOND=M0283,     *19900002
               ID=LDR18,MF=(E,TSTIOPRM)                                 19950002
         BAL   R11,IORETCK    TEST IO RETURN CODES                      20000002
         B     RET0           RETURN WITH RC=0-ERROR                    20050002
         EJECT                                                          20100002
IORETCK  EQU   *              IO RETURN CODE CHECK ROUTINE              20150002
         B     IORC0(R15)     USE RETURN CODE AGAINST BRANCH TABLE      20200002
IORC0    EQU   *                                                        20250002
         B     X0(R11)        RC=0 NORMAL RETURN, CONTINUE              20300002
         B     X0(R11)        RC=4 IO ERROR TRY TO CONTINUE             20350002
         B     RET0           RC=8 SHOULD NEVER HAPPEN                  20360002
         B     X0(R11)        RC=12 IO ERROR TRY TO CONTINUE            20370002
         B     RET4           RC=16 ATTN,RETURN TO INIT WITH RC=4       20380002
         B     X0(R11)        RC=20 RETRY ENTERED TRY TO CONTINUE       20390002
         B     RETURN         RC=24, PASS IT ON BACK TO INIT            20392002
         EJECT                                                          20420002
SCANERR  EQU   *              LOGIC FOR LINK ERRET                      20440002
         XC    TSTIOPRM(L'TSTIOPRM),TSTIOPRM  CLEAR IO PARM LIST        20440402
         LR    R0,R1         LOAD ABEND CODE INTO R0                    20440802
* ISSUE 'LINK TO IKJSCAN SVC ERROR' MESSAGE                             20441602
         IKJEGSIO SVCERR,SVC=6,ABENDRG=(R0),RC=(R15),                  *20441702
               INST11=(NUM,I0043),ID=LDR18,MF=(E,TSTIOPRM)              20459702
         BAL   R11,IORETCK    TEST IO RETURN CODES                      20468702
         B     RET0           TERMINATE WITH RC=0-ERROR                 20477702
         SPACE                                                          20479702
LOADERR  EQU   *             LOGIC FOR LOAD ERRET                       20479802
         XC    TSTIOPRM(L'TSTIOPRM),TSTIOPRM CLEAR IO PARM LIST         20479902
         LR    R0,R1         LOAD ABEND CODE INTO R0                    20482802
* ISSUE 'LOAD HEWLOAD SVC ERROR' MESSAGE                                20486202
         IKJEGSIO SVCERR,SVC=8,ABENDRG=(R0),RC=(R15),                  *20489102
               INST11=(NUM,I0134),ID=LDR18,MF=(E,TSTIOPRM)              20492002
         BAL   R11,IORETCK   TEST IO RC'S                               20494902
         B     RET0          TERMINATE WITH RC=0-ERRORS                 20497802
         SPACE                                                          20500702
GETMERR  EQU   *              GETMAIN ERROR HANDLING LOGIC              20503602
         XC    TSTIOPRM(L'TSTIOPRM),TSTIOPRM  CLEAR IO PARM LIST        20506502
* ISSUE 'GETMAIN SVC ERROR' MESSAGE                                     20509402
         IKJEGSIO SVCERR,SVC=10,RC=(R2),ID=LDR18,MF=(E,TSTIOPRM)        20515202
         BAL   R11,IORETCK    TEST IO RETURN CODES                      20521002
         B     RET0           TERMINATE WITH RC=0                       20526802
         EJECT                                                          20532602
*************************************************                       20534602
*     ESTAE EXIT ROUTINE                                                20536602
*************************************************                       20537002
ESTAERTY EQU   *                                                        20537402
* ESTABLISH PROGRAM ADDRESSABILITY                                      20537802
         DROP  R12                                                      20538202
         USING *,R15         USE R15 AS TEMPORARY BASE REG              20538302
         L     R12,BASEAD    LOAD PROGRAM BASE REGISTER                 20550602
         DROP R15                                                       20560602
         USING IKJEGLDR,R12  ESTABLISH ADDRESSABILITY                   20562602
* REESTABLISH TCOMTAB ADDRESSABILITY                                    20563002
         L     R2,CVTPTR     GET CVT ADDRESS                            20567102
         USING CVT,R2                                                   20569102
         L     R2,CVTTCBP    GET ADDRESS OF TCB WORDS                   20571102
         L     R2,X4(R2)     GET ADDRESS OF THIS TCB                    20571202
         DROP  R2                                                       20575302
         USING TCB,R2                                                   20577302
         L     R2,TCBOTC     GET ADDRESS OF TEST'S(MOTHER) TCB          20579302
         L     R9,TCBTRN     GET TCOMTAB ADDRESS                        20579402
         L     R13,REGSAVE2  GET LOADERS SAVE AREA ADDRESS              20583502
         LA    R15,RC20      SET RC=20                                  20585502
         B     RETURN        RETURN TO INITIALIZATION                   20587502
         EJECT                                                          20587602
         SPACE                                                          20587702
XCTLFORM XCTL  EP=XCTLFORM,SF=L                                         20591802
XCTLLEN  EQU   *-XCTLFORM     *                                         20595902
OPTIONS  DS    0F             ROUND TO FULL WORD                        20600002
         DC    AL2(OPTLN-2)   LENGTH OF LIST.                           20650002
         DC    C'TERM,NOPRINT,NCAL,NOMAP,NORES,LET,NAME=TEMPNAME'      *20700002
               OSLOADER PARAMETERS                                      20750002
OPTLN    EQU   *-OPTIONS                                                20800002
OPTLN1   DS    0F             ROUND TO FULL WORD                        20850002
OPTCORE  EQU   OPTLN1-OPTIONS  OPTIONS CORE REQUIREMENT                 20900002
OBJCORE  EQU   8+OPTCORE+12*8  TOTAL PARAMETER CORE FOR OSLOADER        20950002
AHUNDRED DC    A(100)         *                                         21000002
FOUR     DC    F'4'           FOUR                                      21050002
ATTNCODE DC    F'16'          RETURN CODE 16=ATTENTION                  21100002
HIGHBIT  DC    X'80000000'    HIGH ORDER BIT                            21150002
DUMMYATB DC    X'FF'         DUMMY ABEND TABLE                          21200002
BASEAD   DC    A(IKJEGLDR)   PROGRAM BASE ADDRESS                       21250002
LDRSPL   IKJEGSPL RTRY=ESTAERTY,ABNTB=DUMMYATB,MODNM=IKJEGLDR,         *21300002
               TNM=LOADER    ESTAE PARAMETER LIST                       21350002
         TSTCWORK                                                       21450002
         ORG   CWORKCVT       ORIGIN OF SPACE FOR FOLLOWING DS          21500002
LDRSVCL  DS    XL12           SPACE FOR SVC97 PARAMETER LIST            21550002
LDRESTAE DS    XL16          SPACE FOR ESTAE PARM LIST                  21600002
         EJECT                                                          21650002
         IHACDE                                                         21700002
         EJECT                                                          21750002
         IHALLE                                                         21800002
         EJECT                                                          21850002
CVT      DSECT                                                          21900002
         CVT                                                            21950002
         EJECT                                                          22000002
IKJEGLDR CSECT                                                          22050002
         SPACE                                                          22100002
*        PARSE CONSTANTS AND DSECT                                      22150002
         SPACE                                                          22200002
IKJEGYDR IKJPARM                                                        22250002
DSNAME   IKJPOSIT DSNAME                                                22300002
STRING   IKJPOSIT QSTRING                                               22350002
LOADOBJ  IKJKEYWD                                                       22400002
         IKJNAME 'LOAD'                                                 22450002
         IKJNAME 'OBJECT'                                               22500002
CPNOCP   IKJKEYWD                                                       22550002
         IKJNAME 'CP'                                                   22600002
         IKJNAME 'NOCP'                                                 22650002
         IKJENDP                                                        22700002
         EJECT                                                          22750002
         TCOMTAB                                                        22800002
         SPACE                                                          22950002
         IKJCPPL                                                        23000002
         SPACE                                                          23050002
         IKJECT                                                         23100002
         SPACE                                                          23150002
         IKJTPL                                                         23200002
         SPACE                                                          23250002
         IKJTCB                                                         23300002
         SPACE                                                          23350002
         EJECT                                                          23360002
IKJEGLDR CSECT                                                          23400002
         DS    0F            FORCE FULLWORK ALIGNMENT                   23450002
LDRPATCH DC    50C'Z'        PATCH AREA                                 23500002
         END                                                            24550002
