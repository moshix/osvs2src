         TITLE 'IKJEGPCH, ''ASSIGN'' SUBCOMMAND OF TEST, INITIALIZE'    00070002
         COPY  IKJEGSIO                                                 00080002
         EJECT                                                          00090002
         MACRO                                                          00100002
         VALUPDE                                                        00150002
*********************************************************************** 00200002
*                                                                     * 00250002
*        MAPPING DSECT FOR PDES FOR VALUES                            * 00300002
*                                                                     * 00350002
*        STATUS --  VERSION NO. 02                                    * 00400002
*                                                                     * 00450002
*        CHANGE LEVEL -- 00 - DATE 03/09/72                           * 00500002
*                                                                     * 00550002
*********************************************************************** 00600002
VALUPDE  DSECT                                                          00650002
VALUPTR  DS    F -                      POINTER TO CHARACTER STRING,    00700002
*                                       FIRST BYTE AFTER QUOTE          00750002
VALULNH  DS    H -                      LENGTH OF CHARACTER STRING,     00800002
*                                       EXCLUDING QUOTES                00850002
VALUFLG  DS    XL1 -                    FLAG                            00900002
VALUBIT  EQU   B'10000000' -            BIT ON MEANS VALUE PRESENT      00950002
VALUTYPE DS    CL1 -                    TYPE CHARACTER (CHARACTER       01000002
*                                       PRECEDING QUOTE)                01050002
VALUCHAI DS    F -                      ADDRESS OF NEXT VALUE           01100002
*                                       PDE ON CHAIN                    01150002
VALUPDL  EQU   *-VALUPDE -              LENGTH OF VALUE PDE             01200002
         MEND                                                           01250002
         EJECT                                                          01300002
*********************************************************************** 01350002
*                                                                     * 01400002
* STATUS--                                                            * 01450002
*    CHANGE LEVEL 00, VERSION NUMBER 02, OS/VS2 (5/17/73)             * 01500002
*                                                                     * 01550002
* FUNCTION--                                                          * 01600002
*                                                                     * 01650002
*    IKJEGPCH IS THE FIRST OF TWO LOAD MODULES THAT PROCESS           * 01700002
*    THE ASSIGNMENT OF VALUES FUNCTION OF TEST.  THE ASSIGNMENT       * 01750002
*    FUNCTION MODIFIES VALUES IN MAIN STORAGE AND IN REGISTERS.       * 01800002
*                                                                     * 01850002
*    IKJEGPCH IS LINKED TO FROM IKJEGMNL WITH REGISTER NINE           * 01900002
*    POINTING TO THE TEST COMMUNICATION TABLE (TCOMTAB).  WORK        * 01950002
*    AREAS ARE INITIALIZED THAT WILL LATER BE USED BY IKJEGASN,       * 02000002
*    THE SECOND OF TWO LOAD MODULES THAT PROCESS THE ASSIGNMENT       * 02050002
*    FUNCTION.                                                        * 02100002
*                                                                     * 02150002
*    THE BUFFER IS SCANNED FOR THE SUBCOMMAND PARAMETERS AFTER        * 02200002
*    ISSUING A LINK TO IKJPARS.  IKJPARS IS USED TO BUILD             * 02250002
*    PARAMETER DESCRIPTOR ELEMENTS IN THE PARAMETER DESCRIPTOR        * 02300002
*    LIST.  THE VALUES SCANNED BY IKJPARS ARE VALIDITY CHECKED        * 02350002
*    WHEN IKJEGPCH IS ENTERED AT 1) PCHVALAD TO CHECK THE             * 02400002
*    SPECIFIED ADDRESS AND 2) PCHVALTP TO CHECK THE DATA TYPES        * 02450002
*    AND VALUES.                                                      * 02500002
*                                                                     * 02550002
*    IF THE SCAN IS SUCCESSFUL, A BRANCH TO IKJEGASN IS               * 02600002
*    ISSUED.  IF UNSUCCESSFUL, A RETURN CODE IS SET AND CONTROL       * 02650002
*    IS RETURNED TO IKJEGMNL WITH A BR 14.                            * 02700002
*                                                                     * 02750002
* ENTRY POINTS--                                                      * 02800002
*         IKJEGPCH  MAIN ENTRY POINT, ENTERED FROM IKJEGMNL           * 02850002
*         PCHVALTP  PARSE EXIT ROUTINE FOR VALIDITY CHECKING VALUES   * 02900002
*         PCHVALAD  PARSE EXIT ROUTINE FOR ADDRESS CONVERSION         * 02950002
*                                                                     * 03000002
* INPUT--                                                             * 03050002
*    THE SUBCOMMAND BUFFER AS ENTERED BY THE USER.                    * 03100002
*    REGISTER NINE POINTS TO THE TCOMTAB                              * 03150002
*                                                                     * 03200002
* OUTPUT--                                                            * 03250002
*    AREA 'PCHWORK' INITIALIZED TO PERFORM THE PATCH REQUESTED.       * 03300002
*    REGISTER FIFTEEN CONTAINS A RETURN CODE                          * 03350002
*                                                                     * 03400002
* EXTERNAL REFERENCES--                                               * 03450002
*         IKJEGCVT FOR CONVERSION OF ADDRESSES TO BINARY.             * 03500002
*         IKJEGI0  FOR OUTPUTTING MESSAGES.                           * 03550002
*         IKJPARSE FOR PARSING THE SUBCOMMAND STRING.                 * 03600002
*                                                                     * 03650002
* EXITS, NORMAL--                                                     * 03700002
*         BRANCH TO MODULE 'IKJEGASN'                                 * 03750002
*                                                                     * 03800002
* EXITS, ERROR--                                                      * 03850002
*         BRANCH REGISTER 14 TO CALLER WITH A RETURN CODE OF ZERO     * 03900002
*         IF AN ERROR OCCURED DURING PARSE OR CONVERT.                * 03950002
*         BRANCH REGISTER 14 TO CALLER WITH RETURN CODE SIXTEEN IF    * 04000002
*         AN ATTENTION IS SCHEDULED.                                  * 04050002
*         BRANCH REGISTER 14 RETURNING TO CALLER WITH A CONDITION CODE* 04100002
*         OF TWENTY (20) FROM THE STAE RETRY ROUTINE AFTER AN ABEND.  * 04150002
*         ALSO A 24 INDICATING THE ESTAE COULD NOT BE ISSUED          * 04160002
*                                                                     * 04200002
* TABLES/WORK AREAS --                                                * 04250002
*    1 TCOMTAB, POINTED AT BY REG 9 IS THE TEST COMMUNICATIONS TABLE  * 04300002
*    2 PCHWORK, A DESECT WHICH DESCRIBES PART OF THE COMMON WORKING   * 04350002
*    CORE MAINTAINED BY TEST AND POINTED AT BY HWORKAREA' IN TCOMTAB. * 04400002
*    3 PARAMETER DESCRIPTOR LIST (PDL) PREPARED BY IKJPARSE TO        * 04450002
*    DESCRIBE THE VARIOUS FIELDS OF THE PARSED SUBCOMMAND             * 04500002
*                                                                     * 04550002
* ATTRIBUTES--                                                        * 04600002
*    REENTRANT, REFRESHABLE, PROBLEM PROGRAM.                         * 04650002
*                                                                     * 04700002
* CHARACTER CODE DEPENDENCY--                                         * 04750002
*    OPERATION OF THIS MODULE REQUIRES AND DEPENDS ON THE BIT         * 04800002
*    PATTERNS AND BINARY VALUES OF THE EBCDIC CHARACTER SET.          * 04850002
*    THE DEPENDENCY IS IN TABLE 'PCHTRTBL'. OTHER PORTIONS OF         * 04900002
*    THIS MODULE REQUIRES ONLY REASSEMBLY FOR CHANGE OF CODE.         * 04950002
*                                                                     * 05000002
* NOTES--                                                             * 05050002
*    THIS MODULE REQUIRES THE STANDARD INSTRUCTION SET.               * 05100002
*                                                                     * 05150002
*********************************************************************** 05200002
IKJEGPCH CSECT                                                          05250002
         SPACE                                                          05300002
* REGISTER EQUATES.                                                     05350002
         SPACE                                                          05400002
R0       EQU   0                   GENERAL USE AND PARAMETER REG        05450002
R1       EQU   1                   GENERAL USE AND PARAMETER REG        05500002
R2       EQU   2                   GENERAL USE REG                      05550002
R3       EQU   3                   GENERAL USE REG                      05600002
R4       EQU   4                   GENERAL USE REG                      05650002
R5       EQU   5                   GENERAL USE REG                      05700002
R6       EQU   6                   GENERAL USE REG                      05750002
R7       EQU   7                   GENERAL USE REG                      05800002
R8       EQU   8                   BASE REG FOR PDE'S                   05850002
R9       EQU   9                   BASE REG FOR TCOMTAB, COMM. DSECT    05900002
R10      EQU   10                  INTERNAL LINK REG FOR THIS MODULE    05950002
R11      EQU   11                  BASE REG FOR THIS PROGRAM.           06000002
R12      EQU   12                  *                                    06050002
R13      EQU   13                  PTR TO CURRENT SAVE AREA.            06100002
R14      EQU   14                  GENERAL USE, AND RETURN ADDR REG     06150002
R15      EQU   15                  GENERAL USE, RETURN CODE, CALL REG   06200002
         SPACE 2                                                        06250002
*  DATA EQUATES FOR OPERANDS OF IMMEDIATE TYPE INSTRUCTIONS.            06300002
         SPACE                                                          06350002
CHARA    EQU   C'A'                                                     06400002
CHARZ    EQU   C'Z'                                                     06450002
NULL     EQU   0                   USED FOR BALR R,0 AND BCTR R,0.      06500002
ZERO     EQU   0                                                        06550002
ONE      EQU   1                   SYMBOLIC FOR THE NUMBER 1            06600002
TWO      EQU   2                   SYMBOLIC FOR THE NUMBER '2'          06650002
FOUR     EQU   4                                                        06750002
EIGHT    EQU   8                                                        06800002
TWELVE   EQU   12                  SYMBOLIC FOR 12, USED AS AN RC       06850002
SIXTEEN  EQU   16                                                       06900002
TWENTY   EQU   20                  VALUE FOR COMPARE                    06950002
B24      EQU   24                  SHIFT VALUE                          07000002
R5OFF    EQU   40                  OFFSET INTO SAVE AREA FOR REG 5      07150002
R6OFF    EQU   44                  OFFSET INTO SAVE AREA FOR REG 6      07200002
R10OFF   EQU   60                  OFFSET INTO SAVE AREA FOR R10        07250002
R1OFF    EQU   24                  OFFSET INTO SAVE AREA FOR R1         07260002
DEC0     EQU   C'0'                                                     07300002
DEC1     EQU   C'1'                EBCDIC ONE FOR IMMEDIATE OPERANDS    07350002
DEC9     EQU   C'9'                                                     07400002
LCE      EQU   X'85'               EBCDIC CODE FOR A LOWER CASE 'E'     07450002
UCE      EQU   C'E'                USED IN IMMEDIATE OPERANDS           07500002
PLUS     EQU   C'+'                USED IN IMMEDIATE OPERANDS           07550002
MINUS    EQU   C'-'                USED IN IMMEDIATE OPERANDS           07600002
QUOTE    EQU   C''''               USED FOR SCANNING CHAR STRINGS.      07650002
RPAREND  EQU   C')'                END DELIMITER FOR SCON               07700002
LOW      EQU   4                   SYMBOLIC FOR BR ON COND CODE 1       07750002
HIGH     EQU   2                   SYMBOLIC FOR BR ON COND CODE 2       07800002
HIGHMASK EQU   X'20'               MASK FOR BRANCH HIGH                 07850002
LOEQMASK EQU   X'D0'               MASK FOR BRANCH LOW OR EQUAL         07900002
ODD      EQU   X'01'               FOR TESTING WHETHER AN ADDR IS ODD.  07950002
SIDATAPT EQU   0                   OFFSET INTO SYM-INFO BLOCK FOR DATA  08050002
M2OFFSET EQU   80                  OFFSET INTO BUFFER FOR 2ND LEVEL MSG 08150002
LL00IDL  EQU   14                  LENGTH OF LL00 AND ID FLDS OF MSG    08200002
         SPACE                                                          08450002
FOREPTR  EQU   8                   OFFSET TO FOREWARD PTR IN SAVE AREA  08500002
BACKPTR  EQU   4                   OFFSET TO BACKWARD PTR IN SAVE AREA  08550002
         SPACE                                                          09100002
HEXFF    EQU   X'FF'                                                    09150002
*  SYMBOLIC OFFSETS INTO THE MESSAGE CSECT FOR VARIOUS MESSAGE          10100002
         SPACE                                                          10150002
MSG101   EQU   101                 INVALID ADDRESS MSG                  10200002
MSG279   EQU   279                 WRITE PROTECT                        10250002
MSG212   EQU   212                 DATA FORMAT INCORRECT                10350002
MSG010   EQU   10                  VALUE BEGINNING---                   10400002
MSG209   EQU   209                 DATA STRING CONTAINS                 10450002
MSG210   EQU   210                 DATA STRING TO LONG                  10500002
MSG211   EQU   211                 ABSOLUTE VALUE TO GREAT              10550002
MSG103   EQU   103                 NO CORE MESSAGE (NO SECOND LEVEL)    10552002
MSG104   EQU   104                 ASSIGNMENT FAILED--                  10560002
MSG277   EQU   277                 PARSE RETURN CODE 12                 10570002
INST6    EQU   6                   FIXED INSERT NO. 6 (ASSIGNMENT)      10580002
         EJECT                                                          10600002
         TITLE 'IKJEGPCH, ''SET'' SUBCOMMAND OF TEST, INITIALIZATION'   10650002
*********************************************************************** 10700002
*                                                                     * 10750002
*  BEGIN WITH THE STANDARD LINKAGE CONVENTIONS.                       * 10800002
*                                                                     * 10850002
*********************************************************************** 10900002
         SAVE  (14,12),,*                                               10950002
         LR    R11,R15             PICK UP ENTRY POINT ADDR FOR BASE    11000002
         USING IKJEGPCH,R11                                             11050002
         USING TCOMTAB,R9                                               11100002
         L     R6,REGSAVE2         ADDR OF SAVE AREA                    11150002
         ST    R6,FOREPTR(R13)     FORWARD AND BACKWARD CHAIN           11200002
         ST    R13,BACKPTR(R6)         THE SAVE AREAS.                  11250002
         LR    R13,R6              SET PTR TO NEW SAVE AREA             11300002
         SPACE                                                          11350002
         L     R10,WORKAREA        GET PTR TO WORKING CORE              11400002
         USING TSTCWORK,R10                                             11450002
         LA    R10,CWORKCMD        SET UP BASE OF SUB-CMD WORK CORE     11500002
         USING PCHWORK,R10                                              11550002
         XC    PCHWORK(LPCHWORK),PCHWORK  CLEAR THE CORE                11600002
*                                                                       11650002
*        ISSUE ESTAE MACRO                                              11700002
*                                                                       11750002
         STM   R10,R12,R10OFF(R13)      SAVE BASE PTR                   12000002
         L     R2,TSTSTAE          R2= ADDR OF IKJEGSTA                 12010002
         ESTAE (R2),CT,PARAM=PCHSPL,RECORD=YES,MF=(E,PCHWORK)           12050002
         LTR   R15,R15             TEST RETURN CODE                     12100002
         BZ    STAEOK              CONTINUE IF ZERO                     12150002
         STC   R15,TSTESTRC        SAVE THE RETURN CODE FOR MNL         12160002
         LA    R15,B24             R15= RETURN CODE 24                  12170002
         B     PCHRET1A            PRINT ERROR MESSAGE IF SO            12200002
STAEOK   EQU   *                                                        12250002
         SPACE                                                          12400002
*********************************************************************** 12450002
*                                                                     * 12500002
*  SET UP PARAMETER LIST FOR CALLING PARSE.                           * 12550002
*                                                                     * 12600002
*********************************************************************** 12650002
         SPACE                                                          12700002
         LA    R1,PPCL             PTR TO PARSE LIST                    12750002
         USING PPL,R1              ADDRESSABILITY FOR LIST              12800002
         L     R2,TSTUPT           PTR TO UPT                           12850002
         L     R3,TSTECT           PTR TO ECT                           12900002
         L     R4,TSTCPECB         PTR TO P/P ECB                       12950002
         STM   R2,R4,PPLUPT        PUT PTR TO UPT ECT ECB TO PARSE LIST 13000002
         L     R2,PARSEPCL         PTR TO PARSE PCL                     13150002
         LA    R3,TSTANSPL         PTR TO ANSWER PLACE                  13200002
         L     R4,INBUF            PTR TO INPUT BUFFER                  13250002
         STM   R2,R4,PPLPCL        PUT PTR TO PARSE PCL INTO PARSE LIST 13300002
*                                  PUT PTR TO ANS PLACE INTO PARSE LIST 13350002
*                                  PUT PTR TO INPUT BUF INTO PARSE LIST 13400002
         SPACE                                                          13450002
*  R6 SHOULD STILL CONTAIN THE SAVE AREA ADDRESS AT THIS POINT.         13500002
         SPACE                                                          13550002
         ST    R6,PPLUWA           PUT INTO PARSE LIST                  13600002
         DROP  R1                                                       13650002
         SPACE                                                          13700002
         LINK  EP=IKJPARS,ERRET=LINKFAIL      LINK TO THE PARSE RTN.    13750002
PARSEBLO EQU   *                                                        13800002
         SPACE                                                          13850002
*********************************************************************** 13900002
*                                                                     * 13950002
*  TEST RETURN CODE FROM PARSE.                                       * 14000002
*       IF 0 FALL THRU AND SET UP TO GO TO IKJEGCVT TO CONVERT VALUE. * 14050002
*       IF NON-ZERO, GO ANALYZE FURTHER.                              * 14100002
*                                                                     * 14150002
*********************************************************************** 14200002
         SPACE                                                          14250002
         LTR   R15,R15             TEST RETURN CODE FROM PARSE.         14300002
* PLEASE NOTE THAT A BRANCH TABLE HAS BEEN USED IN LIEU OF NUMEROUS   * 14350002
* TESTS FOR RETURN CODE.                                              * 14400002
         BNZ   PARSRC(R15)         IF NON-ZERO, GO ANALYZE FURTHER.     14450002
         L     R8,TSTANSPL         GET PTR TO THE PDL                   14500002
         USING IKJPARMD,R8                                              14550002
         MVC   VPDE(VALUPDL),VAL   COPY THE CURRENT VALUE PDE           14600002
         LA    R8,ADDR             SET UP A BASE REG FOR ADDR PDE       14650002
         USING IKJPARMA,R8                                              14700002
         TM    PDEFLG4,GENR+SFPR+LFPR  IS ADDRESS TYPE REGISTER         14750002
         BZ    PCH010              IF NOT REG, BRANCH                   14800002
         NC    PDEINDCT,PDEINDCT   IF REG, IS IT INDIRECTED             14850002
         BNZ   PCH012              IF INDIRECTED, BRANCH                14900002
PCH010   EQU   *                                                        14950002
         MVC   A1TYPE,PDEFLG4      MOVE ADDR TYPE TO WORKING CORE       15000002
PCH012   EQU   *                                                        15050002
         L     R2,PDEUSER          GET RESOLVED ADDRESS OR SI PTR       15100002
         LTR   R2,R2               IS ADDR SYMBOLIC                     15150002
         BNM   PCH018              IF NOT SYMBOLIC, BRANCH              15200002
         L     R2,SIDATAPT(R2)     GET ACTUAL ADDR FROM SI BLOCK        15250002
PCH018   EQU   *                                                        15300002
         ST    R2,A1               SAVE RESOLVED ADDR IN WORKING CORE   15350002
         DROP  R8                                                       15400002
         SPACE                                                          15450002
*        AT THIS POINT CONTROL IS PASSED TO 'IKJEGASN', THE MAIN        15500002
*        PROCESSING MODULE FOR THE 'SET' SUBCOMMAND.                    15550002
         L     R15,ASNVCON         R15= ADDR OF IKJEGASN ENTRY          15650002
         BR    R15                 PASS CONTROL TO IKJEGASN             15900002
         SPACE 2                                                        15950002
*     RETURN TO MAINLINE IN AN ERROR CASE.                              16000002
PCHRET1  EQU   *                                                        16100002
         SPACE                                                          16150002
*  TURN OFF SWITCHES USED BY PATCH, AND ISSUE ESTAE 0                   16200002
         SPACE                                                          16250002
         L     R15,PCHRETCD        PICK UP PRESET RETURN CODE.          16350002
PCHRET1A DS    0H                  RETURN TO MNL                        16352002
         NI    TSTFLGS1,HEXFF-PCHLSTVL                                  16360002
         LR    R2,R15              SAVE THE RETURN CODE                 16362002
         ESTAE 0                   REMOVE THE ESTAE                     16370002
         NI    TSTFLGS4,HEXFF-TSTRERTN  TURN OFF ABEND SWITCH           16380002
         LR    R15,R2              RESTORE THE RETURN CODE              16390002
         L     R13,BACKPTR(R13)    GET ADDR OF PREVIOUS SAVE AREA.      16400002
         RETURN  (14,12),T,RC=(15) RETURN TO THE CALLER                 16450002
         EJECT                                                          16500002
PCHVALTP EQU   *                                                        16550002
         SAVE  (14,12)                                                  16600002
         LA    R14,OFFSETTP(R15)   SET UP CORRECT VALIDITY RTN ADDR.    16650002
* PLEASE NOTE THAT A BRANCH TABLE HAS BEEN USED IN LIEU OF NUMEROUS   * 16700002
* TESTS FOR CORRECT COMMON LINK ROUTINE.                              * 16750002
         B     OFFSET1(R15)        GO TO COMMON LINK CONVENTION RTN.    16800002
         SPACE                                                          16850002
PCHVALAD EQU   *                                                        16900002
         SAVE  (14,12)                                                  16950002
         LA    R14,OFFSETAD(R15)   SET UP CORRECT VALIDITY RTN ADDR.    17000002
OFFSET1  EQU   *-PCHVALTP          OFFSET TO GET TO COMMON LINK RTN.    17100002
         L     R12,FOUR(R1)        R12=PTR TO LIST'S SAVE AREA          17150002
         LR    R3,R1               SAVE REG1                            17200002
         LM    R6,R12,R6OFF(R12)   RESTORE REQUIRED REGS OF LIST        17250002
         L     R2,REGSAVE3         PTR TO NEW SAVE AREA                 17300002
         ST    R2,FOREPTR(R13)     FOREWARD CHAIN SAVE AREAS.           17350002
         ST    R13,BACKPTR(R2)     BACKWARD CHAIN SAVE AREAS.           17400002
         LR    R13,R2              SET NEW SAVE AREA                    17450002
         L     R1,ZERO(R3)         GET ADDR OF PDE TO VALIDITY CHECK.   17500002
         L     R2,OUTBUF           R2= ADDR OF OUTBUF                   17550002
         XC    ZERO(FOUR,R2),ZERO(R2)    CLEAR FIRST WORD OF OUTBUF     17600002
         OI    TSTFLGS4,TSTVALCK   INDICATE WE ARE IN A VALIDITY CK     17620002
         BR    R14                 GO TO REQUESTED ROUTINE.             17650002
         SPACE 2                                                        17700002
*  MAIN PORTION OF THE ADDRESS VALIDITY CHECKING ROUTINE.               17750002
         SPACE                                                          17800002
OFFSETAD EQU   *-PCHVALAD          OFFSET FOR ADDR VALIDITY CHECK RTN.  17900002
         LCR   R1,R1               INDICATE REGISTER CONVERSION.        17950002
         L     R15,TSTCONVT        ADDR OF CONVERT ROUTINE FOR CALL.    18000002
         BALR  R14,R15             GO TO CONVERT ROUTINE.               18050002
         LTR   R15,R15             TEST THE RETURN CODE FROM CVT.       18100002
         BNZ   PRCNOTZ             IF RC FROM IKJEGCVT NON-ZERO, BRANCH 18150002
         SPACE                                                          18160002
         L     R4,ZERO(ZERO,R3)    PICK UP PTR TO ADDRESS PDE           18162002
         ST    R4,APDEADDR         SAVE THE ADDR OFPDE FOR USE BY ASN   18162402
         USING IKJPARMA,R4         ESTABLISH ADDRESSABILITY             18164002
         TM    PDEFLG4,GENR+SFPR+LFPR  IS THIS ADDRESS A REGISTER       18166002
         BZ    TSTWRT              BRANCH IF IT IS NOT                  18168002
         NC    PDEINDCT,PDEINDCT   IS REGISTER USED WITH INDIRECT       18168402
         BZ    RETTOPAR            BRANCH IF NOT                        18168802
TSTWRT   TM    PDEFLG5,PDEWRITE    IS ADDRESS IN WRITE PROTECT STORAGE  18170002
         BZ    WRTMSG              INFORM THE USER IF SO                18170402
         DROP  R4                  FREE BASE REGISTER                   18172002
RETTOPAR EQU    *                                                       18200002
         NI    TSTFLGS4,HEXFF-TSTVALCK  TURN OFF VALIDITY CK SW         18210002
         L     R1,OUTBUF           R1= ADDR OF OUTBUF                   18220002
         L     R13,BACKPTR(R13)    GET ADDR OF PREVIOUS SAVE AREA.      18226002
         L     R2,R1OFF(ZERO,R13)  GET THE ADDR OF PARSE PARM LIST      18228002
         OC    ZERO(FOUR,R1),ZERO(R1)  IS THE OUTBUF = ZERO             18230002
         BZ    RETCONT             CONTINUE THE EXIT                    18240002
         ST    R1,EIGHT(R2)        PLACE ADDR IN PARSE PARM LIST        18242402
RETCONT  EQU   *                   EXIT TO PARSE                        18244002
         RETURN (14,12),,RC=(15)   RETURN TO THE PARSE ROUTINE.         18400002
WRTMSG   EQU    *                                                       18410002
         LA    R2,MSG101           R2= MSGNO 101                        18420002
         LA    R3,MSG279           R3= SECOND LEVEL 279                 18430002
         B     MSGOUTA             OUTPUT AN ADDRESS PDE                18440002
         SPACE                                                          18483302
PRCNOTZ  EQU   *                                                        18500002
         CH    R15,HSIXTEEN        IS RC 16 OR 20                       18550002
         BNL   RET12               IF 16 OR 20, BRANCH                  18600002
PRC8     EQU   *                                                        18900002
         LA    R15,EIGHT           SET RC TO PARSE TO 8                 18950002
         B     RETTOPAR            GO RETURN TO PARSE                   19000002
RET12    EQU   *                                                        19050002
         ST    R15,PCHRETCD        SAVE RETURN CODE FROM CVT            19100002
         LA    R15,TWELVE          SET RC TO 12 FOR PARSE               19150002
         B     RETTOPAR            GO RETURN TO PARSE                   19200002
         EJECT                                                          19250002
*  MAIN PORTION OF THE VALUE VALIDITY CHECKING ROUTINE.                 19300002
         SPACE                                                          19350002
OFFSETTP EQU   *-PCHVALTP          OFFSET FOR VALUE VALIDITY CHECK RTN. 19400002
         USING VALUPDE,R1                                               19450002
         TR    VALUTYPE(ONE),PCHTRTBL TRANSLATE TYPE TO UPPER CASE      19500002
         SR    R5,R5               INITIALIZE FOR LATER INSERT CHAR.    19550002
         CLI   VALUTYPE,CHARA      IS TYPE BELOW 'A' IN COLLATING SEQ   19600002
         BL    INVTYPE             IF YES, GO TO ERROR ROUTINE.         19650002
         CLI   VALUTYPE,CHARZ      IS TYPE ABOVE 'Z' IN COLLATING SEQ.  19700002
         BH    INVTYPE             IF YES, TO TO ERROR ROUTINE.         19750002
         IC    R5,VALUTYPE         PICK UP TYPE CHAR FROM PDE.          19800002
         STC   R5,TYPESAVE         SAVE CHARACTER USED FOR TYPE.        19850002
         LA    R14,TYPETBL(R5)     SET UP ADDR OF CORRESPONDING CODE.   19900002
         CLI   ZERO(R14),HEXFF     IS THE TYPE CODE X'FF'               19950002
         BE    INVTYPE             IF EQUAL, TYPE IS INVALID, BRANCH.   20000002
         IC    R5,ZERO(R14)        PICK UP HEX CODE FOR TYPE.           20050002
         STC   R5,VALUTYPE         STORE TYPE CODE IN VALUE PDE.        20100002
         LH    R2,VALULNH          R2=LENGTH OF DATA STRING.            20150002
         L     R6,VALUPTR          R6=ADDR OF DATA STRING               20200002
         LTR   R2,R2               IS THE STRING LNTH ZERO              20250002
         LR    R7,R1               SAVE PTR TO VALUE PDE IN R7.         20300002
         USING VALUPDE,R7                                               20350002
         BZ    INVTYPE             IF STRING LNTH 0,BRANCH              20400002
* PLEASE NOTE THAT A BRANCH TABLE HAS BEEN USED IN LIEU OF NUMEROUS   * 20450002
* TESTS FOR TYPE.                                                     * 20500002
         B     VBTABLE(R5)         BRANCH TO BRANCH TABLE OFFSET BY TYP 20550002
VBTABLE  EQU   *                                                        20600002
         B     CVAL                CODE X'00' FOR CHAR                  20650002
         B     XVAL                CODE X'04' FOR HEXADECIMAL           20700002
         B     BVAL                CODE X'08' FOR BINARY                20750002
         B     XVAL                CODE X'0C' THIS CODE NOT USED.       20800002
         B     FVAL                CODE X'10' FOR FULLWORD FIXED PT     20850002
         B     HVAL                CODE X'14' FOR HALFWORD FIXED PT     20900002
         B     EVAL                CODE X'18' FOR SHORT FLT PT          20950002
         B     DVAL                CODE X'1C' FOR LONG FLT PT           21000002
         B     AQVAL               CODE X'20' FOR A AND Q ADDR CNSTNTS  21050002
         B     YVAL                CODE X'24' FOR Y ADDR CONSTANTS      21100002
         B     SVAL                CODE X'28' FOR S ADDR CONSTANTS      21150002
         B     AQVAL               CODE X'2C' FOR V ADDR CONSTANTS      21200002
         B     PVAL                CODE X'30' FOR PACKED DECIMAL        21250002
         B     ZVAL                CODE X'34' FOR ZONED DECIMAL         21300002
         SPACE                                                          21350002
VALOK    EQU   *                                                        21400002
CVAL     EQU   *                                                        21450002
         SR    R15,R15             SET THE RETURN CODE TO ZERO.         21500002
         B     RETTOPAR            GO RETURN TO PARSE.                  21550002
         EJECT                                                          21600002
*********************************************************************** 21650002
*                                                                     * 21700002
*  THE FOLLOWING ROUTINES CHECK THE VALIDITY OF THE INPUT STRING      * 21750002
*  AGAINST THE REQUIREMENTS FOR THE PARTICULAR TYPE SPECIFIED.        * 21800002
*  AT ENTRY, THE REGISTERS ARE SET AS FOLLOWS.                        * 21850002
*        R1    PTR TO VALUE PDE                                       * 21900002
*        R2    LENGTH OF STRING                                       * 21950002
*        R5    TYPE CODE (IN HEX)                                     * 22000002
*        R6    PTR TO START OF STRING                                 * 22050002
*        R7    PTR TO VALUE PDE                                       * 22100002
*                                                                     * 22150002
*********************************************************************** 22200002
         SPACE                                                          22250002
FVAL     EQU   *                                                        22300002
         LA    R5,FMAX             SET UP TPR TO MAX VALUE FOR F TYPE   22350002
         B     VALNUM              GO TO NUMERIC CHECK ROUTINE          22400002
HVAL     EQU   *                                                        22450002
         LA    R5,HMAX             SET UP TPR TO MAX VALUE FOR H TYPE   22500002
         B     VALNUM              GO TO NUMERIC CHECK ROUTINE          22550002
AQVAL    EQU   *                                                        22600002
         CLI   ZERO(R6),MINUS      A - SIGN  IS INVAL, CHECK FOR IT     22650002
         BE    INVCHAR             IF - SIGN, BRANCH TO ERROR ROUTINE   22700002
         B     FVAL                GO TO NUMERIC CHECK ROUTINE          22750002
YVAL     EQU   *                                                        22800002
         CLI   ZERO(R6),MINUS      A - SIGN IS INVAL, CHECK FOR IT      22850002
         BE    INVCHAR             IF - SIGN, BRANCH TO ERROR ROUTINE   22900002
         B     HVAL                GO TO NUMERIC CHECK ROUTINE          22950002
         SPACE                                                          23000002
*  THE FOLLOWING ROUTINE SETS UP FOR AND CALLS THE NUMERIC SCAN RTN.    23050002
*  IT THE CHECKS THAT THE VALUE DOESN'T EXCEED THE MAXIMUM.             23100002
         SPACE                                                          23150002
VALNUM   EQU   *                                                        23200002
         CLI   ZERO(R6),MINUS      IS CONSTANT PRECEDED BY A - SIGN     23250002
         BE    SIGNIST             IF YES, BRANCH                       23300002
         CLI   ZERO(R6),PLUS       IF NOT A - SIGN, IS IT A + SIGN      23350002
         BNE   DIGITIST            IF NOT A SIGN, GO CHECK DIGITS       23400002
SIGNIST  EQU   *                                                        23450002
         BCTR  R2,NULL             DECREMENT LENGTH 1 FOR SIGN          23500002
         LA    R6,ONE(R6)          INCREMENT STRING PTR PAST SIGN       23550002
DIGITIST EQU   *                                                        23600002
         LR    R3,R2               SAVE LENGTH                          23650002
         LR    R4,R6                   AND PTR TO START OF DATA         23700002
         BAL   R14,NUMSCAN         GO CERTIFY FIELD AS NUMERIC          23750002
         LTR   R15,R15             TEST RETURN CODE                     23800002
         BNZ   INVCHAR             IF RC NON-ZERO, BRANCH TO ERROR RTN  23850002
         CH    R3,ZERO(R5)         CHECK LENGTH OF INPUT STRING         23900002
         BL    VALOK               LENGTH LESS THAN MAX, BRANCH         23950002
         BH    TOOBIG1             LENGTH GREATER THAN MAX, BRANCH      24000002
         SPACE                                                          24050002
*  IF LENGTH EQUALS MAX, COMPARE TO MAX NUMBER FOR THIS CONSTANT TYPE   24100002
         SPACE                                                          24150002
         BCTR  R3,NULL             DECREMENT LENGTH FOR CLC INSTR       24200002
         EX    R3,DATACLC1         COMPARE STRING TO MAX                24250002
         BL    VALOK               IF LOW, BRANCH, CONSTANT OK          24300002
         BH    TOOBIG2             IF HIGH, BRANCH, CONSTANT IN ERROR   24350002
         L     R6,VALUPTR          RESET PTR TO START OF VALUE STRING   24400002
         CLI   ZERO(R6),MINUS      IS THE FIRST CHAR A MINUS SIGN       24450002
         BE    VALOK               IF YES, VALUE IS OK                  24500002
         B     TOOBIG2             IF NOT, BRANCH, CONSTANT IN ERROR    24550002
         SPACE 2                                                        24600002
*  THE FOLLOWING ROUTINE CHECKS THE VALIDITY OF PACKED AND ZONED CONS   24650002
         SPACE                                                          24700002
ZVAL     EQU   *                                                        24750002
         LA    R5,ZMAX             SET UP PTR TO MAX LEN                24800002
         B     ZPVAL02             GO TO COMMON RTN                     24850002
PVAL     EQU   *                                                        24900002
         LA    R5,PMAX             SET UP PTR TO MAX LEN                24950002
ZPVAL02  EQU   *                                                        25000002
         CLI   ZERO(R6),MINUS      IS THE FIRST CHAR A MINUS SIGN       25050002
         BE    ZPVAL04             IF - SIGN, BRANCH                    25100002
         CLI   ZERO(R6),PLUS       IS THE FIRST CHAR A PLUS SIGN        25150002
         BNE   ZPVAL06             IF NOT SIGN, GO TEST FOR NUMBERS     25200002
ZPVAL04  EQU   *                                                        25250002
         BCTR  R2,NULL             DECREMENT LENGTH FOR SIGN            25300002
         LA    R6,ONE(R6)          INCRE THE PTR PAST SIGN              25350002
ZPVAL06  EQU   *                                                        25400002
         CH    R2,ZERO(R5)         IS LENGTH OK                         25450002
         BH    TOOBIG1             IF TOO LONG, BRANCH TO ERROR         25500002
         BAL   R14,NUMSCAN         GO CHECK THAT STRING IS NUMERIC      25550002
         LTR   R15,R15             TEST RETURN CODE                     25600002
         BZ    VALOK               IF RC=0, VALUE IS VALID              25650002
         B     INVCHAR             IF RC NON-ZERO, CONSTANT IN ERROR    25700002
         SPACE                                                          25750002
XVAL     EQU   *                                                        25800002
         LR    R4,R2               R4=INPUT STRING LENGTH               25850002
         CH    R4,XMAX             TEST LENGTH FOR MAX                  25900002
         BH    TOOBIG1             IF GREATER THAN MAX, BRANCH TO ERR   25950002
         BCTR  R4,NULL             DECRE LNTH FOR TRT INSTR             26000002
         EX    R4,VTRT1            TRANSLATE AND TEST FOR VALID CHARS   26050002
         BZ    VALOK               IF CC=0, STRING IS VALID HEX         26100002
         B     INVCHAR             OTHERWISE, BRANCH TO ERROR           26150002
VTRT1    TRT   ZERO(NULL,R6),XTRTT  FOR TESTING FOR VALID HEX DIGITS    26200002
         SPACE                                                          26250002
         SPACE                                                          26300002
DVAL     EQU   *                                                        26350002
         LA    R5,DMAX             SET UP PTRS TO MAX VALUES FOR D TYPE 26400002
         LA    R8,DMAXNEG          POINT TO CONSTANTS FOR NEG EXPONENT  26450002
         B     VALFPNUM            GO TO COMMON FLOATING PT NUM RTN.    26500002
         SPACE                                                          26550002
EVAL     EQU   *                                                        26600002
         LA    R5,EMAX             MAXIMUM VALUES FOR E TYPE            26650002
         LA    R8,DMAXNEG          POINT TO CONSTANTS FOR NEG EXPONENT  26700002
VALFPNUM EQU   *                                                        26750002
         CLI   ZERO(R6),MINUS      DOES STRING BEGIN WITH A - SIGN      26800002
         BE    FPSIGN1             IF YES, BRANCH                       26850002
         CLI   ZERO(R6),PLUS       DOES IT BEGIN WITH A + SIGN          26900002
         BNE   NOFPS1              IF NO SIGN, SKIP PTR ADJUSTMENT      26950002
FPSIGN1  EQU   *                                                        27000002
         LA    R6,ONE(R6)          INCRE PTR PAST SIGN                  27050002
         BCTR  R2,NULL             REDUCE LENGTH 1 FOR SIGN             27100002
NOFPS1   EQU   *                                                        27150002
         LR    R4,R6               SAVE START OF NUMERIC FIELD IN R4    27200002
         LR    R3,R2               SAVE ADJUSTED LENGTH IN R3           27250002
         BAL   R14,NUMSCAN         GO SCAN FOR DECIMAL DIGITS           27300002
         LTR   R15,R15             TEST RETURN CODE                     27350002
         BZ    FPEXP               IF ZERO, FIELD HAS NO EXPONENT       27400002
         CLI   ZERO(R6),LCE        WAS NON-NUMERIC CHAR A LOWER CASE E  27450002
         BE    FPEXP               IF YES, BRANCH                       27500002
         CLI   ZERO(R6),UCE        WAS IT AN UPPER CASE E               27550002
         BNE   INVCHAR             IF NEITHER, BRANCH TO ERROR          27600002
         SPACE 2                                                        27650002
*********************************************************************** 27700002
* THE FOLLOWING CODE CHECKS THAT THE USER ENTERED VALUE IS NOT TOO    * 27750002
* LARGE OR TOO SMALL. THE PROBLEM THIS POSES IS CLEAR IF WE NOTE THAT,* 27800002
* FOR EXAMPLE, 1E+60,10E+59,100E+58 AND 1000E+57 ARE IDENTICAL VALUES.* 27850002
* FOR INTEGRAL VALUES THE EXPONENT PLUS THE LENGTH OF THE COEFFICIENT * 27900002
* MUST NOT EXCEED 76. IF THE SUM IS LESS THAN 76 THE VALUE IS VALID.  * 27950002
* IF THE SUM IS EXACTLY 76 THEN THE COEFFICIENT MUST BE CHECKED TO SEE* 28000002
* THAT THE VALUE ENTERED DOES NOT EXCEED 7.237005...E+75. FOR         * 28050002
* FRACTION VALUES IE VALUES WITH A NEGATIVE EXPONENT THE UNSIGNED     * 28100002
* EXPONENT MINUS THE COEFFICIENT LENGTH MUST NOT EXCEED 78. IF THE SUM* 28150002
* IS LESS THAN 78 THE VALUE IS VALID. IF THE SUM IS EXACTLY 78 THEN   * 28200002
* THE COEFFICIENT MUST BE CHECKED TO SEE THAT THE VALUE ENTERED IS    * 28250002
* GREATER THAN 5.397605...E-79. ONE ALSO MIGHT EXPECT THE SINGLE      * 28300002
* PRECISION POSITIVE LIMIT TO BE 7.2370055E+75. THE LIMIT IS LOWER    * 28350002
* BECAUSE THE FLOATING POINT CONVERSION ROUTINE IN IKJEGCVT ROUNDS    * 28400002
* THE RESULT.                                                         * 28450002
*********************************************************************** 28500002
         SPACE 2                                                        28550002
FPEXP    EQU   *                                                        28600002
         SR    R3,R2               R3 IS NOW COEFFICIENT LENGTH         28650002
         BZ    INVFORMT            IF ZERO LENGTH PUT OUT MESSAGE       28700002
         LR    R15,R3              SAVE A COPY OF COEFFICIENT LENTH     28750002
         LR    R14,R4              SAVE START ADDR OF COEFFICIENT       28800002
VALCHK1  EQU   *                                                        28850002
         CLI   ZERO(R4),DEC0       IS LEADING DIGIT A ZERO              28900002
         BNE   VALCHK3             IF NO BRANCH                         28950002
         LA    R4,ONE(R4)          OTHERWISE POINT TO NEXT DIGIT        29000002
         BCT   R3,VALCHK1          IF MORE DIGITS CHECK NEXT DIGIT      29050002
         LR    R3,R15              RESTORE COEFFICIENT LENGTH           29100002
         LR    R4,R14              RESTORE COEFFICIENT START ADDR       29150002
VALCHK3  EQU   *                                                        29200002
         CH    R3,ZERO(R5)         HOW LONG IS COEFFICIENT              29250002
         BH    TOOBIG1             IF TOO LONG PUT OUT MESSAGE          29300002
         LR    R0,R3               SAVE COEFFICIENT LENGTH              29350002
         LTR   R2,R2               WAS AN EXPONENT ENTERED              29400002
         BZ    VALOK               IF NO VALUE IS OKAY, BRANCH          29450002
         CLI   ONE(R6),MINUS       IS EXPONENT NEGATIVE                 29500002
         BNE   PLUSEXP             IF NO SKIP CODE FOR FRACTION VALUE   29550002
         LCR   R3,R3               MAKE NEGATIVE                        29600002
         B     VALCHK2             SKIP CODE FOR POSITIVE EXPONENT      29650002
PLUSEXP  EQU   *                                                        29700002
         CLI   ONE(R6),PLUS        IS EXPONENT EXPLICITLY POSITIVE      29750002
         BNE   NOEXPSGN            IF NO SKIP CODE FOR AN EXPLICIT SIGN 29800002
VALCHK2  EQU   *                                                        29850002
         LA    R6,ONE(R6)          INCREMENT FOR SIGN                   29900002
         BCTR  R2,NULL             DECREMENT FOR SIGN                   29950002
NOEXPSGN EQU   *                                                        30000002
         LA    R6,ONE(R6)          INCREMENT FOR E                      30050002
         BCTR  R2,NULL             DECREMENT FOR E                      30100002
         CH    R2,TWO(R5)          HOW LONG IS EXPONENT                 30150002
         BH    TOOBIG2             IF TOO LONG PUT OUT MESSAGE          30200002
         LR    R15,R2              SAVE EXPONENT LENGTH                 30250002
         BCTR  R15,NULL            DECREMENT FOR EXECUTE                30300002
         EX    R15,DATAPACK        PACK THE EXPONENT                    30350002
         BAL   R14,NUMSCAN         GO CHECK FOR NUMERIC DIGITS ONLY     30400002
         LTR   R15,R15             WERE THEY ALL NUMERIC                30450002
         BNZ   INVCHAR             IF NO PUT OUT MESSAGE                30500002
         CVB   R0,HOLDAREA+ONE     CONVERT EXPONENT TO BINARY           30550002
         AR    R0,R3               ADD (OR SUBTRACT) COEFFICIENT LENGTH 30600002
         LA    R5,FOUR(R5)         POINT TO POSITIVE CONSTANTS          30650002
         LA    R15,HIGHMASK        LOAD MASK FOR EXECUTION OF BRANCH    30700002
         LTR   R3,R3               IS EXPONENT NEGATIVE                 30750002
         BNM   VALCHK4             IF NOT BRANCH                        30800002
         LR    R5,R8               POINT TO NEGATIVE CONSTANTS          30850002
         LA    R15,LOEQMASK        LOAD MASK FOR BRANCH LOW OR EQUAL    30900002
VALCHK4  EQU   *                                                        30950002
         CH    R0,ZERO(R5)         HOW LARGE IS EXPONENT                31000002
         BH    TOOBIG2             IF TOO LARGE PUT OUT MESSAGE         31050002
         BL    VALOK               IF LOWER THAN LIMIT VALUE IS OKAY    31100002
         LPR   R3,R3               MAKE COEFFICIENT LENGTH POSITIVE     31150002
         BCTR  R3,NULL             DECREMENT FOR COMPARISON TO LIMIT    31200002
         EX    R3,DATACLC1         COMPARE TO LIMITING COEFFICIENT      31250002
         EX    R15,BRANCH          BRANCH IF COEFFICIENT IS NOT VALID   31300002
         B     VALOK               OTHERWISE VALUE IS VALID             31350002
DATAPACK PACK  HOLDAREA+ONE(EIGHT),ZERO(ZERO,R6)                        31400002
BRANCH   NOP   TOOBIG2                                                  31450002
         SPACE                                                          31500002
BVAL     EQU   *                                                        31550002
         CH    R2,XMAX             TEST LENGTH, DOES IT EXCEED MAX      31600002
         BH    TOOBIG1             IF YES, BRANCH TO ERROR              31650002
BINVLOOP EQU   *                                                        31700002
         CLI   ZERO(R6),DEC0       IS CURRENT CHAR A ZERO               31750002
         BE    BIN0                IF YES, BRANCH, CHAR IS VALID        31800002
         CLI   ZERO(R6),DEC1       IS CURRENT CHAR A ONE                31850002
         BNE   INVCHAR             IF NEITHER 0, NOR 1, CHAR INVALID    31900002
BIN0     EQU   *                                                        31950002
         LA    R6,ONE(R6)          INCREMENT PTR TO NEXT CHAR           32000002
         BCT   R2,BINVLOOP         DECRE LNTH AND TEST FOR DONE         32050002
         B     VALOK               RETURN, ALL CHARS VALID              32100002
DATACLC1 CLC   ZERO(NULL,R4),TWO(R5)  COMPARES DIGIT STRINGS TO MAX     32150002
         SPACE 2                                                        32200002
*  ROUTINE TO TEST VALIDITY OF S TYPE VALUES.                           32250002
         SPACE                                                          32300002
SVAL     EQU   *                                                        32350002
         LA    R5,SMAX1            SET PTR TO SCON LIMITS               32400002
SCON04   EQU   *                                                        32450002
         LR    R3,R2               SAVE A COPY OF ORIGINAL LNTH         32500002
         LR    R4,R6               SAVE A COPY OF DATA START ADDR       32550002
         BAL   R14,NUMSCAN         GO SCAN FOR VALID DIGITS             32600002
* PLEASE NOTE THAT A BRANCH TABLE HAS BEEN USED IN LIEU OF NUMEROUS   * 32650002
* TESTS FOR RETURN CODE.                                              * 32700002
         B     SCON06(R15)         USE RETURN CODE AS BRANCH INDEX      32750002
         SPACE                                                          32800002
*  IF RETURN CODE IS ZERO, DATA DOES NOT CONTAIN PARENDS, AN ERROR.     32850002
         SPACE                                                          32900002
SCON06   B     INVFORMT            IF RC=0, BRANCH                      32950002
         SPACE                                                          33000002
*  IF RETURN CODE IS 4, CHECK NON-NUMERIC FOR LEFT OR RIGHT PAREND      33050002
         SPACE                                                          33100002
         SR    R3,R2               CALCULATE LNTH OF NUMERIC PORTION    33150002
         BZ    INVCHAR             IF NO NUMERICS, BRANCH               33200002
         CH    R3,ZERO(R5)         DOES LNTH EXCEED MAX                 33250002
         BH    TOOBIG1             IF LNTH EXCEEDS MAX, BRANCH          33300002
         BL    SCON12              IF LNTH IS LESS THAN MAX, BRANCH     33350002
         SPACE                                                          33400002
*  IF LENGTH EQUALS MAXIMUM, COMPARE VALUE TO MAX VALUE.                33450002
         SPACE                                                          33500002
         BCTR  R3,NULL             DECREMENT LNTH FOR CLC INSTR         33550002
         EX    R3,DATACLC1         COMPARE DATA TO MAX VALUE            33600002
         BH    TOOBIG2             IF VALUE EXCEEDS MAX, BRANCH         33650002
SCON12   EQU   *                                                        33700002
         CLC   ZERO(ONE,R6),SMAXL(R5) COMPARE NON-NUMERIC TO EXPECTED   33750002
         BNE   INVCHAR             IF NOT EXPECTED DELIMITER, BRANCH    33800002
         CLI   ZERO(R6),RPAREND    IS THIS THE FINAL DELIMITER          33850002
         LA    R5,SMAX2            INCRE LIMIT PTR TO REG LIMITS        33900002
         LA    R6,ONE(R6)          INCRE DATA PTR TO NEXT DATA BYTE     33950002
         BE    SCON20              IF FINAL DELIMITER, BRANCH OUT       34000002
         BCT   R2,SCON04           IF MORE CHARACTERS, BRANCH BACK      34050002
         B     INVCHAR             GO TO ERROR ROUTINE                  34100002
         SPACE                                                          34150002
SCON20   EQU   *                                                        34200002
         BCT   R2,INVCHAR          IF MORE CHARACTERS, ERROR, BRANCH    34250002
         B     VALOK               OTHERWISE, VALUE IS OK, RETURN       34300002
         SPACE 2                                                        34350002
INVFORMT EQU   *                                                        34400002
         LA    R2,MSG010           R2= MSG NO 70 FIRST LEVEL            34450002
         LA    R3,MSG212           R3= MSG NO 212 SECOND LEVEL          34500002
         B     MSGOUTV             OUTPUT THE MESSAGE WITH VALUE PDE    34550002
         SPACE                                                          34600002
INVCHAR  EQU   *                                                        34650002
         LA    R2,MSG010           R2= MSG NUMBER 10 FIRST LEVEL        34700002
         LA    R3,MSG209           R3= MSG NUMBER 209 SECOND LEVEL      34750002
         B     MSGOUTV             OUTPUT THE MESSAGE WITH VALUE PDE    34800002
         SPACE                                                          34850002
TOOBIG1  EQU   *                                                        34900002
         LA    R2,MSG010           R2= MSG NUMBER 10 FIRST LEVEL        34950002
         LA    R3,MSG210           R3= MSG NUMBER 210 SECOND LEVEL      35000002
         B     MSGOUTV             OUTPUT THE MESSAGE WITH VALUE PDE    35050002
         SPACE                                                          35100002
TOOBIG2  EQU   *                                                        35150002
         LA    R2,MSG010           R2= MSG NUMBER 10 FIRST LEVEL        35200002
         LA    R3,MSG211           R3= MSG NUMBER 211 SECOND LEVEL      35250002
         B     MSGOUTV             OUTPUT THE MESSAGE WITH VALUE PDE    35300002
         EJECT                                                          35310002
**********************************************************************  35360002
*        MESSAGE PORTION OF THE MODULE                                  35370002
**********************************************************************  35410002
         SPACE                                                          35460002
MSGOUTA  DS    0H                  CALL IO TO OUTPUT MESSAGE            35510002
         XC    TSTIOPRM(L'TSTIOPRM),TSTIOPRM  CLEAR IO PARMETER AREA    35560002
         IKJEGSIO MSG,FIRST=(R2),SECOND=(R3),INST11=(APDE,APDEADDR),   *35610002
               ID=PCH24,MF=(E,TSTIOPRM)     INVOKE IO...                35660002
         B     RETCKTAB(R15)       BRANCH ON THE RETUNR CODE            35710002
         SPACE                                                          35760002
MSGOUTV  DS    0H                  CALL IO TO OUTPUT MESSAGE            35810002
         XC    TSTIOPRM(L'TSTIOPRM),TSTIOPRM  CLEAR IO PARMETER AREA    35860002
         IKJEGSIO MSG,FIRST=(R2),SECOND=(R3),INST11=(VPDE,(R7)),       *35910002
               ID=PCH24,MF=(E,TSTIOPRM)     INVOKE IO...                35960002
         B     RETCKTAB(R15)       BRANCH ON THE RETUNR CODE            36010002
         SPACE                                                          36010402
MSGOUTN  DS    0H                  FIXED INSERT IN THE FIRST MSG        36012002
         XC    TSTIOPRM(L'TSTIOPRM),TSTIOPRM CLEAR IO PARM AREA         36014002
         IKJEGSIO MSG,FIRST=(R2),SECOND=(R3),INST11=(NUM,(R4)),        *36016002
               ID=PCH24,MF=(E,TSTIOPRM)  CALL IO                        36018002
IOCHECK  DS    0H                  CHECK OUT RETURN CODES FROM IO       36018402
         CH    R15,HSIXTEEN        IS THE RETURN CODE 16, 20, 24        36018802
         BNL   PCHRET1A            RETURN TO MNL                        36019202
         SR    R15,R15             ZERO R15                             36019602
         B     PCHRET1A            RETURN TO MNL                        36019702
         SPACE                                                          36020002
LINKFAIL DS    0H                  LINK FAIL IO MESSAGE ROUTINE         36030002
         XC    TSTIOPRM(L'TSTIOPRM),TSTIOPRM  CLEAR IO PARM AREA        36040002
         LR    R3,R15              R3= RETURN CODE                      36042002
         LR    R4,R1               R4= ABEND CODE                       36044002
         IKJEGSIO SVCERR,SVC=6,ABENDRG=(R4),RC=(R3),                   *36050002
               ID=PCH24,MF=(E,TSTIOPRM)   INVOKE IO                     36052002
         B     IOCHECK             CHECK THE RETURN CODES               36054002
         SPACE                                                          36060002
RETCKTAB DS    0H                  RETURN CODE FROM IO TABLE            36110002
         B     PRC8                ASK FOR A REENTER                    36160002
         B     PRC8                ASK FOR A REENTER                    36210002
         B     PRC8                ASK FOR A REENTER                    36260002
         B     PRC8                ASK FOR A REENTER                    36310002
         B     RET12               RETURN TO PARSE AND CLEAN UP         36360002
         B     RET12               RETURN TO PARSE AND CLEAN UP         36410002
         B     RET12               RETURN TO PARSE AND CLEAN UP         36460002
         EJECT                                                          38550002
INVTYPE  EQU   *                                                        38600002
         LA    R15,FOUR            SET RC=J SO PARSE WILL PROMPT        38650002
         B     RETTOPAR            GO RETURN TO PARSE.                  38700002
CVTRC    EQU   *                                                        38750002
         CH    R15,HSIXTEEN        IS THE RETURN CODE 16                38800002
         BE    PCHCC16             IF RC=16, BRANCH TO RC=16 RTN.       38850002
         B     PCHRET1             OTHERWISE, BRANCH TO WRAPUP RTN.     38900002
         SPACE 2                                                        38950002
*********************************************************************** 39000002
*                                                                     * 39050002
*  THE FOLLOWING ROUTINE CHECKS THAT EACH CHAR IN A STRING IS NUMERIC * 39100002
*        INPUT IS   R2 CONTAINS LENGTH OF STRING                      * 39150002
*                   R6 CONTAINS PTR TO START OF STRING                * 39200002
*                   R14 CONTAINS RETURN ADDRESS                       * 39250002
*                                                                     * 39300002
*********************************************************************** 39350002
         SPACE                                                          39400002
NUMSCAN  EQU   *                                                        39450002
         LA    R15,FOUR            SET ERROR RETURN CODE                39500002
NUMSCAN2 EQU   *                                                        39550002
         CLI   ZERO(R6),DEC0       IS CHAR EQ OR GT AN EBCDIC '0'       39600002
         BCR   LOW,R14             IF NOT, RETURN WITH RC=4             39650002
         CLI   ZERO(R6),DEC9       IS CHAR EQ OR LT AN EBCDIC '9'       39700002
         BCR   HIGH,R14            IF NOT, RETURN WITH RC=4             39750002
         LA    R6,ONE(R6)          INCREMENT PTR TO STRING              39800002
         BCT   R2,NUMSCAN2         DECREMENT LENGTH AND LOOP IF MORE    39850002
         SR    R15,R15             SET RETURN CODE TO 0                 39900002
         BR    R14                 RETURN TO CALLER                     39950002
         TITLE 'IKJEGPCH, ASSIGN SUBCOMMAND OF TEST, ERROR ROUTINES'    40000002
PCHCC16  EQU   *                                                        40050002
         LA    R15,SIXTEEN         SETUP A RETURN CODE OF 16.           40100002
         ST    R15,PCHRETCD        STORE RETURN CODE IN RC FIELD.       40150002
         B     PCHRET1             GO RETURN TO CALLER                  40200002
         ORG   *-4                 REUSE SPACE NOT NEEDED FOR RC=0      40250002
PARSRC   EQU   *                                                        40300002
         ORG                                                            40350002
         B     PARSRC04            RETURN CODE OF 4 ROUTINE.            40400002
         B     PCHCC16             GO SET RETURN CODE FOR ATTN DETECTED 40450002
         B     PARSRC12            GO TO ROUTINE FOR RC 12.             40500002
         B     PARSRC16            RETURN CODE IS 16                    40550002
         B     PCHRET1             RETURN TO MAINLINE IMMEDIATELY       40600002
         SPACE                                                          40650002
*  IF PARSE RETURN CODE WAS 4, ENTER HERE AFTER PARSE                   40700002
         SPACE                                                          40750002
PARSRC04 EQU   *                                                        40800002
         OI    TSTFLGS4,TSTFLUSH   TELL MNLINE TO FLUSH STACK           40850002
         XR    R15,R15             ZERO RETURN CODE TO MNLINE           40900002
         ST    R15,PCHRETCD        STORE RETURN CODE IN RC FIELD        40950002
         B     PCHRET1             RETURN TO MNLINE                     41000002
         SPACE                                                          41050002
*  IF PARSE RETURN CODE WAS 12, ENTER HERE AFTER PARSE                  41100002
         SPACE                                                          41150002
PARSRC12 EQU   *                                                        41200002
         LA    R4,INST6            R4= INSERT NO 6 (ASSIGNMENT)         41300002
         LA    R2,MSG104           R2= FIRST LEVEL MSG NO               41302002
         LA    R3,MSG277           R3= SECOND LEVEL MSG NO              41304002
         B     MSGOUTN             FIXED INSET IN FIRST LEVEL           41306002
         SPACE                                                          41308002
*  IF THE RETURN CODE WAS 16, THIS ROUTINE IS BRANCHED TO AFTER PARSE.* 41400002
         SPACE                                                          41450002
PARSRC16 EQU   *                                                        41500002
*        ISSUE THE OUT OF CORE MESSAGE                                  41550002
         XC    TSTIOPRM(L'TSTIOPRM),TSTIOPRM  CLEAR IO WORK AREA        41600002
         LA    R2,MSG103           R2= 103 FIRST LEVEL MSG INDICATOR    41650002
         IKJEGSIO MSG,FIRST=(R2),ID=PCH24,MF=(E,TSTIOPRM)               41660002
         B     IOCHECK             RETURN TO MNL                        41700002
         EJECT                                                          41750002
*********************************************************************** 41800002
*                                                                       41850002
*        STAE RETRY ROUTINE                                             41900002
*                                                                       41950002
*********************************************************************** 42000002
         SPACE 3                                                        42050002
RETRY    EQU   *                                                        42100002
         L     R4,CVTPTR           PTR TO CVT                           42150002
         USING CVT,R4              ADDRESSABILITY FOR CVT               42200002
         L     R4,CVTTCBP          TCB PTR                              42250002
         L     R4,FOUR(R4)         CURRENT TCB PTR                      42300002
         DROP  R4                                                       42350002
         USING TCB,R4              TCB ADDRESSABILITY                   42400002
         L     R9,TCBTRN           TCOMTAB PTR                          42450002
         L     R13,REGSAVE2        PTR TO SAVE AREA                     42500002
         LM    R10,R12,R10OFF(R13)     RESTORE MODULE ADDRESSABILITY    42550002
         LA    R15,TWENTY          INDICATE ERROR RETURN                42560002
*-*-*    NOTE IN BOTH CSECT'S (IKJEGPCH AND IKJEGASN) PCHRET1A WILL     42610002
*-*-*    EXIT TO IKJEGMNL WITH R15 AS THE RETURN CODE.                  42710002
         B     PCHRET1A            RETURN                               42900002
         TITLE 'IKJEGPCH, ASSIGN, CONSTANTS, DSECTS, AND PARSE MACROS'  42950002
         SPACE 2                                                        43200002
****     ADDRESS CONSTANT FOR IKJEGASN                                  43202002
ASNVCON  DC    V(IKJEGASN)         ADDRESS OF IKJEGASN                  43202402
         SPACE 2                                                        43210002
HSIXTEEN DC    H'16'               USED TO TEST FOR RC=16               43900002
         SPACE                                                          44750002
FMAX     DC    H'10'               MAX NUMBER OF CHAR IN F CONSTANT     44800002
         DC    C'2147483648'       MAX NUMBER FOR A,F,Q, AND V TYPES    44850002
HMAX     DC    H'5'                MAX NUMBER OF CHAR IN H CONSTANT     44900002
         DC    C'32768'            MAX NUMBER FOR H AND Y TYPES         44950002
ZMAX     DC    H'16'               MAX NUMBER OF DIGITS IN ZONED DATA   45000002
PMAX     DC    H'31'               MAX LENGTH FOR PACKED STRING         45050002
         SPACE                                                          45100002
*  LIMITS FOR 'D' TYPE FLOATING POINT CONSTANTS.                        45150002
         SPACE                                                          45200002
DMAX     DC    H'17'               COEFFICIENT LENGTH LIMIT             45250002
         DC    H'2'                EXPONENT LENGTH LIMIT                45300002
         DC    H'76'               POSITIVE EXPONENT VALUE LIMIT        45350002
         DC    C'72370055773322621'     POS COEFFICIENT VALUE LIMIT     45400002
DMAXNEG  DC    H'78'               NEGATIVE EXPONENT VALUE LIMIT        45450002
         DC    C'53976053469340278'     NEG COEFFICIENT VALUE LIMIT     45500002
         SPACE                                                          45550002
*  LIMITS FOR 'E' TYPE FLOATING POINT CONSTANTS.                        45600002
         SPACE                                                          45650002
EMAX     DC    H'8'                COEFFICIENT LENGTH LIMIT             45700002
         DC    H'2'                EXPONENT LENGTH LIMIT                45750002
         DC    H'76'               POSITIVE EXPONENT VALUE LIMIT        45800002
         DC    C'72370053'         POSITIVE COEFFICIENT VALUE LIMIT     45850002
         SPACE                                                          45900002
XMAX     DC    H'64'               LIMITS FOR X, C, AND B TYPE CONSTANT 45950002
         SPACE                                                          46000002
SMAX1    DC    H'4'                MAX LNTH FOR SCON DISPLACEMENT FLD   46050002
         DC    C'4095'             MAX VALUE FOR DISPLACEMENT           46100002
SMAXL    EQU   *-SMAX1             SYMBOLIC OFFSET TO DELIMITER         46150002
         DC    C'('                BEGIN DELIMITER FOR REG NUMBER       46200002
         SPACE                                                          46250002
SMAX2    DC    H'2'                MAX LNTH FOR SCON REG NUMBER FLD     46300002
         DC    CL4'15'             MAX VALUE FOR SCON REG NUMBER        46350002
         DC    C')'                END DELIMITER FOR REG NUMBER         46400002
         SPACE                                                          46450002
         SPACE                                                          46550002
PARSEPCL DC    V(IKJEGPMB)         ADDRESS OF PARSE PARM CNTRL LIST     46600002
         SPACE                                                          46650002
*        THE ABEND TABLE FOR PCH AND ASN                                46700002
PCHABEND EQU   *                                                        46800002
         DS    0F                                                       46802002
         DC    AL1(97)             CODE FOR USER CAUSED PROTECTION CHK  46810002
         DC    VL3(PCHABND1)       ANTICIPATED ABEND ADDR FROM DATA MVC 46820002
         DC    AL1(99)             CODE FOR LINK TO PARSE FAILED        46900002
         DC    AL3(PARSEBLO)       ADDRESS OF POTENTIAL ABEND           46950002
         DC    X'FF'               TABLE STOPPER                        47000002
         SPACE                                                          47010002
PCHSPL   IKJEGSPL RTRY=RETRY,ABNTB=PCHABEND,MODNM=IKJEGPCH,            *47020002
               TNM=ASSIGNMENT                                           47030002
         EJECT                                                          47050002
         DS    0D                                                       47100002
PCHTRTBL DC    256AL1(*-PCHTRTBL)  LOWER TO UPPER CASE TR TABLE         47150002
         ORG   PCHTRTBL+C'A'-X'40' SET LOCATION CTR TO LOWER CASE 'A'   47200002
         DC    (C'Z'-C'A'+1)AL1(*-PCHTRTBL+X'40')  DEFINES UPPER CASE   47250002
         ORG                                                            47300002
         SPACE 2                                                        47350002
*********************************************************************** 47400002
*  THE FOLLOWING TABLE IS USED TO CHECK AND TO CONVERT THE ASSEMBLER  * 47450002
*  TYPE CODES FOR VALUES INTO A HEX CODE FOR INTERNAL PROCESSING. THIS* 47500002
*  CODE IS THE SAME AS THAT USED BY THE ASSEMBLER IN SYM RECORDS.     * 47550002
         SPACE                                                          47600002
         ORG   *-C'A'              BACK UP LOC CTR TO ORGIN TABLE       47650002
TYPETBL  EQU   *                                                        47700002
         ORG   *+C'A'              ONLY USE SPACE FOR UPPER CASE ALPHA  47750002
         DC    X'2008001C'         CODES FOR A, B, C, AND D TYPE CONS.  47800002
         DC    X'1810FF14'         CODES FOR E, F, INV, AND H CONS.     47850002
         DC    (C'P'-C'H'-1)X'FF'  CODES FOR INV'S BETWEEN H AND P      47900002
         DC    X'30'               CODE FOR P TYPE CONSTANTS.           47950002
         DC    (C'S'-C'P'-1)X'FF'  CODES FOR INV'S BETWEEN P AND S.     48000002
         DC    X'28FFFFFF'         CODE FOR S TYPE CONS AND 3 INVS.     48050002
         DC    X'FF042434'         CODES FOR INV, X, Y, AND Z CONS.     48100002
*********************************************************************** 48150002
         SPACE 2                                                        48200002
XTRTT    EQU   *                                                        48250002
         DC    256X'01'            NON-ZERO VALUE FOR NON-HEX DIGITS    48300002
         ORG   XTRTT+C'A'-C' '     POSITION FOR LOWER CASE 'ABCDEF'     48350002
         DC    6AL1(0)             DEFINE LOWER CASE ABCDEF             48400002
         ORG   XTRTT+C'A'          POSITION FOR UPPER CASE 'ABCDEF'     48450002
         DC    6AL1(0)             DEFINE UPPER CASE ABCDEF             48500002
         ORG   XTRTT+C'0'          POSITION FOR DECIMAL DIGITS 0-9      48550002
         DC    10AL1(0)            DEFINE DECIMAL DIGITS 0-9            48600002
         DC    50C'Z'              MAINTENANCE AREA                     48650002
         EJECT                                                          48700002
*  PARSE MACROS                                                         48750002
         SPACE                                                          48800002
IKJEGPMB IKJPARM                                                        48850002
ADDR     IKJPOSIT  ADDRESS,PROMPT='ADDRESS',VALIDCK=PCHVALAD            48900002
VAL      IKJPOSIT  VALUE,LIST,ASIS,PROMPT='VALUE',VALIDCK=PCHVALTP      48950002
         IKJENDP                                                        49000002
         EJECT                                                          49050002
*                                                                       49100002
*        FOLLOWING IS A MAP OF THE WORK AREA USED BY THE PATCH AND      49150002
*        ASSIGN MODULES. NOTE THEY MUST BE KEPT IDENTICAL.......        49160002
*                                                                       49200002
PCHWORK  DSECT                                                          49250002
PPCL     DS    7F                  SPACE FOR PARSE PARM LIST            49450002
         SPACE                                                          49500002
ADDRINCR DS    F                   FULLWORD TO HOLD CONSTANTS FOR REGS  49550002
MAXRADDR EQU   ADDRINCR+2          OFFSET FOR MAX REG NUMBER            49600002
PCHRETCD DS    F                   HOLDS CURRENT RETURN CODE            49650002
VPDE     DS    3F                  HOLDS CURRENT COPY OF THE VALUE PDE  49700002
A1       DS    F                   HOLDS CURRENT ADDR TO BE PATCHED     49750002
DUMMYPDE DS    3F                  RESERVE SPACE FOR COPY OF VALUE PDE. 49800002
A1L      DS    H                   HOLDS CURRENT STRING LNTH FOR 'C'    49850002
A1TYPE   DS    X                   CONTAINS TYPE ASSOCIATED WITH VALUE  49900002
         DS    AL3                 RESERVED                             49950002
         DS    XL1                 RESERVED                             50000002
         DS    0D                                                       50050002
         ORG   *-1                 POSITION FOR LENGTH BYTE OF HOLDAREA 50100002
CONAREA1 DS    XL41                USED FOR ADDRESSING PURPOSES ONLY.   50150002
         ORG   CONAREA1            RE-USE SPACE FOR ACTUAL HOLDAREA     50200002
HOLDAREA DS    XL65                TEMP HOLD AREA FOR DATA CONVERSION   50250002
MSGPLIST DS    2F                  PARM LIST FOR CALLING IKJEGIO1.      50300002
TYPESAVE DS    C                   FOR SAVING USER SUPPLIED TYPE CODE   50400002
REGDATA  DS    F                   PATCH VALUE TO BE PLACED IN REGISTER 50410002
ASNSVCL  DS    3F                  3 WORD PARMETER LISTS                50420002
APDEADDR DS    F                   ADDR OF THE ADDR PDE                 50422002
ASNFLGS1 DS    X                   FLAGS FOR USE BY ASN                 50430002
LPCHWORK EQU   *-PCHWORK           LENGTH OF TOTAL DSECT.               50450002
         EJECT                                                          50500002
         VALUPDE                                                        50550002
         EJECT                                                          50600002
         TCOMTAB                                                        50650002
         TITLE 'IKJEGPCH, MESSAGE CSECT'                                50700002
         EJECT                                                          54400002
         IKJPARMA                  EXPAND THE ADDR PDE MAPPING DSECT    54450002
         SPACE 3                                                        54500002
         TSTCWORK                  EXPAND TEST'S CORE MAPPING DSECT     54550002
         EJECT                                                          54600002
CVT      DSECT                                                          54650002
         CVT                                                            54700002
         IKJTCB                                                         54750002
         IKJPPL                                                         54800002
         END                                                            54850002
