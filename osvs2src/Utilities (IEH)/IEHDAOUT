         COPY  LCGASMSW                                          SM4351 00200021
IEHDAOUT CSECT                                                          00250021
*1240484000                                                        3605 00400018
*056000-058000,141200,194000-200000,240000,550000,560000           O122 00500019
*482000,483000                                                  A38484  00520020
*3434                                                            M4970  00550020
*STATUS CHANGE LEVEL 004                                                00650020
*                                                                     * 01000016
*FUNCTION/OPERATION- THIS ROUTINE HANDLES THE DUMP TO A SYSTEM        * 01200016
*   OUTPUT DEVICE FOR THE IEHDDUMP PORTION OF THE IEHDASDR SYSTEM     * 01400016
*   UTILITY PROGRAM. HEADER INFORMATION (TRACK ADDRESS, THE DATA      * 01600016
*   FIELD FOR RECORD ZERO, THE COUNT FIELD) WILL BE FORMATED AND      * 01800016
*   WRITTEN OUT. THE SIZE OF THE INDIVIDUAL RECORD IS COMPUTED        * 02000016
*   AND THEN DIVIDED BY THIRTY-TWO TO OBTAIN THE NUMBER               * 02200016
*   OF COMPLETE (32-BYTE) LINES, AND THE NUMBER OF BYTES IN THE       * 02400016
*   INCOMPLETE LINE IF ANY. AN EBCDIC AND GRAPHIC TRANSLATION IS      * 02600016
*   PERFORMED TO ALLOW THE USER TO EASILY LOCATE VARIOUS FIELDS       * 02800016
*   IN THE OUTPUT DATASET. ONLY THE FIRST AND LAST LINES OF           * 03000016
*   IDENTICAL RECORD DATA WILL BE PLACED IN THE OUTPUT DATASET,       * 03200016
*   SEPARATED BY THIS MESSAGE-                                        * 03400016
*              ' LINE(S) SAME AS ABOVE '                              * 03600016
*                                                                     * 03800016
*ENTRY POINTS- THE ONLY ENTRY POINT IS -IEHDAOUT-, AND CONTROL IS     * 04000016
*   ALWAYS RECEIVED FROM THE -IEHDEXCP- ROUTINE.                      * 04200016
*                                                                     * 04400016
*INPUT- REGISTER 1 WILL POINT TO THE CURRENT TRACK ADDRESS(CCHH).     * 04500016
*       REGISTER 2 WILL CONTAIN THE ENTRY POINT FOR -IEHDPRNT-.       * 04600016
*       REGISTER 3 WILL CONTAIN A POINTER TO THE END OF THE DATA      * 04700016
*       IN THE DATA BUFFER.                                           * 04800016
*       REGISTER 10 WILL CONTAIN A POINTER TO THE CONTROL BLOCK.      * 05000016
*       REGISTER 12 WILL CONTAIN A POINTER TO THE WORK AREA.          * 05200016
*       AOUTSW (IN IEHDBLKS) WILL DESCRIBE THE REQUIRED FORMATTING O122 05220019
*              X'00' - FORMAT TRACK ADDRESS, R0 DATA AND RECORDS   O122 05240019
*              X'01' - FORMAT RECORDS ONLY                         O122 05260019
*              X'02' - DO NOT FORMAT                               O122 05280019
*       TRACKPT (IN IEHDBLKS) WILL CONTAIN A POINTER TO THE        O122 05300019
*              BEGINNING OF THE DATA                               O122 05320019
*                                                                     * 05400016
*EXITS-NORMAL- UPON COMPLETION, CONTROL IS RETURNED TO IEHDEXCP OR O122 05600019
*   IEHDRCVR.  NO RETURN CODE IS PASSED.                           O122 05800019
*                                                                     * 06000016
*EXITS-ERROR- NONE.                                                   * 06200016
*                                                                     * 06300016
*EXTERNAL ROUTINES- THE CONVERTED TRACK DATA WILL BE PLACED IN        * 06400016
*   THE OUTPUT BUFFER AND THEN WRITTEN OUT BY THE MESSAGE             * 06600016
*   WRITER ROUTINE(IEHDPRNT).                                         * 06800016
*                                                                     * 07000016
*TABLES/WORK AREAS- UPON ENTRY, REGISTERS 10 AND 12 POINT TO A        * 07200016
*   FUNCTION BLOCK AND WORK AREA RESPECTIVELY. THEY ARE DESCRIBED     * 07400016
*   IN THE DSECTS CALLED -FUNCBLK- AND -WORK-.                        * 07600016
*                                                                     * 07800016
*ATTRIBUTES- SERIALLY REUSABLE, RELOCATABLE, NON-PRIVILEGED.          * 07900016
 TITLE 'IEHDAOUT-DUMP TO SYSTEM OUTPUT DEVICE FOR IEHDASDR UTILITY'     08200016
*  THE FOLLOWING ARE REGISTER ASSIGNMENTS.                              08400016
GR0      EQU   0                                                        08600016
GR1      EQU   1                                                        08800016
DATAPTR  EQU   2                       POINTER TO DATA IN DATA BUFFER.  09000016
SUMTOTAL EQU   3                       TOTAL LENGTH OF DATA.            09200016
GR4      EQU   4                                                        09400016
GR5      EQU   5                                                        09600016
GR6      EQU   6                                                        09800016
GR7      EQU   7                                                        10000016
GR8      EQU   8                                                        10200016
GR9      EQU   9                                                        10400016
GR10     EQU   10                                                       10600016
GR11     EQU   11                                                       10800016
GR12     EQU   12                                                       11000016
GR13     EQU   13                                                       11200016
GR14     EQU   14                                                       11400016
GR15     EQU   15                                                       11600016
*                                                                       11800016
         SPACE                                                          12000016
         USING FUNCBLK,10                                               12200016
         USING IEHDAOUT,11                                              12400016
         USING WORK,12                                                  12600016
         SPACE                                                          12800016
         SAVE  (14,12),T               SAVE THE REGISTERS.              13000016
         LR    GR11,GR15               LOAD THE BASE REGISTER.          13200016
         LA    GR7,OUTSAVE             SAVE AREA ADDRESS.               13400016
         ST    GR7,8(GR13)             ADDRESS OF NEW AREA TO OLD.      13600016
         ST    GR13,4(GR7)             ADDRESS OF OLD AREA TO NEW.      13800016
         LR    GR13,GR7                SAVE AREA FOR CALLED ROUTINES.   14000016
         LR    GR15,DATAPTR            ENTRY PT. FOR PRNT TO LINK REG.  14060016
         L     DATAPTR,TRACKPT         POINTER TO DATA             O122 14070019
         LA    DATAPTR,0(DATAPTR)      CLEAR HIGH ORDER BYTE       O122 14080019
         LA    SUMTOTAL,0(SUMTOTAL)    CLEAR HIGH ORDER BYTE       O122 14090019
         MVI   1(SUMTOTAL),X'FF'       FLAG END OF BUFFER        A53156 14092000
         CLI   AOUTSW,FMTCNT           FORMAT COUNTS ONLY          O122 14100019
         BE    COUNTS                  YES                         O122 14110019
         BL    TRACKIN                 NO, FORMAT ENTIRE TRACK     O122 14120019
*        DO NOT FORMAT DATA  (USED WHEN IEHDRCVR PRINTS THE        O122 14130019
*        REMAINDER OF TRACK AS ONE RECORD)                         O122 14140019
         LR    GR7,SUMTOTAL            END OF DATA IN BUFFER       O122 14150019
         SR    GR7,DATAPTR             LENGTH OF DATA TO BE PRINTEDO122 14160019
         B     PRTRCORD                PRINT THE RECORD            O122 14170019
         EJECT                                                          14200016
*********************************************************************** 14400016
*                                                                     * 14600016
*        MOVE IN 'TRACK',R0 DATA', AND 'COUNT' KEYWORDS               * 14800016
*        WITH ASSOCIATED TRACK INFORMATION. COMPUTE THE NUMBER        * 15000016
*        OF COMPLETE LINES IF ANY, AND THE LENGTH OF THE              * 15200016
*        POSSIBLE INCOMPLETE LINE.                                    * 15400016
*                                                                     * 15600016
*********************************************************************** 15800016
         SPACE                                                          16000016
TRACKIN  BAL   GR9,OUTPUT              SPACE A LINE.                    16400016
         MVC   MESS(10),TRACKKEY       MOVE IN 'TRACK' KEYWORD.         16800016
         UNPK  MESS+12(9),0(5,GR1)     TRACK ADDR(CCHH) TO OUTPUT BUFF. 17200016
         MVI   MESS+20,C' '            CLEAR TRASH FROM UNPACK.         17500016
         TR    MESS+12(8),IOTAB-240    GRAPHIC TRACK ADDRESS.           17800016
         MVC   MESS+26(7),R0DATA       MOVE IN 'R0 DATA' KEYWORD.       18000016
         UNPK  MESS+35(9),0(5,DATAPTR)   UNPACK ACTUAL R0 DATA          18200016
         UNPK  MESS+43(9),4(5,DATAPTR)         TO OUTPUT BUFFER.        18400016
         MVI   MESS+51,C' '            CLEAR TRASH FROM UNPACK.         18600016
         TR    MESS+35(16),IOTAB-240                                    18800016
         LA    DATAPTR,8(DATAPTR)      POINT TO R1 COUNT FIELD.         19000016
         BAL   GR9,OUTPUT              PRINT A LINE.                    19200016
         BAL   GR9,OUTPUT              SPACE A LINE.                    19300016
COUNTS   CR    DATAPTR,SUMTOTAL        COMPLETED PRINTING.         O122 19500019
         BNL   RETURN                  YES--RETURN.                O122 19700019
COUNTIN  MVC   MESS+5(5),COUNTKEY      MOVE IN 'COUNT'KEYWORD.     O122 19900019
         UNPK  MESS+12(9),0(5,DATAPTR)   UNPACK COUNT FOR               20200016
         UNPK  MESS+20(9),4(5,DATAPTR)     PRINT OUT.                   20400016
         MVI   MESS+28,C' '            CLEAR TRASH FROM UNPACK.         20600016
         TR    MESS+12(16),IOTAB-240   GRAPHIC COUNT.                   20800016
         BAL   GR9,OUTPUT              PRINT A LINE.                    21000016
         BAL   GR9,OUTPUT              SPACE A LINE.                    21100016
         EJECT                                                          21200016
         SR    GR7,GR7                 CLEAR WORK REGISTER.             21400016
         IC    GR7,5(DATAPTR)          KEY LENGTH.                      21600016
         AH    GR7,6(DATAPTR)          PLUS DATA LENGTH.                21800016
         LA    DATAPTR,8(DATAPTR)      POINT TO R1 DATA FIELD.          22000016
PRTRCORD MVC   LOCCTR(4),ZERO          CLEAR LOCATION COUNTER.     O122 22100019
         SR    GR6,GR6                 CLEAR WORK REGISTER.             22200016
         D     GR6,THIRTY2             HOW MANY COMPLETE LINES.         22400016
         STH   GR6,INCOMPLT            SAVE LENGTH OF INCOMPL LINE.     22600016
         LTR   GR7,GR7                 ANY COMPLETE LINES.              22800016
         BP    FORMAT04                YES--GO TO PROCESS.              23000016
COUNTIN1 LH    GR7,INCOMPLT            LENGTH OF POSSIBLE INCOMPL LINE. 23200016
         LTR   GR7,GR7                 SIZE OF INCOMPLETE LINE=0.       23400016
         BP    FORMAT08                NO--GO TO PROCESS.               23600016
COUNTIN2 CR    DATAPTR,SUMTOTAL        END OF DATA THIS TRACK.          23800016
         BNL   RETURN                  YES--RETURN.                O122 23900019
         BAL   GR9,OUTPUT              SPACE A LINE.                    24100016
         ST    DATAPTR,SAVESLOT        STORE TO SEE IF DOUBLE WORD.     24200016
         TM    SAVESLOT+3,X'07'        IS PTR ON DOUBLE WORD BOUNDARY   24400016
         BC    8,COUNTIN               YES--GO TO PROCESS NXT RECORD.   24600016
         NI    SAVESLOT+3,X'F8'        NO--FORCE TO DOUBLE WORD.        24800016
         L     DATAPTR,SAVESLOT        POINTER BACK INTO REGISTER.      25000016
         LA    DATAPTR,8(DATAPTR)      ADD 8 AFTER FORCING DOUBLE.      25200016
         B     COUNTIN                 GO PROCESS NEXT RECORD.          25400016
         EJECT                                                          25600016
*********************************************************************** 25800016
*                                                                     * 26000016
*   PROCESS COMPLETE LINES HERE.                                      * 26200016
*                                                                     * 26400016
*        GR7 HAS NUMBER OF COMPLETE LINES FOR LOOP CONTROL.           * 26600016
*                                                                     * 26800016
*********************************************************************** 27000016
         SPACE                                                          27200016
FORMAT04 L     GR6,FORM5FPT            COMPARE BUFFER ADDRESS.          27400016
         L     GR5,THIRTY2             LENGTH FOR EXEC.                 27600016
         BCTR  GR5,0                   DECREMENT FOR EXEC.              27800016
         EX    GR5,MOVE                DATA TO COMPARE BUFFER.          28000016
         MVI   SAMESW,0                SET SWITCH OFF.                  28200016
         USING PRNTAREA,4                                               28400016
FORMAT05 LA    GR4,MESS                ADDRESS OF OUTPUT BUFFER.        28600016
         L     GR5,THIRTY2             LENGTH FOR EXEC.                 28800016
         BAL   GR9,GRAPHIC             CREATE ENGLISH TRANSLATION.      29200016
FORMAT06 CLI   0(GR8),X'FF'            IS THIS A DELIMITER.             29600016
         BC    8,FORMAT07              YES--LINE IS COMPLETE.           29800016
         SR    GR0,GR0                 CLEAR WORK REGISTER.             30000016
         IC    GR0,0(GR8)              GR0=DISPL FROM BEG. OF LINE.     30200016
         SR    GR5,GR5                 CLEAR WORK REGISTER.             30400016
         IC    GR5,1(GR8)              GR5=SIZE OF FIELD-1.             30600016
         AR    GR4,GR0                 ADDRESS OF FIELD W/IN A LINE.    30800016
         EX    GR5,MOVE1               MOVE FIELD TO WORK AREA.         31000016
         UNPK  DAWORK(9),DAWORK(5)                                      31200016
         TR    DAWORK(8),IOTAB-240                                      31400016
         LA    GR5,1(GR5,GR5)          GR5=SIZE OF UNPACKED FIELD.      31600016
         EX    GR5,MOVE2               MOVE UNPACKED FIELD TO BUFFER.   31800016
         LA    GR8,2(GR8)              BUMP LAYOUT FIELD PTR.           32000016
         LA    DATAPTR,4(DATAPTR)      BUMP DATA FIELD POINTER.         32200016
         B     FORMAT06                CONTINUE PROCESSING THIS LINE.   32400016
         SPACE                                                          32600016
         EJECT                                                          32800016
*********************************************************************** 33000016
*                                                                     * 33200016
*        CHECK FOR IDENTICAL LINES HERE.                              * 33400016
*                                                                     * 33600016
*********************************************************************** 33800016
         SPACE                                                          34000016
FORMAT07 BAL   GR9,OUTPUT              PRINT A LINE.                    34200016
COMPARE  L     GR5,LOCCTR              LOCATION COUNTER.                34400016
         A     GR5,THIRTY2             INCREMENT AND                    34600016
         ST    GR5,LOCCTR              STORE FOR FUTURE USE.            34800016
         CLI   SAMESW,X'F0'            LAST ENTRY.                      35000016
         BE    COUNTIN1                YES--GO TO SEE IF END OF DATA.   35200016
         L     GR6,FORM5FPT            COMPARE BUFFER ADDRESS.          35400016
         CLC   0(32,GR6),0(DATAPTR)    TWO LINES ALIKE.                 35600016
         BE    SAMELINE                YES--GO TO HANDLE.               35800016
         BCT   GR7,FORMAT04            PROCESS NEXT LINE IF ANY MORE.   36000016
         CLI   SAMESW,X'FF'            LAST LINE PRINTED A MSG.         36200016
         BNE   COUNTIN1                NO--GO SEE IF DONE WITH TRACK.   36400016
COMPARE1 MVI   SAMESW,X'F0'            SET LAST ENTRY SWITCH.           36500016
         S     GR5,THIRTY2             DECREMENT AND                    36530016
         ST    GR5,LOCCTR              STORE LOCATION COUNTER.          36560016
COMPARE2 S     DATAPTR,THIRTY2         DECREMENT DATA PTR.              36600016
         B     FORMAT05                GO TO PROCESS THIS LINE.         37000016
SAMELINE BCTR  GR7,0                   DECREMENT LINE COUNTER.          37080016
         LTR   GR7,GR7                 SEE IF LAST LINE.                37160016
         BZ    COMPARE1                YES--GO TO PROCESS AND PRINT IT. 37240016
         A     DATAPTR,THIRTY2         NO---BUMP DATA PTR.              37320016
         CLI   SAMESW,X'FF'            SAME MSG ALREADY WRITTEN.        37400016
         BE    COMPARE                 YES--NO MESSAGE THIS TIME.       37600016
         CLC   0(32,GR6),0(DATAPTR)    3 LINES IN A ROW ALIKE.          37660016
         BNE   COMPARE2                NO--GO PRINT THE LINE.           37730016
         MVI   SAMESW,X'FF'            SET SWITCH TO INDIC. MSG OUT.    37800016
         MVC   MESS+10(21),SAME        SET UP MESSAGE.                  38000016
         B     FORMAT07                GO TO WRITE THE MESSAGE.         38200016
         EJECT                                                          38400016
*********************************************************************** 38600016
*                                                                     * 38800016
*        HANDLE INCOMPLETE LINE HERE.                                 * 39000016
*                                                                     * 39200016
*********************************************************************** 39400016
         SPACE                                                          39600016
         USING PRNTAREA,4                                               39800016
FORMAT08 LA    GR4,MESS                ADDRESS OF OUTPUT BUFFER.        40000016
         LR    GR5,GR7                 LENGTH FOR EXEC.                 40200016
         BAL   GR9,GRAPHIC             GO TO TRANSLATE TO GRAPHIC.      40400016
FORMAT09 SR    GR0,GR0                 CLEAR WORK REGISTER.             40600016
         IC    GR0,0(GR8)              GR0=DISPL. FROM BEG. OF LINE.    40800016
         SR    GR5,GR5                 CLEAR WORK REGISTER.             41000016
         IC    GR5,1(GR8)              GR5=SIZE OF FIELD-1.             41200016
         AR    GR4,GR0                 ADDRESS OF FIELD WITHIN A LINE.  41400016
         LA    GR5,1(GR5)              TOTAL SIZE OF FIELD              41600016
         CR    GR7,GR5                 INCOMPLETE LINE LESS THAN 4.     41800016
         BH    FORMAT10                NO--CONTINUE.                    42000016
         LR    GR5,GR7                 LENGTH OF REMAINING DATA.        42200016
         SR    GR7,GR7                 CLEAR REGISTER.                  42400016
         B     FORMAT11                GO TO PROCESS.                   42600016
FORMAT10 SR    GR7,GR5                 DECREMENT LENGTH.                42800016
FORMAT11 BCTR  GR5,0                   DECREMENT FOR EXEC.              43100016
         EX    GR5,MOVE1               MOVE FIELD TO WORK AREA.         43400016
         AR    DATAPTR,GR5             INCREMENT                        43460016
         LA    DATAPTR,1(DATAPTR)        DATA POINTER.                  43520016
         UNPK  DAWORK(9),DAWORK(5)     PREP. FOR CHANGE TO GRAPHIC.     43600016
         TR    DAWORK(8),IOTAB-240                                      43800016
         LA    GR5,1(GR5,GR5)          GR5=SIZE OF UNPACKED FIELD.      44000016
         EX    GR5,MOVE2               MOVE UNPACKED FIELD TO BUFFER.   44200016
         LA    GR8,2(GR8)              INCREMENT LAYOUT FIELD PTR.      44400016
         LTR   GR7,GR7                 END OF INCOMPLETE LINE.          44600016
         BP    FORMAT09                NO--CONTINUE PROCESSING.         44800016
         BAL   GR9,OUTPUT              PRINT A LINE.                    45000016
         B     COUNTIN2                GO SEE IF MORE RECS. THIS TRK.   45200016
         EJECT                                                          45400016
*********************************************************************** 45600016
*                                                                     * 45800016
*        CONVERT DATA TO GRAPHICS,OR PRINTABLE ENGLISH.               * 46000016
*                                                                     * 46200016
*********************************************************************** 46400016
         SPACE                                                          46600016
         USING PRNTAREA,4                                               46800016
GRAPHIC  MVI   DELIMITA,C'*'           DELIMIT THE GRAPHIC              47000016
         MVI   DELIMITB,C'*'             FIELD IN THE OUTPUT BUFFER.    47200016
         LA    GR6,COREBLK             SET FOR EXEC.                    47400016
         BCTR  GR5,0                   DECREMENT FOR EXEC.              47600016
         EX    GR5,MOVE                STORAGE BLK TO PRINT AREA.       47800016
         LA    GR5,1(GR5)              INCREMENT LOOP CONTROL.          48000016
LOOP     CLI   0(GR6),X'C1'             CHAR MORE THAN C0       A38484  48200020
         BNL   CHECK                    YES - CHECK                     48400020
         CLI   0(GR6),C' '             IS CHAR BLANK.                   48600016
         BE    EXITOK                  YES--NO MODIFY.                  48800016
DOT      EQU   *                                                   3605 48900018
         MVI   0(GR6),C'.'             UNPRINTABLE CHAR.                49000016
EXITOK   LA    GR6,1(GR6)              INCREMENT BUFFER POINTER.        49200016
         BCT   GR5,LOOP                LOOP THRU TO END.                49400016
         LA    GR8,COREBLK+32          END OF GRAPHIC AREA.             49600016
LOOP1    CR    GR8,GR6                 A COMPLETE LINE.                 50200016
         BC    8,LOOP2                 YES--GO TO TRANSLATE.            50400016
         MVI   0(GR6),C'.'             NO--MOVE IN '.'                  50600016
         LA    GR6,1(GR6)              INCREMENT POINTER.               50800016
         B     LOOP1                   GO CHECK IF LINE COMPLETE.       51000016
LOOP2    MVC   DAWORK(3),LOCCTR+1      LOCATION CTR TO WORK AREA.       51300016
         UNPK  DAWORK(7),DAWORK(4)     UNPACK AND                       51600016
         TR    DAWORK(6),IOTAB-240       TRANSLATE TO GRAPHIC.          51800016
         MVC   CNTAREA(6),DAWORK       MOVE LOCATION CTR. TO BUFFER.    52000016
         LA    GR8,LAYOUT              LAYOUT LINE.                     52200016
         BR    GR9                     RETURN TO CALLER.                52400016
CHECK    EQU   *                                                   3605 52410018
         CLI   0(GR6),C'I'             IS IT A THRU I.             3605 52420018
         BNH   EXITOK                  YES-OKAY.                   3605 52430018
         CLI   0(GR6),C'J'             IS IT A J OR ABOVE.         3605 52440018
         BL    DOT                     NO-NONPRINTABLE CHARACTER.  3605 52450018
         CLI   0(GR6),C'R'             IS IT J THRU R.             3605 52460018
         BNH   EXITOK                  YES-OKAY.                   3605 52470018
         CLI   0(GR6),C'S'             IS IT AN S OR ABOVE.        3605 52480018
         BL    DOT                     NO-NONPRINTABLE CHARACTER.  3605 52490018
         CLI   0(GR6),C'Z'             IS IT AN S THRU Z.          3605 52500018
         BNH   EXITOK                  YES-OKAY.                   3605 52510018
         CLI   0(GR6),C'0'             IS IT GREATER THAN ZERO.    3605 52520018
         BL    DOT                     NO-NONPRINTABLE CHARACTER.  3605 52530018
         CLI   0(GR6),C'9'             IS IT 0 THRU 9.             3605 52540018
         BNH   EXITOK                  YES-OKAY.                   3605 52550018
         B     DOT                     NO-NONPRINTABLE CHARACTER.  3605 52560018
         EJECT                                                          52600016
*********************************************************************** 52800016
*                                                                     * 53000016
*       PRINT A LINE IN THE OUTPUT DATASET.                           * 53200016
*                                                                     * 53400016
*********************************************************************** 53600016
         SPACE                                                          53800016
OUTPUT   BALR  GR14,GR15               BRANCH TO -IEHDPRNT-.            54000016
         BR    GR9                     RETURN TO CALLER                 54200016
         SPACE                                                          54400016
*********************************************************************** 54600016
*                                                                     * 54800016
*        RETURN TO IEHDEXCP OR IEHDRCVR HERE.                      O122 55000019
*                                                                     * 55200016
*********************************************************************** 55400016
         SPACE                                                          55600016
RETURN   L     GR13,OUTSAVE+4          RESTORE REGISTER 13.             55800016
         RETURN (14,12),T              RETURN                      O122 56000019
         SPACE                                                          56200016
         SPACE                                                          56400016
MOVE     MVC   0(1,GR6),0(DATAPTR)       STORAGE BLK TO PRINT AREA.     56600016
         SPACE                                                          56800016
MOVE1    MVC   DAWORK(1),0(DATAPTR)      DATA TO WORK AREA.             57000016
         SPACE                                                          57200016
MOVE2    MVC   0(1,GR4),DAWORK         UNPKED DATA TO PRINT BUFFER.     57400016
         EJECT                                                          57600016
*                                                                       57800016
*        CONSTANTS USED BY THIS ROUTINE.                                58000016
*                                                                       58200016
         SPACE                                                          58400016
         DC    C'MAINTENANCE AREA'                                      58450002
MAINT    DS    50F                      MAINT AREA FOR ZAPS     XL02912 58500002
DAWORK   DS    3F                                                       58600016
OUTSAVE  DS    18F                     SAVE AREA.                       58800016
SAVESLOT DS    1F                                                       59000016
INCOMPLT DS    1H                                                       59200016
LOCCTR   DS    1F                                                       59400016
ZERO     DC    F'0'                                                     59600016
THIRTY2  DC    F'32'                                                    59800016
TRACKKEY DC    C'**** TRACK'                                            60000016
R0DATA   DC    C'R0 DATA'                                               60200016
COUNTKEY DC    C'COUNT'                                                 60400016
SAMESW   DC    XL1'00'                                                  60600016
         DS    0F                                                       60800016
         SPACE                                                          62000016
IOTAB    DC    C'0123456789ABCDEF'                                      62200016
         SPACE                                                          62400016
SAME     DC    C'LINE(S) SAME AS ABOVE'                                 62600016
         SPACE                                                          62800016
LAYOUT   DC    X'0903'                                                  63000016
         DC    X'0903'                                                  63200016
         DC    X'0903'                                                  63400016
         DC    X'0903'                                                  63600016
         DC    X'0D03'                                                  63800016
         DC    X'0903'                                                  64000016
         DC    X'0903'                                                  64200016
         DC    X'0903'                                                  64400016
         DC    X'FF'                                                    64600016
         SPACE                                                          64800016
*                                                                       65000016
*   DSECTS USED BY THIS ROUTINE.                                        65200016
*                                                                       65400016
PRNTAREA DSECT                                                          65600016
CNTAREA  DS    CL6                     LOCATION COUNTER.                65800016
BLANKA   DS    CL3                                                      66000016
EBCBLK   DS    CL74                                                     66200016
BLANKB   DS    CL3                                                      66400016
DELIMITA DS    CL1                                                      66600016
COREBLK  DS    CL32                                                     66800016
DELIMITB DS    CL1                                                      67000016
         EJECT                                                          67200016
         IEHDWORK                                                       67400016
         EJECT                                                          67600016
         IEHDBLKS                                                       67800016
         END                                                            68000016
