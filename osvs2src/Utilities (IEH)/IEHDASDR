         COPY  LCGASMSW                                          SM4351 00350021
IEHDASDR CSECT                                                          00400021
*                                                                     * 00600016
*0000   RE-ASSEMBLE FOR CHANGE TO IEHDWORK                     @ZA01201 00610002
*        RELEASE 23 DELETIONS                                         * 00620020
*        RELEASE 22 DELETIONS                                         * 00640020
*        RELEASE 21 DELETIONS                                         * 00660020
*        RELEASE 20 DELETIONS                                         * 00680020
*1418591000,825000                                               A32161 00700020
*1418562000,589000,590000-591000,887000                          M2680  00720020
*1418887000                                                      M2683  00750020
*1418887000                                                      S20201 00800020
*STATUS CHANGE LEVEL 003                                                00900020
*                                                                     * 01200016
*FUNCTION/OPERATION- THE ENTRY POINT FOR THE IEHDASDR SYSTEM UTILITY  * 01500016
*   PROGRAM IS THIS ROUTINE. CONTROL IS RECEIVED VIA A LINK OR        * 01800016
*   AN ATTACH MACRO ISSUED BY THE CALLING ROUTINE. -IEHDASDR- WILL    * 02100016
*   DO A GETMAIN FOR THE WORK AREA WHICH IS THEN ZEROED OUT,          * 02400016
*   PROCESSES ALL PARAMETERS POINTED TO BY REGISTER 1 (LINECNT,N,     * 02700016
*   INPUT FOR INVOCATION), AND THEN VIA THE XCTL MACRO, WILL PASS     * 03000016
*   CONTROL TO THE -IEHDASDS- ROUTINE.                                * 03300016
*                                                                     * 03600016
*ENTRY POINTS- THE ONLY ENTRY POINT IS -IEHDASDR-.                    * 03900016
*                                                                     * 04200016
*INPUT- REGISTER ONE WILL POINT TO A THREE WORD PARAMETER LIST        * 04500016
*   CONSTRUCTED AS FOLLOWS-                                           * 04800016
*                                                                     * 05100016
*                  **************************                         * 05400016
*                  * PTR TO PARM FIELD INFO *                         * 05700016
*                  *     (IF SUPPLIED)      *                         * 06000016
*                  **************************                         * 06300016
*                  * PTR TO INVOCATION      *                         * 06600016
*                  *     PARAMETERS         *                         * 06900016
*                  **************************                         * 07200016
*                  * PTR TO INVOCATION      *                         * 07500016
*                  *     PARAMETERS         *                         * 07800016
*                  **************************                         * 08100016
*                                                                     * 08400016
*EXTIS-NORMAL- CONTROL IS PASSED TO THE -IEHDASDS- ROUTINE VIA THE    * 08700016
*   XCTL MACRO.                                                       * 09000016
*                                                                     * 09300016
*EXITS-ERROR- CONTROL WILL BE RETURNED TO THE CALLINE ROUTINE WITH    * 09600016
*   A RETURN CODE OF 16 FOR ANY ERROR DETECTED IN THE PASSED          * 09900016
*   PARAMETERS.                                                       * 10200016
*                                                                     * 10500016
*EXTERNAL ROUTINES- NONE.                                             * 10800016
*                                                                     * 11100016
*TABLES/WORK AREAS- THIS ROUTINE DOES A GETMAIN FOR THE WORK AREA,    * 11400016
*   DESCRIBED BY THE DSECT -WORK-.                                    * 11700016
*                                                                     * 12000016
*ATTRIBUTES- SERIALLY REUSABLE, RELOCATABLE.                          * 12300016
*                                                                     * 12600016
 TITLE   'IEHDASDR --- INVOCATION AND PARM INFO HANDLER'                12900016
*   THE FOLLOWING ARE REGISTER ASSIGNMENTS.                             13200016
GR0      EQU   0                                                        13500016
GR1      EQU   1                                                        13800016
GR2      EQU   2                                                        14100016
GR3      EQU   3                                                        14400016
GR4      EQU   4                                                        14700016
GR5      EQU   5                                                        15000016
GR6      EQU   6                                                        15300016
GR7      EQU   7                                                        15600016
GR8      EQU   8                                                        15900016
GR9      EQU   9                                                        16200016
GR10     EQU   10                                                       16500016
GR11     EQU   11                                                       16800016
GR12     EQU   12                                                       17100016
GR13     EQU   13                                                       17400016
GR14     EQU   14                                                       17700016
GR15     EQU   15                                                       18000016
ROOTBASE EQU   11                                                       18300016
WORKBASE EQU   12                                                       18600016
M39      EQU   39                      MESSAGE 839.              A32161 18630020
M47      EQU   47                      MESSAGE 847.              A32161 18660020
C16      EQU   16                      RETURN CODE.              A32161 18690020
K1       EQU   1                       DISPLACEMENT CONSTANT     A32161 18720020
K2       EQU   2                       DISPLACEMENT CONSTANT     A32161 18750020
K6       EQU   6                       DISPLACEMENT CONSTANT     A32161 18780020
NUM      EQU   X'F0'                   DDEGITAL TEST VALUE.      A32161 18810020
         SPACE 1                                                        18900016
DELIM    EQU   X'7E'                   PARM FIELD DELIMITER TEST.       19200016
         SPACE 1                                                        19500016
         USING WORK,12                 SET UP BASE FOR WORK AREA        19800016
         USING IEHROOT,ROOTBASE                                         20100016
         SPACE                                                          20400016
IEHROOT  SAVE  (14,12),T               SAVE THE REGISTERS.              20700016
         LR    ROOTBASE,GR15           LOAD THE BASE REGISTER.          21000016
         LR    GR7,GR1                 SAVE PASSED POINTER, IF ANY.     21300016
         SPACE 3                                                        21600016
*   OBTAIN CORE FOR AND ZERO OUT WORKAREA.                              21900016
         SPACE 1                                                        22200016
GETCORE  GETMAIN R,LV=0+WORKSIZE                                        22500016
         SPACE 1                                                        22800016
         LR    WORKBASE,GR1            SAVE ADDRESS OF WORKAREA.        23100016
         LA    GR3,WORKSIZE            LENGTH OF WORKAREA.              23400016
         BAL   GR14,CLEAR              CLEAR WORKAREA TO ZERO.          23700016
         EJECT                                                          24000016
*   SET DEFAULT OPTIONS HERE.                                           24300016
*   THESE OPTIONS WILL BE CHANGED BY PARM FIELD INFO, IF SUPPLIED.      24600016
         MVI   PAGENO+1,1              DEFAULT IS PAGE ONE.             24900016
         MVI   LINECNT+1,58            FIXED LINE COUNT.                25200016
         MVI   LINECNT1+1,58           CURRENT LINE COUNT.              25500016
         LA    GR4,ENDCLIST-CSIZE      END OF QUEUE POINTER.            25800016
         ST    GR4,ENDCLIST            SET UP END OF QUEUE POINTER.     26100016
         MVC   SYSINDD(8),CONSYSIN     DEFAULT DDNAME TO WORK AREA.     26400016
         MVC   SYSOUTDD(8),CONSPRIN    DEFAULT DDNAME TO WORK AREA.     26700016
         SPACE 1                                                        27000016
* THIS ROUTINE CHECKS THE PARAMETER LISTS PASSED WHEN THE PROGRAM IS    27300016
*  INVOKED.  REGISTER 1 POINTS TO A THREE FULL WORD AREA IN CORE WHICH  27600016
*  CONTAINS ADDRESSES OF THE THREE POSSIBLE PARAMETER LISTS.  THE HIGH  27900016
*  ORDER BYTE OF EACH OF THE FULL WORDS MUST BE EITHER HEX 00 OR HEX 80 28200016
*  THE 80 SIGNIFIES THAT THIS IS THE LAST PARAMETER LIST POINTER.  THE  28500016
*  FIRST POINTER IS TO THE LIST THAT EQUALS THE PARM VALUES FOUND ON    28800016
*  THE EXECUTE CARD. TWO PARM VALUES ARE POSSIBLE, A LINE COUNT         29100016
*  PARAMETER AND A POWER FACTOR WHICH CONTROLS THE NUMBER OF            29400016
*  FUNCTIONS WHICH CAN BE EXECUTING CONCURRENTLY.                       29700016
*  THE SECOND POINTER IS TO THE DDNAME LIST WHICH MUST                  30000016
*  BEGIN ON A HALF WORD BOUNDARY THAT IS ALSO NOT A FULL WORD           30300016
*  BOUNDARY.  THE TWO HIGH ORDER BYTES CONTAIN A COUNT OF THE NUMBER    30600016
*  OF BYTES IN THE REMAINDER OF THE LIST.  ALL ENTRIES IN THE LIST HAVE 30900016
*  A MAXIMUM LENGTH OF EIGHT BYTES AND ARE LEFT-JUSTIFIED AND PADDED    31200016
*  WITH BLANKS.  IF A NAME IS OMITTED WITHIN THE LIST, THE EIGHT-BYTE   31500016
*  ENTRY MUST CONTAIN BINARY ZEROES.  NAMES ARE OMITTED FROM THE END BY 31800016
*  SHORTENING THE LIST.  THE SEQUENCE OF THE ENTRIES IS AS FOLLOWS:     32100016
*                                                                       32400016
*                  ENTRY 1   00000000                                   32700016
*                        2   00000000                                   33000016
*                        3   00000000                                   33300016
*                        4   00000000                                   33600016
*                        5   SYSIN                                      33900016
*                        6   SYSPRINT                                   34200016
*                        7   00000000                                   34500016
*                        8   00000000                                   34800016
*                        9   00000000                                   35100016
*                                                                       35400016
*  THE THIRD PARAMETER LIST PERTAINS TO HEADER INFORMATION.             35700016
*  THE THIRD POINTER IS TO THE SIX BYTE LIST CONTAINING A PAGE COUNT    36000016
*  FOR THE OUTPUT DEVICE.  THE FIRST TWO BYTES OF THIS LIST CONTAIN THE 36300016
*  LENGTH IN BYTES OF THE HEADING LIST.  THE REMAINING FOUR BYTES       36600016
*  CONTAIN A PAGE NUMBER THAT THE UTILITY PROGRAM IS TO PLACE ON THE    36900016
*  FIRST PAGE OF PRINTED OUTPUT.                                        37200016
         EJECT                                                          37500016
* THIS SECTION GETS THE ADDRESS OF THE DDNAMES IF ANY ARE PROVIDED,     37800016
*  OTHERWISE STANDARD DDNAMES ARE PROVIDED                              38100016
SETOPT   LR    GR1,GR7                 RESTORE POINTER TO PARMS, IF ANY 38400016
         TM    0(GR1),X'80'            ONE PARAMETER ONLY.              38700016
         BO    TEST1                   YES-GO PROCESS.                  39000016
         TM    4(GR1),X'80'            TWO PARAMETERS ONLY.             39300016
         BO    TEST2                   YES-GO PROCESS                   39600016
         TM    8(GR1),X'80'            THREE PARAMETERS ONLY            39900016
         BZ    SETCODE                 NO--MUST BE INVOCATION ERROR.    40200016
TEST3    OC    9(3,GR1),9(GR1)         IS THE THIRD POINTER VALID.      40500016
         BZ    TEST2                   NO--GO PROCESS 2ND PARAMETER.    40800016
         L     GR2,8(GR1)              POINTER TO PAGE NUMBER.          41100016
         OC    0(2,GR2),0(GR2)         IS LENGTH ZERO.                  41400016
         BZ    TEST2                   YES-GO PROCESS 2ND PARAMETER.    41700016
*   PROCESS PASSED PAGE NUMBER HERE.                                    42000016
         MVI   PAGESW,X'FF'            INDICATE PAGE NO. PASSED.        42300016
         XC    SARG(8),SARG            CLEAR.                           42600016
         PACK  SARG+5(3),2(4,GR2)      PACK GRAPHICS.                   42900016
         CVB   GR3,SARG                CHANGE TO BINARY.                43200016
         STH   GR3,PAGENO              SET UP PAGE NUMBER.              43500016
TEST2    OC    5(3,GR1),5(GR1)         IS THE 2ND POINTER VALID.        43800016
         BZ    TEST1                   NO--GO PROCESS 1ST PARAMETER.    44100016
         L     GR2,4(GR1)              POINTER TO DDNAME LIST.          44400016
         OC    0(2,GR2),0(GR2)         IS LIST LENGTH ZERO.             44700016
         BZ    TEST1                   YES-GO PROCESS 1ST PARAMETER.    45000016
*   PROCESS DDNAME LIST HERE.                                           45300016
         CLI   1(GR2),48               DDNAME LIST LONG ENOUGH/SYSPRINT 45600016
         BNL   TESTTWO                 YES-GO PROCESS SYSPRINT/SYSIN    45900016
         CLI   1(GR2),40               DDNAME LIST LONG ENOUGH/SYSIN.   46200016
         BNL   TESTONE                 YES-GO PROCESS SYSIN ONLY.       46500016
         B     TEST1                   GO-PROCESS FIRST PARAMETER.      46800016
TESTTWO  OC    42(8,GR2),42(GR2)       CHECK FOR SYSPRINT DDNAME.       47100016
         BZ    TESTONE                 NONE-GO TEST FOR SYSIN.          47400016
         MVC   SYSOUTDD(8),42(GR2)     MOVE IN USERS NAME FOR SYSPRINT. 47700016
TESTONE  OC    34(8,GR2),34(GR2)       CHECK FOR SYSIN DDNAME.          48000016
         BZ    TEST1                   NONE-GO PROCESS 1ST PARAMETER.   48300016
         MVC   SYSINDD(8),34(GR2)      MOVE IN USERS NAME FOR SYSIN.    48600016
TEST1    OC    1(3,GR1),1(GR1)         IS FIRST PARAMETER VALID.        48900016
         BZ    HEADPRIN                NO--NO PARM OPTIONS TODAY.       49200016
         L     GR2,0(GR1)              POINTER TO PARM LIST.            49500016
         OC    0(2,GR2),0(GR2)         IS LIST LENGTH ZERO.             49800016
         BZ    HEADPRIN                YES                              50100016
         LH    GR3,0(GR2)              LENGTH OF PARAMETER LIST.        50400016
         LA    GR3,1(GR3)                                               50700016
         LA    GR5,1(GR2)              POINTER TO BYTE BEFORE OPTION.   51000016
         LA    GR2,1(GR2)              START OF FIELD TO SCAN.          51300016
         SPACE 1                                                        51600016
SEEK     BCTR  GR3,0                   MINUS ONE FROM BYTE COUNT.       51900016
         LTR   GR3,GR3                 END OF TEST.                     52200016
         BE    HEADPRIN                YES-NO MORE OPTIONS              52500016
         LA    GR5,1(GR5)              INCREMENT SCAN POINTER.          52800016
         CLI   0(GR5),DELIM            IS THIS A DELIMITER.             53100016
         BNH   TESTCNT2                YES-GO TEST FOR LINE COUNT.      53400016
         SPACE 1                                                        53700016
         LTR   GR3,GR3                 END OF TEST.                     54000016
         BNE   SEEK                    NO--LOOP UNTIL DELIMITER.        54300016
         SPACE 1                                                        54600016
TESTCNT2 CLC   1(8,GR2),LINCT          IS PARAMETER LINECOUNT.          54900016
         BE    FOUNDL                  YES-GO PROCESS.                  55200016
         SPACE 1                                                        55500016
         CLC   1(2,GR2),NEQUAL         IS PARAMETER A 'N='.             55800016
         BE    FOUNDN                  YES-GO PROCESS.                  56100016
         SPACE 1                                                        56400016
         LR    GR2,GR5                 RESET START POINTER.             56700016
TESTCNT1 LTR   GR3,GR3                 IS LIST EXAUSTED                 57000016
         BE    HEADPRIN                YES-NO MORE OPTIONS.             57300016
         B     SEEK                    NO--CONTINUE SCAN.               57600016
         SPACE 1                                                        57900016
FOUNDL   EQU   *                       HANDLE THE LINE COUNT HERE.      58200016
         BCTR  GR3,0                   ACCOUNT FOR '=' SIGN.            58500016
         CH    GR3,TWO                 AT LEAST TWO CHARACTER PARAMETER 58800016
         BL    SETCODE                  NO - INVOCATION ERROR    M2680  58900020
         TM    9(GR2),X'F0'            1ST DIGIT A DECIMAL DIGIT.       59400016
         BC    12,SETCODE              NO--INVOCATION ERROR.            59700016
         TM    10(GR2),X'F0'           2ND DIGIT A DECIMAL DIGIT.       60000016
         BC    12,SETCODE              NO--INVOCATION ERROR.            60300016
         XC    SARG(8),SARG            CLEAR.                           60600016
         PACK  SARG+6(2),9(2,GR2)      PACK GRAPHICS.                   60900016
         BCTR  GR3,0                   ACCOUNT FOR ONE DIGIT.           61200016
         LA    GR2,10(GR2)             POINT TO LAST DIGIT.             61500016
         CH    GR3,ONE                 MORE THAN 2 DIGITS LEFT IN LIST. 61800016
         BNH   CONT                    NO--PROCESS THIS LINE COUNT.     62100016
         CLI   1(GR2),DELIM            SOME DELIMITER FOLLOW 2ND DIGIT. 62400016
         BH    SETCODE                 NO--INVOCATION ERROR.            62700016
CONT     LR    GR5,GR2                 RESET POINTER.                   63000016
         CVB   GR9,SARG                CHANGE TO BINARY.                63300016
         STC   GR9,SARG                TEMPORARY STORAGE.               63600016
         CLI   SARG,0                  DOES THE LINE COUNT EQUAL ZERO.  63900016
         BNH   SETCODE                 YES-INVOCATION ERROR.            64200016
         CLI   SARG,99                 LINE COUNT MORE THAN 99.         64500016
         BH    SETCODE                 YES-INVOCATION ERROR.            64800016
         STH   GR9,LINECNT             FIXED LINE COUNT.                65100016
         STH   GR9,LINECNT1            CURRENT LINE COUNT.              65400016
         B     TESTCNT1                                                 65700016
         EJECT                                                          66000016
*   SET UP THE QUEUE SIZE HERE, IF SPECIFIED.                           66300016
FOUNDN   XC    SARG(8),SARG            ZERO.                            66600016
         BCTR  GR3,0                   ACCOUNT FOR '=' SIGN.            66900016
         CH    GR3,ONE                 MORE THAN 1 BYTE LEFT IN LIST.   67200016
         BNH   CONT1                   NO--GO PROCESS 'N'.              67500016
         CLI   2(GR5),DELIM            DELIMITER FOLLOW THE 1 DIGIT.    67800016
         BH    SETCODE                 NO--INVOCATION ERROR.            68100016
CONT1    EQU   *                                                        68400016
         LA    GR2,1(GR5)              RESET POINTER.                   68700016
         LR    GR5,GR2                 RESET POINTER -LAST DIGIT.       69000016
         CLI   0(GR2),C'0'             N GREATER THAN 0.                69300016
         BNH   SETCODE                 NO--INVOCATION ERROR.            69600016
         CLI   0(GR2),C'6'             N GREATER THAN 6.                69900016
         BH    SETCODE                 YES-INVOCATION ERROR.            70200016
         PACK  SARG+7(1),0(1,GR2)      PACK GRAPHICS.                   70500016
         CVB   GR9,SARG                'N' VALUE IN BINARY.             70800016
         SPACE 1                                                        71100016
FOUND1   SLA   GR9,2                   N TIMES BYTES/ENTRY.             71400016
         LA    GR4,PTRFUNC1-CSIZE      ADDRESS OF CONTROL LIST.         71700016
         AR    GR4,GR9                 PLUS N TIMES BYTES PER ENTRY.    72000016
         ST    GR4,ENDCLIST            POINTS TO LAST USEABLE SLOT.     72300016
         B     TESTCNT1                                                 72600016
         SPACE                                                          72900016
*********************************************************************** 73200016
*                                                                     * 73500016
*   THIS ROUTINE WILL CLEAR ANY BLOCK OF CORE TO ZERO.                * 73800016
*                                                                     * 74100016
*     REGISTER 1 MUST CONTAIN THE STARTING ADDRESS UPON ENTRY.        * 74400016
*     REGISTER 2 IS A WORK REGISTER.                                  * 74700016
*     REGISTER 3 MUST CONTAIN THE SIZE UPON ENTRY.                    * 75000016
*     REGISTER 14 IS THE RETURN REGISTER.                             * 75300016
*                                                                     * 75600016
*********************************************************************** 75900016
         SPACE                                                          76200016
CLEAR    LA    GR2,255                 SET FOR EXECUTE.                 76500016
         MVI   0(GR1),X'00'            PRIME FIRST BYTE FOR EXEC.       76800016
EXEC     CH    GR3,TWO56               WILL THIS BE THE LAST PASS.      77100016
         BNH   LASTTIME                YES-DO ONE MORE MOVE.            77400016
         EX    GR2,MVZO                CLEAR 256 BYTES.                 77700016
         LA    GR1,256(GR1)            INCREMENT ADDRESS.               78000016
         SH    GR3,TWO56               UPDATE BYTE COUNT.               78300016
         B     EXEC                    RETURN TO CHECKPOINT.            78600016
         SPACE 1                                                        78900016
LASTTIME BCTR  GR3,0                   DECREMENT FOR MVC.               79200016
         EX    GR3,MVZO                CLEAR REMAINING PORTION.         79500016
         BR    GR14                    RETURN.                          79800016
         EJECT                                                          80100016
HEADPRIN EQU   *                       PASS CONTROL TO 2ND LOAD/MONITOR 80400016
         ST    GR1,PRINT1              PASS PARM POINTER TO 2ND LOAD.   80700016
         LR    GR1,WORKBASE            PASS WORK AREA ADDRESS.          81000016
         L     GR14,12(GR13)           RESTORE REGISTER 14.             81300016
         LA    GR15,XCTL               XCTL LIST.                       81600016
         XCTL  (2,12),MF=(E,(1)),SF=(E,(15))                            81900016
         SPACE 3                                                        82200016
SETCODE  EQU   *                                                 A32161 82220020
         USING IHADCB,GR4                                        A32161 82240020
         LA    GR4,WRIT                GET DCB ADDRESS.          A32161 82260020
         ST    GR4,PRINT1              STORE IN WORK AREA.       A32161 82280020
         MVC   DCBDDNAM(8),CONSPRIN  PUT DDNAME IN DCB.          A32161 82300020
         MVC   PRNTSW,0                CLEAR SWITCH.             A32161 82320020
         LA    GR1,OPENLST             GET OPEN LST ADDR.        A32161 82340020
         OPEN  MF=(E,(1))              OPEN PRINTER DCB.         A32161 82360020
         TM    DCBOFLGS,WAITING        SUCCESSFUL OPEN.          A32161 82380020
         BZ    SETCODE                 NO-TRY AGAIN.             A32161 82400020
         BAL   GR8,WRITMSG             PRINT HEADING.            A32161 82420020
         LA    GR1,M47                 GET MESSAGE 847.          A32161 82440020
         BAL   GR8,MSGSETUP            PLACE MSG BUFFER.         A32161 82460020
         BAL   GR8,WRITMSG             PRINT MESSAGE.            A32161 82480020
         LA    GR1,M39                 GET RETURN CODE MSG.      A32161 82500020
         BAL   GR8,MSGSETUP            PLACE IN MSG BUFFER.      A32161 82520020
         LA    GR6,C16                 RETURN CODE.              A32161 82540020
         CVD   GR6,DECODE              CHANGE TO DECIMAL.        A32161 82560020
         UNPK  K1(K2,GR1),DECODE+K6(K2)                          A32161 82580020
         OI    K2(GR1),NUM                                       A32161 82600020
         BAL   GR8,WRITMSG             PRINT MESSAGE.            A32161 82620020
         LA    GR1,CLOSLST             GET ADDR OF CLOSE.        A32161 82640020
         CLOSE MF=(E,(1))              CLOSE PRINTER DCB.        A32161 82660020
         LR    GR1,WORKBASE          ADDRESS OF WORK AREA.       A32161 82680020
         FREEMAIN R,LV=0+WORKSIZE,A=(1)                                 82800016
         SPACE                                                          83100016
         LA    GR15,16                                                  83400016
         RETURN (14,12),T,RC=(15)      RESTORE/RETURN W/PROPER CODE.    83700016
MSGSETUP EQU   *                                                 A32161 83730020
         LINK  EP=IEHDMSGB             OBTAIN PROPER MSG.        A32161 83760020
         BR    GR8                     RETURN TO CALLER.         A32161 83790020
WRITMSG  EQU   *                                                 A32161 83820020
         LINK  EP=IEHDPRNT             PRINT PROPER MSG.         A32161 83850020
         BR    GR8                     RETURN TO CALLER.         A32161 83880020
FILIN    MVC   0(0,GR1),0(GR3)         PARM INTO MSG.            A32161 83910020
         DC    C'MAINTENANCE AREA'                              XL02912 83920002
MAINT    DS    50F                                              XL02912 83930002
OPENLST  OPEN  (WRIT,(OUTPUT)),MF=L                              A32161 83940020
CLOSLST  CLOSE (WRIT),MF=L                                       A32161 83970020
         SPACE 1                                                        84000016
*ASSUMED DDNAME CONSTANTS                                               84300016
*                                                                       84600016
CONSYSIN DC    CL8'SYSIN   '                                            84900016
CONSPRIN DC    CL8'SYSPRINT'                                            85200016
         SPACE 1                                                        85500016
SARG     DS    1D                      WORK AREA.                       85800016
TWO56    DC    H'256'                  COMPARE CONSTANT.                86100016
ONE      DC    H'1'                    COMPARE CONSTANT.                86400016
TWO      DC    H'2'                    COMPARE CONSTANT.                86700016
MVZO     XC    0(1,GR1),0(GR1)         EXECUTED TO CLEAR CORE.          87000016
         SPACE                                                          87300016
XCTL     XCTL  EP=IEHDASDS,SF=L                                         87600016
         SPACE                                                          87900016
NEQUAL   DC    C'N='                   PARM FIELD KEYWORD.              88200016
LINCT    DC    C'LINECNT='             PARM FIELD KEYWORD.              88500016
ZEROES   DC    CL2'0'                  LINECNT TEST VALUE.       A32161 88550020
DECODE   DS    1D            USE FOR PACK INSTRUCTION            A32161 88600020
LIST     DC    F'0'                                              A32161 88650020
WRIT     DCB   DSORG=PS,MACRF=PM,RECFM=FBSA,BLKSIZE=121,         M2680 X88700020
               LRECL=121,DDNAME=SYSPRINT,EODAD=SETCODE           A32161 88750020
         EJECT                                                          88800016
         IEHDWORK                                                       89100016
         DCBD  DSORG=PS                                          A32161 89200020
         END                                                            89400016
