         TITLE 'IEHDLABL - PERFORMS LABEL FUNCTION  IEHDASDR UTILITY'   00302121
         COPY  LCGASMSW                                                 00302421
IEHDLABL CSECT                                                          00304000
*********************************************************************** 00310099
*                                                                       00315099
*                     FIXES THIS MODULE                                 00320099
*                     LATEST FIRST.                                     00325099
*                                                                       00330099
*********************************************************************** 00335099
*                                                                       00340099
*D 66000                                  SU32 ONLY            @ZA25515 00342099
*C 113000                                 SU32 ONLY            @ZA25515 00345099
*A 112000-112500                          SU32 ONLY            @ZA25515 00348099
*A 217160-217190,753100                   SU32 ONLY            @ZA25515 00355099
*                                                                       00360099
*  NO DELETION                            SU32                 @G32DSPD 00365099
*                                                                     * 00370099
*003600,011900,012300,212500,318200                                O122 00375099
*                                                                     * 00380099
*STATUS- CHANGE LEVEL 001                                          O122 00385099
*********************************************************************** 00390099
      EJECT                                                             00395099
*FUNCTION/OPERATION- THIS ROUTINE HANDLES THE -LABEL- FUNCTION        * 00420016
*   OF THE IEHDASDR SYSTEM UTILITY PROGRAM. LABEL CONSISTS OF         * 00450016
*   WRITING A NEW VOLUME SERIAL NUMBER ON A SPECIFIED DIRECT          * 00480016
*   ACCESS DEVICE. IN ADDITION, IT WILL OPTIONALLY ADD AN OWNER       * 00510016
*   NAME TO THE VOLUME LABEL RECORD.                                  * 00540016
*   RACF AUTHORIZATION IS REQUIRED ON THOSE VOL IDS THAT ARE   @G32DSPD 00540532
*   DEFINED TO RACF.                                           @G32DSPD 00550532
*                                                                     * 00570016
*ENTRY POINTS- THE ONLY ENTRY POINT IS -IEHDLABL-, AND CONTROL        * 00600016
*   IS ALWAYS RECEIVED FROM THE MONITOR MODULE IEHDASDS.              * 00630016
*                                                                     * 00660016
*INPUT- POINTERS TO A WORKAREA(REGISTER 12) AND TO A CONTROL          * 00690016
*   BLOCK (REGISTER 10).                                              * 00720016
*   DATA IS ON A DIRECT ACCESS DEVICE. ONLY THE VOLUME LABEL          * 00750016
*   RECORD (RECORD 3 ON TRACK 0) WILL BE READ.                        * 00780016
*                                                                     * 00810016
*EXITS- NORMAL- A SUCCESSFUL LABEL OPERATION RESULTS IN A RETURN      * 00840016
*   TO THE MONITOR(IEHDASDS) WITH A RETURN CODE OF 0. A LABEL         * 00870016
*   COMPLETE MESSAGE IS ALSO PLACED IN THE MESSAGE DATA SET.          * 00900016
*                                                                     * 00930016
*EXITS- ERROR- ANY ERROR ENCOUNTERED IS DESCRIBED BY AN APPROPRIATE   * 00960016
*   MESSAGE TO THE MESSAGE DATA SET. A RETURN CODE GREATER THAN       * 00990016
*   ZERO WILL ALSO BE PASSED BACK TO THE MONITOR.                     * 01020016
*                                                                     * 01050016
*EXTERNAL ROUTINES- THIS ROUTINE IS ALWAYS ENTERED FROM THE           * 01080016
*   MONITOR MODULE IEHDASDS. FOUR OTHER CSECTS ARE USED BY THIS       * 01110016
*   ROUTINE. ANY MESSAGES ISSUED WILL BE FORMULATED BY THE MESSAGE    * 01140016
*   ROUTINE (IEHDMSGB) AND PRINTED BY THE PRINT ROUTINE (IEHDPRNT)    * 01170016
*   THE END OF EXTENT APPENDAGE (IGG019P8) HANDLES THE LIMITS AND  O122 01190019
*   EXTENTS IN THE DEB FOR ALL DA DEVICES INVOLVED. IGC0008B (SVC 82) * 01210016
*   ENTERS THE NEW VOLUME SERIAL NUMBER INTO THE UCB, AND FOR AN   O122 01220019
*   OFFLINE LABEL FUNCTION WILL REQUEST OPERATOR CONFIRMATION AND  O122 01230019
*   BUILD A DEB IN PROTECTED CORE.                                 O122 01240019
*                                                                     * 01260016
*TABLES/WORK AREAS- UPON ENTRY REGISTERS 10 AND 12 POINT TO A         * 01290016
*   FUNCTION BLOCK AND A WORK AREA RESPECTIVELY. THEY ARE DESCRIBED   * 01320016
*   IN THE DSECTS CALLED -FUNCBLK- AND -WORK-.                        * 01350016
*                                                                     * 01380016
*ATTRIBUTES- SERIALLY REUSABLE, RELOCATABLE.                          * 01410016
*                                                                     * 01440016
*   THE FOLLOWING ARE REGISTER ASSIGNMENTS.                             01500016
GR0      EQU   0                                                        01800016
GR1      EQU   1                                                        02100016
GR2      EQU   2                                                        02400016
GR3      EQU   3                                                        02700016
GR4      EQU   4                                                        03000016
GR5      EQU   5                                                        03300016
GR6      EQU   6                                                        03600016
GR7      EQU   7                                                        03900016
GR8      EQU   8                                                        04200016
GR9      EQU   9                                                        04500016
GR10     EQU   10                                                       04800016
GR11     EQU   11                                                       05100016
GR12     EQU   12                                                       05400016
GR13     EQU   13                                                       05700016
GR14     EQU   14                                                       06000016
GR15     EQU   15                                                       06300016
UCBPTR   EQU   9                                                        06900016
BASEREG  EQU   11                                                       07200016
WORKBASE EQU   12                                                       07500016
         USING FUNCBLK,10                                               07800016
         USING IEHDLABL,BASEREG                                         08100016
         USING WORK,12                                                  08400016
         SAVE  (14,12),T,*             SAVE THE REGISTERS               08700016
         LR    BASEREG,GR15            LOAD THE BASE REGISTER.          09000016
         LA    GR7,LABLSAVE            SAVE AREA ADDRESS.               09300016
         ST    GR7,8(GR13)             ADDRESS OF NEW AREA TO OLD.      09600016
         ST    GR13,4(GR7)             ADDRESS OF OLD AREA TO NEW.      09900016
         LR    GR13,GR7                LOAD SAVE AREA ADDRESS.          10200016
         B     APARNO                  BRANCH AROUND APAR NO   @ZA25515 11200099
         DC    C'SU32 OZ25515'         LAST FIX THIS MODULE    @ZA25515 11250099
APARNO   L     GR8,RDDCBAD             POINTER TO DCB          @ZA25515 11300099
         USING IHADCB,8                                                 12200016
         L     UCBPTR,TUCBPTR          PICK UP POINTER TO UCB           14600016
         USING UCB,9                                                    20400016
         LA    GR4,0                   INITIALIZE BIN NO. TO 0          20500016
         ST    GR4,RDSEEK                                               20600016
         AIF   ('&LIB' EQ 'LIB2').X234300                       XL03912 20650005
         CLI   UCBID,X'FF'             IS THIS MAIN UCB                 20700016
         BNE   PASSBY                  NO-GO PICK UP BIN NUMBER         20800016
.X234300 ANOP                                                   XL03912 20850005
         TM    UCBTBYT3,UCB3DACC       IS THIS A DIRECT ACCESS DEVICE   20900016
         BZ    BADPARM                 NO--MUST BE BAD PARAMETER.       21000016
         TM    SRTESTAT,SRTESYSR       THIS A SYSTEMS RESIDENCE VOLUME  21100016
         BO    BADDEV                  YES - NOT ALLOWED                21200016
CHKMORE  EQU   *                                                   O122 21206019
         CLI   DDNAME2,X'40'           OFFLINE LABEL REQUEST.      O122 21212019
         BNE   SKIP                    NO-NO NEED TO CHECK FURTHER.O122 21218019
         TM    SRTESTAT-UCBOB(UCBPTR),SRTEONLI  YES-DEVICE ONLINE. O122 21224019
         BO    ONLINERR                YES-SET UP ERROR MESSAGE.   O122 21230019
         LA    GR1,RDAREA              ADDR. OF AREA FOR LIST.     O122 21236019
         ST    UCBPTR,0(GR1)           STORE UCB PTR. IN LIST.     O122 21242019
         MVI   0(GR1),X'8F'            SET UP BUILD DEB INDICATOR. O122 21248019
         ST    GR8,4(GR1)              STORE DCB PTR. IN LIST.     O122 21254019
         MVI   4(GR1),X'88'            SET LAST ENTRY INDICATOR.   O122 21260019
         SVC   POST                    CONSTRUCT DEB.              O122 21266019
         LTR   GR15,GR15               WAS DEB SUCCESSFULLY BUILT. O122 21272019
         BNZ   ONLINERR                NO-SET UP ERROR MESSAGE.    O122 21278019
         ST    GR10,DCBIOBAD           FUNCTION BLK PTR TO DCB.    O122 21284019
         B     READ                    GO TO READ IN OLD LABEL.    O122 21290019
         AIF   ('&LIB' EQ 'LIB2').X234301                       XL03912 21292005
         DROP  9                                                        21300016
         USING DCELBBNR,9                                               21400016
PASSBY   MVC   RDSEEK+1(2),DCELBBNR    PICK UP BIN NO. FOR 2321         21500016
         B     CHKMORE                 CHECK IF OFFLINE REQUEST.   O122 21550019
.X234301 ANOP                                                   XL03912 21560005
SKIP     EQU   *                                                YL02912 21650002
*********************************************************************** 21655032
*     THIS SECTION OF CODE PERFORMS RACF SECURITY CHECKING.IF THE NEW * 21660032
*  VOLID IS RACF DEFINED, THE USER MUST HAVE ALTER ACCESS TO IT. IF   * 21665032
*  THE OLD VOLID IS RACF DEFINED THE USER MUST HAVE ALTER ACCESS TO   * 21670032
*  IT. NO SECURITY CHECKING WILL TAKE PLACE IT RACF IS NOT ACTIVE IN  * 21675032
*  THE SYSTEM.                                                 @G32DSPD 21680032
*********************************************************************** 21685032
         L     GR5,CVTPTR              GET CVTPTR              @G32DSPD 21690032
         USING CVT,GR5                                         @G32DSPD 21695032
         L     GR5,CVTRAC              GET RACF POINTER        @G32DSPD 21700032
         DROP  GR5                                             @G32DSPD 21705032
         LTR   GR5,GR5                 IS RACF ACTIVE ?        @G32DSPD 21710032
         BZ    NULLRACF                NO,SKIP RACF CHECKS     @G32DSPD 21715032
         USING RCVT,GR5                DO CHECK IF RACF IS     @ZA25515 21716099
         TM    RCVTSTA1,RCVTDASD       CURRENT.                @ZA25515 21717099
         BZ    NULLRACF                NO DONT RACHECK         @ZA25515 21718099
         DROP  GR5                     YES DO RACHECK          @ZA25515 21719099
*  TEST THE OLD VOLID FOR RACF SECURITY                        @G32DSPD 21720032
         MVC   RACVOL,UCBVOLI                                  @G32DSPD 21725032
         RACHECK ATTR=ALTER,ENTITY=RACVOL,CLASS='DASDVOL'      @G32DSPD 21730032
         CLM   GR15,B'0001',RC8        USER UNAUTHORIZED ?     @G32DSPD 21735032
         BE    UNAUTH                  YES,GO TERMINATE        @G32DSPD 21740032
*  TEST THE NEW VOLID FOR RACF SECURITY                        @G32DSPD 21745032
         MVC   RACVOL(6),SERNO                                 @G32DSPD 21750032
         RACHECK ATTR=ALTER,ENTITY=RACVOL,CLASS='DASDVOL'      @G32DSPD 21755032
         CLM   GR15,B'0001',RC8        USER UNAUTHORIZED ?     @G32DSPD 21760032
         BE    UNAUTH                  YES,GO TERMINATE        @G32DSPD 21765032
NULLRACF EQU   *                                               @G32DSPD 21770032
*   END RACF AUTHORIZATION CHECKS.                             @G32DSPD 21775032
         SPACE 2                                               @G32DSPD 21780032
         MVC   REQUEST+VOL(L6),SRTEVOLI SET VOLID TO MSG.       YL02912 21800002
         DROP  GR9                                              YL02912 21850002
         L     GR9,CVTPTR               GET ADDRESS OF CVT.     YL02912 21860002
         USING CVT,GR9                                          YL02912 21870002
         L     GR9,CVTTCBP              GET PTR TO TCB ADDRESS. YL02912 21880002
         L     GR9,D4(GR9)              GET TCB ADDRESS.        YL02912 21890002
         L     GR9,D12(GR9)             GET TIOT ADDRESS.       YL02912 21892002
         MVC   REQUEST+JJJ(L8),D0(GR9)  SET JOB NAME TO MSG.    YL02912 21894002
         MVC   REQUEST+SSS(L8),D8(GR9)  SET STEP NAME TO MSG.   YL02912 21896002
REQUEST  WTOR  'IEH853D CONFIRM REQUEST TO LABEL CURRENT VOLID=      . *21898002
               JJJJJJJJ,SSSSSSSS.',REPLY,1,WTORECB,ROUTCDE=(4),DESC=(2) 21898402
         TM    WTORECB,COMPLETE         IS OPERATION COMPLETE?  YL02912 21898802
         BO    TESTANSW                 YES,GO CHECK ANSWER.    YL02912 21899202
         WAIT  ECB=WTORECB              NO, AWAIT COMPLETION.   YL02912 21899602
TESTANSW MVI   WTORECB,ZERO             CLEAR ECB POST BITS.    YL02912 21899702
         CLI   REPLY,C'U'               REPLY EQUAL 'U'?        YL02912 21899802
         BE    USE                      YES, CONTINUE LABEL.    YL02912 21899902
         CLI   REPLY,C'T'               REPLY EQUAL 'T'?        YL02912 21949902
         BE    TERM                     YES, TERMINATE.         YL02912 21959902
         MVC   ERRANS+ERJJ(L8),D0(GR9)  SET JOB NAME TO MSG.    YL02912 21969902
         MVC   ERRANS+ERSS(L8),D8(GR9)  SET STEP NAME TO MSG.   YL02912 21979902
ERRANS   WTO   'IEH808I REPLY IN ERROR. REPLY WITH  U  OR  T , JJJJJJJJ*21989902
               ,SSSSSSSS.',ROUTCDE=(4),DESC=(5)                 YL02912 21991902
         B     REQUEST                  GO WRITE REQUEST AGAIN. YL02912 21993902
         DROP  GR9                                              YL02912 21995902
         EJECT                                                          21997902
*********************************************************************** 21999902
*                                                                     * 22200016
*  THIS ROUTINE OPENS THE DATA CONTROL BLOCKS FOR ALL DIRECT ACCESS   * 22500016
*     DEVICES INVOLVED. THE OPEN IS ALWAYS PERFORMED ON THE FORMAT    * 22800016
*     FOUR DSCB OF THE VTOC.                                          * 23100016
*                                                                     * 23400016
*********************************************************************** 24000016
USE      EQU   *                                                        24010002
         MVC   DCBDDNAM(8),DDNAME2     PUT DDNAME IN DCB                24050002
         MVC   OPENLD+1(3),RDDCBAD+1   MOVE DCB ADDRESS TO LIST         24100016
OPENJ    LA    GR9,RDAREA    BUFFER ADDRESS TO READ JFCB.               24300016
         ST    GR9,LIST                                                 24600016
         MVI   LIST,X'87'              EXIT LIST CODE                   24700016
         RDJFCB MF=(E,OPENLD)                                           24900016
         USING JFCB,9                                                   25200016
         MVI   JFCBDSNM,X'04'          SET DSNAME FIELD FOR             25500016
         MVC   JFCBDSNM+1(43),JFCBDSNM   FORMAT 4 DSCB.                 25800016
         MVI   JFCBTSDM,X'08'          STOP WRITE OF JFCB AT OPEN       25900016
         OPEN  MF=(E,OPENLD),TYPE=J                                     26100016
         TM    DCBOFLGS,X'10'          WAS THERE A SUCCESSFUL OPEN.     27000016
         BZ    NOOPEN                  NO--GIVE AN ERROR MESSAGE        27300016
         NI    DCBMACRF+1,X'F3'        SET BITS OFF TO PREVENT CLOSE    27600016
*                                        FROM WRITING AN EOF RECORD     27900016
*                                        AND UPDATING THE DSCB.         28200016
         ST    GR10,DCBIOBAD           FUNCTION BLOCK ADDRESS IN DCB    28300016
         EJECT                                                     O122 28310019
*********************************************************************** 28320016
*                                                                     * 28340016
*  THIS ROUTINE READS THE VOLUME LABEL, ENTERS THE NEW VOLUME SERIAL  * 28360016
*     NUMBER, AND OWNER ID IF ANY, AND WRITES OUT THE NEW LABEL ONTO  * 28380016
*     THE VOLUME.                                                       28400016
*                                                                     * 28420016
*********************************************************************** 28440016
         DROP  9                                                        28480016
READ     EQU   *                                                   O122 28500019
         LA    GR5,RDAREA              SET UP READ/WRITE AREA           28520016
         ST    GR5,RDWRTCCW                IN CHANNEL PROGRAM           28540016
         MVI   RDWRTCCW,6              SRT OP CODE FOR READ             28560016
         MVI   RDWRTCCW+4,X'20'                                         28570016
         EXCP  RDIOB                   READ VOLUME LABLE                28580016
         BAL   GR4,WAITD               AWAIT COMPLETION OF I/O          28600016
         MVI   POSTSW,X'00'            INITIALIZE SWITCH                28606016
         CLI   DDNAME2,X'40'           OFFLINE LABEL REQUEST.      O122 28608019
         BE    NOSET                   YES-CONTINUE PROCESSING.    O122 28610019
         CLC   RDAREA+4(6),SERNO       IS NEW LABEL SAME AS OLD         28612016
         BNE   NOSET                   NO-UCB CAN BE POSTED             28618016
         MVI   POSTSW,X'01'            YES-SET SWITCH TO PREVENT POST   28624016
NOSET    MVC   RDAREA+4(6),SERNO       MOVE IN NEW SERIAL NUMBER        28630016
         TM    SEQSW,OWNERID           IS OWNER ID SPECIFIED            28640016
         BZ    WRITEOUT                NO - GO WRITE VOLUME LABEL       28660016
         MVC   RDAREA+41(10),OWNID     MOVE IN NEW OWNERID              28680016
WRITEOUT MVI   RDWRTCCW,5              SET OP CODE FOR WRITE            28700016
         MVI   RDWRTCCW+4,X'60'                                         28710016
         EXCP  RDIOB                   WRITE VOLUME LABEL               28720016
         BAL   GR4,WAITD               AWAIT COMPLETION OF I/O          28740016
         CLI   POSTSW,X'01'            SHOULD NEW SERIAL NO GO IN UCB   28743016
         BE    FINISH                  NO-GO RETURN CONTROL             28746016
         CLI   DDNAME2,X'40'           OFFLINE LABEL REQUEST.      O122 28746219
         BNE   POSTA                   NO-CONTINUE.                O122 28746419
         LA    GR1,RDAREA              AREA FOR LIST.              O122 28746619
         LA    GR2,SERNO               PTR. TO NEW SERIAL NUMBER.  O122 28746819
         MVC   0(4,GR1),TUCBPTR        UCB PTR. TO LIST.           O122 28747019
         MVI   0(GR1),X'88'            SET SPECIAL POST INDICATOR. O122 28747219
         ST    GR2,4(GR1)              PTR. TO NEW SERIAL NO.      O122 28747419
         SR    GR2,GR2                 CLEAR REGISTER.             O122 28747619
         ST    GR2,8(GR1)              ZERO VTOR PTR. IN LIST.     O122 28747819
         MVC   12(4,GR1),DCBDEBAD      DEB PTR. TO LIST.           O122 28748019
         MVI   12(GR1),X'80'           SET LAST ENTRY INDICATOR.   O122 28748219
         B     POSTB                   GO POST THE UCB.            O122 28748419
POSTA    EQU   *                                                   O122 28748619
         MVC   PARMLIST+1(3),TUCBPTR+1 PUT UCB POINTER IN LIST SVC      28749016
         LA    GR1,SERNO               PUT ADDRESS OF NEW SERIAL        28752016
         ST    GR1,PARMLIST+4              NUMBER IN LIST FOR SVC       28755016
         LA    GR1,PARMLIST            PUT ADDRESS OF LIST IN REGISTER  28758016
POSTB    EQU   *                                                   O122 28759019
         SVC   POST                                                     28761016
FINISH   LA    GR1,6                   SET UP SUCCESSFUL COMPLETION     28770016
*                                         MESSAGE                       28780016
         BAL   GR9,MSGBLD                                               28782016
         MVC   0(8,GR1),DDNAME2        PUT DDNAME IN MESSAGE            28784016
         MVC   40(6,GR1),SERNO         PUT SERIAL NO IN MESSAGE         28786016
         MVC   MESS+8(8),NAME          PUT MODULE NAME IN MESSAGE       28788016
         LA    GR9,GOODRTN                                              28790016
         B     MSGPRNT                 GO PRINT MESSAGE                 28792016
**********************************************************************  28800016
*                        WAIT           ROUTINE                       * 28900016
**********************************************************************  29000016
WAITD    TM    DISKECB,COMPLETE        IS THE OPERATION COMPLETE.       29100016
         BO    TESTD                   YES-GO TEST THE STATUS.          29400016
         WAIT  ECB=DISKECB             AWAIT COMPLETION OF DISK.        29700016
TESTD    CLI   DISKECB,GOOD            ALL OK.                          30000016
         BNE   IOERR                   NO--I/O ERROR.                   30300016
         MVI   DISKECB,0               CLEAR POSTED BITS IN THE ECB.    30600016
         BR    GR4                     RETURN.                          30900016
*********************************************************************** 30930016
*                                                                     * 30960016
*  THIS SECTION WRITES OUT MESSAGES, SETS RETURN CODES, AND RETURNS   * 30990016
*     CONTROL.                                                        * 31020016
*                                                                     * 31050016
*********************************************************************** 31080016
TERM     EQU   *                                                YL02912 31090002
         LA    GR1,MESS57               INDICATE LABEL DENIED.  YL02912 31100002
         BAL   GR9,MSGBLD               GO BUILD MESSAGE.       YL02912 31102002
         MVC   D0(L8,GR1),DDNAME2       PUT DDNAME IN MSG.      YL02912 31104002
         B     ERRPRNT                  PRT MSG AND TERMINATE.  YL02912 31106002
NOOPEN   LA    GR1,5                   BAD OPEN                         31110016
         B     ERRSET                                                   31140016
BADPARM  LA    GR1,10                  INCORRECT DEVICE                 31170016
ERRSET   BAL   GR9,MSGBLD              GO BUILD MESSAGE                 31200016
         MVC   0(8,GR1),DDNAME2        PUT DDNAME IN MESSAGE            31230016
         B     ERRPRNT                 GO PRINT MESSAGE                 31260016
BADDEV   LA    GR1,14                  IS SYSTEM RESIDENCE VOLUME       31290016
         BAL   GR9,MSGBLD              GO BUILD MESSAGE                 31320016
         MVC   0(8,GR1),DDNAME2        PUT DDNAME IN MESSAGE            31350016
         MVC   MESS+8(8),NAME          PUT MODULE NAME IN MESSAGE       31380016
         B     ERRPRNT                 GO PRINT MESSAGE                 31410016
ONLINERR EQU   *                                                   O122 31413019
         LA    GR1,24                  MESSAGE NUMBER.             O122 31416019
         BAL   GR9,MSGBLD              GO BUILD MESSAGE.           O122 31419019
         MVC   0(8,GR1),DDNAME2        DDNAME TO MESSAGE.          O122 31422019
         MVC   MESS+9(6),NAME          MOVE LABEL KEYWORD          O122 31425019
         MVC   MESS+15(1),MESS+14            INTO THE MESSAGE.     O122 31428019
         B     ERRPRNT                 PRINT MESSAGE AND TERMINATE.O122 31431019
* RACF AUTHORIZATION NOT HELD BY USER PRINT MSG AND TERMINATE  @G32DSPD 31431432
UNAUTH   LA    GR1,MESS66             SET FOR MSG IEH866I      @G32DSPD 31431832
         BAL   GR9,MSGBLD             GET MESSAGE TEXT         @G32DSPD 31432232
         MVC   0(L'LABELNM,GR1),LABELNM INDICATE LABEL FUNCT.  @G32DSPD 31432632
         MVC   20(L'DDNAME2,GR1),DDNAME2 INSERT DDNAME         @G32DSPD 31433032
         B     ERRPRNT                GO TERMINATE             @G32DSPD 31433432
         EJECT                                                     O122 31434019
IOERR    LA    GR1,13                  I/O ERROR                        31440016
         BAL   GR9,MSGBLD              GO BUILD MESSAGE                 31470016
         SYNADAF ACSMETH=EXCP,PARM1=RDIOB                               31500016
         MVC   MESS+22(78),49(GR1)     PUT I/O INFO IN BUFFER    SM4350 31530021
         SYNADRLS                                                       31560016
ERRPRNT  BAL   GR9,MSGPRNT             GO PRINT ERROR MESSAGE           31590016
SETCODE  LA    GR2,8                   SET UP RETURN CODE/ERROR  XM1377 31800001
ENDUP    EQU   *                                                   O122 31808019
         CLI   DDNAME2,X'40'           OFFLINE LABEL REQUEST.      O122 31816019
         AIF   ('&LIB' EQ 'LIB1').X227107                               31816502
         BNE   CKDCB                    NO, TEST DCB.            YM3475 31817002
         CLC   DCBDEBAD(4),BLANKS       WAS DEB REMOVED?         YM3475 31817502
         BE    AROUND                   YES, GO RETURN.          YM3475 31818002
         LA    GR1,RDAREA               AREA FOR LIST.           YM3475 31818502
         MVI   0(GR1),DELDEB            DELETE DEB REQUEST.      YM3475 31819002
         MVC   12(4,GR1),DCBDEBAD       DEB ADDR INTO LIST       YM3475 31819502
         MVI   12(GR1),PARMEND          SET LAST ENTRY IND.      YM3475 31820002
         SVC   POST                     GO DELETE DEB.           YM3475 31820502
         B     AROUND                   SKIP CLOSE               YM3475 31821002
CKDCB    EQU   *                                                 YM3475 31821502
         AGO   .X227108                                          YM3475 31822002
.X227107 ANOP                                                    YM3475 31822502
         BE    AROUND                  YES-NO FURTHER CHECKING.    O122 31824019
.X227108 ANOP                                                    YM3475 31828002
         TM    DCBOFLGS,X'10'          NO-WAS DATA SET OPENED.     O122 31832019
         BZ    AROUND                  NO- SKIP  CLOSE                  31840016
         CLOSE ((8))                   CLOSE DATA SET                   31880016
AROUND   EQU   *                                                 YM5579 31890001
         LR    GR15,GR2                PUT RETURN CODE BACK IN REG 15   31900016
         L     GR13,LABLSAVE+4         RESTORE REGISTER 13       YM5579 31920001
         RETURN (14,12),T,RC=(15)      RETURN WITH PROPER CODE          32000016
         EJECT                                                     O122 32100019
MSGBLD   LINK  EP=IEHDMSGB             GO CONSTRUCT MESSAGE             32200016
         BR    GR9                                                      32400016
MSGPRNT  LINK  EP=IEHDPRNT             GO PRINT MESSAGE                 32600016
         BR    GR9                                                      32800016
GOODRTN  LA    GR2,0                   SUCCESS COMPLETION CODE   YM5579 33000001
         L     GR3,PTRCFUNC            PICK UP POINTER TO CURRENT FUNC. 33300016
         MVI   0(GR3),COMPLETE         SET COMPLETE BIT FOR FUNCTION.   33400016
         B     ENDUP                                                    33500016
LIST     DS    0F            FOR READ IN JFCB                           33900016
         DC    X'87'                                                    34200016
         DC    AL3(0)                                                   34500016
OPENLD   RDJFCB (,(OUTPUT)),MF=L       PARAMETER LIST FOR OPEN          34560016
PARMLIST DC    X'08'                   PARAMETER LIST FOR SVC           34620016
         DC    AL3(0)                     TO POST UCB                   34680016
         DC    F'0'                                                     34740016
         DC    X'80'                                                    34800016
         DC    AL3(0)                                                   34860016
POSTSW   DC    X'00'                   SWITCH TO INDICATE POST UCB      34920016
POST     EQU   82                                                       34980016
RACVOL   DS    CL6                      VOLSER ENTITY          @G32DSPD 34980532
RC8      DC    AL1(8)                   RETURN CODE 8          @G32DSPD 34982032
         AIF   ('&LIB' EQ 'LIB1').X227106                        YM3475 35000002
DELDEB   EQU   X'F8'                    DEB DELETE REQUEST.      YM3475 35020002
PARMEND  EQU   X'80'                    END OF PARM LIST IND.    YM3475 35040002
BLANKS   DC    X'40404040'              OFFLINE DDNAME END.      YM3475 35060002
LABELNM  DC    C'LABEL'                 FUNCTION FOR IEH866I   @G32DSPD 35060532
.X227106 ANOP                                                    YM3475 35080002
D0       EQU   0                        DISPLACEMENT VALUE 0    YL02912 35090002
D4       EQU   4                        DISPLACEMENT VALUE 4    YL02912 35092002
D8       EQU   8                        DISPLACEMENT VALUE 8    YL02912 35094002
D12      EQU   12                       DISPLACEMENT VALUE 12   YL02912 35096002
MESS66   EQU   66                       IEH866I OFFSET         @G32DSPD 35096532
L6       EQU   6                        LENGTH VALUE OF 6.      YL02912 35097232
L8       EQU   8                        LENGTH VALUE OF 8.      YL02912 35097932
ZERO     EQU   0                        IMMEDIATE CONSTANT.     YL02912 35098802
MESS57   EQU   57                       MESSAGE IEH857I.        YL02912 35099202
WTORECB  DC    F'0'                     ECB FOR WTOR INST.      YL02912 35099602
LABLSAVE DS    18F                                                      35100016
NAME     DC    C'LABEL OF'             MODULE NAME CONSTANT             35150016
GOOD     EQU   X'7F'                   NO I/O ERRORS                    35200016
VOL      EQU   63                       DISPLACEMENT FOR VOLID. YL02912 35210002
JJJ      EQU   71                       DISPLACEMENT FOR JOB.   YL02912 35220002
SSS      EQU   80                       DISPLACEMENT FOR STEP.  YL02912 35230002
ERJJ     EQU   56                       DISPLACEMENT FOR JOB.   YL02912 35240002
ERSS     EQU   65                       DISPLACEMENT FOR STEP.  YL02912 35242002
         EJECT                                                     O122 35250019
CCWLIST  CCW   X'31',RDSEEK+3,X'60',5     SEARCH FOR RECORD 3           35300016
         CCW   8,CCWLIST,0,0              REPEAT UNTIL FOUND            35400016
RDWRTCCW CCW   0,0,X'20',80               READ/WRITE DATA               35500016
CCWLIST1 CCW   X'31',RDSEEK+3,X'60',5  SEARCH FOR RECORD3               35550016
         CCW   X'08',CCWLIST1,0,0      REPEAT UNTIL FOUND               35600016
RDBKCHK  CCW   X'06',0,X'30',80        READ CHECK                       35650002
*      LABL DA WRITE-OUT ECB.                                           35700016
         DS    0D                      NECESSARY ALIGNMENT.             36000016
DISKECB  DC    F'0'                    WAIT//COMPLETE BITS              36300016
*      LABL DA WRITE-OUT IOB.                                           36600016
RDIOB    DS    0F                                                       36900016
RDFLAGS  DC    XL2'2000'               FLAG1 AND FLAG2                  37200016
RDSENSE  DC    H'0'                    SENSE BYTES.                     37500016
RDECBAD  DC    A(DISKECB)              ECB ADDRESS                      37800016
RDSTATUS DC    2F'0'                   CHANNEL STATUS.                  38100016
RDCCWAD  DC    A(CCWLIST)              CCW ADDRESS.                     38400016
RDDCBAD  DC    A(RDDCB)                DCB ADDRESS.                     38700016
RDRESAD  DC    F'0'                    RESTART ADDRESS.                 39000016
RDBLKCT  DC    H'1'                    BLOCK COUNT INCREMENT.           39300016
RDERROR  DC    H'0'                    ERROR COUNT.                     39600016
RDSEEK   DC    1F'0'                   MBCCHHR.                         39800016
         DC    X'00000003'                                              40000016
         EJECT                                                     O122 40100019
*      LABL DA WRITE-OUT DCB.                                           40200016
         AIF   ('&LIB' EQ 'LIB1').OSDCB                                 40300000
RDDCB    DCB   DSORG=PS,MACRF=(E),DEVD=DA,EXLST=LIST,EOEA=P8,    M5431 F41200000
               SIOA=P7,PGFX=YES                                   M5431 41250000
         AGO   .C1                                                      41300000
.OSDCB   ANOP                                                           41350000
RDDCB    DCB   DSORG=PS,MACRF=(E),DEVD=DA,EXLST=LIST,EOEA=P8            41400000
.C1      ANOP                                                           41450000
REPLY    DS    CL1                      AREA FOR WTOR REPLY.    YL02912 41500002
         EJECT                                                          42200016
         IEHDWORK                                                       43500016
         EJECT                                                          48500016
         IEHDBLKS                                                       53500016
VLABEL   DSECT                                                          72300016
VOLUME   DS    CL3                     LABEL IDENTIFIER.                72600016
VOLNO    DS    CL1                     LABEL NUMBER.                    72900016
SERIAL   DS    CL6                     LABEL SERIAL NUMBER.             73200016
SECURITY DS    CL1                     VOLUME SECURITY.                 73500016
VTOCPTR  DS    CL10                    POINTER TO VTOC                  73800016
         DS    CL20                    RESERVED.                        74100016
OWNER    DS    CL10                    OWNER IDENTIFICATION.            74400016
         DS    CL29                    BLANK.                           74700016
         EJECT                                                          75300016
         ICHPRCVT                                              @ZA25515 75310099
         EJECT                                                          75320099
CVT      DSECT                                                          75350002
         CVT                                                            75400032
         EJECT                                                          75450002
UCB      DSECT                                                          75900016
         IEFUCBOB                                                       76200016
         EJECT                                                          76300016
JFCB     DSECT                                                          76500016
         IEFJFCBN                                                       76800016
         EJECT                                                          76900016
         DCBD  DSORG=PS                                                 77100016
         END                                                            77400016
