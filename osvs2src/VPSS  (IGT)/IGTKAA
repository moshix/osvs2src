TKAA     TITLE 'IGTKAA -- VPSS SVC-111 ROUTER'                          00400000
IGTKAA   CSECT ,                                                        00800000
*/* * START OF SPECIFICATIONS ************************************** */ 01200000
*/*                                                                  */ 01600000
*/*   MODULE-NAME = IGTKAA                                           */ 02000000
*/*                                                                  */ 02400000
*/*   DESCRIPTIVE-NAME = VPSS SVC-111 ROUTER                         */ 02800000
*/*                                                                  */ 03200000
*/*   COPYRIGHT = NONE                                               */ 03600000
*/*                                                                  */ 04000000
*/*   STATUS = RELEASE 1, MODIFICATION LEVEL 0.                      */ 04400000
*/*                                                                  */ 04800000
*/*   FUNCTION = TO PASS CONTROL TO THE APPROPRIATE VPSS SVC         */ 05200000
*/*       ROUTINE BASED UPON THE FUNCTION CODE SPECIFIED IN          */ 05600000
*/*       GENERAL REGISTER 0.                                        */ 06000000
*/*                                                                  */ 06400000
*/*   NOTES = THIS ROUTINE RUNS AS A TYPE-2 SVC.  CONTROL IS         */ 06800000
*/*       GIVEN TO THIS ROUTINE BY SVC 111.                          */ 07200000
*/*                                                                  */ 07600000
*/*     DEPENDENCIES = THE ROUTINE MUST BE POINTED TO BY THE         */ 08000000
*/*                    END-OF-BLOCK ROUTINE POINTER IN THE           */ 08400000
*/*                    ASSOCIATED VPSS SUBSYSTEM DEB.  SVC-111       */ 08800000
*/*                    LOADS THIS ADDRESS (AFTER VERIFYING THE DEB)  */ 09200000
*/*                    AND BRANCHS TO THE ROUTINE.                   */ 09600000
*/*                                                                  */ 10000000
*/*     RESTRICTIONS = THIS ROUTINE MUST OBEY ALL THE STANDARD       */ 10400000
*/*         PROCESSING RULES FOR A TYPE-2 SVC.                       */ 10800000
*/*                                                                  */ 11200000
*/*     REGISTER-CONVENTIONS = NONE                                  */ 11600000
*/*                                                                  */ 12000000
*/*     PATCH-LABEL = APPATCH                                        */ 12400000
*/*                                                                  */ 12800000
*/*   MODULE-TYPE = CSECT                                            */ 13200000
*/*                                                                  */ 13600000
*/*     PROCESSOR = VS/2 ASSEMBLER                                   */ 14000000
*/*                                                                  */ 14400000
*/*     MODULE SIZE = SEE END OF SOURCE LISTING                      */ 14800000
*/*                                                                  */ 15200000
*/*     ATTRIBUTES = RE-ENTRANT                                      */ 15600000
*/*                                                                  */ 16000000
*/*   ENTRY-POINT = IGTKAA                                           */ 16400000
*/*                                                                  */ 16800000
*/*     PURPOSE = ROUTE VPSS SVC-111 REQUEST TO THE PROPER           */ 17200000
*/*               PROCESSING ROUTINE.                                */ 17600000
*/*                                                                  */ 18000000
*/*     LINKAGE = BRANCH FROM SVC-111, WHICH WAS ENTERED VIA AN      */ 18400000
*/*               SVC REQUEST.                                       */ 18800000
*/*                                                                  */ 19200000
*/*     INPUT = THE FOLLOWING REGISTERS ARE INITIALIZED ON ENTRY --  */ 19600000
*/*         R0  - FUNCTION CODE (SET BY USER)                        */ 20000000
*/*         R1  - APRL ADDRESS OR DEB ADDRESS (COMPLEMENTED)         */ 20400000
*/*               (SET BY USER)                                      */ 20800000
*/*         R2  - ADDRESS OF DEB                                     */ 21200000
*/*         R3  - ADDRESS OF CVT                                     */ 21600000
*/*         R4  - ADDRESS OF CURRENT TCB                             */ 22000000
*/*         R7  - ADDRESS OF CURRENT ASCB                            */ 22400000
*/*         R14 - RETURN ADDRESS (ADDRESS OF AN SVC 3)               */ 22800000
*/*         R15 - ADDRESS OF THIS ROUTINE                            */ 23200000
*/*         REGISTER  0 - SRB ADDRESS (COMPLEMENTED IF POST STATUS)  */ 23600000
*/*         REGISTER  1 - IOSB ADDRESS                               */ 24000000
*/*         REGISTER 14 - RETURN ADDRESS                             */ 24400000
*/*         REGISTER 15 - ENTRY POINT                                */ 24800000
*/*         ROUTINE IS ENTERED IN KEY-0, SUPERVISOR STATE,           */ 25200000
*/*              UNLOCKED, TASK MODE.                                */ 25600000
*/*                                                                  */ 26000000
*/*     OUTPUT = THE FOLLOWING REGISTERS ARE PASSED TO THE           */ 26400000
*/*         SPECIFIED VPSS SVC PROCESSING ROUTINE --                 */ 26800000
*/*         R0  - FUNCTION CODE (SET BY USER)                        */ 27200000
*/*         R1  - APRL ADDRESS OR DEB ADDRESS (COMPLEMENTED)         */ 27600000
*/*               (SET BY USER)                                      */ 28000000
*/*         R2  - ADDRESS OF DEB                                     */ 28400000
*/*         R3  - ADDRESS OF CVT                                     */ 28800000
*/*         R4  - ADDRESS OF CURRENT TCB                             */ 29200000
*/*         R7  - ADDRESS OF CURRENT ASCB                            */ 29600000
*/*         R11 - ADDRESS OF APDX ASSOCIATED WITH THIS DEB           */ 30000000
*/*         R15 - ADDRESS OF THE PROCESSING ROUTINE                  */ 30400000
*/*         ROUTINE IS ENTERED IN KEY-0, SUPERVISOR STATE,           */ 30800000
*/*              UNLOCKED, TASK MODE.                                */ 31200000
*/*         NOTE THAT REGISTER 14 IS NOT VALID.                      */ 31600000
*/*                                                                  */ 32000000
*/*   EXIT-NORMAL = EXIT IS VIA A BRANCH TO ONE OF THE FOLLOWING     */ 32400000
*/*                 ROUTINES (BASED ON REGISTER 0 FUNCTION CODE)--   */ 32800000
*/*                 REG-0   MODULE   FUNCTION                        */ 33200000
*/*                   0     IGTKBA   ISSUE VPSS STARTIO REQUEST      */ 33600000
*/*                   1     IGTKCA   PROCESS 'XLTE' CALL (TRANSLATE) */ 34000000
*/*                   2     IGTKDA   PROCESS 'RLSE' CALL (RELEASE)   */ 34400000
*/*                                                                  */ 34800000
*/*   EXIT-ERROR = CONTROL RETURNS TO THE USER VIA SVC-3 IF          */ 35200000
*/*                THE SPECIFIED FUNCTION CODE IS INVALID.           */ 35600000
*/*                APRLFDBK IS SET TO REFLECT THE ERROR.             */ 36000000
*/*                REGISTER 15 IS SET NON-ZERO.                      */ 36400000
*/*                                                                  */ 36800000
*/*   EXTERNAL-REFERENCES = THE FOLLOWING SYSTEM ROUTINES AND        */ 37200000
*/*                         CONTROL BLOCKS ARE USED.                 */ 37600000
*/*                                                                  */ 38000000
*/*     ROUTINES = NONE                                              */ 38400000
*/*                                                                  */ 38800000
*/*     DATA-AREAS = NONE                                            */ 39200000
*/*                                                                  */ 39600000
*/*     CONTROL-BLOCKS =                                             */ 40000000
*/*         DEB      - R                                             */ 40400000
*/*         APRL     - R, M                                          */ 40800000
*/*         APCT     - R                                             */ 41200000
*/*         APDX     - R                                             */ 41600000
*/*                                                                  */ 42000000
*/*   TABLES = NONE                                                  */ 42400000
*/*                                                                  */ 42800000
*/*   MACROS =                                                       */ 43200000
*/*       MODESET (IN-LINE EXPANSION)                                */ 43600000
*/*       IGTXEPLG                                                   */ 44000000
*/*                                                                  */ 44400000
*/*   CHANGE-ACTIVITY = NONE                                         */ 44800000
*/*                                                                  */ 45200000
*/*   MESSAGES = NONE                                                */ 45600000
*/*                                                                  */ 46000000
*/*   ABEND-CODES = NONE                                             */ 46400000
*/*                                                                  */ 46800000
*/* * END OF SPECIFICATIONS **************************************** */ 47200000
         EJECT ,                                                        47600000
         COPY  IGTXSYSO            SCP DEFINITION                       48000000
         SPACE 2                                                        48400000
         USING IGTKAA,APENTRY      BASE REGISTER                        48800000
         B     STARTUP             SKIP IN-CORE ID                      49200000
         DC    YL1(COREIDE-*-1)    * ID LENGTH                          49600000
         DC    C'IGTKAA'           * PROGRAM NAME                       50000000
         DC    C' &SYSDATE'        * COMPILE DATE                       50400000
COREIDE  EQU   *                   END OF ID                            50800000
         SPACE 2                                                        51200000
STARTUP  DS    0H                  START OF PROCESSING CODE             51600000
         SPACE ,                                                        52000000
*                                                                       52400000
** ESTABLISH CONTROL BLOCK POINTERS TO                                  52800000
** APRL, APDX, APCT, DEB                                                53200000
*                                                                       53600000
         USING APRL,R1             APRL PTR SET ON INPUT                54000000
         USING DEBBASIC,R2         DEB PTR SET ON INPUT                 54400000
         L     APDXPTR,DEBIRBAD    VPSS DEB EXTENSION                   54800000
         USING APDX,APDXPTR        * TELL ASSEMBLER                     55200000
         L     APCTPTR,APDXAPCT    VPSS CONTROL TABLE                   55600000
         USING APCT,APCTPTR        * TELL ASSEMBLER                     56000000
*                                                                       56400000
** LOCATE PROPER PROCESSING ROUTINE BASED UPON FUNCTION                 56800000
** CODE SPECIFIED IN REGISTER-0. ALLOWABLE VALUES ARE                   57200000
** 0-EXECUTE, 1-TRANSLATE, 2-RELEASE.                                   57600000
*                                                                       58000000
         N     R0,CLEARBIT         CLEAR UNUSED BITS                    58400000
*                                  * R0= 0-3 NOW                        58800000
         AR    APENTRY,R0          CHANGE BASE REGISTER BY              59200000
         AR    APENTRY,R0          * 2*FUNC_CODE                        59600000
*                                                                       60000000
** BASE REGISTER CAN ONLY BE USED TO ACCESS DISPTAB NOW SINCE           60400000
** APENTRY VALUE DEPENDS ON FUNCTION CODE (REGISTER 0). ALTHOUGH        60800000
** THE NEXT INSTRUCTION APPEARS TO ONLY REFERENCE THE 1ST               61200000
** ENTRY IN DISPTAB, IT ACTUALLY LOADS THE PROPER FUNCTION              61600000
** ENTRY SINCE THE BASE REGISTER HAS BEEN CHANGED.                      62000000
*                                                                       62400000
         LH    APENTRY,DISPTAB     LOAD OFFSET INTO APCT OF             62800000
*                                  * ADCON POINTING TO THE              63200000
*                                  * PROPER PROCESSING ROUTINE.         63600000
         DROP  APENTRY             NO BASE REGISTER EXISTS              64000000
         L     APENTRY,APCT(APENTRY) ADDRESS PROCESSING ROUTINE         64400000
*                                  * (POINTS TO APCTBR14 IF             64800000
*                                  * INVALID FUNCTION CODE).            65200000
         BALR  APRETURN,APENTRY    GO TO PROCESSING ROUTINE             65600000
*                                                                       66000000
** CONTROL RETURNS ONLY IF INVALID FUNCTION CODE. VALID                 66400000
** ROUTINES EXIT VIA AN SVC-3. INVALID FUNCTION CODES                   66800000
** CAUSE CONTROL TO BE PASSED TO 'APCTBR14' WHICH RETURNS               67200000
** IMMEDIATELY.                                                         67600000
*                                                                       68000000
         EJECT ,                                                        68400000
*                                                                       68800000
** INVALID FUNCTION CODE PASSED IN REGISTER-0. FLAG THE                 69200000
** APRL AND EXIT TO THE CALLER VIA AN SVC 3.                            69600000
*                                                                       70000000
         BALR  APCODE,*-*          ESTABLISH BASE REGISTER              70400000
         USING *,APCODE            * TELL ASSEMBLER                     70800000
         AIF   ('&VPSSSYS' NE 'MVS').VS1B MVS EXPANSION ?               71200000
         MODESET KEYADDR=DEBPROTG, SWITCH TO USER KEY                  *71600000
               WORKREG=13          * TO MODIFY APRL.                    72000000
         AGO   .SYSB               SKIP VS1 EXPANSION                   72400000
.VS1B    ANOP  ,                   VS1 EXPANSION                        72800000
         MODESET KEY=NZERO         SWITCH TO USER KEY                  *73200000
                                   TO MODIFY APRL.                      73600000
.SYSB    ANOP  ,                   NOW IN USER PROTECT KEY              74000000
         TM    APRLOPT0,APRLNRC    IF RETURN CODES NOT WANTED,          74400000
         BO    NOPOST              * SKIP SETTING ERROR INDICATORS      74800000
         MVI   APRLRCT,APCCTLOG    LOGICAL ERROR                        75200000
         MVI   APRLRCC,APCCI       DETECTED BY SVC I/O PROCESSOR        75600000
         MVI   APRLRCN,APCCI001    ERROR CODE                           76000000
         LA    R0,APCMI001         ERROR MESSAGE NUMBER                 76400000
         STH   R0,APRLMSGN         * IN APRL.                           76800000
         LA    R0,APCMK000         NO KEYWORD SPECIFIED                 77200000
         STH   R0,APRLVALN         * SO ELIMINATE IT.                   77600000
         MVI   APRLECBC,APRLECBP   SHOW POSTED (=X'40)                  77650000
NOPOST   EQU   *                   APRL POSTING SKIPED                  78000000
         LA    APRCODE,APCCRERR    REG 15 RETURN CODE (ERROR)           78400000
         SVC   3                   EXIT TO CALLER                       78800000
         SPACE 2                                                        79200000
         DROP  ,                   ELIMINATE BASE REGISTERS             79600000
         EJECT ,                                                        80000000
*********************************************************************** 80400000
**                                                                   ** 80800000
**       CONSTANTS                                                   ** 81200000
**                                                                   ** 81600000
*********************************************************************** 82000000
         SPACE ,                                                        82400000
CLEARBIT DC    A(X'00000003')      ONLY LEAVE BITS 30-31                82800000
         SPACE 3                                                        83200000
*                                                                       83600000
** THIS TABLE CONTAINS THE OFFSETS WITHIN THE APCT OF                   84000000
** AN ADCON FOR THE ROUTINE TO PROCESS THE FUNCTION                     84400000
** SPECIFIED IN REGISTER-0. ENTRY 0 CORRESPONDS TO                      84800000
** FUNCTION CODE OF 0, ETC. INVALID FUNCTION CODES MAP                  85200000
** TO PASSING CONTROL TO 'APCTBR14' WITH CAUSES AN                      85600000
** IMMEDIATE RETURN TO THIS PROGRAM FOR ERROR PROCESSING.               86000000
*                                                                       86400000
         USING APCT,0              S-CON BASE REGISTER = 0              86800000
DISPTAB  DS    00S                 REG0 FUNCTION                        87200000
         DC    01S(APCTZKBA)       00 - STARTIO                         87600000
         DC    01S(APCTZKCA)       01 - TRANSLATE                       88000000
         DC    01S(APCTZKDA)       02 - RELEASE                         88400000
         DC    01S(APCTABR)        03 - INVALID FUNCTION                88800000
         EJECT ,                                                        89200000
*********************************************************************** 89600000
**                                                                   ** 90000000
**       DSECTS                                                      ** 90400000
*********************************************************************** 90800000
         SPACE ,                                                        91200000
         IGTXDSCT IGTXAPRL         VPSS ACB REQUEST                     91600000
         IGTXDSCT IGTXAPCC         VPSS CALL RETURN CODES               92000000
         IGTXDSCT IGTXAPCM         CALL ERROR MESSAGE VALUES            92400000
         IGTXDSCT IGTXAPCT         VPSS CONTROL TABLE                   92800000
         IGTXDSCT IGTXAPDX         VPSS DEB EXTENSION                   93200000
         EJECT ,                                                        93600000
         IEZDEB ,                  DATA EXTENT BLOCK                    94000000
         EJECT ,                                                        94400000
IGTKAA   CSECT ,                   RESUME CSECT CODE                    94800000
         IGTXEPLG ,                VPSS DSECTS & REGISTERS              95200000
TCBPTR   EQU   R4                  POINTER TO CURRENT TCB               95600000
         SPACE 5                                                        96000000
         END   IGTKAA              MODULE END                           96400000
