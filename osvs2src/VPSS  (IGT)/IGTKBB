TKBB     TITLE 'IGTKBB - VPSS I/O DRIVER DIE ROUTINE'                   00200000
*/* * START OF SPECIFICATIONS ************************************** */ 00400000
*/*                                                                  */ 00600000
*/*   MODULE-NAME = IGTKBB                                           */ 00800000
*/*                                                                  */ 01000000
*/*   DESCRIPTIVE-NAME = DISABLED INTERRUPT EXIT FOR VPSS            */ 01200000
*/*                      I/O DRIVER                                  */ 01400000
*/*                                                                  */ 01600000
*/*   COPYRIGHT = NONE                                               */ 01800000
*/*                                                                  */ 02000000
*/*   STATUS = RELEASE 1, MODIFICATION LEVEL 0.                      */ 02200000
*/*                                                                  */ 02400000
*/*   FUNCTION = PROCESS THE COMPLETED I/O REQUESTS.  IF THE         */ 02600000
*/*       I/O EVENT COMPLETED SUCCESSFULLY, RETURN TO IOS+8          */ 02800000
*/*       INDICATING NO FURTHER IOS PROCESSING IS NECESSARY.         */ 03000000
*/*       IF THERE IS AN I/O ERROR, RETURN TO IOS+0 WHO WILL         */ 03200000
*/*       SCHEDULE POST STATUS AND THE ERP TO ATTEMPT TO RESOLVE     */ 03400000
*/*       THE I/O ERROR. IF COMPLETION IS NORMAL (IOS+8), IOS        */ 03600000
*/*       WILL RETURN TO THE SECOND ENTRY OF THE DIE, WHO WILL       */ 03800000
*/*       SCHEDULE NOTIFICATION OF THE USER.                         */ 04000000
*/*                                                                  */ 04200000
*/*   NOTES = THE GENERAL INTERFACE SPECIFICATIONS ARE DETERMINED    */ 04400000
*/*       BY VS/2 (MVS) IOS INTERFACES AS DOCUMENTED IN THE          */ 04600000
*/*       IOS DESIGN NOTEBOOK.                                       */ 04800000
*/*                                                                  */ 05000000
*/*     DEPENDENCIES = NONE                                          */ 05200000
*/*                                                                  */ 05400000
*/*     RESTRICTIONS = THIS ROUTINE IS ENTERED PHYSICALLY DISABLED   */ 05600000
*/*         AND MUST NOT ENABLE SINCE IT EXECUTES AS AN EXTENSION    */ 05800000
*/*         OF IOS.  THEREFORE THIS MODULE MUST BE FIXED IN REAL     */ 06000000
*/*         STORAGE.  MODULE IGTUJA WILL INSURE THIS MODULE IS       */ 06200000
*/*         IN STORAGE.  NO ADDRESSABILITY EXISTS TO THE ADDRESS     */ 06400000
*/*         SPACE ASSOCIATED WITH THE I/O REQUEST.                   */ 06600000
*/*                                                                  */ 06800000
*/*     REGISTER-CONVENTIONS = NONE                                  */ 07000000
*/*                                                                  */ 07200000
*/*     PATCH-LABEL = APPATCH.                                       */ 07400000
*/*                                                                  */ 07600000
*/*   MODULE-TYPE = CSECT.                                           */ 07800000
*/*                                                                  */ 08000000
*/*     PROCESSOR = VS ASSEMBLER.                                    */ 08200000
*/*                                                                  */ 08400000
*/*     MODULE SIZE = SEE END OF SOURCE LISTING.                     */ 08600000
*/*                                                                  */ 08800000
*/*     ATTRIBUTES = RE-ENTRANT.                                     */ 09000000
*/*                                                                  */ 09200000
*/*   ENTRY-POINT = IGTKBB.                                          */ 09400000
*/*                                                                  */ 09600000
*/*     PURPOSE = PROCESS THE VPSS COMPLETED I/O REQUESTS.  THIS     */ 09800000
*/*         DISABLED INTERRUPT EXIT SERVES AS A NORMAL AND           */ 10000000
*/*         ABNORMAL I/O APPENDAGE.                                  */ 10200000
*/*                                                                  */ 10400000
*/*     LINKAGE = BALR FROM IOS.                                     */ 10600000
*/*                                                                  */ 10800000
*/*     INPUT =                                                      */ 11000000
*/*         REGISTER  2 - IOSB ADDRESS                               */ 11200000
*/*         REGISTER  7 - UCB ADDRESS                                */ 11400000
*/*         REGISTER 13 - ADDRESS OF 16 WORD SAVE AREA               */ 11600000
*/*         REGISTER 14 - RETURN ADDRESS                             */ 11800000
*/*         REGISTER 15 - ENTRY POINT ADDRESS (IGTKBB)               */ 12000000
*/*         PSW IS KEY-0, SUPERVISOR STATE, DISABLED. LOCK           */ 12200000
*/*             FOR THE UCB ASSOCIATED WITH THE I/O IS HELD.         */ 12400000
*/*             SINCE THIS PROGRAM RUNS AS AN EXTENSION TO IOS,      */ 12600000
*/*             NO ADDRESSIBILITY TO THE ASSOCIATED ADDRESS          */ 12800000
*/*             SPACE EXISTS.                                        */ 13000000
*/*                                                                  */ 13200000
*/*     OUTPUT =                                                     */ 13400000
*/*         REGISTER  1 - ADDRESS OF NEW SRB TO PLACE ON SOME LCH    */ 13600000
*/*                       FOR RETURN TO IOS+4 (REDRIVE). OTHERWISE   */ 13800000
*/*                       THIS REGISTER IS NOT USED.                 */ 14000000
*/*         REGISTER 0, 2-15 - MUST BE THE SAME AS WHEN THIS         */ 14200000
*/*                            ROUTINE WAS ENTERED.                  */ 14400000
*/*         PSW MUST BE THE SAME AS ON INPUT. THE UCB LOCK MUST      */ 14600000
*/*             STILL BE HELD.                                       */ 14800000
*/*                                                                  */ 15000000
*/*   EXIT-NORMAL = RETURN TO IOS AT ADDRESS GIVEN IN REGISTER 14    */ 15200000
*/*                 PLUS OFFSET OF EITHER 0, 4 OR 8.                 */ 15400000
*/*       R14+0  - PURSUE NORMAL I/O PROCESSING OF SRB/IOSB          */ 15600000
*/*                (SCHEDULE POST STATUS).                           */ 15800000
*/*       R14+4  - PURSUE NORMAL I/O PROCESSING OF SRB/IOSB,         */ 16000000
*/*                BUT REGISTER 1 CONTAINS THE ADDRESS OF ANOTHER    */ 16200000
*/*                SRB/IOSB TO BE ENQUEUED ON THE APPROPRIATE        */ 16400000
*/*                LOGICAL CHANNEL QUEUE.  IT MAY SPECIFY ANOTHER    */ 16600000
*/*                UCB ADDRESS. IF THE DEVICE CONTROL UNIT AND       */ 16800000
*/*                CHANNEL ARE AVAILABLE, THE REQUEST WILL BE        */ 17000000
*/*                STARTED BEFORE EXITING BASIC IOS.  THE FOLLOWING  */ 17200000
*/*                FIELDS ARE RESET TO ZERO BY IOS PRIOR TO          */ 17400000
*/*                ENQUEUING AND MUST NOT BE RESET BY THE DIE:       */ 17600000
*/*                    IOSEX, IOSCC, IOSCSW, IOSPCHN                 */ 17800000
*/*       R14+8  - IMPLIES IGNORE FURTHER PROCESSING OF THIS         */ 18000000
*/*                SRB/IOSB. THAT IS, POST STATUS WILL NOT BE        */ 18200000
*/*                SCHEDULED, NO FURTHER EXIT PROCESSING WILL        */ 18400000
*/*                BE PERFORMED, AND ERROR RECOVERY WILL NOT BE      */ 18600000
*/*                ATTEMPTED.  IN THIS INSTANCE, BOTH IOS AND        */ 18800000
*/*                THE DIE ROUTINE HAVE RESOURCES TO FREE.           */ 19000000
*/*                THEREFORE TWO ENTRIES TO THE DIE ROUTINE ARE      */ 19200000
*/*                DEFINED.  FIRST, THE DIE ROUTINE IS ENTERED       */ 19400000
*/*                FOR NORMAL PROCESSING AND IT RETURNS TO OFFSET    */ 19600000
*/*                8 INDICATING TO IOS TO DISPOSE OF ANY RESOURCES   */ 19800000
*/*                ALLOCATED TO THE SRB/IOSB (ERP WORKAREA, ETC.).   */ 20000000
*/*                AFTER IOS FREES ITS RESOURCES, THE DIE ROUTINE    */ 20200000
*/*                IS ENTERED AGAIN FOR DISPOSITION OF THE SRB/IOSB. */ 20400000
*/*                THE DISPOSITION CAN BE OF ANY FORM, E.G. --       */ 20600000
*/*                RETURNING IT TO A POOL, SCHEDULING THE SRB,       */ 20800000
*/*                OR RETURNING IT TO IOS AS A NEW REQUEST.  THE     */ 21000000
*/*                STANDARD RETURN TO IOS FROM THE SECOND EXIT       */ 21200000
*/*                IS TO REGISTER 14+0 IMPLYING CONTINUE WITHOUT     */ 21400000
*/*                THE USUAL POST STATUS SCHEDULING.  HOWEVER,       */ 21600000
*/*                REGISTER 14+4 MAY ALSO BE TAKEN PASSING TO IOS    */ 21800000
*/*                AN SRB/IOSB (COULD BE THE ONE JUST DISPOSED OF)   */ 22000000
*/*                WHICH IS TO BE INTRODUCED AS A NEW I/O REQUEST.   */ 22200000
*/*                   SINCE THE DIE IS AN EXTENSION OF IOS, CARE     */ 22400000
*/*                MUST BE TAKEN TO INSURE THAT THE IGNORED          */ 22600000
*/*                REQUEST HAS BEEN ACCOUNTED FOR IF INVOLVED IN     */ 22800000
*/*                A PURGE QUIESCE OPERATION.  IF THE IOSB           */ 23000000
*/*                INDICATES THAT A PURGE QUIESCE WAS ACTIVE FOR     */ 23200000
*/*                THE REQUEST, THE QUIESCE COUNT IN THE IOS PURGE   */ 23400000
*/*                INTERFACE BLOCK MUST BE DECREMENTED.              */ 23600000
*/*                   THE IOSB INDICATOR (IOSDIESE) IS USED TO       */ 23800000
*/*                DETERMINE IF THIS IS THE FIRST OR SECOND TIME     */ 24000000
*/*                THAT THE DIE HAS BEEN ENTERED FOR THIS INTERRUPT. */ 24200000
*/*                                                                  */ 24400000
*/*   EXIT-ERROR = NONE                                              */ 24600000
*/*                                                                  */ 24800000
*/*   EXTERNAL-REFERENCES =                                          */ 25000000
*/*                                                                  */ 25200000
*/*     ROUTINES = NONE                                              */ 25400000
*/*                                                                  */ 25600000
*/*     DATA-AREAS = NONE                                            */ 25800000
*/*                                                                  */ 26000000
*/*     CONTROL-BLOCKS =                                             */ 26200000
*/*         PSA      - R                                             */ 26400000
*/*         CVT      - R                                             */ 26600000
*/*         LSMQ     - M (VIA SCHEDULE MACRO)                        */ 26800000
*/*         SRB      - M                                             */ 27000000
*/*         IOSB     - R,M                                           */ 27200000
*/*         UCB      -                                               */ 27400000
*/*         APIO     - R,M                                           */ 27600000
*/*                                                                  */ 27800000
*/*   TABLES = NONE                                                  */ 28000000
*/*                                                                  */ 28200000
*/*   MACROS =                                                       */ 28400000
*/*       SCHEDULE                                                   */ 28600000
*/*       IGTXEPLG                                                   */ 28800000
*/*                                                                  */ 29000000
*/*   CHANGE-ACTIVITY = NONE                                         */ 29200000
*/*                                                                  */ 29400000
*/*   MESSAGES = NONE                                                */ 29600000
*/*                                                                  */ 29800000
*/*   ABEND-CODES = NONE                                             */ 30000000
*/*                                                                  */ 30200000
*/* * END OF SPECIFICATIONS **************************************** */ 30400000
         EJECT ,                                                        30600000
IGTKBB   CSECT ,                   VPSS DIE ROUTINE                     30800000
         USING *,R15               INPUT BASE                           31000000
         B     STARTUP             SKIP IN-CORE ID                      31200000
         DC    YL1(COREIDE-*-1)    * ID LENGTH                          31400000
         DC    C'IGTKBB'           * MODULE NAME                        31600000
         DC    C' &SYSDATE'        * ASSEMBLY DATE                      31800000
COREIDE  EQU   *                   * END OF ID                          32000000
         SPACE 3                                                        32200000
STARTUP  DS    0H                  START OF CODE                        38000000
*                                                                       38200000
** INPUT REGISTERS                                                      38400000
*                                                                       38600000
         USING IGTKBB,R15          BASE REGISTER                        38800000
         USING APIO,R2             APIO FOR CURRENT I/O                 39000000
*                                  * NOTE: IOSB IS START OF APIO        39200000
         USING APSVRG14,R13        SAVEAREA (16-WORDS ONLY)             39400000
         USING UCBOB,R7            UCB ASSOCIATED WITH THE I/O          39600000
         USING FLC,*-*             0-4095 NEEDS NO BASE                 39800000
         PUSH  USING               SAVE INPUT USING STATUS              40000000
         EJECT ,                                                        40200000
*********************************************************************** 40400000
**                                                                    * 40600000
** IF THIS IS SECOND ENTRY, GO TO CLEAN-UP PROCESSING.                * 40800000
** IF ERP IN CONTROL OR I/O COMPLETED IN ERROR,                       * 41000000
** RETURN TO IOS+0 TO SCHEDULE NORMAL POST STATUS                     * 41200000
** PROCESSING. IF I/O WAS SUCCESSFUL (ONLY CE AND                     * 41400000
** DE ON), RETURN TO IOS+8 TO BYPASS POST STATUS.                     * 41600000
** THIS WILL CAUSE SECOND ENTRY TO DIE WHICH WILL                     * 41800000
** SCHEDULE SRB TO POST THE CALLER. THIS IS THE                       * 42000000
** MAINLINE PATH OF VPSS I/O PROCESSING.                              * 42200000
** IF CHANNEL END AND DEVICE END SEPARATED, ATTEMPT                   * 42400000
** TO RECOMBINE, AND THEM PROCESS AS IF THEY OCCURRED                 * 42600000
** ON THE SAME INTERRUPT (OR THE CSWS TOGETHER).                      * 42800000
**                                                                    * 43000000
*********************************************************************** 43200000
         SPACE 3                                                        43400000
         TM    IOSFLB,IOSDIESE     SECOND ENTRY TO DIE ?                43600000
         BO    DIE2EXIT            * YES -- GO FREE RESOURCES           43800000
         STCK  2(R13)              SAVE BYTES 4-7 OF TOD CLOCK          44000000
         L     R1,4(,R13)          * USED BY IGTKBC TO                  44200000
         ST    R1,APIOTMEE         * CALCULATE I/O LOAD                 44400000
         TM    IOSFLA,IOSERR       IF ERP IN CONTROL, DO NOT            44600000
         BOR   R14                 * PROCESS (NON-MAINLINE)             44800000
XRETRY1  EQU   *                   RETRY COMBINED CE/DE STATUS          45000000
         LH    R1,CDECE            SEE IF CHANNEL & DEVICE END          45200000
         CH    R1,IOSTATUS         * WITH ALL OTHER INDICATORS OFF      45400000
         BE    8(,R14)             * (GOOD I/O), SKIP IOS PROCESSING    45600000
         EJECT ,                                                        45800000
*********************************************************************** 46000000
**                                                                    * 46200000
**       EITHER I/O ERROR OCCURRED OR CHANNEL AND DEVICE END          * 46400000
**       SEPARATED.  IF THE FORMER, RETURN TO IOS TO PASS CONTROL     * 46600000
**       TO THE ERP.  IF CHANNEL END WITHOUT DEVICE END, SAVE THE     * 46800000
**       CURRENT CSW.  IF DEVICE END WITHOUT CHANNEL END, AND THE     * 47000000
**       CHANNEL END CSW SAVED, "OR" THE TWO CSW'S TOGETHER AND       * 47200000
**       RETRY THE PROCESS.                                           * 47400000
**                                                                    * 47600000
*********************************************************************** 47800000
         SPACE 3                                                        48000000
         TM    IOSTSA,CXCE+CXDE    CHANNEL & UNIT END SEPARATED ?       48200000
         BNMR  R14                 * NO -- RETURN FOR ERP PROCESSING    48400000
         TM    IOSTSA,CXCE         CHANNEL END ?                        48600000
         BO    XCHANEND            * YES -- GO SAVE                     48800000
*                                  MUST BE DEVICE END                   49000000
         TM    APIOFLG2,APIOCSWT   CSW SAVED ON CHANNEL END ?           49200000
         BZR   R14                 * NO RETURN TO IOS                   49400000
         NI    APIOFLG2,X'FF'-APIOCSWT CLEAR FLAG                       49600000
         OC    IOSCSW,APIOTCSW+1   COMBINE CE & DE CSW                  49800000
         B     XRETRY1             RE-CHECK I/O STATUS                  50000000
         SPACE 2                                                        50200000
XCHANEND EQU   *                   CHANNEL END WITHOUT DEVICE END       50400000
         MVC   APIOTCSW,IOSCSW-1   SAVE CSW FOR DE PROCESSING           50600000
         OI    APIOFLG2,APIOCSWT   SHOW CSW SAVED ON CE                 50800000
         BR    R14                 RETURN TO IOS                        51000000
         SPACE 2                                                        51200000
         POP   USING               RESTORE INPUT USING STATUS           51400000
         EJECT ,                                                        51600000
*********************************************************************** 51800000
**                                                                    * 52000000
**       SECOND EXIT LOGIC FOR VPSS I/O DRIVER DIE.                   * 52200000
**       SCHEDULE THE SRB ASSOCIATED WITH THE IOSB TO POST            * 52400000
**       OR RESUME THE USER.  THE STANDARD VPSS TERMINATION           * 52450000
**       ROUTINE (USED BY POST STATUS) IS ENTERED TO PERFORM          * 52500000
**       THE POST OR RESUME (IGTKBC).  USE OF THE DIE BYPASSES        * 52550000
**       THE OVERHEAD OF POST STATUS FOR THE MAINLINE I/O PATH        * 52600000
**       (NO ERRORS DETECTED).                                        * 52650000
**                                                                    * 53600000
*********************************************************************** 53800000
         SPACE 3                                                        54000000
         PUSH  USING               SAVE INPUT USING STATUS              54200000
         SPACE 3                                                        54400000
DIE2EXIT EQU   *                   2ND EXIT LOGIC FOR DIE               54600000
** SCHEDULE IGTKBC USING SRB ASSOCIATED                                 54800000
** WITH THE IOSB IN REGISTER 2.                                         65400000
*                                                                       65600000
** INPUT REGISTERS:                                                     65800000
**    1  - ADDRESS OF SRB TO SCHEDULE                                   66000000
** DESTROYED REGISTERS:                                                 66200000
**    14, 15, 0, 1                                                      66400000
*                                                                       66600000
         STM   R14,R0,0(APSVPTR)   SAVE DESTROYED REGISTERS             67000000
         LR    R14,R15             SWITCH ENTRY POINTS                  67200000
         DROP  R15                 * TELL THE ASSEMBLER                 67400000
         USING IGTKBB,R14          * OF THE NEW BASE                    67600000
         L     R1,APIOTERM         SET TERMINATION ROUTINE ADDR         67800000
         ST    R1,SRBEP-SRB+APIOSRB * IN THE SRB (IGTKBC)               68000000
         LA    R1,APIOSRB          POINT TO SRB                         68200000
         SCHEDULE SRB=(1),         SRB ADDRESS IN REGISTER 1           *71000000
               SCOPE=LOCAL         PUT ON LOCAL PRIORITY QUEUE         *71200000
                                   BYPASS POST STATUS                   71400000
         LM    R14,R0,0(APSVPTR)   RELOAD DESTROYED REGISTERS           71600000
         BR    R14                 RETURN TO IOS                        71800000
         SPACE 2                                                        72000000
         POP   USING               RESTORE INPUT USING STATUS           72200000
         EJECT ,                                                        72400000
*********************************************************************** 72600000
**                                                                    * 72800000
**       CONSTANTS                                                    * 73000000
**                                                                    * 73200000
*********************************************************************** 73400000
         SPACE 2                                                        73600000
CDECE    DC    01Y(X'0C00')        CHANNEL/DEVICE END ON,               73800000
*                                  * ERROR INDICATORS OFF               74000000
         EJECT ,                                                        74200000
*********************************************************************** 74400000
**                                                                    * 74600000
**       EQUATES                                                      * 74800000
**                                                                    * 75000000
*********************************************************************** 75200000
         SPACE 2                                                        75400000
CXCE     EQU   X'08'               CHANNEL END INDICATOR IN CSW         75600000
CXDE     EQU   X'04'               DEVICE END INDICATOR IN CSW          75800000
         EJECT ,                                                        76000000
*********************************************************************** 76200000
**                                                                    * 76400000
**       VPSS DSECTS REQUIRED                                         * 76600000
**                                                                    * 76800000
*********************************************************************** 77000000
         SPACE 2                                                        77200000
         IGTXDSCT IGTXAPIO         VPSS I/O ELEMENT                     77400000
         IGTXDSCT IGTXAPSV         SAVEAREA FORMAT                      77600000
         EJECT ,                                                        77800000
*********************************************************************** 78000000
**                                                                    * 78200000
**       MAKE IOSB PART OF APIO CONTROL BLOCK                         * 78400000
**                                                                    * 78600000
*********************************************************************** 78800000
         SPACE 2                                                        79000000
IOSB     DSECT ,                   APIO/IOSB DSECT                      79200000
         PRINT NOGEN                                                    79400000
         IECDIOSB ,                I/O SUPERVISOR BLOCK (IN APIO)       79600000
         PRINT GEN                                                      79800000
         SPACE 2                                                        80000000
IOSB     DSECT ,                   APIO/IOSB DSECT                      80200000
         ORG   IOSB                APIO STARTS WITH THE IOSB            80400000
         IGTXAPIO DSECT=NO         APIO IS PART OF IOSB DSECT           80600000
         EJECT ,                                                        80800000
         IHASRB ,                  SERVICE REQUEST BLOCK (IN APIO)      81000000
         EJECT ,                                                        81200000
         PRINT NOGEN                                                    81400000
         IHAPSA ,                  LOW CORE (0-4095)                    81600000
         PRINT GEN                                                      81800000
         EJECT ,                                                        82000000
         CVT   DSECT=YES           COMMUNICATIONS VECTOR TABLE          82200000
         EJECT ,                                                        82400000
         PRINT NOGEN                                                    82600000
         IHAASVT ,                 ASCB VECTOR TABLE                    82800000
         PRINT GEN                                                      83000000
         EJECT ,                                                        83200000
         IEFUCBOB ,                UNIT CONTROL BLOCK FOR I/O           83400000
         EJECT ,                                                        83600000
IGTKBB   CSECT ,                   RESUME CONTROL SECTION               83800000
         SPACE 2                                                        84000000
         IGTXEPLG ,                REGISTERS & VPSS DSECTS              84200000
         SPACE 3                                                        84400000
RTAPSV   EQU   R9                  TEMPORARY SAVEAREA POINTER           84600000
         END   ,                   MODULE END                           84800000
