TMAA     TITLE 'IGTMAA -- USER/LPA INTERFACE ROUTINE'                   00300000
IGTMAA   CSECT                                                          00600000
*/* * START OF SPECIFICATIONS ************************************** */ 00900000
*/*                                                                  */ 01200000
*/*   MODULE-NAME = IGTMAA                                           */ 01500000
*/*                                                                  */ 01800000
*/*   DESCRIPTIVE-NAME = VPSS USER/LPA INTERFACE ROUTINE             */ 02100000
*/*                                                                  */ 02400000
*/*   COPYRIGHT = NONE                                               */ 02700000
*/*                                                                  */ 03000000
*/*   STATUS = RELEASE 1, MODIFICATION LEVEL 0.                      */ 03300000
*/*                                                                  */ 03600000
*/*   FUNCTION = THIS ROUTINE BRANCHES TO THE RESIDENT ACCESS METHOD */ 03900000
*/*       MODULE LOCATED IN THE LINK PACK AREA (MVS) OR THE          */ 04200000
*/*       RAM (VS1).  ON THE INITIAL ENTRY, IGTMAA LINKS TO          */ 04500000
*/*       IGTMAB TO LOCATE THE RESIDENT MODULE AND INITIALIZE THE    */ 04800000
*/*       APUT AND APAT.  SUBSEQUENT CALLS USE A BRANCH ENTRY TO     */ 05100000
*/*       THE INTERFACE ROUTINE.                                     */ 05400000
*/*                                                                  */ 05700000
*/*   NOTES =                                                        */ 06000000
*/*     THIS ROUTINE MUST BE LINK-EDITED WITH AN ALIAS OF VPSS.      */ 06300000
*/*                                                                  */ 06600000
*/*     DEPENDENCIES =                                               */ 06900000
*/*        THE NAME OF THE SUBSYSTEM TO PROCESS THESE REQUESTS       */ 07200000
*/*        (NORMALLY VPSS) IS CONTAINED IN THE 4-CHARACTER           */ 07500000
*/*        CONSTANT IGTMAA01.  THIS IS USED BY IGTMAB (FIRST CALL    */ 07800000
*/*        TO IGTMAA) TO ESTABLISH THE CONTROL BLOCKS CONNECTING     */ 08100000
*/*        THE USER WITH THE CORRECT SUBSYSTEM.  IGTMAA01 IS         */ 08400000
*/*        DECLARED AS 'ENTRY' TO ALLOW THE USER TO MODIFY THIS      */ 08700000
*/*        VALUE VIA AN 'EXTRN'.  THIS IS NOT REQUIRED IF THE        */ 09000000
*/*        STANDARD SUBSYSTEM NAME (VPSS) IS USED OR THE             */ 09300000
*/*        INSTALLATION MODIFIES THIS VALUE.  THIS FEATURE IS OF     */ 09600000
*/*        USE WHEN TESTING AN ALTERNATE 3838 PROCESSING SUBSYSTEM.  */ 09900000
*/*                                                                  */ 10200000
*/*     RESTRICTIONS =  THIS ROUTINE MUST ASSEMBLE IDENTICALLY IN    */ 10500000
*/*                     AN MVS AND A VS1 ENVIRONMENT SINCE IT IS     */ 10800000
*/*                     LINK-EDITED WITH THE USER.                   */ 11100000
*/*                                                                  */ 11400000
*/*     REGISTER-CONVENTIONS = STANDARD VPSS CONVENTIONS             */ 11700000
*/*                                                                  */ 12000000
*/*     PATCH-LABEL = APPATCH                                        */ 12300000
*/*                                                                  */ 12600000
*/*   MODULE-TYPE = CSECT                                            */ 12900000
*/*                                                                  */ 13200000
*/*     PROCESSOR = VS/2 ASSEMBLER                                   */ 13500000
*/*                                                                  */ 13800000
*/*     MODULE SIZE = SEE END OF ASSEMBLER LISTING                   */ 14100000
*/*                                                                  */ 14400000
*/*     ATTRIBUTES = REUSABLE.  NOTE THAT THIS PROGRAM IS            */ 14700000
*/*                  RE-ENTRANT EXCEPT FOR THE INITIAL CALL.         */ 15000000
*/*                  HOWEVER MULTIPLE INITIAL CALLS ARE HANDLED      */ 15300000
*/*                  BY IGTMAB.  THEREFORE IGTMAA WILL FUNCTION IN   */ 15600000
*/*                  A RE-ENTRANT ENVIRONMENT.                       */ 15900000
*/*                                                                  */ 16200000
*/*   ENTRY-POINT = IGTMAA                                           */ 16500000
*/*                                                                  */ 16800000
*/*     PURPOSE = SAME AS FUNCTION                                   */ 17100000
*/*                                                                  */ 17400000
*/*     LINKAGE = ENTERED FROM USER'S PROGRAM.  ENTERED IN           */ 17700000
*/*               THE USER'S KEY, PROBLEM STATE, UNLOCKED, TASK      */ 18000000
*/*               MODE, ENABLED.                                     */ 18300000
*/*                                                                  */ 18600000
*/*     INPUT = THE FOLLOWING REGISTERS ARE INITIALIZED ON ENTRY --  */ 18900000
*/*         REGISTER  1 - ADDRESS OF THE VPSS CALL PARAMETER LIST    */ 19200000
*/*         REGISTER 13 - ADDRESS OF 72-BYTE SAVEAREA.               */ 19500000
*/*         REGISTER 14 - RETURN ADDRESS                             */ 19800000
*/*         REGISTER 15 - ADDRESS OF THIS ROUTINE (IGTMAA).          */ 20100000
*/*                                                                  */ 20400000
*/*     OUTPUT = ON THE INITIAL CALL TO IGTMAA (VPSS), THE APUT      */ 20700000
*/*              AND APAT ARE CONTRUCTED VIA A LINK TO IGTMAA.       */ 21000000
*/*              ON THIS CALL AND SUBSEQUENT CALLS, NO OTHER         */ 21300000
*/*              OUTPUT IS PRODUCED BY THIS MODULE.                  */ 21600000
*/*                                                                  */ 21900000
*/*   ENTRY-POINT = IGTMAA01                                         */ 22200000
*/*                                                                  */ 22500000
*/*     PURPOSE = IDENTIFIES THE 4-CHARACTER NAME OF THE             */ 22800000
*/*               SUBSYSTEM TO PROCESS THESE REQUESTS.               */ 23100000
*/*               ALLOWS THE APPLICATION PROGRAM TO USE              */ 23400000
*/*               A SUBSYSTEM OTHER THAN VPSS.  THIS VALUE           */ 23700000
*/*               MUST BE CHANGED PRIOR TO THE FIRST CALL            */ 24000000
*/*               TO VPSS (ENTRY POINT IGTMAA) IF AN ALTERNATE       */ 24300000
*/*               SUBSYSTEM IS WANTED.                               */ 24600000
*/*                                                                  */ 24900000
*/*     LINKAGE = NON-EXECUTABLE ENTRY POINT.  SHOULD BE REFERENCED  */ 25200000
*/*               VIA AN ADCON AND AN EXTRN STATEMENT.               */ 25500000
*/*                                                                  */ 25800000
*/*     INPUT = N/A.                                                 */ 26100000
*/*                                                                  */ 26400000
*/*     OUTPUT = N/A.                                                */ 26700000
*/*                                                                  */ 27000000
*/*   EXIT-NORMAL = CONTROL PASSES TO IGTMBA TO PERFORM THE          */ 27300000
*/*                 REQUESTED FUNCTION.  THE FOLLOWING REGISTERS     */ 27600000
*/*                 ARE SET ON EXIT:                                 */ 27900000
*/*                 R0   - ADDRESS OF WORD CONTAINING ADDRESS OF     */ 28200000
*/*                        THE APUT.                                 */ 28500000
*/*                 R1   - ADDRESS OF THE USER'S PARAMETER LIST      */ 28800000
*/*                        (SAME AS ON INPUT).                       */ 29100000
*/*                 R2-R13 - UNCHANGED FROM INPUT.                   */ 29400000
*/*                 R14  - RETURN ADDRESS TO APPLICATION PROGRAM     */ 29700000
*/*                        (SAME AS ON INPUT TO IGTMAA).             */ 30000000
*/*                 R15  - ADDRESS OF IGTMBA                         */ 30300000
*/*                 CONTROL EXITS IN THE USER'S KEY, PROBLEM         */ 30600000
*/*                 STATE, ENABLED, OWNING NO LOCKS (SAME AS INPUT). */ 30900000
*/*                                                                  */ 31200000
*/*   EXIT-ERROR  = IF UNABLE TO INITIALIZE THE APUT/APAT            */ 31500000
*/*                 (NORMALLY SUBSYSTEM DOES NOT EXIST),             */ 31800000
*/*                 CONTROL RETURNS TO THE CALLER.                   */ 32100000
*/*                 THE FOLLOWING REGISTERS ARE PASSED TO THE        */ 32400000
*/*                 USER --                                          */ 32700000
*/*                 R0-R14 - UNCHANGED                               */ 33000000
*/*                 R15    - CONTAINS A 12 (SET BY IGTMBA).          */ 33300000
*/*                 CONTROL EXITS IN THE USER'S KEY, PROBLEM         */ 33600000
*/*                 STATE, ENABLED, OWNING NO LOCKS (SAME AS INPUT). */ 33900000
*/*                                                                  */ 34200000
*/*   EXTERNAL-REFERENCES = THE FOLLOWING ROUTINES AND               */ 34500000
*/*                         CONTROL BLOCKS ARE USED.                 */ 34800000
*/*                                                                  */ 35100000
*/*     ROUTINES =                                                   */ 35400000
*/*         IGTMAB  -  USER/VPSS CONNECTION                          */ 35700000
*/*         IGTMBA  -  VPSS FAST PATH PROCESSOR                      */ 36000000
*/*                                                                  */ 36300000
*/*     DATA-AREAS = NONE                                            */ 36600000
*/*                                                                  */ 36900000
*/*     CONTROL-BLOCKS =                                             */ 37200000
*/*         PSA      - R                                             */ 37500000
*/*         CVT      - R                                             */ 37800000
*/*         APSV     - R, M                                          */ 38100000
*/*                                                                  */ 38400000
*/*   TABLES = NONE                                                  */ 38700000
*/*                                                                  */ 39000000
*/*   MACROS =                                                       */ 39300000
*/*       IGTXEPLG                                                   */ 39600000
*/*       LINK                                                       */ 39900000
*/*       GETMAIN                                                    */ 40200000
*/*       FREEMAIN                                                   */ 40500000
*/*                                                                  */ 40800000
*/*   CHANGE-ACTIVITY = NONE                                         */ 41100000
*/*                                                                  */ 41400000
*/*   MESSAGES = NONE                                                */ 41700000
*/*                                                                  */ 42000000
*/*   ABEND-CODES = NONE                                             */ 42300000
*/*                                                                  */ 42600000
*/* * END OF SPECIFICATIONS **************************************** */ 42900000
         EJECT ,                                                        43200000
         COPY  IGTXSYSO            DEFINE SYSTEM PARAMETERS             43500000
         EJECT ,                                                        43800000
VPSS     DS    0D                  USER ENTRY POINT NAME                44100000
         ENTRY VPSS                * (ALIAS)                            44400000
         SPACE 3                                                        44700000
         USING IGTMAA,APENTRY      BASE REGISTER                        45000000
         SPACE 3                                                        45300000
         B     STARTUP             SKIP IDENTIFIER                      45600000
         DC    YL1(COREIDE-*-1)    LENGTH OF ID                         45900000
         DC    C'IGTMAA'           CSECT NAME                           46200000
         DC    C' &SYSDATE'        ASSEMBLY DATE                        46500000
COREIDE  EQU   *                   END OF ID                            46800000
         SPACE 3                                                        47100000
         CNOP  2,4                 FORCE HALFWORD (NON-FULL) ALIGNMENT  47400000
         SPACE 3                                                        47700000
STARTUP  DS    0H                  START OF CODE                        48000000
         L     APENTRY,ADDRINTF    ADDRESS OF INTERFACE ROUTINE         48300000
         BALR  R0,APENTRY          GOTO IT. R0 -> INTERFACE LIST        48600000
         SPACE 3                                                        48900000
*                                                                       49200000
** INTERFACE TABLE POINTERS                                             49500000
** USED TO POINT TO COMMON USER CONTROL BLOCKS.                         49800000
*                                                                       50100000
AAPUT    DC    A(*-*)              ADDRESS OF APUT.                     50400000
*                                  * INITIALIZED BY IGTMAB              50700000
         DC    XL1'80'             END-OF-LIST INDICATOR                51000000
*                                  * MUST REMAIN ON                     51300000
         DC    AL3(0)              NOT CURRENTLY USED                   51600000
         SPACE 3                                                        51900000
*                                                                       52200000
** FIELDS PASSED TO IGTMAB FOR INITIALIZATION                           52500000
*                                                                       52800000
         DS    0A                  FULLWORD ALIGNMENT                   53100000
AMNAME   DC    CL4'VPSS'           SUBSYSTEM NAME                       53400000
ADDRINTF DC    A(LOCATE)           ADDRESS OF INTERFACE ROUTINE         53700000
*                                  * ON FIRST CALL, GOES TO 'LOCATE'    54000000
*                                  * WHICH LINKS TO IGTMBA TO RESET     54300000
*                                  * ADDRINTF TO THE PROPER VALUE.      54600000
USER     DC    A(*-*)              RESERVED FOR INSTALLATION USE        54900000
         SPACE 2                                                        55200000
IGTMAA01 EQU   AMNAME,4,C'C'       DEFINE ENTRY POINT                   55500000
         ENTRY IGTMAA01            * FOR SUBSYSTEM NAME                 55800000
         EJECT ,                                                        56100000
*********************************************************************** 56400000
**                                                                   ** 56700000
**       THIS CODE ENTERED ONLY ON INITIAL CALL TO VPSS.  IT         ** 57000000
**       LINKS TO IGTMAB TO CONSTRUCT THE APUT AND APAT, AND TO      ** 57300000
**       SET ADDRINTF TO THE ADDRESS OF THE INITIAL VPSS             ** 57600000
**       PROCESSING ROUTINE IN THE LINK PACK AREA.  THE ADDRESS      ** 57900000
**       OF THE CONSTRUCTED APUT IS PLACED AT LABEL 'AAPUT' FOR      ** 58200000
**       USE BY FUTURE CALLS TO VPSS.                                ** 58500000
**                                                                   ** 58800000
**       ON RETURN FROM IGTMAB, CONTROL RETURN TO THE BEGINNING OF   ** 59100000
**       THIS MODULE TO PROCESS THE USER'S REQUEST.  FUTURE CALLS    ** 59400000
**       TO THIS MODULE CAUSE CONTROL TO BE PASSED DIRECTLY TO       ** 59700000
**       THE INITIAL VPSS PROCESSING ROUTINE IN THE LINK PACK AREA   ** 60000000
**       (BECAUSE OF THE MODIFICATION OF FIELD 'ADDRINTF').          ** 60300000
**                                                                   ** 60600000
*********************************************************************** 60900000
         SPACE 3                                                        61200000
LOCATE   DS    0H                  INITIAL CALL -- LINK TO IGTMAB       61500000
         USING LOCATE,APENTRY      BASE REGISTER                        61800000
         USING APSV,APSVPTR        INPUT SAVEAREA POINTER               62100000
         STM   R14,R12,APSVRG14    SAVE INPUT REGISTERS                 62400000
         LR    APCODE,APENTRY      ESTABLISH BASE IN                    62700000
         DROP  APENTRY             NON-DYNAMIC REGISTER                 63000000
         USING LOCATE,APCODE       TELL ASSEMBLER                       63300000
         LA    R0,APSVLN+LINKLN    REQUIRED GETMAIN LENGTH              63600000
         GETMAIN R,LV=(0)          OBTAIN DYNAMIC STORAGE               63900000
         ST    R1,APSVNEXT         USE FIRST PART AS SAVEAREA           64200000
         ST    APSVPTR,APSVPREV-APSV(,R1) * CHAIN SAVEAREAS             64500000
         LR    APSVPTR,R1          * POINT TO NEW SAVEAREA              64800000
         L     TPTR,FLCCVT-FLC(*-*) ADDRESS OF CVT                      65100000
         L     TPTR,CVTLINK-CVTMAP(,TPTR) LINKLIB DCB ADDRESS           65400000
         LINK  EP=IGTMAB,          GO TO USER/VPSS CONNECTION ROUTINE  *65700000
               DCB=(TPTR),         * USE LINKLIB DCB (INCLUDES LPA)    *66000000
               PARAM=(AAPUT,       * INPUT: ADDRESS OF APUT RETURNED   *66300000
               AMNAME,             * * SUBSYSTEM NAME                  *66600000
               ADDRINTF,           * * @ OF INTERFACE RTN RETURNED     *66900000
               USER),              * * RESERVED FOR INSTALLATION MODS  *67200000
               VL=1,               * FLAG END OF PARM LIST WITH X'80'  *67500000
               SF=(E,APSV+APSVLN)  * LINK WORKAREA AFTER OS SAVEAREA    67800000
         LR    R1,APSVPTR          ADDRESS OF SAVEAREA FOR FREEMAIN     68100000
         L     APSVPTR,APSVPREV    POINT TO INPUT SAVEAREA              68400000
         ST    APRCODE,APSVRG15    SAVE IGTMAB RETURN CODE              68700000
         LA    R0,APSVLN+LINKLN    LENGTH TO FREEMAIN                   69000000
         FREEMAIN R,LV=(0),A=(1)   FREE TEMPORARY STORAGE               69300000
         LM    R14,R12,APSVRG14    RELOAD INPUT REGISTERS               69600000
*                                  * R15 = RETURN CODE FROM IGTMBA      69900000
         DROP  APCODE              BASE REGISTER LOST                   70200000
         LTR   APRCODE,APRCODE     IF NON-ZERO RETURN CODE              70500000
         BNZR  APRETURN            * RETURN TO CALLER WITHOUT           70800000
*                                  * PROCESSING REQUEST.                71100000
*                                  * INITIALIZATION FAILED.             71400000
         BALR  APENTRY,*-*         RE-ESTABLISH A BASE                  71700000
         USING *,APENTRY           TELL ASSEMBLER                       72000000
         L     APENTRY,BASEADDR    RELOAD INITIAL BASE ADDRESS          72300000
         BR    APENTRY             RETRY IGTMAA LOGIC                   72600000
*                                  * THIS TIME CONTROL PASSES TO IGTMBA 72900000
*                                  * (INITIAL VPSS PROCESSING ROUTINE)  73200000
         SPACE 3                                                        73500000
BASEADDR DC    A(IGTMAA)           RETRY ADDRESS                        73800000
*                                  * (CSECT START)                      74100000
         EJECT ,                                                        74400000
********************************************************************* * 74700000
**                                                                    * 75000000
**       VPSS AND SYSTEM DSECTS                                       * 75300000
**                                                                    * 75600000
**       ALTHOUGH DIFFERENT MVS AND VS1 SYSTEM MACROS USED, THIS      * 75900000
**       CSECT MUST ASSEMBLE IDENTICALLY IN MVS AND VS1 SINCE IT      * 76200000
**       IS LINK-EDITED WITH THE USER (WHO MAY RUN ON BOTH SYSTEMS).  * 76500000
**                                                                    * 76800000
********************************************************************* * 77100000
         SPACE 3                                                        77400000
LINK     DSECT ,                   SIZE OF LINK MACRO LIST              77700000
         LINK  EPLOC=*-*,DCB=*-*,SF=L FORMAT OF LINK MACRO              78000000
LINKLN   EQU   *-LINK              LENGTH OF LINK-MACRO PARAMETER LIST  78300000
         EJECT ,                                                        78600000
         IGTXAPSV ,                SAVEAREA                             78900000
         EJECT ,                                                        79200000
         AIF   ('&VPSSSYS' EQ 'VS1').VS1GEN IF VS/1, SKIP MVS VERSIONS  79500000
         CVT   SYS=AOS2,DSECT=YES  COMMUNICATIONS VECTOR TABLE          79800000
         EJECT ,                                                        80100000
         IHAPSA ,                  LOW STORAGE OVERLAY                  80400000
         AGO   .DSECTOK            DSECTS GENERATED                     80700000
.VS1GEN  ANOP  ,                   GENERATE VS1 DSECTS                  81000000
         CVT   SYS=AOS1,DSECT=YES  COMMUNICATIONS VECTOR TABLE          81300000
         EJECT ,                                                        81600000
         IHAFLC ,                  LOW STORAGE OVERLAY                  81900000
.DSECTOK ANOP  ,                   DSECTS GENERATED                     82200000
         EJECT ,                                                        82500000
IGTMAA   CSECT ,                   RESUME PROGRAM                       82800000
         IGTXEPLG ,                EXPAND REQUIRED VALUES               83100000
TPTR     EQU   R2                  TEMPORARY LINKLIB DCB POINTER        83400000
         END   ,                                                        83700000
