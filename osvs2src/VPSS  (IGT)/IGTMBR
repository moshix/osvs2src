TMBR     TITLE 'IGTMBR -- VPSS CALL INTERFACE ERROR MESSAGE (APEM) CONS*00200000
               TANTS'                                                   00400000
         MACRO                                                          00600000
         MSG   &NUMBER,&MSG,&LAST= SYNAD ERROR MESSAGE                  00800000
         GBLA  &IGTMBRN            NUMBER OF ENTRIES                    01000000
         GBLC  &IGTMBRL            SYSNDX FOR FIRST CALL                01200000
         LCLA  &N                  HOLD &NUMBER VALUE                   01400000
         LCLA  &E                  HOLD ERROR NUMBER                    01600000
         COPY  IGTXSYSA            SYSTEM STATUS                        01800000
         AIF   (&IGTMBRN NE 0).NFIRST CHECK FOR FIRST CALL              02000000
.*                                                                      02200000
.* INITIALIZE VARIABLES                                                 02400000
.*                                                                      02600000
         DC    A(TOT&SYSNDX)       NUMBER OF ENTRIES                    02800000
         DC    CL15'INVALID MSG 000' MSG 0-USE IF MSG NOT DEFINED       03000000
         SPACE 2                                                        03200000
&IGTMBRL SETC  '&SYSNDX'           SAVE INITIAL SYSNDX VALUE            03400000
.NFIRST  ANOP  ,                   INITIALIZATION COMPLETE              03600000
.*                                                                      03800000
.* CHECK FOR LAST CALL ENTRY                                            04000000
.*                                                                      04200000
         AIF   ('&LAST' EQ '' OR '&LAST' EQ 'NO').NLAST                 04400000
         AIF   ('&LAST' NE 'YES').LASTERR                               04600000
         AIF   ('&MSG' EQ '').LASTOK INSURE NO MESSAGE SPECIFIED        04800000
         IGTXMERR ERRN=09,         * 'LAST=YES' AND 'MSG' MUTUALLY     *05000000
               KEYWORD=(MSG,LAST), * EXCLUSIVE. GIVE ERROR MESSAGE     *05200000
               VALUE=(&MSG,&LAST), * AND DEFAULT 'MSG' TO NULL.        *05400000
               DEFAULT=NULL,       *                                   *05600000
               ERRLVL=4            * MINOR ERROR                        05800000
.LASTOK  ANOP  ,                   VALID LAST CALL                      06000000
*                                                                       06200000
** END OF SYNAD ERROR MESSAGES                                          06400000
*                                                                       06600000
TOT&IGTMBRL EQU &IGTMBRN           NUMBER OF MESSAGES                   06800000
         MEXIT ,                   END OF MACRO EXPANSION               07000000
.LASTERR IGTXMERR ERRN=1,          LAST= NOT PROPERLY SPECIFIED        *07200000
               KEYWORD=LAST,       * GIVE ERROR MESSAGE AND            *07400000
               VALUE=(&LAST),      * DEFAULT TO LAST=NO                *07600000
               QUANT='YES AND NO', * (IE EXPECT MORE ERROR MESSAGES)   *07800000
               DEFAULT=NO          *                                    08000000
.NLAST   ANOP  ,                   NOT LAST CALL -- PROCESS ENTRY       08200000
.*                                                                      08400000
.* BUILD MESSAGE DC                                                     08600000
.*                                                                      08800000
         AIF   (T'&NUMBER EQ 'N').MSGOK1 MSG NUMBER MUST BE A           09000000
&N       SETA  &IGTMBRN+1          * SELF DEFINING TERM                 09200000
         IGTXMERR ERRN=6,          * IF NOT, GIVE ERROR MSG &          *09400000
               KEYWORD=NUMBER,     * DEFAULT TO NEXT SEQUENTIAL        *09600000
               VALUE=(&NUMBER),    * MESSAGE NUMBER                    *09800000
               QUANT='SELF-DEFINING TERMS',                            *10000000
               DEFAULT=&N                                               10200000
         AGO   .NSET               * NUMBER NOW SET                     10400000
.MSGOK1  ANOP  ,                   IF NUMBER IS SELF-DEFINING, THEN     10600000
&N       SETA  &NUMBER             * INSURE THAT MESSAGES ARE IN        10800000
         AIF   (&N GT &IGTMBRN).MSGOK2 * ASCENDING SEQUENCE.            11000000
&N       SETA  &IGTMBRN+1          * IF NOT, THEN GIVE AN ERROR         11200000
         IGTXMERR ERRN=11,         * MESSAGE AND DEFAULT TO            *11400000
               VALUE=(&NUMBER),    * THE NEXT SEQUENTIAL NUMBER        *11600000
               KEYWORD=(NUMBER,ASCENDING),                             *11800000
               DEFAULT=&N                                               12000000
         AGO   .NSET               * NUMBER NOW SET                     12200000
.MSGOK2  ANOP  ,                   MESSAGE NUMBER VALID                 12400000
.*                                                                      12600000
.* IN NOT PROCESSING NEXT ID, THEN GENERATE DUMMY                       12800000
.* MESSAGES BETWEEN &IGTMBRN+1 AND &N-1.                                13000000
.*                                                                      13200000
.DUMMYMG ANOP  ,                   LOOP FOR DUMMY MESSAGES              13400000
         AIF   (&N EQ &IGTMBRN+1).NSET READY FOR NEW MSG ?              13600000
         DC    CL15'INVALID MSG &IGTMBRN' MSG NUMBER NOT USED           13800000
&IGTMBRN SETA  &IGTMBRN+1          INCREMENT NUMBER OF MESSAGES         14000000
         AGO   .DUMMYMG            RETRY TEST FOR MSG LOCATION          14200000
.NSET    ANOP  ,                   AT CORRECT MSG LOCATION              14400000
.*                                                                      14600000
.** VALIDATE MSG CONTENTS. IT MUST BE OF THE FORM:                      14800000
.**      'MSG'                                                          15000000
.** WHERE MSG IS OF 1-15 CHARACTERS & DOES NOT CONTAIN QUOTES           15200000
.*                                                                      15400000
         AIF   (K'&MSG LT 2).MSGBAD0 MUST BE ENCLOSED IN QUOTES         15600000
         AIF   ('&MSG'(1,1) NE '''' OR                                 *15800000
               '&MSG'(K'&MSG,1) NE '''').MSGBAD0 IF NOT, IS ERROR       16000000
         AIF   (K'&MSG GT 17).MSGBAD1 MSG NOT LONGER THAN 15            16200000
         DC    CL15&MSG            SYNAD MESSAGE                        16400000
         AGO   .EXIT               GO TO EXIT LOGIC                     16600000
.MSGBAD0 ANOP  ,                   NOT ENCLOSED IN QUOTES               16800000
&E       SETA  1                   ERROR MESSAGE NUMBER                 17000000
         AGO   .MSGBAD             GO GENERATE MESSAGE                  17200000
.MSGBAD1 ANOP  ,                   MSG TOO LONG                         17400000
&E       SETA  3                   ERROR MESSAGE NUMBER                 17600000
.MSGBAD  ANOP  ,                   INVALID MSG VALUE                    17800000
         IGTXMERR ERRN=&E,         * SHOW ALLOWABLE RANGE              *18000000
               KEYWORD=MESSAGE,    * & DEFAULT TAKEN                   *18200000
               VALUE=(&MSG),       * FOR &MSG                          *18400000
               RANGE='1 TO 15 CHARACTERS ENCLOSED IN QUOTES.',         *18600000
               DEFAULT='''INVALID MSG &N'''                             18800000
         DC    CL15'INVALID MSG &N' *** INVALID MSG SPECIFIED ***       19000000
.EXIT    ANOP  ,                   CHECK FOR LAST REQUEST               19200000
         SPACE 2                   SPACING BETWEEN REQUESTS             19400000
&IGTMBRN SETA  &IGTMBRN+1          INCREMENT NUMBER OF MESSAGES         19600000
         MEND  ,                   END OF MACRO                         19800000
         EJECT ,                                                        20000000
         MACRO                                                          20200000
         KYWD  &NUMBER,&KEYWORD,&LAST= SYNAD ERROR MESSAGE KEYWORD      20400000
         GBLA  &IGTMBRM            NUMBER OF ENTRIES                    20600000
         GBLC  &IGTMBRP            SYSNDX FOR FIRST CALL                20800000
         LCLA  &N                  HOLD &NUMBER VALUE                   21000000
         LCLA  &E                  HOLD ERROR NUMBER                    21200000
         COPY  IGTXSYSA            SYSTEM STATUS                        21400000
         AIF   (&IGTMBRM NE 0).NFIRST CHECK FOR FIRST CALL              21600000
.*                                                                      21800000
.* INITIALIZE VARIABLES                                                 22000000
.*                                                                      22200000
         DC    A(TOT&SYSNDX)       NUMBER OF ENTRIES                    22400000
         DC    CL08'********'      NAME 00-USE IF KEYWORD NOT DEFINED   22600000
         SPACE 2                                                        22800000
&IGTMBRP SETC  '&SYSNDX'           SAVE INITIAL SYSNDX VALUE            23000000
.NFIRST  ANOP  ,                   INITIALIZATION COMPLETE              23200000
.*                                                                      23400000
.* CHECK FOR LAST CALL ENTRY                                            23600000
.*                                                                      23800000
         AIF   ('&LAST' EQ '' OR '&LAST' EQ 'NO').NLAST                 24000000
         AIF   ('&LAST' NE 'YES').LASTERR                               24200000
         AIF   ('&KEYWORD' EQ '').LASTOK INSURE NO KEYWORD SPECIFIED    24400000
         IGTXMERR ERRN=09,         * 'LAST=YES' AND 'KEYWORD' MUTUALLY *24600000
               KEYWORD=(KEYWORD,LAST), * EXCLUSIVE. GIVE ERROR MESSAGE *24800000
               VALUE=(&KEYWORD,&LAST), * AND DEFAULT 'MSG' TO NULL.    *25000000
               DEFAULT=NULL,       *                                   *25200000
               ERRLVL=4            * MINOR ERROR                        25400000
.LASTOK  ANOP  ,                   VALID LAST CALL                      25600000
*                                                                       25800000
** END OF SYNAD ERROR MESSAGE KEYWORDS                                  26000000
*                                                                       26200000
TOT&IGTMBRP EQU &IGTMBRM           NUMBER OF KEYWORDS                   26400000
         MEXIT ,                   END OF MACRO EXPANSION               26600000
.LASTERR IGTXMERR ERRN=1,          LAST= NOT PROPERLY SPECIFIED        *26800000
               KEYWORD=LAST,       * GIVE ERROR MESSAGE AND            *27000000
               VALUE=(&LAST),      * DEFAULT TO LAST=NO                *27200000
               QUANT='YES AND NO', * (IE EXPECT MORE ERROR MESSAGES)   *27400000
               DEFAULT=NO          *                                    27600000
.NLAST   ANOP  ,                   NOT LAST CALL                        27800000
.*                                                                      28000000
.* PROCESS KEYWORD                                                      28200000
.*                                                                      28400000
         AIF   (T'&NUMBER EQ 'N').KWDOK1 KWD NUMBER MUST BE A           28600000
&N       SETA  &IGTMBRM+1          * SELF DEFINING TERM                 28800000
         IGTXMERR ERRN=6,          * IF NOT, GIVE ERROR MSG &          *29000000
               KEYWORD=NUMBER,     * DEFAULT TO NEXT SEQUENTIAL        *29200000
               VALUE=(&NUMBER),    * KEYWORD NUMBER                    *29400000
               QUANT='SELF-DEFINING TERMS',                            *29600000
               DEFAULT=&N                                               29800000
         AGO   .NSET               * NUMBER NOW SET                     30000000
.KWDOK1  ANOP  ,                   IF NUMBER IS SELF-DEFINING, THEN     30200000
&N       SETA  &NUMBER             * INSURE THAT KEYWORDS ARE IN        30400000
         AIF   (&N GT &IGTMBRM).KWDOK2 * ASCENDING SEQUENCE.            30600000
&N       SETA  &IGTMBRM+1          * IF NOT, THEN GIVE AN ERROR         30800000
         IGTXMERR ERRN=11,         * MESSAGE AND DEFAULT TO            *31000000
               VALUE=(&NUMBER),    * THE NEXT SEQUENTIAL NUMBER        *31200000
               KEYWORD=(NUMBER,ASCENDING),                             *31400000
               DEFAULT=&N                                               31600000
         AGO   .NSET               * NUMBER NOW SET                     31800000
.KWDOK2  ANOP  ,                   KEYWORD NUMBER VALID                 32000000
.*                                                                      32200000
.* IN NOT PROCESSING NEXT ID, THEN GENERATE DUMMY                       32400000
.* KEYWORDS BETWEEN &IGTMBRM+1 AND &N-1.                                32600000
.*                                                                      32800000
.DUMMYKW ANOP  ,                   LOOP FOR DUMMY KEYWORDS              33000000
         AIF   (&N EQ &IGTMBRM+1).NSET READY FOR NEW KEYWORD ?          33200000
         DC    CL08'KYWD &IGTMBRM' KEYWORD NOT USED                     33400000
&IGTMBRM SETA  &IGTMBRM+1          INCREMENT NUMBER OF KEYWORDS         33600000
         AGO   .DUMMYKW            RETRY TEST FOR KEYWORD LOCATION      33800000
.NSET    ANOP  ,                   AT CORRECT LOCATION                  34000000
.*                                                                      34200000
.** VALIDATE KEYWORD CONTENTS. IT MUST BE OF THE FORM:                  34400000
.**      KEYWORD                                                        34600000
.** WHERE KEYWORD IS FROM 1-8 CHARACTERS AND                            34800000
.** NOT SURROUNDED BY QUOTES.                                           35000000
.*                                                                      35200000
         AIF   (K'&KEYWORD EQ 0).KWDBAD0 MUST EXIST                     35400000
         AIF   (K'&KEYWORD GT 08).KWDBAD1 KWD NOT LONGER THAN 8         35600000
         DC    CL08'&KEYWORD'      KEYWORD                              35800000
         AGO   .EXIT               GO TO EXIT LOGIC                     36000000
.KWDBAD0 ANOP  ,                   KEYWORD DOES NOT EXIST               36200000
&E       SETA  2                   ERROR MESSAGE NUMBER                 36400000
         AGO   .KWDBAD             GO GENERATE MESSAGE                  36600000
.KWDBAD1 ANOP  ,                   KWD TOO LONG                         36800000
&E       SETA  3                   ERROR MESSAGE NUMBER                 37000000
.KWDBAD  ANOP  ,                   INVALID KWD VALUE                    37200000
         IGTXMERR ERRN=&E,         * SHOW ALLOWABLE RANGE              *37400000
               KEYWORD=KEYWORD,    * & DEFAULT TAKEN                   *37600000
               VALUE=(&KEYWORD),   * FOR &KEYWORD                      *37800000
               RANGE='1 TO 8 CHARACTERS NOT ENCLOSED IN QUOTES.',      *38000000
               DEFAULT='KYWD &N'                                        38200000
         DC    CL08'KYWD &N'       *** INVALID KWD SPECIFIED ***        38400000
.EXIT    ANOP  ,                   CHECK FOR LAST REQUEST               38600000
         SPACE 2                   SPACING BETWEEN REQUESTS             38800000
&IGTMBRM SETA  &IGTMBRM+1          INCREMENT NUMBER OF KEYWORDS         39000000
.DONE    MEXIT ,                   END OF MACRO EXPANSION               39200000
         MEND  ,                   END OF MACRO                         39400000
         EJECT ,                                                        39600000
IGTMBR   CSECT ,                                                        39800000
*/* * START OF SPECIFICATIONS ************************************** */ 40000000
*/*                                                                  */ 40200000
*/*   MODULE-NAME = IGTMBR                                           */ 40400000
*/*                                                                  */ 40600000
*/*   DESCRIPTIVE-NAME = VPSS CALL ERROR MESSAGES AND KEYWORDS       */ 40800000
*/*                                                                  */ 41000000
*/*   COPYRIGHT = NONE                                               */ 41200000
*/*                                                                  */ 41400000
*/*   STATUS = RELEASE 1, MODIFICATION LEVEL 0.                      */ 41600000
*/*                                                                  */ 41800000
*/*   FUNCTION = THIS ROUTINE CONTAINS THE ERROR MESSAGES            */ 42000000
*/*       (15-CHARACTERS) AND KEYWORDS (8-CHARACTERS) USED TO        */ 42200000
*/*       CONSTRUCT VPSS ERROR MESSAGES.                             */ 42400000
*/*                                                                  */ 42600000
*/*   NOTES =                                                        */ 42800000
*/*                                                                  */ 43000000
*/*     DEPENDENCIES =                                               */ 43200000
*/*        ALL ROUTINES USING THIS MODULE MUST BE CHANGED IF THE     */ 43400000
*/*        FORMAT OF THIS MODULE CHANGES.  ADDITION OF NEW ERROR     */ 43600000
*/*        MESSAGES AND KEYWORDS WILL ONLY REQUIRE AN ASSEMBLY OF    */ 43800000
*/*        THOSE MODULES WHICH USE THE NEW VALUES.  MACRO IGTXAPCM   */ 44000000
*/*        CONTAINS THE INDEXES INTO THE MESSAGE AND KEYWORD         */ 44200000
*/*        SECTIONS OF THIS MODULE.  MACRO IGTXAPMV DESCRIBES THE    */ 44400000
*/*        FORMAT OF THIS MODULE.                                    */ 44600000
*/*                                                                  */ 44800000
*/*     RESTRICTIONS = NONE.                                         */ 45000000
*/*                                                                  */ 45200000
*/*     REGISTER-CONVENTIONS = STANDARD VPSS CONVENTIONS             */ 45400000
*/*                                                                  */ 45600000
*/*     PATCH-LABEL = APPATCH                                        */ 45800000
*/*                                                                  */ 46000000
*/*   MODULE-TYPE = CSECT                                            */ 46200000
*/*                                                                  */ 46400000
*/*     PROCESSOR = VS/2 ASSEMBLER                                   */ 46600000
*/*                                                                  */ 46800000
*/*     MODULE SIZE = SEE END OF ASSEMBLER LISTING                   */ 47000000
*/*                                                                  */ 47200000
*/*     ATTRIBUTES = RE-ENTRANT, ONLY-LOADABLE (CONSTANTS ONLY)      */ 47400000
*/*                                                                  */ 47600000
*/*   ENTRY-POINT = IGTMBR                                           */ 47800000
*/*                                                                  */ 48000000
*/*     PURPOSE = SAME AS FUNCTION                                   */ 48200000
*/*                                                                  */ 48400000
*/*     LINKAGE = N/A.  CONTAINS CONSTANTS ONLY.  NO EXECUTABLE      */ 48600000
*/*               CODE EXISTS IN THIS MODULE.                        */ 48800000
*/*                                                                  */ 49000000
*/*     INPUT = N/A.                                                 */ 49200000
*/*                                                                  */ 49400000
*/*     OUTPUT = N/A.                                                */ 49600000
*/*                                                                  */ 49800000
*/*   EXIT-NORMAL = N/A.                                             */ 50000000
*/*                                                                  */ 50200000
*/*   EXIT-ERROR  = N/A.                                             */ 50400000
*/*                                                                  */ 50600000
*/*   EXTERNAL-REFERENCES = THE FOLLOWING ROUTINES AND               */ 50800000
*/*                         CONTROL BLOCKS ARE USED.                 */ 51000000
*/*                                                                  */ 51200000
*/*     ROUTINES = N/A.                                              */ 51400000
*/*                                                                  */ 51600000
*/*     DATA-AREAS = NONE.                                           */ 51800000
*/*                                                                  */ 52000000
*/*     CONTROL-BLOCKS =                                             */ 52200000
*/*         APEM     -       C                                       */ 52400000
*/*                                                                  */ 52600000
*/*   TABLES = NONE                                                  */ 52800000
*/*                                                                  */ 53000000
*/*   MACROS =                                                       */ 53200000
*/*       MSG   -- INTERNAL MACRO                                    */ 53400000
*/*       KYWD  -- INTERNAL MACRO                                    */ 53600000
*/*       IGTXMERR  -- GENERATE VPSS MACRO ERROR MESSAGE             */ 53800000
*/*       IGTXEPLG                                                   */ 54000000
*/*                                                                  */ 54200000
*/*   CHANGE-ACTIVITY = NONE                                         */ 54400000
*/*                                                                  */ 54600000
*/*   MESSAGES =                                                     */ 54800000
*/*      CONTAINS SKELETON APEM.                                     */ 55000000
*/*                                                                  */ 55200000
*/*   ABEND-CODES = NONE                                             */ 55400000
*/*                                                                  */ 55600000
*/* * END OF SPECIFICATIONS **************************************** */ 55800000
         SPACE 3                                                        56000000
         COPY  IGTXSYSO            DEFINE SCP CHARACTERISTICS           56200000
         SPACE 5                                                        56400000
*********************************************************************** 56600000
*                                                                     * 56800000
*        ADDRESSES OF INDIVIDUAL TABLES                               * 57000000
*                                                                     * 57200000
*********************************************************************** 57400000
         SPACE 3                                                        57600000
         DC    A(APEM)             ADDRESS OF SKELETON SYNAD MESSAGE    57800000
         DC    A(KEYWORDS)         ADDRESS OF KEYWORD TABLE             58000000
         DC    A(MSGS)             ADDRESS OF SYNAD MESSAGE TABLE       58200000
         EJECT ,                                                        58400000
*********************************************************************** 58600000
*                                                                     * 58800000
*        SYNAD MESSAGE SKELETON                                       * 59000000
*                                                                     * 59200000
*********************************************************************** 59400000
         SPACE 5                                                        59600000
         IGTXAPEM LIST=YES,DSECT=NO GENERATE SYNAD MESSAGE              59800000
         EJECT ,                                                        60000000
*********************************************************************** 60200000
*                                                                     * 60400000
*        KEYWORD CONSTANTS                                            * 60600000
*                                                                     * 60800000
*********************************************************************** 61000000
         SPACE 5                                                        61200000
KEYWORDS DS    0D                  KEYWORD CONSTANTS                    61400000
 KYWD          001,REG                                                  61600000
 KYWD          002,STRN                                                 61800000
 KYWD          003,OWN                                                  62000000
 KYWD          004,LERR                                                 62200000
 KYWD          005,PERR                                                 62400000
 KYWD          006,INTV                                                 62600000
 KYWD          007,EMSG                                                 62800000
 KYWD          008,RBA                                                  63000000
 KYWD          009,RCDE                                                 63200000
 KYWD          010,BSAD                                                 63400000
 KYWD          011,IUNIT                                                63600000
 KYWD          012,IFUNC                                                63800000
 KYWD          013,IERROR                                               64000000
 KYWD          014,IAPRL                                                64200000
 KYWD          015,ISTATE                                               64400000
 KYWD          016,IFMY                                                 64600000
 KYWD          017,YARRV                                                64800000
 KYWD          018,ICTY                                                 65000000
 KYWD          019,IDLY                                                 65200000
 KYWD          020,YARRVX                                               65400000
 KYWD          021,ICTYX                                                65600000
 KYWD          022,IDLYX                                                65800000
 KYWD          023,IFMX                                                 66000000
 KYWD          024,XARRV                                                66200000
 KYWD          025,ICTX                                                 66400000
 KYWD          026,IDLX                                                 66600000
 KYWD          027,XARRVX                                               66800000
 KYWD          028,ICTXX                                                67000000
 KYWD          029,IDLXX                                                67200000
 KYWD          030,IFMU                                                 67400000
 KYWD          031,UARRV                                                67600000
 KYWD          032,ICTU                                                 67800000
 KYWD          033,IDLU                                                 68000000
 KYWD          034,UARRVX                                               68200000
 KYWD          035,ICTUX                                                68400000
 KYWD          036,IDLUX                                                68600000
 KYWD          037,COND                                                 68800000
 KYWD          038,IVAL1                                                69000000
 KYWD          039,IVAL2                                                69200000
 KYWD          040,LABEL                                                69400000
 KYWD          041,ACIT                                                 69600000
 KYWD          042,ICIT                                                 69800000
 KYWD          043,ACCW                                                 70000000
 KYWD          044,ICCW                                                 70200000
 KYWD          045,APIO                                                 70400000
 KYWD          046,NULL                                                 70600000
 KYWD          047,SP-000                                               70800000
 KYWD          048,SP-252                                               71000000
 KYWD          049,SP-230                                               71200000
 KYWD          050,ACB                                                  71400000
 KYWD          051,APLB                                                 71600000
 KYWD          052,APRL                                                 71800000
 KYWD          053,DEBCHK                                               72000000
 KYWD          054,FREEMAIN                                             72200000
 KYWD          055,GETMAIN                                              72400000
 KYWD          056,SETLOCK                                              72600000
 KYWD          057,PGFIX                                                72800000
 KYWD          058,PGFREE                                               73000000
 KYWD          059,SETFRR                                               73200000
         AIF   ('&VPSSSYS' NE 'MVS').VS1K060 SKIP IF VS1                73400000
 KYWD          060,ESTAE                                                73600000
         AGO   .MVSK060            SKIP VS1 EXPANSION                   73800000
.VS1K060 ANOP  ,                   VS1 EXPANSION                        74000000
 KYWD          060,STAE                                                 74200000
.MVSK060 ANOP  ,                   KEYWORD 060 FOR MVS/VS1 GENERATED    74400000
 KYWD          061,CCW                                                  74600000
 KYWD          062,SP-241                                               74800000
 KYWD          063,APXL                                                 75000000
 KYWD          064,VSL                                                  75200000
 KYWD          065,APDX                                                 75400000
 KYWD          066,IDAL                                                 75600000
 KYWD          067,APCI                                                 75800000
 KYWD          068,APMF                                                 76000000
 KYWD          069,SP-245                                               76020000
 KYWD          070,SMF                                                  76040000
 KYWD          071,SUSPEND                                              76060000
 KYWD          072,RESUME                                               76080000
 KYWD          073,APUB                                                 76100000
 KYWD          074,FESTAE                                               76120000
 KYWD          LAST=YES            GENERATE THE NUMBER OF ENTRIES       76200000
         EJECT ,                                                        76400000
*********************************************************************** 76600000
*                                                                     * 76800000
*        SYNAD MESSAGES                                               * 77000000
*                                                                     * 77200000
*********************************************************************** 77400000
         SPACE 5                                                        77600000
MSGS     DS    0D                  SYNAD MESSAGES                       77800000
 MSG           001,'UNSUPPORT FUNC'                                     78000000
 MSG           002,'UNKNOWN FUNC'                                       78200000
 MSG           003,'OUT OF RANGE'                                       78400000
 MSG           004,'ACB MISSING'                                        78600000
 MSG           005,'ACB EXISTS'                                         78800000
 MSG           006,'ACB OPEN'                                           79000000
 MSG           007,'ACB CLOSED'                                         79200000
 MSG           008,'BLD NOT ACTIVE'                                     79400000
 MSG           009,'INVLD NBR PARMS'                                    79600000
 MSG           010,'INVALID KEYWORD'                                    79800000
 MSG           011,'INVALID VALUE'                                      80000000
 MSG           012,'DD STMT MISSING'                                    80200000
 MSG           013,'INVALID DD STMT'                                    80400000
 MSG           014,'VALUE NOT GIVEN'                                    80600000
 MSG           015,'CANNOT MODIFY'                                      80800000
 MSG           016,'JCL DOWNGRADE'                                      81000000
 MSG           017,'MODULE MISSING'                                     81200000
 MSG           018,'NO STORAGE'                                         81400000
 MSG           019,'INVLD SVC FUNC'                                     81600000
 MSG           020,'DDNAME IN USE'                                      81800000
 MSG           021,'BAD CNTRL BLOCK'                                    82000000
 MSG           022,'(UN)ALLOC FAIL'                                     82200000
 MSG           023,'INSTAL EXIT REJ'                                    82400000
 MSG           026,'NOT DWORD BDY'                                      82600000
 MSG           027,'DUPLICATE LABEL'                                    82800000
 MSG           029,'UNRESLVD LABELS'                                    83000000
 MSG           030,'NOT A WORD BDY'                                     83200000
 MSG           031,'NO FREE APRLS'                                      83400000
 MSG           032,'NOT TRANSLATED'                                     83600000
 MSG           033,'SUPV SERV FAIL'                                     83800000
 MSG           034,'APIO ACTIVE'                                        84000000
 MSG           035,'PGM CHK REF'                                        84200000
 MSG           036,'UNEXPECTD ABEND'                                    84400000
 MSG           037,'LOGIC ERROR'                                        84600000
 MSG           039,'TABLE OVERFLOW'                                     84800000
 MSG           040,'UNKNOWN COND'                                       85000000
 MSG           041,'STARTIO ERROR'                                      85200000
 MSG           042,'PURGED REQUEST'                                     85400000
 MSG           043,'UNIT EXCEPTION'                                     85600000
 MSG           044,'PROGRAM CHK'                                        85800000
 MSG           045,'PROTECTION CHK'                                     86000000
 MSG           046,'CHAN DATA CHK'                                      86200000
 MSG           047,'CHAN CNTL CHK'                                      86400000
 MSG           048,'INTFCE CNTL CHK'                                    86600000
 MSG           049,'CHAINING CHK'                                       86800000
 MSG           050,'UNIT CHECK'                                         87000000
 MSG           051,'CMD REJECT'                                         87200000
 MSG           052,'INT REQ'                                            87400000
 MSG           053,'BUS OUT CHK'                                        87600000
 MSG           054,'EQP CHECK'                                          87800000
 MSG           055,'DATA CHECK'                                         88000000
 MSG           056,'NOT IPLED'                                          88200000
 MSG           057,'I/O LIMIT CHK'                                      88400000
 MSG           058,'I/O LENGTH CHK'                                     88600000
 MSG           059,'CHAN PGM SHORT'                                     88800000
 MSG           060,'INCORRECT LNGTH'                                    89000000
 MSG           061,'UNKN CIT OPCODE'                                    89200000
 MSG           062,'INVLD CIT OPTN'                                     89400000
 MSG           063,'INVLD CIT BRNCH'                                    89600000
 MSG           064,'EXCEED BRNCH CT'                                    89800000
 MSG           065,'INDEX OVERFLOW'                                     90000000
 MSG           066,'INDEX DIV BY 0'                                     90200000
 MSG           067,'BLK STOR LIM CK'                                    90400000
 MSG           068,'DATA CONVERT CK'                                    90600000
 MSG           069,'FLT PT OVERFLOW'                                    90800000
 MSG           070,'FLT PT UNDERFLW'                                    91000000
 MSG           071,'FLT PT UNNORM'                                      91200000
 MSG           072,'FLT PT DIV BY 0'                                    91400000
 MSG           073,'DATA DEPENDENT'                                     91600000
 MSG           074,'EXCL PORT REIPL'                                    91650000
 MSG           075,'NO PORT AVAIL'                                      91700000
 MSG           LAST=YES            GENERATE THE NUMBER OF ENTRIES       91800000
         EJECT ,                                                        92000000
         IGTXAPCC LIST=YES         SHOW ERROR CODES                     92200000
         EJECT ,                                                        92400000
         IGTXAPCM LIST=YES         SHOW MESSAGE/KEYWORD INDEX VALUES    92600000
         EJECT ,                                                        92800000
         IGTXEPLG ,                GENERATE PATCH FOR IDENTIFICATION    93000000
         END   ,                                                        93200000
