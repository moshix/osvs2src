TUAB     TITLE 'IGTUAB -- GET SAVEAREA/WORKAREA'                        00300000
IGTUAB   CSECT ,                                                        00600000
*/* * START OF SPECIFICATIONS ************************************** */ 00900000
*/*                                                                  */ 01200000
*/*   MODULE-NAME = IGTUAB                                           */ 01500000
*/*                                                                  */ 01800000
*/*   DESCRIPTIVE-NAME = GET SAVEAREA/WORKAREA (NOT INITIAL)         */ 02100000
*/*                      (IGTYGSV MACRO SUPPORT)                     */ 02400000
*/*                                                                  */ 02700000
*/*   COPYRIGHT = NONE                                               */ 03000000
*/*                                                                  */ 03300000
*/*   STATUS = RELEASE 1, MODIFICATION LEVEL 0.                      */ 03600000
*/*                                                                  */ 03900000
*/*   FUNCTION = THIS ROUTINE WILL PROVIDE A SAVEAREA-WORKAREA       */ 04200000
*/*       FOR THE CALLING ROUTINE.  AN ATTEMPT WILL BE MADE TO       */ 04500000
*/*       ACQUIRE THE AREA FROM THE CURRENT APSM.  IF UNAVAILABLE,   */ 04800000
*/*       A NEW APSM WILL BE OBTAINED TO HOLD THE NEW SAVEAREA.      */ 05100000
*/*       THE STORAGE FOR THIS APSM WILL BE RELEASED WHEN THE        */ 05400000
*/*       SUBROUTINE EXITS. THIS ROUTINE SUPPORTS THE IGTYGSV        */ 05700000
*/*       MACRO WITH INITIAL=NO (THE DEFAULT) CODED.                 */ 06000000
*/*       STORAGE MANAGEMENT IS LIFO (LAST ACQUIRED STORAGE MUST     */ 06300000
*/*       BE INITIALLY RELEASED STORAGE).                            */ 06600000
*/*                                                                  */ 06900000
*/*   NOTES =                                                        */ 07200000
*/*     THIS MACRO MUST SUPPORT PARAMETERS GENERATED BY THE          */ 07500000
*/*     GETMAIN R,LV= MACRO (USED FOR PL/S WITH STACK OPTION)        */ 07800000
*/*                                                                  */ 08100000
*/*     DEPENDENCIES =                                               */ 08400000
*/*        REGISTER 13 MUST POINT TO A VPSS EXTENDED SAVEAREA        */ 08700000
*/*        (APSV) WHICH HAS BEEN CREATED BY IGTUAA, IGTUAB, OR       */ 09000000
*/*        IGTUAF.                                                   */ 09300000
*/*                                                                  */ 09600000
*/*     RESTRICTIONS =                                               */ 09900000
*/*        SINCE THIS ROUTINE MAY BE ENTERED IN EITHER PROBLEM OR    */ 10200000
*/*        SUPERVISOR STATE AND IN ANY KEY, THIS PROGRAM MAY NOT     */ 10500000
*/*        ISSUE PRIVLEDGED INSTRUCTIONS OR UPDATE SYSTEM CONTROL    */ 10800000
*/*        BLOCKS.                                                   */ 11100000
*/*                                                                  */ 11400000
*/*     REGISTER-CONVENTIONS = STANDARD VPSS CONVENTIONS             */ 11700000
*/*                                                                  */ 12000000
*/*     PATCH-LABEL = APPATCH                                        */ 12300000
*/*                                                                  */ 12600000
*/*   MODULE-TYPE = CSECT                                            */ 12900000
*/*                                                                  */ 13200000
*/*     PROCESSOR = VS/2 ASSEMBLER                                   */ 13500000
*/*                                                                  */ 13800000
*/*     MODULE SIZE = SEE END OF ASSEMBLER LISTING                   */ 14100000
*/*                                                                  */ 14400000
*/*     ATTRIBUTES = RE-ENTRANT.                                     */ 14700000
*/*                                                                  */ 15000000
*/*   ENTRY-POINT = IGTUAB                                           */ 15300000
*/*                                                                  */ 15600000
*/*     PURPOSE = SAME AS FUNCTION                                   */ 15900000
*/*                                                                  */ 16200000
*/*     LINKAGE = ENTERED UNLOCKED, TASK MODE, ENABLED. MAY BE       */ 16500000
*/*               IN ANY KEY AND EITHER PROBLEM OR SUPERVISOR        */ 16800000
*/*               STATE.                                             */ 17100000
*/*                                                                  */ 17400000
*/*     INPUT = THE FOLLOWING REGISTERS ARE INITIALIZED ON ENTRY     */ 17700000
*/*             (SET BY MACRO IGTYGSV INITIAL=NO) --                 */ 18000000
*/*         REGISTER  0 - BYTE-0 IGNORED. STORAGE ACQUIRED FROM      */ 18300000
*/*                       SAME SUBPOOL AS INITIAL=YES REQUESTOR.     */ 18600000
*/*                       BYTES 1-3 CONTAIN THE REQUIRED LENGTH      */ 18900000
*/*                       OF THE SAVEAREA/WORKAREA.                  */ 19200000
*/*         REGISTER  1 - IGNORED.                                   */ 19500000
*/*         REGISTER 12 - ADDRESS OF THE APCT.                       */ 19800000
*/*         REGISTER 13 - ADDRESS OF CURRENT APSV (VPSS EXTENDED     */ 20100000
*/*                       SAVEAREA).                                 */ 20400000
*/*         REGISTER 14 - RETURN ADDRESS                             */ 20700000
*/*         REGISTER 15 - ADDRESS OF THIS ROUTINE (IGTUAB).          */ 21000000
*/*                                                                  */ 21300000
*/*     OUTPUT = A NEW APSV AND APSC ARE CONSTRUCTED.  IF THE        */ 21600000
*/*              REQUIRED STORAGE WAS NOT AVAILABLE IN THE CURRENT   */ 21900000
*/*              APSM, A NEW STORAGE BLOCK WAS GETMAINED AND AN      */ 22200000
*/*              APSM BUILT TO DESCRIBE IT.  THIS STORAGE WILL BE    */ 22500000
*/*              FREED WHEN THE NEW SUBROUTINE EXITS.                */ 22800000
*/*                                                                  */ 23100000
*/*   EXIT-NORMAL = THE FOLLOWING REGISTERS ARE RETURNED TO THE      */ 23400000
*/*                 CALLER --                                        */ 23700000
*/*                 REGISTER  0 - UNPREDICTABLE                      */ 24000000
*/*                 REGISTER  1 - ADDRESS OF NEW SAVEAREA            */ 24300000
*/*                 REGISTER 2-13 - SAME AS ON INPUT                 */ 24600000
*/*                 REGISTER 14 - RETURN ADDRESS                     */ 24900000
*/*                 REGISTER 15 - UNPREDICTABLE                      */ 25200000
*/*                                                                  */ 25500000
*/*   EXIT-ERROR  = NONE.                                            */ 25800000
*/*                                                                  */ 26100000
*/*   EXTERNAL-REFERENCES = THE FOLLOWING ROUTINES AND               */ 26400000
*/*                         CONTROL BLOCKS ARE USED.                 */ 26700000
*/*                                                                  */ 27000000
*/*     ROUTINES = NONE.                                             */ 27300000
*/*                                                                  */ 27600000
*/*     DATA-AREAS = NONE                                            */ 27900000
*/*                                                                  */ 28200000
*/*     CONTROL-BLOCKS =                                             */ 28500000
*/*         APCT     - R                                             */ 28800000
*/*         APSC     - R,       C                                    */ 29100000
*/*         APSM     - R,       C                                    */ 29400000
*/*         APSV     - R,       C                                    */ 29700000
*/*                                                                  */ 30000000
*/*   TABLES = NONE                                                  */ 30300000
*/*                                                                  */ 30600000
*/*   MACROS =                                                       */ 30900000
*/*       IGTXEPLG                                                   */ 31200000
*/*       GETMAIN                                                    */ 31500000
*/*                                                                  */ 31800000
*/*   CHANGE-ACTIVITY = NONE                                         */ 32100000
*/*                                                                  */ 32400000
*/*   MESSAGES = NONE                                                */ 32700000
*/*                                                                  */ 33000000
*/*   ABEND-CODES = NONE                                             */ 33300000
*/*                                                                  */ 33600000
*/* * END OF SPECIFICATIONS **************************************** */ 33900000
         EJECT ,                                                        34200000
IGTUAB   CSECT ,                                                        34500000
         COPY  IGTXSYSO            DEFINE SCP CHARACTERISTICS           34800000
         SPACE 2                                                        35100000
         USING APCT,APCTPTR        VPSS CONTROL TABLE                   35400000
         USING APSV,APSVPTR        VPSS SAVE AREA                       35700000
         USING IGTUAB,APENTRY      BASE REGISTER FOR IGTUAB             36000000
         B     STARTUP             SKIP IDENTIFIER                      36300000
         DC    YL1(COREIDE-*-1)    LENGTH OF ID                         36600000
         DC    C'IGTUAB'           CSECT NAME                           36900000
         DC    C' &SYSDATE'        ASSEMBLY DATE                        37200000
COREIDE  EQU   *                   END OF ID                            37500000
         SPACE 2                                                        37800000
STARTUP  DS    0H                  START OF CODE                        38100000
         AH    RCORESZ,EXTRA       ADD EXTRA STOREAGE REQUIREMENT       38400000
*                                  * FOR APSC AND FOR                   38700000
*                                  * DOUBLEWORD ROUND-UP                39000000
         N     RCORESZ,ROUND       ROUND TO DOUBLEWORD. ALSO            39300000
*                                  * ELIMINATE SUBPOOL                  39600000
         L     APSCPTR,APSVAPSC    POINT TO CURRENT STORAGE             39900000
*                                  * MANAGEMENT CONTROL WORD            40200000
         USING APSC,APSCPTR        TELL ASSEMBLER                       40500000
         AH    RCORESZ,APSCCRSZ    = REQUIRED_LN + CURRENT_LN           40800000
         CH    RCORESZ,APSCMXSZ    GREATER THAN MAXIMUM SIZE ?          41100000
         BH    NEWAPSM             YES, GO GET NEW APSM                 41400000
         SPACE 3                                                        41700000
*                                                                       42000000
** NEW SAVEAREA/WORKAREA FITS IN CURRENT EXTRA WORKAREA                 42300000
** CONSTRUCT NEW SAVE AREA, AND ADJUST SIZE FIELDS.                     42600000
*                                                                       42900000
         PUSH  USING               SAVE CURRENT USING VALUES            43200000
         DROP  APENTRY             CHANGING BASE REGISTER VALUE         43500000
*                                  * NOTE: NO BASE REGISTER USED        43800000
         LH    RCOREDIF,APSCCRSZ   CURRENT SAVEAREA SIZE                44100000
         SR    RCORESZ,RCOREDIF    RCORESZ = USER'S REQUEST SIZE        44400000
*                                  * + APSC SIZE, ROUNDED-              44700000
*                                  * UP TO DOUBLE WORD BOUNDARY         45000000
         SH    RCOREDIF,APSCMXSZ   SET RCOREDIF = APSCMXSZ-APSCCRSZ     45300000
         LCR   RCOREDIF,RCOREDIF   * = MAX SIZE OF NEW SAVEAREA-        45600000
*                                  * WORKAREA                           45900000
         AH    APSCPTR,APSCCRSZ    POINT TO NEW APSC                    46200000
         STH   RCOREDIF,APSCMXSZ   NEW MAXIMUM LENGTH                   46500000
         STH   RCORESZ,APSCCRSZ    NEW CURRENT LENGTH                   46800000
         SR    RTEMPA,RTEMPA       CLEAR PREVIOUS POINTER               47100000
         ST    RTEMPA,APSCAPSC     * SINCE FIRST APSC THIS APSV         47400000
         ST    APSCPTR,APSVAPSC-APSV+APSC+APSCLN SET NEW                47700000
*                                  * SAVEAREA STORAGE MANAGEMENT        48000000
*                                  * CONTROL WORD POINTER TO            48300000
*                                  * THIS APSC                          48600000
         LA    RNEWAPSV,APSCLN(,APSCPTR) SKIP APSC.                     48900000
*                                  * NOW POINTING TO NEW                49200000
*                                  * SAVEAREA/WORKAREA                  49500000
         ST    RTEMPA,APSVFREE-APSV(,RNEWAPSV) * NO APSM BLOCKS         49800000
*                                  * TO FREE WHEN APSV FREED            50100000
         L     RTEMPA,APSVAPSM     NEW APSVAPSM IS SAME AS              50400000
         ST    RTEMPA,APSVAPSM-APSV(,RNEWAPSV) * PREVIOUS               50700000
*                                  * APSVAPSM SINCE USING               51000000
*                                  * SAME APSM.                         51300000
*                                  * FREED.                             51600000
*                                                                       51900000
** PROCESSING COMPLETE. REG 1 POINTS TO NEW SAVEAREA/WORKAREA.          52200000
** NON-STANDARD OS SAVEAREA FIELDS HAVE BEEN INITIALIZED.               52500000
*                                                                       52800000
         BR    APRETURN            RETURN TO CALLER                     53100000
*                                  * REG 1 -> NEW SAVEAREA              53400000
         SPACE 2                                                        53700000
         POP   USING               RE-ESTABLISH ASSEMBLER POINTERS      54000000
         EJECT ,                                                        54300000
NEWAPSM  EQU   *                   CURRENT APSM WILL NOT                54600000
*                                  * HOLD NEW SAVEAREA/WORKAREA         54900000
*                                  * GETMAIN NEW BLOCK.                 55200000
         SH    RCORESZ,APSCCRSZ    RESTORE REQUIRED SIZE                55500000
         DROP  APENTRY             MODIFYING OLD BASE                   55800000
         L     RTEMPB,APSVAPSM     CURRENT APSM                         56100000
         L     RTEMPB,APSMREGS-APSM(,RTEMPB) POINT TO REGISTER          56400000
*                                  * SAVE AREA                          56700000
         STM   R2,R14,4*(R2-R0)(RTEMPB) SAVE INPUT REGISTERS            57000000
         LR    RREGSPTR,RTEMPB     SAVE REGISTER POINTER                57300000
         SPACE 3                                                        57600000
         BALR  APCODE,0            ESTABLISH BASE REGISTER              57900000
         USING *,APCODE            TELL ASSEMBLER                       58200000
*                                                                       58500000
         LR    RUSERREQ,RCORESZ    SAVE USER'S REQUEST SIZE             58800000
         AH    RCORESZ,CAPSMLN     ALLOW FOR HEADER INFORMATION         59100000
         L     RGETSIZE,APCTSMAD   MINIMUM REQUEST SIZE                 59400000
         CLR   RCORESZ,RGETSIZE    COMPARE REQUESTED:MINIMUM SIZES      59700000
         BNH   USEAPCT             USE MAXIMUM OF THE                   60000000
         LR    RGETSIZE,RCORESZ    * TWO VALUES                         60300000
USEAPCT  EQU   *                   RGETSIZE = MAX                       60600000
         A     RGETSIZE,CARNDPGE   ROUND UPWARD TO                      60900000
         N     RGETSIZE,CRNDPGE    * PAGE BOUNDARY                      61200000
         AIF   ('&VPSSSYS' NE 'MVS').VS1A IF MVS SCP, USE SVC 120       61500000
         L     RTEMPE,APSVAPSM     USE SUBPOOL FROM PREVIOUS            61800000
         IC    R15,APSMSIZE-APSM(RTEMPE) * APSM (MUST BE REG 15)        62100000
*                                  * (PROPOGATED FROM INITIAL=YES CALL) 62400000
         LR    RCORESZ,RGETSIZE    ALSO SET RCORESZ = MAX               62700000
         ICM   RGETSIZE,BYTE0,APSMSIZE-APSM(RTEMPE) SAVE SP FOR FREE    63000000
         GETMAIN RU,               UNCONDITIONALLY OBTAIN STORAGE      *63300000
               LV=(0),             * REQUEST LENGTH IN REG 0           *63600000
               SP=(15),            * SUBPOOL IN REGISTER 15            *63900000
               BNDRY=PAGE          * START ON PAGE BOUNDARY             64200000
         AGO   .SYSA               SKIP VS1 GENERATE                    64500000
.VS1A    ANOP  ,                   GENERATE VS1 GETMAIN                 64800000
         L     RTEMPE,APSVAPSM     USE SUBPOOL FROM PREVIOUS            65100000
         ICM   RGETSIZE,BYTE0,APSMSIZE-APSM(RTEMPE) * APSM              65400000
*                                  * (PROPOGATED FROM INITIAL=YES CALL) 65700000
         LR    RCORESZ,RGETSIZE    ALSO SET RCORESZ = MAX               66000000
         GETMAIN R,LV=(0)          OBTAIN REQUESTED STORAGE             66300000
.SYSA    ANOP  ,                   GETMAIN GENERATED                    66600000
*                                  * REG1 -> ACQUIRED AREA              66900000
*                                                                       67200000
** CONSTRUCT NEW APSM AND CHAIN TO PREVIOUS APSM CONTROL BLOCKS         67500000
** IN LIFO ORDER. THEN CONSTRUCT USER'S SAVE AREA - WORK AREA.          67800000
*                                                                       68100000
         USING APSM,APSMPTR        USE TO CONSTRUCT APSM                68400000
         ST    RGETSIZE,APSMSIZE   LENGTH OF APSM                       68700000
         L     RTEMPC,ID           FILL-IN STORAGE IDENTIFIER           69000000
         ST    RTEMPC,APSMID       * ('APSM')                           69300000
         SR    RTEMPC,RTEMPC       SHOW NO MORE APSM ENTRIES            69600000
         ST    RTEMPC,APSMAPSV     * OWNED BY THIS APSV                 69900000
         ST    RTEMPC,APSMNEXT     SHOW END OF APSM CHAIN               70200000
         L     RTAPSM,APSVAPSM     POINT TO CURRENT APSM                70500000
         ST    RTAPSM,APSMPREV     CHAIN THE APSM ENTRIES               70800000
*                                  * TOGETHER (FORWARD & BACKWARD)      71100000
         ST    APSMPTR,APSMNEXT-APSM(,RTAPSM) * TOGETHER (LIFO)         71400000
*                                  * USE FORWARD & BACKWARD CHAINS      71700000
         ST    RREGSPTR,APSMREGS   PASS STORAGE MANAGEMENT REGISTER     72000000
*                                  * SAVEAREA POINTER TO NEW APSM.      72300000
*                                                                       72600000
** CONSTRUCT USER SAVEAREA-WORKAREA                                     72900000
*                                                                       73200000
         DROP  APSVPTR             PREVENT USE OF R13                   73500000
         USING APSV-APSMLN0-APSCLN,APSMPTR SAVEAREA AFTER               73800000
*                                  * HEADER & APSC                      74100000
         ST    APSMPTR,APSVAPSM    START OF APSM                        74400000
         ST    APSMPTR,APSVFREE    FREE APSM AT SUBROUTINE TERMINATION  74700000
         LA    APSCPTR,APSMLN0(,APSMPTR) SKIP APSM HEADER               75000000
         USING APSV,APSVPTR        RE-ESTABLISH REG 13                  75300000
         USING APSC,APSCPTR        NOW POINTING AT APSC                 75600000
         SH    RGETSIZE,CAPSMLN    ELIMINATE HEADER SIZE                75900000
         STH   RGETSIZE,APSCMXSZ   LENGTH OF AVAILABLE AREA             76200000
         STH   RUSERREQ,APSCCRSZ   CURRENT LENGTH                       76500000
         SR    RTEMPD,RTEMPD       FIRST NO PREVIOUS APSC               76800000
         ST    RTEMPD,APSCAPSC     * THIS APSV                          77100000
         ST    APSCPTR,APSVAPSC-APSV+APSCLN(,APSCPTR) POINTER           77400000
*                                  * TO CURRENT APSC                    77700000
         LA    RNEWAPSV,APSCLN(,APSCPTR) POINT TO NEW                   78000000
*                                  * SAVEAREA/WORKAREA                  78300000
         LM    R2,R14,4*(R2-R0)(RREGSPTR) RE-LOAD INPUT REGS            78600000
*                                  * REG 1 -> NEW SAVEAREA/WORKAREA     78900000
         BR    APRETURN            RETURN TO CALLER.                    79200000
         EJECT ,                                                        79500000
********************************************************************* * 79800000
**                                                                    * 80100000
**       CONSTANTS                                                    * 80400000
**                                                                    * 80700000
********************************************************************* * 81000000
         SPACE 2                                                        81300000
CAPSMLN  DC    Y(APSMLN0)          LENGTH OF APSM HEADER                81600000
EXTRA    DC    Y(7+APSCLN)         ALLOW FOR APSC AND ROUND TO          81900000
ROUND    DC    A(X'00007FF8')      * MULTIPLE OF 8 (DOUBLEWORD) AND     82200000
*                                  * CLEAR THE SUBPOOL NUMBER.          82500000
ID       DC    CL4'APSM'           CONTROL BLOCK IN-CORE ID             82800000
         AIF   ('&VPSSSYS' NE 'MVS').VS1B IF MVS PAGE=4K                83100000
CARNDPGE DC    A(X'00000FFF')      FORCE UPWARD TO PAGE                 83400000
CRNDPGE  DC    A(X'00007000')      FORCE TO PAGE                        83700000
         AGO   .SYSB               SKIP VS1 CONSTANT GENERATES          84000000
.VS1B    ANOP  ,                   IF VS1, PAGE=2K                      84300000
CARNDPGE DC    A(X'000007FF')      FORCE UPWARD TO PAGE                 84600000
CRNDPGE  DC    A(X'00007800')      FORCE TO PAGE                        84900000
.SYSB    ANOP  ,                   PAGE ROUND CONSTANTS GENERATED       85200000
         SPACE 5                                                        85500000
********************************************************************* * 85800000
**                                                                    * 86100000
**       EQUATES                                                      * 86400000
**                                                                    * 86700000
********************************************************************* * 87000000
         SPACE 2                                                        87300000
BYTE0    EQU   B'1000'             BYTE 0 OF WORD                       87600000
BYTE1    EQU   B'0100'             BYTE 1 OF WORD                       87900000
BYTE2    EQU   B'0010'             BYTE 2 OF WORD                       88200000
BYTE3    EQU   B'0001'             BYTE 3 OF WORD                       88500000
         SPACE 5                                                        88800000
********************************************************************* * 89100000
**                                                                    * 89400000
**       DEFINE REQUESTED VPSS DSECTS                                 * 89700000
**                                                                    * 90000000
********************************************************************* * 90300000
         SPACE 2                                                        90600000
         IGTXDSCT IGTXAPCT         CONTROL TABLE                        90900000
         IGTXDSCT IGTXAPSC         STORAGE MANAGEMENT CONTROL WORD      91200000
         IGTXDSCT IGTXAPSM         STORAGE MANAGEMENT CONTROL BLOCK     91500000
         IGTXDSCT IGTXAPSV         SAVEAREA/WORKAREA                    91800000
         EJECT ,                                                        92100000
         IGTXEPLG ,                DSECTS AND REGISTER EQUATES          92400000
RCORESZ  EQU   R0                  REQUESTED DYNAMIC STORAGE SIZE       92700000
RTEMPC   EQU   R0                  TEMPORARY VALUE HOLDER               93000000
RTEMPD   EQU   R0                  TEMPORARY VALUE HOLDER               93300000
RTEMPE   EQU   R1                  TEMPORARY VALUE HOLDER               93600000
RNEWAPSV EQU   R1                  NEW APSV ADDRESS                     93900000
*                                  * (RETURNED TO USER)                 94200000
RREGSPTR EQU   R3                  POINTER TO SAVE REGISTERS            94500000
RGETSIZE EQU   R8                  GETMAIN SIZE (INCLUDES SUBPOOL)      94800000
RUSERREQ EQU   R9                  USER STORAGE REQUEST SIZE            95100000
RTAPSM   EQU   R10                 TEMPORARY POINTER TO APSM            95400000
RCOREDIF EQU   R15                 REMAINING EXTRA WORKAREA SPACE       95700000
RTEMPA   EQU   R15                 TEMPORARY VALUE HOLDER               96000000
RTEMPB   EQU   R15                 TEMPORARY VALUE HOLDER               96300000
         SPACE 5                                                        96600000
         END   ,                                                        96900000
