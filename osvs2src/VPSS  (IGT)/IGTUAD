TUAD     TITLE 'IGTUAD -- GET DYNAMIC STORAGE'                          00300000
IGTUAD   CSECT ,                   GET CORE ROUTINE                     00600000
*/* * START OF SPECIFICATIONS ************************************** */ 00900000
*/*                                                                  */ 01200000
*/*   MODULE-NAME = IGTUAD                                           */ 01500000
*/*                                                                  */ 01800000
*/*   DESCRIPTIVE-NAME = GET DYNAMIC STORAGE                         */ 02100000
*/*                      (IGTYGSTK MACRO SUPPORT)                    */ 02400000
*/*                                                                  */ 02700000
*/*   COPYRIGHT = NONE                                               */ 03000000
*/*                                                                  */ 03300000
*/*   STATUS = RELEASE 1, MODIFICATION LEVEL 0.                      */ 03600000
*/*                                                                  */ 03900000
*/*   FUNCTION = THIS ROUTINE PROVIDEDS THE CALLER WITH A DYNAMIC    */ 04200000
*/*       WORKAREA.  IF POSSIBLE, THE STORAGE WILL COME FROM         */ 04500000
*/*       THE EXTRA STORAGE IN THE CURRENT APSM.  OTHERWISE A NEW    */ 04800000
*/*       STORAGE BLOCK IS ACQUIRED BY A GETMAIN AND DESCRIBED       */ 05100000
*/*       BY A NEW APSM.  THIS NEWLY ACQUIRED AREA WILL BE RELEASED  */ 05400000
*/*       WHEN THE CURRENT SUBROUTINE EXITS.                         */ 05700000
*/*       THIS ROUTINE SUPPORTS THE IGTYGSTK MACRO.                  */ 06000000
*/*       STORAGE MANAGEMENT IS LIFO (LAST ACQUIRED STORAGE MUST     */ 06300000
*/*       BE INITIALLY RELEASED STORAGE).                            */ 06600000
*/*                                                                  */ 06900000
*/*   NOTES =                                                        */ 07200000
*/*     THIS MACRO SUPPORTS THE PARAMETERS REQUIRED FOR THE          */ 07500000
*/*     GETMAIN R,LV= MACRO.                                         */ 07800000
*/*                                                                  */ 08100000
*/*     DEPENDENCIES =                                               */ 08400000
*/*        REGISTER 13 MUST POINT TO A VPSS EXTENDED SAVEAREA        */ 08700000
*/*        (APSV) WHICH HAS BEEN CREATED BY IGTUAA, IGTUAD, OR       */ 09000000
*/*        IGTUAF.                                                   */ 09300000
*/*                                                                  */ 09600000
*/*     RESTRICTIONS =                                               */ 09900000
*/*        SINCE THIS ROUTINE MAY BE ENTERED IN EITHER PROBLEM OR    */ 10200000
*/*        SUPERVISOR STATE AND IN ANY KEY, THIS PROGRAM MAY NOT     */ 10500000
*/*        ISSUE PRIVLEDGED INSTRUCTIONS OR UPDATE SYSTEM CONTROL    */ 10800000
*/*        BLOCKS.                                                   */ 11100000
*/*        STORAGE MANAGEMENT IS LIFO (LAST ACQUIRED STORAGE MUST    */ 11400000
*/*        BE INITIALLY RELEASED STORAGE).                           */ 11700000
*/*                                                                  */ 12000000
*/*     REGISTER-CONVENTIONS = STANDARD VPSS CONVENTIONS             */ 12300000
*/*                                                                  */ 12600000
*/*     PATCH-LABEL = APPATCH                                        */ 12900000
*/*                                                                  */ 13200000
*/*   MODULE-TYPE = CSECT                                            */ 13500000
*/*                                                                  */ 13800000
*/*     PROCESSOR = VS/2 ASSEMBLER                                   */ 14100000
*/*                                                                  */ 14400000
*/*     MODULE SIZE = SEE END OF ASSEMBLER LISTING                   */ 14700000
*/*                                                                  */ 15000000
*/*     ATTRIBUTES = RE-ENTRANT.                                     */ 15300000
*/*                                                                  */ 15600000
*/*   ENTRY-POINT = IGTUAD                                           */ 15900000
*/*                                                                  */ 16200000
*/*     PURPOSE = SAME AS FUNCTION                                   */ 16500000
*/*                                                                  */ 16800000
*/*     LINKAGE = ENTERED UNLOCKED, TASK MODE, ENABLED. MAY BE       */ 17100000
*/*               IN ANY KEY AND EITHER PROBLEM OR SUPERVISOR        */ 17400000
*/*               STATE.                                             */ 17700000
*/*                                                                  */ 18000000
*/*     INPUT = THE FOLLOWING REGISTERS ARE INITIALIZED ON ENTRY     */ 18300000
*/*             (SET BY MACRO IGTYGSTK) --                           */ 18600000
*/*         REGISTER  0 - BYTE-0 IGNORED. STORAGE ACQUIRED FROM      */ 18900000
*/*                       SAME SUBPOOL AS THE SAVEAREA (APSV).       */ 19200000
*/*                       BYTES 1-3 CONTAIN THE REQUIRED LENGTH      */ 19500000
*/*                       OF THE NEW WORKAREA.                       */ 19800000
*/*         REGISTER  1 - IGNORED.                                   */ 20100000
*/*         REGISTER 12 - ADDRESS OF THE APCT.                       */ 20400000
*/*         REGISTER 13 - ADDRESS OF CURRENT APSV (VPSS EXTENDED     */ 20700000
*/*                       SAVEAREA).                                 */ 21000000
*/*         REGISTER 14 - RETURN ADDRESS                             */ 21300000
*/*         REGISTER 15 - ADDRESS OF THIS ROUTINE (IGTUAD).          */ 21600000
*/*                                                                  */ 21900000
*/*     OUTPUT = A DYNAMIC AREA IS RETURNED TO THE CALLER.  IF       */ 22200000
*/*              POSSIBLE THE AREA IS CARVED FROM THE REMAINING      */ 22500000
*/*              FREEAREA IN THE CURRENT APSM.  OTHERWISE A          */ 22800000
*/*              NEW APSM IS CREATED FOR THE NEWLY ACQUIRED STORAGE. */ 23100000
*/*              THE NEW APSM IS FREED WHEN THE CURRENT SUBROUTINE   */ 23400000
*/*              EXITS (OR A IGTYFSTK IS ISSUED).                    */ 23700000
*/*                                                                  */ 24000000
*/*   EXIT-NORMAL = THE FOLLOWING REGISTERS ARE RETURNED TO THE      */ 24300000
*/*                 CALLER --                                        */ 24600000
*/*                 REGISTER  0 - UNPREDICTABLE                      */ 24900000
*/*                 REGISTER  1 - ADDRESS OF ACQUIRED DYNAMIC AREA   */ 25200000
*/*                 REGISTER 2-13 - SAME AS ON INPUT                 */ 25500000
*/*                 REGISTER 14 - RETURN ADDRESS                     */ 25800000
*/*                 REGISTER 15 - UNPREDICTABLE                      */ 26100000
*/*                                                                  */ 26400000
*/*   EXIT-ERROR  = NONE.                                            */ 26700000
*/*                                                                  */ 27000000
*/*   EXTERNAL-REFERENCES = THE FOLLOWING ROUTINES AND               */ 27300000
*/*                         CONTROL BLOCKS ARE USED.                 */ 27600000
*/*                                                                  */ 27900000
*/*     ROUTINES = NONE.                                             */ 28200000
*/*                                                                  */ 28500000
*/*     DATA-AREAS = NONE                                            */ 28800000
*/*                                                                  */ 29100000
*/*     CONTROL-BLOCKS =                                             */ 29400000
*/*         APCT     - R                                             */ 29700000
*/*         APSC     - R, M,    C                                    */ 30000000
*/*         APSM     - R, M,    C                                    */ 30300000
*/*         APSV     - R, M                                          */ 30600000
*/*                                                                  */ 30900000
*/*   TABLES = NONE                                                  */ 31200000
*/*                                                                  */ 31500000
*/*   MACROS =                                                       */ 31800000
*/*       IGTXEPLG                                                   */ 32100000
*/*       GETMAIN                                                    */ 32400000
*/*                                                                  */ 32700000
*/*   CHANGE-ACTIVITY = NONE                                         */ 33000000
*/*                                                                  */ 33300000
*/*   MESSAGES = NONE                                                */ 33600000
*/*                                                                  */ 33900000
*/*   ABEND-CODES = NONE                                             */ 34200000
*/*                                                                  */ 34500000
*/* * END OF SPECIFICATIONS **************************************** */ 34800000
         EJECT ,                                                        35100000
         COPY  IGTXSYSO            DEFINE SCP CHARACTERISTICS           35400000
         SPACE 2                                                        35700000
         USING APCT,APCTPTR        VPSS CONTROL TABLE                   36000000
         USING APSV,APSVPTR        VPSS SAVE AREA                       36300000
         USING IGTUAD,APENTRY      BASE REGISTER                        36600000
         SPACE 5                                                        36900000
         B     STARTUP             SKIP IDENTIFIER                      37200000
         DC    YL1(COREIDE-*-1)    LENGTH OF ID                         37500000
         DC    C'IGTUAD'           CSECT NAME                           37800000
         DC    C' &SYSDATE'        ASSEMBLY DATE                        38100000
COREIDE  EQU   *                   END OF ID                            38400000
         SPACE 2                                                        38700000
STARTUP  DS    0H                  START OF CODE                        39000000
         L     APSCPTR,APSVAPSC    POINT TO STORAGE MANAGEMENT          39300000
         USING APSC,APSCPTR        * CONTROL WORD                       39600000
         AH    RCORESZ,EXTRA       ROUND REQUEST UP TO                  39900000
         N     RCORESZ,ROUND       * DOUBLEWORD BOUNDARY                40200000
         AH    RCORESZ,APSCCRSZ    = AMOUNT OF APSM USED                40500000
         CH    RCORESZ,APSCMXSZ    TOO MUCH ?                           40800000
         BH    TOOMUCH             YES--GO GET NEW BLOCK                41100000
*                                                                       41400000
** NEW WORKAREA CAN BE OBTAINED FROM CURRENT APSM                       41700000
*                                                                       42000000
         PUSH  USING               SAVE CURRENT STATUS                  42300000
         DROP  APENTRY             MODIFYING BASE REGISTER              42600000
*                                  * NOTE: NO BASE REGISTER EXISTS      42900000
         LH    RTEMPA,APSCCRSZ     SAVE CURRENT USED LENGTH             43200000
         STH   RCORESZ,APSCCRSZ    NEW 'CURRENTLY USED' SIZE            43500000
         AR    APSCPTR,RTEMPA      DYNAMIC AREA = START OF              43800000
*                                  * APSC + PREVIOUS_APSCCRSZ           44100000
*                                  * REG 1 -> DYNAMIC AREA              44400000
         SPACE 2                                                        44700000
         BR    APRETURN            DONE SO RETURN TO CALLER             45000000
         SPACE 2                                                        45300000
         POP   USING               RESET REG USEAGE                     45600000
         EJECT ,                                                        45900000
TOOMUCH  EQU   *                   CANNOT USE CURRENT APSM              46200000
*                                  * MUST ACQUIRE NEW APSM              46500000
         SH    RCORESZ,APSCCRSZ    RESTORE REQUIRED SIZE                46800000
         DROP  APENTRY             MODIFYING OLD BASE                   47100000
         L     RTEMPB,APSVAPSM     CURRENT APSM                         47400000
         L     RTEMPB,APSMREGS-APSM(,RTEMPB) POINT TO                   47700000
*                                  * REGISTER SAVEAREA                  48000000
         STM   R2,R14,4*(R2-R0)(RTEMPB) SAVE INPUT REGISTERS            48300000
         LR    RREGSPTR,RTEMPB     SAVE POINTER TO REGS                 48600000
         SPACE 3                                                        48900000
         BALR  APCODE,0            ESTABLISH NEW BASE                   49200000
         USING *,APCODE            TELL ASSEMBLER                       49500000
*                                                                       49800000
         LR    RUSERREQ,RCORESZ    SAVE USER'S REQUEST SIZE             50100000
         AH    RCORESZ,CAPSMLN1    ALLOW FOR HEADER INFORMATION + APSC  50400000
         L     RGETSIZE,APCTSMAD   MINIMUM REQUEST SIZE                 50700000
         CLR   RCORESZ,RGETSIZE    COMPARE REQUESTED:MINIMUM SIZES      51000000
         BNH   USEAPCT             USE MAXIMUM OF THE                   51300000
         LR    RGETSIZE,RCORESZ    * TWO VALUES                         51600000
USEAPCT  EQU   *                   RGETSIZE = MAX                       51900000
         A     RGETSIZE,CARNDPGE   ROUND REQUEST UP TO                  52200000
         N     RGETSIZE,CRNDPGE    * PAGE SIZE MULTIPLE                 52500000
         AIF   ('&VPSSSYS' NE 'MVS').VS1A IF MVS SCP, USE SVC 120       52800000
         L     RTEMPE,APSVAPSM     USE SUBPOOL FROM PREVIOUS            53100000
         IC    R15,APSMSIZE-APSM(RTEMPE) * APSM (MUST BE REG 15)        53400000
*                                  * (PROPOGATED FROM INITIAL=YES CALL) 53700000
         LR    RCORESZ,RGETSIZE    ALSO SET RCORESZ = MAX               54000000
         ICM   RGETSIZE,BYTE0,APSMSIZE-APSM(RTEMPE) SAVE SP FOR FREE    54300000
         GETMAIN RU,               UNCONDITIONALLY OBTAIN STORAGE      *54600000
               LV=(0),             * REQUEST LENGTH IN REG 0           *54900000
               SP=(15),            * SUBPOOL IN REGISTER 15            *55200000
               BNDRY=PAGE          * START ON PAGE BOUNDARY             55500000
         AGO   .SYSA               SKIP VS1 GENERATE                    55800000
.VS1A    ANOP  ,                   GENERATE VS1 GETMAIN                 56100000
         L     RTEMPE,APSVAPSM     USE SUBPOOL FROM PREVIOUS            56400000
         ICM   RGETSIZE,BYTE0,APSMSIZE-APSM(RTEMPE) * APSM              56700000
*                                  * (PROPOGATED FROM INITIAL=YES CALL) 57000000
         LR    RCORESZ,RGETSIZE    ALSO SET RCORESZ = MAX               57300000
         GETMAIN R,LV=(0)          OBTAIN REQUESTED STORAGE             57600000
.SYSA    ANOP  ,                   GETMAIN GENERATED                    57900000
*                                  * REG1 -> ACQUIRED AREA              58200000
*                                                                       58500000
** CONSTRUCT NEW APSM AND CHAIN TO PREVIOUS APSM CONTROL BLOCKS         58800000
** IN LIFO ORDER. THEN CONSTRUCT USER'S DYNAMIC AREA.                   59100000
*                                                                       59400000
         USING APSM,APSMPTR        POINTING TO NEW APSM                 59700000
         L     RTEMPC,ID           FILL-IN STORAGE IDENTIFIER           60000000
         ST    RTEMPC,APSMID       * ('APSM')                           60300000
         ST    RGETSIZE,APSMSIZE   LENGTH OF APSM                       60600000
         L     RTEMPC,APSVFREE     FREE THIS APSM WHEN OWNING           60900000
         ST    RTEMPC,APSMAPSV     * SUBROUTINE EXITS.                  61200000
         ST    APSMPTR,APSVFREE    * (LIFO QUEUE).                      61500000
         L     RTAPSM,APSVAPSM     GET 'CURRENT' APSM                   61800000
         ST    APSMPTR,APSVAPSM    SHOW NEW 'CURRENT' APSM              62100000
         ST    APSMPTR,APSMNEXT-APSM(,RTAPSM) CHAIN APSM ENTRIES        62400000
         ST    RTAPSM,APSMPREV     * TOGETHER (FORWARD & BACKWARD)      62700000
         SR    RTEMPC,RTEMPC       SHOW NEW APSM IS                     63000000
         ST    RTEMPC,APSMNEXT     * CURRENT END OF CHAIN.              63300000
         ST    RREGSPTR,APSMREGS   PASS STORAGE MANAGEMENT REGISTER     63600000
*                                  * SAVEAREA POINTER TO NEW APSM.      63900000
*                                                                       64200000
** OBTAIN USER'S DYNAMIC AREA                                           64500000
*                                                                       64800000
         LA    APSCPTR,APSMLN0(,APSMPTR) POINT TO APSC                  65100000
         USING APSC,APSCPTR        * FOLLOWING APSM HEADER              65400000
         L     RTEMPD,APSVAPSC     SAVE ADDRESS OF CURRENT              65700000
         ST    RTEMPD,APSCAPSC     * APSC (LIFO CHAIN).                 66000000
         SH    RGETSIZE,CAPSMLN    ELIMINATE HEADER SIZE                66300000
         STH   RGETSIZE,APSCMXSZ   MAX LENGTH IN APSC                   66600000
         LA    RUSERREQ,APSCLN(,RUSERREQ) INCREMENT USER REQUEST        66900000
*                                  * SIZE BY SIZE OF APSC               67200000
         STH   RUSERREQ,APSCCRSZ   CURRENT LENGTH                       67500000
         ST    APSCPTR,APSVAPSC-APSV+APSCLN(,APSCPTR) UPDATE            67800000
*                                  * SAVEAREA TO POINT TO NEW APSC      68100000
         LA    RUSRAREA,APSCLN(,APSCPTR) POINT TO USER'S AREA           68400000
         SPACE 2                                                        68700000
         LM    R2,R14,4*(R2-R0)(RREGSPTR) RELOAD INPUT REGS             69000000
*                                  * REG 1 -> DYNAMIC AREA              69300000
         BR    APRETURN            RETURN TO CALLER                     69600000
         EJECT ,                                                        69900000
********************************************************************* * 70200000
**                                                                    * 70500000
**       CONSTANTS                                                    * 70800000
**                                                                    * 71100000
********************************************************************* * 71400000
         SPACE 2                                                        71700000
CAPSMLN1 DC    Y(APSMLN0+APSCLN)   INCREASE USER REQUEST FOR            72000000
*                                  * EXTRA CONTROL BLOCKS               72300000
CAPSMLN  DC    Y(APSMLN0)          ELIMINATE APSM HEADER FROM           72600000
*                                  MAXIMUM SIZE IN APSC                 72900000
EXTRA    DC    Y(7)                USED TO ROUND VALUE TO MULTIPLE      73200000
ROUND    DC    A(X'00007FF8')      * OF 8 (DOUBLEWORD) AND TO CLEAR     73500000
*                                  * THE SUBPOOL NUMBER                 73800000
ID       DC    CL4'APSM'           CONTROL BLOCK IN-CORE IDENTIFIER     74100000
         AIF   ('&VPSSSYS' NE 'MVS').VS1B IF MVS, PAGE = 4K             74400000
CARNDPGE DC    A(X'00000FFF')      ROUND UP TO PAGE BOUNDARY            74700000
CRNDPGE  DC    A(X'00007000')      * CONSTANTS                          75000000
         AGO   .SYSB               SKIP VS1 VERSIONS OF CONSTANTS       75300000
.VS1B    ANOP  ,                   VS1 PAGE SIZE = 2K                   75600000
CARNDPGE DC    A(X'000007FF')      ROUND UP TO PAGE BOUNDARY            75900000
CRNDPGE  DC    A(X'00007800')      * CONSTANTS                          76200000
.SYSB    ANOP  ,                   PAGE SIZE CONSTANTS GENERATED        76500000
         SPACE 5                                                        76800000
********************************************************************* * 77100000
**                                                                    * 77400000
**       EQUATES                                                      * 77700000
**                                                                    * 78000000
********************************************************************* * 78300000
         SPACE 2                                                        78600000
BYTE0    EQU   B'1000'             BYTE 0 OF WORD                       78900000
BYTE1    EQU   B'0100'             BYTE 1 OF WORD                       79200000
BYTE2    EQU   B'0010'             BYTE 2 OF WORD                       79500000
BYTE3    EQU   B'0001'             BYTE 3 OF WORD                       79800000
         SPACE 5                                                        80100000
********************************************************************* * 80400000
**                                                                    * 80700000
**       DEFINE REQUESTED VPSS DSECTS                                 * 81000000
**                                                                    * 81300000
********************************************************************* * 81600000
         SPACE 2                                                        81900000
         IGTXDSCT IGTXAPCT         CONTROL TABLE                        82200000
         IGTXDSCT IGTXAPSC         STORAGE MANAGEMENT CONTROL WORD      82500000
         IGTXDSCT IGTXAPSM         STORAGE MANAGEMENT CONTROL BLOCK     82800000
         IGTXDSCT IGTXAPSV         SAVEAREA/WORKAREA                    83100000
         EJECT ,                                                        83400000
         IGTXEPLG ,                DSECTS & REGISTERS                   83700000
RCORESZ  EQU   R0                  REQUESTED DYNAMIC STORAGE SIZE       84000000
RTEMPC   EQU   R0                  TEMPORARY VALUE HOLDER               84300000
RTEMPD   EQU   R0                  TEMPORARY VALUE HOLDER               84600000
RTEMPE   EQU   R1                  TEMPORARY VALUE HOLDER               84900000
RUSRAREA EQU   R1                  RETURN POINTER TO USER'S             85200000
*                                  * ACQUIRED AREA                      85500000
RREGSPTR EQU   R3                  POINTER TO SAVE REGISTERS            85800000
RGETSIZE EQU   R8                  GETMAIN SIZE (INCLUDES SUBPOOL)      86100000
RUSERREQ EQU   R9                  USER STORAGE REQUEST SIZE            86400000
RTAPSM   EQU   R10                 TEMPORARY POINTER TO APSM            86700000
RTEMPA   EQU   R15                 TEMPORARY VALUE HOLDER               87000000
RTEMPB   EQU   R15                 TEMPORARY VALUE HOLDER               87300000
         SPACE 5                                                        87600000
         END   ,                                                        87900000
