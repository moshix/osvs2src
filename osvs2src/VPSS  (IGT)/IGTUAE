TUAE     TITLE 'IGTUAE -- FREE DYNAMIC STORAGE'                         00500000
IGTUAE   CSECT ,                   FREE CORE ROUTINE                    01000000
*/* * START OF SPECIFICATIONS ************************************** */ 01500000
*/*                                                                  */ 02000000
*/*   MODULE-NAME = IGTUAE                                           */ 02500000
*/*                                                                  */ 03000000
*/*   DESCRIPTIVE-NAME = FREE DYNAMIC STORAGE                        */ 03500000
*/*                      (IGTYFSTK MACRO SUPPORT)                    */ 04000000
*/*                                                                  */ 04500000
*/*   COPYRIGHT = NONE                                               */ 05000000
*/*                                                                  */ 05500000
*/*   STATUS = RELEASE 1, MODIFICATION LEVEL 0.                      */ 06000000
*/*                                                                  */ 06500000
*/*   FUNCTION = THIS ROUTINE RELEASES STORAGE PREVIOUSLY OBTAINED   */ 07000000
*/*       FROM THE CURRENT APSM (USING EITHER IGTYGSV OR IGTYGSTK).  */ 07500000
*/*       IF THE ENTIRE APSM IS NOW FREE, THE APSM AND RELATED       */ 08000000
*/*       STORAGE IS FREEMAINED, AND THE PREVIOUS APSM MADE THE      */ 08500000
*/*       CURRENT APSM.  THIS ROUTINE MAY NOT BE USED TO FREE THE    */ 09000000
*/*       SUBROUTINE'S APSV (USE IGTYFSV).                           */ 09500000
*/*       THIS ROUTINE SUPPORTS THE IGTYFSTK MACRO.                  */ 10000000
*/*       STORAGE MANAGEMENT IS LIFO (LAST ACQUIRED STORAGE MUST     */ 10500000
*/*       BE INITIALLY RELEASED STORAGE).                            */ 11000000
*/*                                                                  */ 11500000
*/*   NOTES =                                                        */ 12000000
*/*     THIS MACRO SUPPORTS THE PARAMETERS REQUIRED FOR THE          */ 12500000
*/*     FREEMAIN R,LV= MACRO.                                        */ 13000000
*/*                                                                  */ 13500000
*/*     DEPENDENCIES =                                               */ 14000000
*/*        REGISTER 13 MUST POINT TO A VPSS EXTENDED SAVEAREA        */ 14500000
*/*        (APSV) WHICH HAS BEEN CREATED BY IGTUAA, IGTUAE, OR       */ 15000000
*/*        IGTUAF.                                                   */ 15500000
*/*                                                                  */ 16000000
*/*     RESTRICTIONS =                                               */ 16500000
*/*        SINCE THIS ROUTINE MAY BE ENTERED IN EITHER PROBLEM OR    */ 17000000
*/*        SUPERVISOR STATE AND IN ANY KEY, THIS PROGRAM MAY NOT     */ 17500000
*/*        ISSUE PRIVLEDGED INSTRUCTIONS OR UPDATE SYSTEM CONTROL    */ 18000000
*/*        BLOCKS.                                                   */ 18500000
*/*        STORAGE MANAGEMENT IS LIFO (LAST ACQUIRED STORAGE MUST    */ 19000000
*/*        BE INITIALLY RELEASED STORAGE).                           */ 19500000
*/*                                                                  */ 20000000
*/*     REGISTER-CONVENTIONS = STANDARD VPSS CONVENTIONS             */ 20500000
*/*                                                                  */ 21000000
*/*     PATCH-LABEL = APPATCH                                        */ 21500000
*/*                                                                  */ 22000000
*/*   MODULE-TYPE = CSECT                                            */ 22500000
*/*                                                                  */ 23000000
*/*     PROCESSOR = VS/2 ASSEMBLER                                   */ 23500000
*/*                                                                  */ 24000000
*/*     MODULE SIZE = SEE END OF ASSEMBLER LISTING                   */ 24500000
*/*                                                                  */ 25000000
*/*     ATTRIBUTES = RE-ENTRANT.                                     */ 25500000
*/*                                                                  */ 26000000
*/*   ENTRY-POINT = IGTUAE                                           */ 26500000
*/*                                                                  */ 27000000
*/*     PURPOSE = SAME AS FUNCTION                                   */ 27500000
*/*                                                                  */ 28000000
*/*     LINKAGE = ENTERED UNLOCKED, TASK MODE, ENABLED. MAY BE       */ 28500000
*/*               IN ANY KEY AND EITHER PROBLEM OR SUPERVISOR        */ 29000000
*/*               STATE.                                             */ 29500000
*/*                                                                  */ 30000000
*/*     INPUT = THE FOLLOWING REGISTERS ARE INITIALIZED ON ENTRY     */ 30500000
*/*             (SET BY MACRO IGTYFSTK) --                           */ 31000000
*/*         REGISTER  0 - BYTE-0 IGNORED. STORAGE RELEASED FROM      */ 31500000
*/*                       SAME SUBPOOL AS APSV.                      */ 32000000
*/*                       BYTES 1-3 CONTAIN THE LENGTH OF THE        */ 32500000
*/*                       DYNAMIC STORAGE TO FREE.                   */ 33000000
*/*         REGISTER  1 - IGNORED.                                   */ 33500000
*/*         REGISTER 12 - ADDRESS OF THE APCT.                       */ 34000000
*/*         REGISTER 13 - ADDRESS OF CURRENT APSV (VPSS EXTENDED     */ 34500000
*/*                       SAVEAREA).                                 */ 35000000
*/*         REGISTER 14 - RETURN ADDRESS                             */ 35500000
*/*         REGISTER 15 - ADDRESS OF THIS ROUTINE (IGTUAE).          */ 36000000
*/*                                                                  */ 36500000
*/*     OUTPUT = THE REQUESTED STORAGE IS ADDED TO THE FREE SPACE    */ 37000000
*/*              IN THE CURRENT APSM.  IF THE APSM IS NOW            */ 37500000
*/*              COMPLETELY FREE, THE CURRENT APSM AREA IS FREED     */ 38000000
*/*              AND THE PREVIOUS APSM MADE CURRENT.                 */ 38500000
*/*                                                                  */ 39000000
*/*   EXIT-NORMAL = THE FOLLOWING REGISTERS ARE RETURNED TO THE      */ 39500000
*/*                 CALLER --                                        */ 40000000
*/*                 REGISTER  0 - UNPREDICTABLE                      */ 40500000
*/*                 REGISTER  1 - UNPREDICTABLE                      */ 41000000
*/*                 REGISTER 2-13 - SAME AS ON INPUT                 */ 41500000
*/*                 REGISTER 14 - RETURN ADDRESS                     */ 42000000
*/*                 REGISTER 15 - UNPREDICTABLE                      */ 42500000
*/*                                                                  */ 43000000
*/*   EXIT-ERROR  = NONE.                                            */ 43500000
*/*                                                                  */ 44000000
*/*   EXTERNAL-REFERENCES = THE FOLLOWING ROUTINES AND               */ 44500000
*/*                         CONTROL BLOCKS ARE USED.                 */ 45000000
*/*                                                                  */ 45500000
*/*     ROUTINES = NONE.                                             */ 46000000
*/*                                                                  */ 46500000
*/*     DATA-AREAS = NONE                                            */ 47000000
*/*                                                                  */ 47500000
*/*     CONTROL-BLOCKS =                                             */ 48000000
*/*         APCT     - R                                             */ 48500000
*/*         APSC     - R, M, D                                       */ 49000000
*/*         APSM     - R, M, D                                       */ 49500000
*/*         APSV     - R, M                                          */ 50000000
*/*                                                                  */ 50500000
*/*   TABLES = NONE                                                  */ 51000000
*/*                                                                  */ 51500000
*/*   MACROS =                                                       */ 52000000
*/*       IGTXEPLG                                                   */ 52500000
*/*       FREEMAIN                                                   */ 53000000
*/*                                                                  */ 53500000
*/*   CHANGE-ACTIVITY = NONE                                         */ 54000000
*/*                                                                  */ 54500000
*/*   MESSAGES = NONE                                                */ 55000000
*/*                                                                  */ 55500000
*/*   ABEND-CODES = NONE                                             */ 56000000
*/*                                                                  */ 56500000
*/* * END OF SPECIFICATIONS **************************************** */ 57000000
         EJECT ,                                                        57500000
         COPY  IGTXSYSO            DEFINE SCP CHARACTERISTICS           58000000
         SPACE 3                                                        58500000
         USING APCT,APCTPTR        VPSS CONTROL TABLE                   59000000
         USING APSV,APSVPTR        VPSS SAVE AREA                       59500000
         USING IGTUAE,APENTRY      BASE REGISTER                        60000000
         SPACE 5                                                        60500000
         B     STARTUP             SKIP IDENTIFIER                      61000000
         DC    YL1(COREIDE-*-1)    LENGTH OF ID                         61500000
         DC    C'IGTUAE'           CSECT NAME                           62000000
         DC    C' &SYSDATE'        ASSEMBLY DATE                        62500000
COREIDE  EQU   *                   END OF ID                            63000000
         SPACE 2                                                        63500000
STARTUP  DS    0H                  START OF CODE                        64000000
         L     APSCPTR,APSVAPSC    POINT TO STORAGE MANAGEMENT          64500000
         USING APSC,APSCPTR        * CONTROL WORD (APSC)                65000000
         AH    RCORESZ,EXTRA       ROUND REQUEST UP TO                  65500000
         N     RCORESZ,ROUND       * DOUBLEWORD BOUNDARY                66000000
         SH    RCORESZ,APSCCRSZ    SET APSCCRSZ =                       66500000
         LCR   RCORESZ,RCORESZ     * AMOUNT OF STORAGE                  67000000
         STH   RCORESZ,APSCCRSZ    * NOW USED.                          67500000
         CH    RCORESZ,CAPSCLN     IF MORE THAN APSC LEFT,              68000000
         BHR   APRETURN            * APSM NOT EMPTY, SO DONE            68500000
         EJECT ,                                                        69000000
*                                                                       69500000
** CURRENT APSM EMPTY. MUST ISSUE FREEMAIN.                             70000000
*                                                                       70500000
         L     APSCPTR,APSCAPSC    PREVIOUS APSC FOR THIS SAVEAREA      71000000
         ST    APSCPTR,APSVAPSC    MAKE THIS APSC CURRENT               71500000
         L     APSMPTR,APSVAPSM    POINT TO APSM HEADER                 72000000
         USING APSM,APSMPTR        POINTING TO CURRENT APSM             72500000
         DROP  APENTRY             CHANGING BASE REGISTER CONTENT       73000000
*                                  NOTE: NO BASE REGISTER EXISTS        73500000
         L     RTEMPA,APSMAPSV     NEXT APSM OWNED BY THIS APSV         74000000
*                                  * (IF ANY)                           74500000
         ST    RTEMPA,APSVFREE     SAVE IN APSV FOR SUBROUTINE EXIT     75000000
         L     RTEMPA,APSMPREV     PREVIOUS APSM                        75500000
         ST    RTEMPA,APSVAPSM     MAKE IT NEW 'CURRENT' APSM           76000000
         SR    RTEMPB,RTEMPB       SET 'NEXT' FIELD IN NEW CURRENT      76500000
         ST    RTEMPB,APSMNEXT-APSM(,RTEMPA) * TO ZERO (END-OF-QUEUE)   77000000
         L     RCORESZ,APSMSIZE    SUBPOOL/SIZE OF APSM TO RELEASE      77500000
         FREEMAIN R,LV=(0),A=(1)   RELEASE APSM                         78000000
         SPACE 2                                                        78500000
         BR    APRETURN            DONE -- SO EXIT                      79000000
         EJECT ,                                                        79500000
********************************************************************* * 80000000
**                                                                    * 80500000
**       CONSTANTS                                                    * 81000000
**                                                                    * 81500000
********************************************************************* * 82000000
         SPACE 2                                                        82500000
CAPSCLN  DC    Y(APSCLN)           LENGTH OF APSC USED TO DETERMINE     83000000
*                                  * IF APSM IS EMPTY.                  83500000
EXTRA    DC    Y(7)                USED TO ROUND VALUE TO MULTIPLE      84000000
ROUND    DC    A(X'00007FF8')      * OF 8 (DOUBLEWORD) AND TO CLEAR     84500000
*                                  * THE SUBPOOL NUMBER                 85000000
         SPACE 5                                                        85500000
********************************************************************* * 86000000
**                                                                    * 86500000
**       DEFINE REQUESTED VPSS DSECTS                                 * 87000000
**                                                                    * 87500000
********************************************************************* * 88000000
         SPACE 2                                                        88500000
         IGTXDSCT IGTXAPCT         CONTROL TABLE                        89000000
         IGTXDSCT IGTXAPSC         STORAGE MANAGEMENT CONTROL WORD      89500000
         IGTXDSCT IGTXAPSM         STORAGE MANAGEMENT CONTROL BLOCK     90000000
         IGTXDSCT IGTXAPSV         SAVEAREA/WORKAREA                    90500000
         EJECT ,                                                        91000000
         IGTXEPLG ,                GENERATE DSECTS & COMMON VALUES      91500000
RCORESZ  EQU   R0                  REQUESTED DYNAMIC STORAGE SIZE       92000000
RTEMPB   EQU   R0                  TEMPORARY VALUE HOLDER               92500000
RTEMPA   EQU   R15                 TEMPORARY VALUE HOLDER               93000000
         SPACE 5                                                        93500000
         END   ,                                                        94000000
