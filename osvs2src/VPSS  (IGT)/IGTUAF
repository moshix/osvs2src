TUAF     TITLE 'IGTUAF -- INITIALIZE SAVEAREA/WORKAREA'                 00500000
IGTUAF   CSECT ,                   INITIALIZE SAVEAREA-WORKAREA         01000000
*/* * START OF SPECIFICATIONS ************************************** */ 01500000
*/*                                                                  */ 02000000
*/*   MODULE-NAME = IGTUAF                                           */ 02500000
*/*                                                                  */ 03000000
*/*   DESCRIPTIVE-NAME = INITIALIZE SAVEAREA/WORKAREA                */ 03500000
*/*                                                                  */ 04000000
*/*   COPYRIGHT = NONE                                               */ 04500000
*/*                                                                  */ 05000000
*/*   STATUS = RELEASE 1, MODIFICATION LEVEL 0.                      */ 05500000
*/*                                                                  */ 06000000
*/*   FUNCTION = THIS ROUTINE WILL CREATE THE REQUIRED VPSS          */ 06500000
*/*       STORAGE MANAGEMENT CONTROL BLOCKS (APSM, APSC, APSV)       */ 07000000
*/*       IN A CALLER PROVIDED WORKAREA AND RETURN THE ADDRESS OF    */ 07500000
*/*       THE APSC.  THIS PROVIDES THE SAME FUNCTION AS IGTUAA       */ 08000000
*/*       EXCEPT THE USER PROVIDES THE WORKAREA AND THE ADDRESS      */ 08500000
*/*       OF THE APSC RATHER THAN THE APSV IS RETURNED.              */ 09000000
*/*       STORAGE MANAGEMENT IS LIFO (LAST ACQUIRED STORAGE MUST     */ 09500000
*/*       BE INITIALLY RELEASED STORAGE).                            */ 10000000
*/*                                                                  */ 10500000
*/*   NOTES =                                                        */ 11000000
*/*     THIS ROUTINE MAY NOT ASSUME REGISTER 13 POINTS TO A          */ 11500000
*/*     SAVEAREA.                                                    */ 12000000
*/*                                                                  */ 12500000
*/*     DEPENDENCIES =                                               */ 13000000
*/*        THE USER IS REQUIRED TO COMPLETE APSCCRSZ AND TO          */ 13500000
*/*        BYPASS THE APSC WHEN BUILDING THE SAVEAREA/DYNAMIC AREA.  */ 14000000
*/*        THE USER MUST ALSO INSURE THAT THE SUPPLIED AREA IS       */ 14500000
*/*        LARGE ENOUGH TO HOLD THE APSM, APSC, AND APSV AS WELL     */ 15000000
*/*        AS THE USER'S CONTROL BLOCKS.                             */ 15500000
*/*                                                                  */ 16000000
*/*     RESTRICTIONS =                                               */ 16500000
*/*        SINCE THIS ROUTINE MAY BE ENTERED IN EITHER PROBLEM OR    */ 17000000
*/*        SUPERVISOR STATE AND IN ANY KEY, THIS PROGRAM MAY NOT     */ 17500000
*/*        ISSUE PRIVLEDGED INSTRUCTIONS OR UPDATE SYSTEM CONTROL    */ 18000000
*/*        BLOCKS.                                                   */ 18500000
*/*                                                                  */ 19000000
*/*     REGISTER-CONVENTIONS = STANDARD VPSS CONVENTIONS             */ 19500000
*/*                                                                  */ 20000000
*/*     PATCH-LABEL = APPATCH                                        */ 20500000
*/*                                                                  */ 21000000
*/*   MODULE-TYPE = CSECT                                            */ 21500000
*/*                                                                  */ 22000000
*/*     PROCESSOR = VS/2 ASSEMBLER                                   */ 22500000
*/*                                                                  */ 23000000
*/*     MODULE SIZE = SEE END OF ASSEMBLER LISTING                   */ 23500000
*/*                                                                  */ 24000000
*/*     ATTRIBUTES = RE-ENTRANT.                                     */ 24500000
*/*                                                                  */ 25000000
*/*   ENTRY-POINT = IGTUAF                                           */ 25500000
*/*                                                                  */ 26000000
*/*     PURPOSE = SAME AS FUNCTION                                   */ 26500000
*/*                                                                  */ 27000000
*/*     LINKAGE = ENTERED UNLOCKED, TASK MODE, ENABLED. MAY BE       */ 27500000
*/*               IN ANY KEY AND EITHER PROBLEM OR SUPERVISOR        */ 28000000
*/*               STATE.                                             */ 28500000
*/*                                                                  */ 29000000
*/*     INPUT = THE FOLLOWING REGISTERS ARE INITIALIZED ON ENTRY --  */ 29500000
*/*         REGISTER  0 - BYTE-0 CONTAINS THE SUBPOOL OF THE         */ 30000000
*/*                       STORAGE.  BYTES 1-3 CONTAIN THE LENGTH     */ 30500000
*/*                       OF THE SUPPLIED AREA.                      */ 31000000
*/*         REGISTER  1 - ADDRESS OF THE USER SUPPLIED STORAGE       */ 31500000
*/*                       TO BE FORMATTED INTO A VPSS APSM.          */ 32000000
*/*         REGISTER 12 - ADDRESS OF THE APCT.                       */ 32500000
*/*         REGISTER 14 - RETURN ADDRESS                             */ 33000000
*/*         REGISTER 15 - ADDRESS OF THIS ROUTINE (IGTUAF).          */ 33500000
*/*         NOTE: REGISTER 13 DOES NOT POINT TO A SAVEAREA.          */ 34000000
*/*                                                                  */ 34500000
*/*     OUTPUT = AN APSM IS CONSTRUCTED TO DESCRIBE THE SUPPLIED     */ 35000000
*/*              STORAGE.  AN APSC AND APSV ARE INITIALIZED FOR      */ 35500000
*/*              THE NEW SAVEAREA/WORKAREA. THE CALLER IS            */ 36000000
*/*              RESPONSIBLE FOR COMPLETING APSCCRSZ.                */ 36500000
*/*                                                                  */ 37000000
*/*   EXIT-NORMAL = THE FOLLOWING REGISTERS ARE RETURNED TO THE      */ 37500000
*/*                 CALLER --                                        */ 38000000
*/*                 REGISTER  0 - UNPREDICTABLE                      */ 38500000
*/*                 REGISTER  1 - ADDRESS OF APSC FOR NEW            */ 39000000
*/*                               SAVEAREA/WORKAREA                  */ 39500000
*/*                 REGISTER 2-13 - SAME AS ON INPUT                 */ 40000000
*/*                 REGISTER 14 - RETURN ADDRESS                     */ 40500000
*/*                 REGISTER 15 - UNPREDICTABLE                      */ 41000000
*/*                                                                  */ 41500000
*/*   EXIT-ERROR  = NONE.                                            */ 42000000
*/*                                                                  */ 42500000
*/*   EXTERNAL-REFERENCES = THE FOLLOWING ROUTINES AND               */ 43000000
*/*                         CONTROL BLOCKS ARE USED.                 */ 43500000
*/*                                                                  */ 44000000
*/*     ROUTINES = NONE.                                             */ 44500000
*/*                                                                  */ 45000000
*/*     DATA-AREAS = NONE                                            */ 45500000
*/*                                                                  */ 46000000
*/*     CONTROL-BLOCKS =                                             */ 46500000
*/*         APCT     - R                                             */ 47000000
*/*         APSC     - R,       C                                    */ 47500000
*/*         APSM     - R,       C                                    */ 48000000
*/*         APSV     - R,       C                                    */ 48500000
*/*                                                                  */ 49000000
*/*   TABLES = NONE                                                  */ 49500000
*/*                                                                  */ 50000000
*/*   MACROS =                                                       */ 50500000
*/*       IGTXEPLG                                                   */ 51000000
*/*                                                                  */ 51500000
*/*   CHANGE-ACTIVITY = NONE                                         */ 52000000
*/*                                                                  */ 52500000
*/*   MESSAGES = NONE                                                */ 53000000
*/*                                                                  */ 53500000
*/*   ABEND-CODES = NONE                                             */ 54000000
*/*                                                                  */ 54500000
*/* * END OF SPECIFICATIONS **************************************** */ 55000000
         EJECT ,                                                        55500000
         COPY  IGTXSYSO            SCP CHARACTERISTICS                  56000000
         SPACE 2                                                        56500000
         USING APCT,APCTPTR        CONTROL TABLE                        57000000
         USING IGTUAF,APENTRY      BASE                                 57500000
         B     STARTUP             SKIP IN-CORE ID                      58000000
         DC    YL1(COREIDE-*-1)    ID LENGTH                            58500000
         DC    C'IGTUAF'           PGM NAME                             59000000
         DC    C' &SYSDATE'        ASSEMBLY DATE                        59500000
COREIDE  EQU   *                   END OF ID                            60000000
         SPACE 2                                                        60500000
STARTUP  DS    0H                  START OF CODE                        61000000
*                                                                       61500000
** SAVE INPUT REGS IN APSM TO BE CONSTRUCTED                            62000000
*                                                                       62500000
         USING APSM,APSMPTR        APSM FIRST IN DYNAMIC AREA           63000000
         ST    RTEMPA,APSMSAVE     SAVE INPUT VALUES                    63500000
*                                                                       64000000
** CONSTRUCT APSM                                                       64500000
*                                                                       65000000
         XC    APSM(APSMLN0),APSM  INITIALIZE UNUSED FIELDS             65500000
         MVC   APSMID,CAPSMID      IN-CORE ID                           66000000
         ST    R0,APSMSIZE         SUBPOOL/LENGTH                       66500000
         LA    RTEMPA,APSMSAVE     REGISTER SAVEAREA POINTER            67000000
         ST    RTEMPA,APSMREGS     * IN THIS APSM                       67500000
*                                                                       68000000
** CONSTRUCT APSC                                                       68500000
*                                                                       69000000
         USING APSC-APSMLN1,APSMPTR APSC AFTER APSM                     69500000
         SH    R0,CAPSMLN1         APSC LENGTH IS INPUT LENGTH          70000000
         STH   R0,APSCMXSZ         * MINUS APSM LENGTH                  70500000
         SR    R0,R0               NO PREVIOUS APSC                     71000000
         ST    R0,APSCAPSC         * EXISTS                             71500000
*                                  NOTE APSCCRSZ FILLED BY USER         72000000
*                                                                       72500000
** CONSTRUCT APSV                                                       73000000
*                                                                       73500000
         USING APSV-APSCLN-APSMLN1,APSMPTR APSV AFTER APSC              74000000
         LA    RTEMPA,APSMLN1(,APSMPTR) POINTER TO APSC IN              74500000
         ST    RTEMPA,APSVAPSC     * APSV                               75000000
         ST    APSMPTR,APSVAPSM    CURRENT APSM                         75500000
         ST    APSMPTR,APSVFREE    FREE APSM AT SUBROUTINE TERMINATION  76000000
*                                                                       76500000
** RELOAD SAVED REGISTERS.                                              77000000
** POINT TO APSC.                                                       77500000
** RETURN.                                                              78000000
*                                                                       78500000
         USING APSM,APSMPTR        TELL ASSEMBLER OF POINTER            79000000
         L     RTEMPA,APSMSAVE     RELOAD CHANGED INPUT REGS            79500000
         LA    APSCPTR,APSM+APSMLN1 POINT TO APSC. RETURNED TO CALLER   80000000
         BR    APRETURN            EXIT                                 80500000
         EJECT ,                                                        81000000
*********************************************************************** 81500000
**                                                                    * 82000000
**       CONSTANTS                                                    * 82500000
**                                                                    * 83000000
*********************************************************************** 83500000
         SPACE 2                                                        84000000
CAPSMLN1 DC    Y(APSMLN1)          INITIAL APSM LENGTH                  84500000
CAPSMID  DC    CL4'APSM'           IN-CORE ID FOR APSM                  85000000
         SPACE 5                                                        85500000
*********************************************************************** 86000000
**                                                                    * 86500000
**       DEFINE REQUESTED VPSS DSECTS                                 * 87000000
**                                                                    * 87500000
*********************************************************************** 88000000
         SPACE 2                                                        88500000
         IGTXDSCT IGTXAPCT         CONTROL TABLE                        89000000
         IGTXDSCT IGTXAPSC         STORAGE MANAGEMENT CONTROL WORD      89500000
         IGTXDSCT IGTXAPSM         STORAGE MANAGEMENT CONTROL BLOCK     90000000
         IGTXDSCT IGTXAPSV         EXTENDED SAVEAREA                    90500000
         EJECT ,                                                        91000000
         IGTXEPLG ,                DEFINE DSECTS & REGISTERS            91500000
RTEMPA   EQU   R2                  TEMPORARY CNTRL BLK POINTER          92000000
         SPACE 5                                                        92500000
         END   ,                   CSECT END                            93000000
