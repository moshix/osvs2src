TUBB     TITLE 'IGTUBB -- CROSS MEMORY POST ROUTINE'                    00500000
IGTUBB   CSECT ,                   POST SERVICE ROUTINE                 01000000
*/* * START OF SPECIFICATIONS ************************************** */ 01500000
*/*                                                                  */ 02000000
*/*   MODULE-NAME = IGTUBB                                           */ 02500000
*/*                                                                  */ 03000000
*/*   DESCRIPTIVE-NAME = CROSS MEMORY POST ROUTINE (MVS)             */ 03500000
*/*                      INTERPARTITION POST ROUTINE (VS1)           */ 04000000
*/*                      (SUPPORTS IGTYPOST MACRO)                   */ 04500000
*/*                                                                  */ 05000000
*/*   COPYRIGHT = NONE                                               */ 05500000
*/*                                                                  */ 06000000
*/*   STATUS = RELEASE 1, MODIFICATION LEVEL 0.                      */ 06500000
*/*                                                                  */ 07000000
*/*   FUNCTION = THIS ROUTINE WILL POST THE SPECIFIED ECB            */ 07500000
*/*       CONTAINED IN ANOTHER ADDRESS SPACE (OR PARTITION) WITH     */ 08000000
*/*       A COMPLETION CODE OF 0.  IF AN ERROR OCCURS IN THE POST    */ 08500000
*/*       PROCESSING, THE CALLER IS NOT NOTIFIED.                    */ 09000000
*/*                                                                  */ 09500000
*/*   NOTES =                                                        */ 10000000
*/*                                                                  */ 10500000
*/*     DEPENDENCIES =                                               */ 11000000
*/*        THIS ROUTINE SUPPORTS THE IGTYPOST MACRO AND ASSUMES      */ 11500000
*/*        A 'COMPARE AND SWAP' POST HAS BEEN ATTEMPTED.             */ 12000000
*/*                                                                  */ 12500000
*/*     RESTRICTIONS =                                               */ 13000000
*/*        THE ECB MUST BE IN CSA OR SQA.  THIS ROUTINE ASSUMES      */ 13500000
*/*        IT CAN REFERENCE THE STORAGE FROM ANY ADDRESS SPACE.      */ 14000000
*/*                                                                  */ 14500000
*/*     REGISTER-CONVENTIONS = STANDARD VPSS CONVENTIONS             */ 15000000
*/*                                                                  */ 15500000
*/*     PATCH-LABEL = APPATCH                                        */ 16000000
*/*                                                                  */ 16500000
*/*   MODULE-TYPE = CSECT                                            */ 17000000
*/*                                                                  */ 17500000
*/*     PROCESSOR = VS/2 ASSEMBLER                                   */ 18000000
*/*                                                                  */ 18500000
*/*     MODULE SIZE = SEE END OF ASSEMBLER LISTING                   */ 19000000
*/*                                                                  */ 19500000
*/*     ATTRIBUTES = RE-ENTRANT.                                     */ 20000000
*/*                                                                  */ 20500000
*/*   ENTRY-POINT = IGTUBB                                           */ 21000000
*/*                                                                  */ 21500000
*/*     PURPOSE = SAME AS FUNCTION                                   */ 22000000
*/*                                                                  */ 22500000
*/*     LINKAGE = ENTERED UNLOCKED, TASK MODE, ENABLED,              */ 23000000
*/*               SUPERVISOR STATE.  IT MAY BE IN ANY KEY.           */ 23500000
*/*                                                                  */ 24000000
*/*     INPUT = THE FOLLOWING REGISTERS ARE INITIALIZED ON ENTRY     */ 24500000
*/*             (SET BY MACRO IGTYPOST) --                           */ 25000000
*/*         REGISTER  0 - ADDRESS OF THE ASCB FOR THE ADDRESS        */ 25500000
*/*                       SPACE WAITING ON THE ECB.                  */ 26000000
*/*         REGISTER  1 - ADDRESS OF THE ECB TO POST.                */ 26500000
*/*         REGISTER 12 - ADDRESS OF THE APCT.                       */ 27000000
*/*         REGISTER 13 - ADDRESS OF CURRENT APSV (VPSS EXTENDED     */ 27500000
*/*                       SAVEAREA).                                 */ 28000000
*/*         REGISTER 14 - RETURN ADDRESS                             */ 28500000
*/*         REGISTER 15 - ADDRESS OF THIS ROUTINE (IGTUBB).          */ 29000000
*/*                                                                  */ 29500000
*/*     OUTPUT = THE ECB IS POSTED WITH A COMPLETION CODE OF 0.      */ 30000000
*/*              (THE POST MAY ACTUALLY ONLY BE SCHEDULED WHEN       */ 30500000
*/*              THE POST IS ISSUED.)                                */ 31000000
*/*                                                                  */ 31500000
*/*   EXIT-NORMAL = THE FOLLOWING REGISTERS ARE RETURNED TO THE      */ 32000000
*/*                 CALLER --                                        */ 32500000
*/*                 REGISTER  0 - UNPREDICTABLE                      */ 33000000
*/*                 REGISTER  1 - UNPREDICTABLE                      */ 33500000
*/*                 REGISTER 2-13 - SAME AS ON INPUT                 */ 34000000
*/*                 REGISTER 14 - RETURN ADDRESS                     */ 34500000
*/*                 REGISTER 15 - UNPREDICTABLE                      */ 35000000
*/*                 CONTROL IS RETURNED IN SUPERVISOR STATE,         */ 35500000
*/*                 TASK MODE, UNLOCKED, ENABLED AND IN THE KEY      */ 36000000
*/*                 OF THE CALLER (SAME AS ON INPUT).                */ 36500000
*/*                                                                  */ 37000000
*/*   EXIT-ERROR  = NONE.                                            */ 37500000
*/*                                                                  */ 38000000
*/*   EXTERNAL-REFERENCES = THE FOLLOWING ROUTINES AND               */ 38500000
*/*                         CONTROL BLOCKS ARE USED.                 */ 39000000
*/*                                                                  */ 39500000
*/*     ROUTINES = NONE.                                             */ 40000000
*/*                                                                  */ 40500000
*/*     DATA-AREAS =                                                 */ 41000000
*/*         ECB                                                      */ 41500000
*/*                                                                  */ 42000000
*/*     CONTROL-BLOCKS =                                             */ 42500000
*/*         PSA      - R                                             */ 43000000
*/*         APCT     - R                                             */ 43500000
*/*         APSV     - R, M                                          */ 44000000
*/*                                                                  */ 44500000
*/*   TABLES = NONE                                                  */ 45000000
*/*                                                                  */ 45500000
*/*   MACROS =                                                       */ 46000000
*/*       IGTXEPLG                                                   */ 46500000
*/*       POST                                                       */ 47000000
*/*                                                                  */ 47500000
*/*   CHANGE-ACTIVITY = NONE                                         */ 48000000
*/*                                                                  */ 48500000
*/*   MESSAGES = NONE                                                */ 49000000
*/*                                                                  */ 49500000
*/*   ABEND-CODES = NONE                                             */ 50000000
*/*                                                                  */ 50500000
*/* * END OF SPECIFICATIONS **************************************** */ 51000000
         EJECT ,                                                        51500000
         COPY  IGTXSYSO            DEFINE SCP CHARACTERISTICS           52000000
         SPACE 2                                                        52500000
         USING IGTUBB,APENTRY      TELL ASSEMBLER OF BASE               53000000
         USING APCT,APCTPTR        AP CONTROL TABLE                     53500000
         USING APSV,APSVPTR        SAVEAVEA OVERLAY                     54000000
         SPACE 2                                                        54500000
         B     STARTUP             SKIP IDENTIFIER                      55000000
         DC    YL1(COREIDE-*-1)    LENGTH OF ID                         55500000
         DC    C'IGTUBB'           CSECT NAME                           56000000
         DC    C' &SYSDATE'        ASSEMBLY DATE                        56500000
COREIDE  EQU   *                   END OF ID                            57000000
         SPACE 2                                                        57500000
STARTUP  DS    0H                  START OF CODE                        58000000
         AIF   ('&VPSSSYS' NE 'MVS').SKIP1 ASCB EXISTS ONLY FOR MVS     58500000
*                                                                       59000000
** IF ASCB TO POST IS CURRENT, USE                                      59500000
** STANDARD POST MACRO INTERFACE.                                       60000000
*                                                                       60500000
         USING PSA,0               LOW CORE OVERLAY (ABSOLUTE)          61000000
         C     RASCBPTR,PSAAOLD    ECB IN CURRENT MEMORY ?              61500000
         BE    CURRENT             * YES--GO DO NORMAL POST             62000000
         SPACE 3                                                        62500000
.SKIP1   ANOP  ,                   MVS ASCB CHECK PROCESSED             63000000
*                                                                       63500000
** BUILD CROSS-MEMORY POST PARAMETER LIST:                              64000000
**   +00 - ADDRESS OF ECB                                               64500000
**   +04 - ADDRESS OF ASCB FOR WAITING TCB (MVS ONLY)                   65000000
**   +08 - ADDRESS OF ERROR EXIT ROUTINE (BR_14 IN APCT) (MVS ONLY)     65500000
** USE INPUT SAVEAREA FOR THIS WORKAREA                                 66000000
*                                                                       66500000
         ST    RECBPTR,POSTECB     ADDRESS OF ECB                       67000000
         AIF   ('&VPSSSYS' NE 'MVS').SKIP2 SKIP ASCB/ERRET IF NOT MVS   67500000
         ST    RASCBPTR,POSTASCB   ADDRESS OF ASCB                      68000000
         LA    RTEMPA,APCTBR14     ERROR RETURN IS                      68500000
         ST    RTEMPA,POSTERET     * SR 15,15 / BR 14                   69000000
.SKIP2   ANOP  ,                   MVS ASCB/ERRET PROCESSED             69500000
         POST  MF=(E,POST)         ISSUE CROSS-MEMORY POST              70000000
         BR    APRETURN            RETURN TO CALLER                     70500000
         AIF   ('&VPSSSYS' NE 'MVS').SKIP3 SKIP NORMAL POST IF NOT MVS  71000000
         EJECT ,                                                        71500000
*                                                                       72000000
** ECB IN CURRENT ADDRESS SPACE. USE STANDARD POST MACRO.               72500000
*                                                                       73000000
CURRENT  EQU   *                                                        73500000
         POST  (RECBPTR),0         ISSUE NORMAL POST (NOT X-MEMORY)     74000000
         BR    APRETURN            RETURN TO CALLER                     74500000
.SKIP3   ANOP  ,                   MVS NORMAL POST PROCESSED            75000000
         EJECT ,                                                        75500000
         AIF   ('&VPSSSYS' NE 'MVS').SKIP4 MVS DSECTS REQUIRED ?        76000000
         IHAPSA ,                  LOW CORE                             76500000
IGTUBB   CSECT ,                   RESUME CSECT CODE                    77000000
         EJECT ,                                                        77500000
.SKIP4   ANOP  ,                   MVS REQUIRED DSECTS PROCESSED        78000000
         IGTXAPSV LIST=YES         SAVEAREA FORMAT                      78500000
         SPACE 5                                                        79000000
********************************************************************* * 79500000
**                                                                    * 80000000
**       CROSS-MEMORY POST OVERLAY (USE INPUT SAVEAREA)               * 80500000
**                                                                    * 81000000
********************************************************************* * 81500000
         SPACE ,                                                        82000000
POST     EQU   APSVRG15+00,4,C'F'  MF=L FORMAT FOR CROSS-MEMORY POST    82500000
*                                  * (USE REG 15, 0, AND 1 SLOTS)       83000000
POSTECB  EQU   POST+00,4,C'A'      ADDRESS OF ECB TO POST               83500000
         AIF   ('&VPSSSYS' NE 'MVS').SKIP5 MVS EQUATES REQUIRED ?       84000000
POSTASCB EQU   POST+04,4,C'A'      ADDRESS OF ASCB FOR THE              84500000
*                                  * ADDRESS SPACE THAT CONTAINS        85000000
*                                  * THE ECB TO POST.                   85500000
POSTERET EQU   POST+08,4,C'A'      ADDRESS OF ERROR-EXIT ROUTINE        86000000
*                                  * SET TO 'BR 14' ROUTINE             86500000
.SKIP5   ANOP  ,                   MVS REQUIRED EQUATES PROCESSED       87000000
         SPACE 2                                                        87500000
IGTUBB   CSECT ,                   RESUME CSECT CODE                    88000000
         EJECT ,                                                        88500000
********************************************************************* * 89000000
**                                                                    * 89500000
**       VPSS DSECT REQUESTS                                          * 90000000
**                                                                    * 90500000
********************************************************************* * 91000000
         SPACE ,                                                        91500000
         IGTXDSCT IGTXAPCT         CONTROL TABLE                        92000000
         EJECT ,                                                        92500000
         IGTXEPLG ,                VPSS DSECTS/REGISTERS                93000000
RTEMPA   EQU   R0                  TEMPORARY VALUE HOLDER (MVS ONLY)    93500000
RASCBPTR EQU   R0                  ADDRESS OF ASCB FOR ADDRESS          94000000
*                                  * SPACE CONTAINING ECB (MVS ONLY)    94500000
RECBPTR  EQU   R1                  ADDRESS OF ECB TO POST               95000000
         SPACE 5                                                        95500000
         END   ,                                                        96000000
