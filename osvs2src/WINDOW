         MACRO                                                          00050002
&NAME    WINDOW &PARM                                                   00100002
         LCLC  &TEMP                                                    00150002
         LCLC  &TEMP2                                                   00200002
&TEMP    SETC  'IHB1&SYSNDX'           GENERATE UNIQUE LABEL            00250002
&TEMP2   SETC  'IHB2&SYSNDX'           GENERATE UNIQUE LABEL            00300002
         AIF   ('&PARM' EQ '').ERROR   CHECK IF OPERAND SPECIFIED       00350002
         AIF   ('&PARM'(1,1) EQ '(').RXFORM1 CHECK IF RX FORM           00400002
         STCTL 0,0,&PARM               SAVE THE CURRENT C.R. 0 VALUE    00450002
         AGO   .COMMON                 PERFORM COMMON WINDOW OPERATION  00500002
.RXFORM1 ANOP                          RX FORM SPECIFIED                00550002
         STCTL 0,0,0&PARM              SAVE THE CURRENT C.R. 0 VALUE    00600002
.COMMON  ANOP                          COMMON WINDOW CODE               00650002
         STCTL 0,0,PSACR0              STORE C.R. 0 IN WORK AREA        00700002
         NC    PSACR0(4),&TEMP         CLEAR ALL EXTERNAL INTERRUPTS    00750002
         OC    PSACR0(4),&TEMP2        SET MFA AND EMS BITS             00800002
         LCTL  0,0,PSACR0              LOAD C.R. 0                      00850002
         STOSM PSACR0,X'01'            ENABLE FOR EMS/MFA INTERRUPTS    00900002
         STNSM PSACR0,X'FE'            DISABLE FOR EMS/MFA INTERRUPTS   00950002
         STCTL 0,0,PSACR0              STORE CR0 IN WORK AREA           00970002
         AIF   ('&PARM'(1,1) EQ '(').RXFORM2 CHECK IF RX FORM           01000002
         OC    &PARM.(1),PSACR0        INSURE TIMER BIT STAYS SAME      01020002
         LCTL  0,0,&PARM               RELOAD ORIGINAL C.R. 0           01050002
         AGO   .END                    EXIT                             01100002
.ERROR   ANOP                          NO C.R. 0 SAVE AREA WAS SPEC     01150002
         MNOTE 8,'NO C.R. 0 SAVE AREA ADDRESS WAS SPECIFIED'            01200002
         AGO   .END2                   EXIT                             01250002
.RXFORM2 ANOP                          RX FORM SPECIFIED                01300002
         OC    0(1,&PARM(1).),PSACR0   INSURE TIMER BIT STAYS SAME      01320002
         LCTL  0,0,0&PARM              RELOAD ORIGINAL C.R. 0           01350002
.END     ANOP                          FINISHED                         01400002
         B     *+12                    BRANCH AROUND CONSTANTS          01450002
&TEMP    DC    X'FFFFC31F'             DISABLE C.R. 0 MASK              01500002
&TEMP2   DC    X'0000C000'             ENABLE EMS/MFA MASK              01550002
.END2    ANOP                                                           01600002
         MEND                                                           01650002
